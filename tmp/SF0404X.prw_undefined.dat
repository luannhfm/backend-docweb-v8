#INCLUDE "PROTHEUS.CH"

/*/{Protheus.doc} SF0404X
	Relatório de Movimentos de Vacinas.

@author	Walmir Gonçalves da Silva Junior
@since	24/03/2016
@version 1.0

@return Nil

/*/

User Function SF0404X()
Private _cAlias		:= GetNextAlias()
Private _cAliasB2	:= GetNextAlias()
Private _oReport
Private _nQtdSld	:= 0

AjustaSX1("SF0404X")

_oReport:= ReportDef()
_oReport:PrintDialog()

Return

Static Function ReportDef()

Local _cTitle     := "Movimentação de Vacinas"
Local _oReport
Local _oMoviment
Local _oPeriod
Local _oColTot
Local _nVal := 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                         ³
//³ mv_par01               Data de	                             ³
//³ mv_par02               Data até		                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte("SF0404X",.F.)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criacao do componente de impressao                                      ³
//³                                                                        ³
//³TReport():New                                                           ³
//³ExpC1 : Nome do relatorio                                               ³
//³ExpC2 : Titulo                                                          ³
//³ExpC3 : Pergunte                                                        ³
//³ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  ³
//³ExpC5 : Descricao                                                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
_oReport:= TReport():New("SF0404X",_cTitle,"SF0404X", {|_oReport| ReportPrint(_oReport)},_cTitle)
_oReport:SetPortrait()
//_oReport:HideParamPage()
//_oReport:HideHeader()
//_oReport:HideFooter()
_oReport:SetTotalInLine(.F.)
_oReport:ldisableorientation := .T.

_oPeriod := TRSection():New(_oReport,"Período", {_cAlias})
TRCell():New(_oPeriod, "MV_PAR1",			_cAlias, "Incío" ,,16)
TRCell():New(_oPeriod, "MV_PAR2",			_cAlias, "Fim"	 ,,16)
TRCell():New(_oPeriod, "D3_COD",			_cAlias, "Cd. Prod"	,,TamSX3("D3_COD")[1])
TRCell():New(_oPeriod, "B1_DESC",			_cAlias, "Produto"	,,100)
TRCell():New(_oPeriod, "B2_QATU",			_cAlias, "Sld Estoque"	,PesqPict("SB2","B2_QATU"),16)

_oPeriod:Cell( "MV_PAR1"		):SetBlock({||  mv_par01	})
_oPeriod:Cell( "MV_PAR2"		):SetBlock({||  mv_par02	})
_oPeriod:Cell( "B2_QATU"		):SetBlock({|| _nQtdSld		})

_oMoviment := TRSection():New(_oReport,"Movimentações", {_cAlias})
//TRCell():New(_oMoviment, "D3_FILIAL",	_cAlias, "Filial"		)
TRCell():New(_oMoviment, "F5_TIP",		_cAlias, "Tipo Mov"		,,12)
TRCell():New(_oMoviment, "D3_EMISSAO", 	_cAlias, "Data"			,PesqPict("SD3","D3_EMISSAO"), 16)
TRCell():New(_oMoviment, "D3_XFILCN9",	_cAlias, "Fil Contr"	)
TRCell():New(_oMoviment, "D3_XNUMCN9",	_cAlias, "Contrato"		,,TamSX3("D3_XNUMCN9")[1])
TRCell():New(_oMoviment, "D3_XREVCN9",	_cAlias, "Revisão"		)
TRCell():New(_oMoviment, "A1_CGC",		_cAlias, "CNPJ"			)
TRCell():New(_oMoviment, "D3_XRAZCLI",	_cAlias, "Cliente"		,,60)
//TRCell():New(_oMoviment, "D3_COD",		_cAlias, "Cd. Prod"		,,TamSX3("D3_COD")[1])
//TRCell():New(_oMoviment, "B1_DESC", 	_cAlias, "Produto"		,,40)
TRCell():New(_oMoviment, "D3_UM",		_cAlias, "UM"			,,5)
TRCell():New(_oMoviment, "D3_QUANTT",	_cAlias, "Quantidade"	)

_oMoviment:Cell( "D3_EMISSAO"	):SetBlock({||  StoD((_cAlias)->D3_EMISSAO)	})
_oMoviment:Cell( "A1_CGC"		):SetBlock({||  POSICIONE("SA1",1,XFILIAL("SA1")+ (_cAlias)->D3_XCLIENT+ (_cAlias)->D3_XCLILJ, "A1_CGC") })

_oColTot := TRCollection():New("Tota","SUM", , "Totalizadores",PesqPict("SB2","B2_QATU"), "(_cAlias)->F5_TIP", .T., .F., _oMoviment,, "(_cAlias)->D3_QUANT")

Return(_oReport)

Static Function ReportPrint(_oReport)
Local _oPeriod	 := _oReport:Section(1)
Local _oMoviment := _oReport:Section(2)
MsgRun( "Selecionando registros...", "Aguarde" ,  {|| GerQuery() } )
//----------------------------------------------------------------------------
//
//----------------------------------------------------------------------------
Private oFont1,oFont2,oFont3,oFont3N,oFont4,oFont5,oFont6,oFont7,oFont7N,oFont8,oFont9,oFont9N,oFont10

oFont1  := TFont():New( 'Arial', 08, 08, .T., .T., 5, .T., 5, .F., .F. )
oFont2  := TFont():New( 'Arial', 08, 08, .T., .F., 5, .T., 5, .F., .F. )
oFont3  := TFont():New( 'Arial', 09, 09, .T., .F., 5, .T., 5, .F., .F. )
oFont3N := TFont():New( 'Arial', 09, 09, .T., .T., 5, .T., 5, .F., .F. )
oFont4  := TFont():New( 'Arial', 09, 09, .T., .F., 5, .T., 5, .F., .F. )
oFont5  := TFont():New( 'Arial', 10, 10, .T., .T., 5, .T., 5, .F., .F. )
oFont6  := TFont():New( 'Arial', 10, 10, .T., .F., 5, .T., 5, .F., .F. )
oFont7  := TFont():New( 'Arial', 12, 12, .T., .F., 5, .T., 5, .F., .F. )
oFont7N := TFont():New( 'Arial', 12, 12, .T., .T., 5, .T., 5, .F., .F. )
oFont8  := TFont():New( 'Arial', 12, 12, .T., .F., 5, .T., 5, .F., .F. )
oFont9  := TFont():New( 'Arial', 16, 16, .T., .F., 5, .T., 5, .F., .F. )
oFont9N := TFont():New( 'Arial', 16, 16, .T., .T., 5, .T., 5, .F., .F. )
oFont10 := TFont():New( 'Arial', 20, 20, .T., .T., 5, .T., 5, .F., .F. )

_oPeriod:Init()
_oPeriod:PrintLine()
_oPeriod:Finish()
_oMoviment:Print()

(_cAlias)->(DBCloseArea())

Return

Static Function GerQuery()
Local _cQuery := ""
Local _dDataINI := mv_par01
Local _dDataFIM := mv_par02
	
_cQuery := " SELECT SD3.D3_FILIAL, SD3.D3_COD, B1.B1_DESC, SD3.D3_TM, SD3.D3_EMISSAO, " + CRLF
_cQuery += " 	Case When SD3.D3_TM = '499' then 'D' when SD3.D3_TM = '999' then 'R' Else SF5.F5_TIPO End As F5_TIP, SD3.D3_UM, SA1.A1_NOME, SA1.A1_CGC, SD3.D3_XRAZCLI, SD3.D3_XCLIENT, SD3.D3_XCLILJ," + CRLF
_cQuery += " 	SD3.D3_XFILCN9, SD3.D3_XNUMCN9, SD3.D3_XREVCN9, SD3.D3_NUMSEQ, SD3.D3_CHAVE, SD3.D3_QUANT, " + CRLF
_cQuery += " 	Case When SF5.F5_TIPO = 'D' Or SD3.D3_TM = '499' " + CRLF
_cQuery += " 	Then Concat(Concat('+(', Cast(SD3.D3_QUANT As Varchar(50))),')') " + CRLF
_cQuery += " 	Else Concat(Concat('-(', Cast(SD3.D3_QUANT As Varchar(50))),')') End As D3_QUANTT " + CRLF
_cQuery += " FROM "+RetSQLName("SD3")+" SD3  INNER JOIN " + CRLF
_cQuery += " "+RetSQLName("SB1")+" B1 ON SD3.D3_COD = B1.B1_COD AND B1.B1_TIPO = 'IM' LEFT JOIN " + CRLF
_cQuery += " "+RetSQLName("SF5")+" SF5 ON SF5.F5_CODIGO = SD3.D3_TM LEFT JOIN " + CRLF
_cQuery += " "+RetSQLName("CN9")+" CN9 ON CN9.CN9_FILIAL = SD3.D3_XFILCN9 AND CN9.CN9_NUMERO = SD3.D3_XNUMCN9 AND " + CRLF
_cQuery += " CN9.CN9_REVISA = SD3.D3_XREVCN9 LEFT JOIN " + CRLF
_cQuery += " "+RetSQLName("SA1")+" SA1 ON SA1.A1_COD = CN9.CN9_CLIENT AND SA1.A1_LOJA = CN9.CN9_LOJACL " + CRLF
_cQuery += " WHERE " + CRLF
_cQuery += " 	SD3.D3_FILIAL = '"+ xFilial("SD3") +"' AND " + CRLF
_cQuery += " 	SD3.D_E_L_E_T_ = ' ' AND " + CRLF
_cQuery += " 	B1.D_E_L_E_T_ = ' ' " + CRLF


If !Empty(_dDataINI) .And. !Empty(_dDataFIM) 
	_cQuery += " AND SD3.D3_EMISSAO between '"+Dtos(_dDataINI)+"' AND '"+Dtos(_dDataFIM)+"' " + CRLF
EndIf

_cQuery += " ORDER BY SD3.D3_EMISSAO " + CRLF

MemoWrite("C:\temp\"+FunName()+"_"+ProcName()+".TXT",_cQuery)

DBUseArea(.T., "TOPCONN", TcGenQry(,,ChangeQuery(_cQuery)),_cAlias, .F.,.F.)

_cQuery := ""
_cQuery += " SELECT SD3.D3_COD, SB2.B2_QATU " + CRLF
_cQuery += " FROM "+RetSQLName("SD3")+" SD3 INNER JOIN " + CRLF
_cQuery += " "+RetSQLName("SB2")+" SB2 ON SB2.B2_FILIAL = '"+ xFilial("SB2") +"' AND "
_cQuery += " SB2.B2_COD = SD3.D3_COD AND SB2.B2_LOCAL = SD3.D3_LOCAL " + CRLF
_cQuery += " WHERE " + CRLF
_cQuery += " 	SD3.D3_FILIAL = '"+ (_cAlias)->D3_FILIAL +"' AND " + CRLF
_cQuery += " 	SD3.D3_NUMSEQ = '"+ (_cAlias)->D3_NUMSEQ +"' AND " + CRLF
_cQuery += " 	SD3.D3_CHAVE = '"+ 	(_cAlias)->D3_CHAVE  +"' AND " + CRLF
_cQuery += " 	SD3.D3_COD = '"+ 	(_cAlias)->D3_COD 	 +"' AND " + CRLF
_cQuery += " 	SD3.D_E_L_E_T_ = ' ' AND " + CRLF
_cQuery += " 	SB2.D_E_L_E_T_ = ' ' " + CRLF

DBUseArea(.T., "TOPCONN", TcGenQry(,,ChangeQuery(_cQuery)),_cAliasB2, .F.,.F.)

If (_cAliasB2)->(!Eof())
	_nQtdSld := (_cAliasB2)->B2_QATU
	(_cAliasB2)->(DBCloseArea())
EndIf

Return

Static Function AjustaSX1(_cPerg)

	u_SFPUTSX1(_cPerg, '01', 'Data DE?'		,'','','mv_ch1','D',8,00,00,'G','','','','','mv_par01','','','','','','','','','','','','','','','','',,'','','')
	u_SFPUTSX1(_cPerg, '02', 'Data ATE?'	,'','','mv_ch2','D',8,00,00,'G','','','','','mv_par02','','','','','','','','','','','','','','','','',,'','','')

Return	