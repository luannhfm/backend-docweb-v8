#Include 'Protheus.ch'
#Include 'TopConn.ch'

/*/{Protheus.doc} SN73R01J
Relatorio de Visitas - SENAI

@type 		function
@author 	Jose Leite de Barros Neto
@since 	04/07/2016
@version 	1.0
@return 	Nil, Nulo
/*/
User Function SN73R01J()
	
	Local _oReport		:= Nil
	Local _cPerg 		:= FunName()
	
	If TRepInUse()
		fCriaSx1( _cPerg )
		If Pergunte( _cPerg )
			_oReport := ReportDef( _cPerg )
			_oReport:PrintDialog()
		EndIf
	EndIf

Return


/** {Protheus.doc} ReportDef
Funcao que define as colunas

@author: 	Jose Leite de Barros Neto
@since: 	04/07/2016
@Uso: 		SFIEMT
*/
Static Function ReportDef(p_cPerg)
	
	Local oReport	:= Nil
	Local oSection	:= Nil
	Local oBreak		:= Nil
	Local cTitle 	:= "Relatorio de Visitas"  
	
	oReport := TReport():New(p_cPerg,cTitle,p_cPerg,{|oReport| PrintReport(oReport)},cTitle)
	oReport:SetLandScape()
	
	oSection:= TRSection():New(oReport,OemToAnsi(""),{"AD5","AC5","SA1","SA3","ADY","ADZ"})
		
	oFilial:= TRCell():New(oSection, "_cFilial","TRA", "Filial")
	oFilial:SetSize(10)
	
	oData:= TRCell():New(oSection, "_cData","TRA", "Dt. Visita")
	oData:SetSize(10)
	
	oTipoV:= TRCell():New(oSection, "_cTipoV","TRA", "Tp. da Visita")
	oTipoV:SetSize(12)
	
	oCGC:= TRCell():New(oSection, "_cCGC","TRA", "CNPJ")
	oCGC:SetSize(20)
	
	oRazaoS:= TRCell():New(oSection, "_cRazaoS","TRA", "Razão Social")
	oRazaoS:SetSize(40)
	
	oNReduz:= TRCell():New(oSection, "_cNReduz","TRA", "Nome Fantasia")
	oNReduz:SetSize(40)
	
	oCtoCli:= TRCell():New(oSection, "_cCtoCli","TRA", "Tempo Contato")
	oCtoCli:SetSize(20)
	
	oVend:= TRCell():New(oSection, "_cVend","TRA", "Vendedor")
	oVend:SetSize(25)
	
	oVistAc:= TRCell():New(oSection, "_cVistAc","TRA", "Visita Acomp.")
	oVistAc:SetSize(25)
	
	oAcomp:= TRCell():New(oSection, "_cAcomp","TRA", "Acompanhante")
	oAcomp:SetSize(25)
	
	oDuracao:= TRCell():New(oSection, "_cDuracao","TRA", "Duração da Visita")
	oDuracao:SetSize(25)
	
	oProduto:= TRCell():New(oSection, "_cProduto","TRA", "Produto Of.")
	oProduto:SetSize(40)
	
	oProposta:= TRCell():New(oSection, "_cProp","TRA", "Proposta?")
	oProposta:SetSize(8)
	
Return( oReport )


/** {Protheus.doc} ReportDef
Funcao que gera o relatorio

@author: 	Jose Leite de Barros Neto
@since: 	04/07/2016
@Uso: 		SFIEMT
*/
Static Function PrintReport(oReport)
	
	Local oSection 	:= oReport:Section(1)
	Local _cQuery 	:= ""
	Local _cHrIni 	:= ""
	Local _cHrFim 	:= ""
	Local _cFilial	:= ""
	Local _cData		:= DtoC(dDataBase)
	Local _cTipoV	:= ""
	Local _cCGC		:= ""
	Local _cRazaoS	:= ""
	Local _cNReduz	:= ""
	Local _cCtoCli	:= ""	
	Local _cVend		:= ""
	Local _cVistAc	:= ""
	Local _cAcomp	:= ""
	Local _cDuracao	:= ""
	Local _cProduto	:= ""
	Local _cProp		:= ""
	
	If Select("TRA") > 0
		DbSelectarea("TRA")
		DbCloseArea()
	Endif
	
	_cQuery := "SELECT						"
	_cQuery += CRLF + "AD5_FILIAL,		"	
	_cQuery += CRLF + "AD5_DATA,		"
	_cQuery += CRLF + "AD5_CODCLI, 		"
	_cQuery += CRLF + "AD5_LOJA, 		"
	_cQuery += CRLF + "AD5_PROSPE, 		"
	_cQuery += CRLF + "AD5_LOJPRO,		"
  	_cQuery += CRLF + "AC5_DESCRI,		"
  	_cQuery += CRLF + "A3_NOME, 		"
  	_cQuery += CRLF + "ADZ_XVEND,		"
  	_cQuery += CRLF + "AD5_XRHINI,		"
  	_cQuery += CRLF + "AD5_XRHFIM,		"
  	_cQuery += CRLF + "ADZ_DESCRI,		"
  	_cQuery += CRLF + "CASE WHEN ADZ_PRODUT IS NULL THEN 'N' ELSE 'S' END ADZ_PROP	"
	_cQuery += CRLF + "FROM " + RetSqlName("AD5") + " AD5 "
	_cQuery += CRLF + "INNER JOIN " + RetSqlName("AC5") + " AC5 ON AC5_FILIAL = '"+ FwxFilial('AC5') +"'		" 
	_cQuery += CRLF + "															AND AC5_EVENTO = AD5_EVENTO 				"
	_cQuery += CRLF + "															AND AC5.D_E_L_E_T_ <> '*'					"
  	_cQuery += CRLF + "INNER JOIN " + RetSqlName("SA3") + " SA3 ON A3_FILIAL  = '"+ xFilial('SA3') +"'			" 
  	_cQuery += CRLF + "															AND A3_COD = AD5_VEND 						"
  	_cQuery += CRLF + "															AND SA3.D_E_L_E_T_ <> '*'					"
	_cQuery += CRLF + "LEFT JOIN  " + RetSqlName("AD1") + " AD1 ON 	AD1_FILIAL = AD5_FILIAL 						"
   _cQuery += CRLF + "                            					AND AD1_NROPOR = AD5_NROPOR						"
   _cQuery += CRLF + "                            					AND AD1.D_E_L_E_T_ <> '*'						"
  	If cFilAnt == "03MT0001"
  		_cQuery += CRLF + "LEFT JOIN " + RetSqlName("ADY") + " ADY ON ADY_FILIAL = AD5_FILIAL "
  	Else
  		_cQuery += CRLF + "LEFT JOIN " + RetSqlName("ADY") + " ADY ON ADY_FILIAL = '"+ FwxFilial('ADY')+"'		"
  	EndIf
  	_cQuery += CRLF + "															AND ADY_OPORTU = AD5_NROPOR 				"
  	_cQuery += CRLF + "															AND ADY.D_E_L_E_T_ <> '*'					"
  	If cFilAnt == "03MT0001"
  		_cQuery += CRLF + "LEFT JOIN " + RetSqlName("ADZ") + " ADZ ON ADZ_FILIAL = AD5_FILIAL	"
  	Else
  		_cQuery += CRLF + "LEFT JOIN " + RetSqlName("ADZ") + " ADZ ON ADZ_FILIAL = '"+ FwxFilial('ADZ')+"'		"
  	EndIf
  	_cQuery += CRLF + "															AND ADZ_PROPOS = ADY_PROPOS					" 
  	_cQuery += CRLF + "															AND ADZ_REVISA = ADY_PREVIS 				"
  	_cQuery += CRLF + "															AND ADZ.D_E_L_E_T_ <> '*'					"
	_cQuery += CRLF + "WHERE																		"
	If cFilAnt == "03MT0001"
		_cQuery += CRLF + " AD5_FILIAL 		 	>= '"+ cFilAnt +"'							"
		_cQuery += CRLF + " AND AD5_FILIAL 	<= '03MT0099'								"
	Else
		_cQuery += CRLF + " AD5_FILIAL 		 = '"+ FwxFilial("AD5") +"'					"
	EndIf
	_cQuery += CRLF + " AND	AD5_VEND 	>= '"+ MV_PAR01 +"'								"
  	_cQuery += CRLF + " AND	AD5_VEND 	<= '"+ MV_PAR02 +"'								"
  	_cQuery += CRLF + " AND	AD5_DATA 	>= '" + DtoS(MV_PAR05) + "'						" 
  	_cQuery += CRLF + " AND 	AD5_DATA 	<= '" + DtoS(MV_PAR06) + "'						"
	_cQuery += CRLF + " GROUP BY AD5_FILIAL, AD5_DATA, AD5_CODCLI, AD5_LOJA,   	" 
	_cQuery += CRLF + " AD5_PROSPE, AD5_LOJPRO, AC5_DESCRI, A3_NOME, ADZ_XVEND, 	" 
	_cQuery += CRLF + " AD5_XRHINI, AD5_XRHFIM, ADZ_DESCRI, ADZ_PRODUT 			 	"
	_cQuery += CRLF + " ORDER BY AD5_FILIAL "
	
	TCQUERY _cQuery NEW ALIAS "TRA"
		
	DbSelectArea("TRA")
	TRA->(DbGoTop())
		
	oReport:SetMeter(RecCount())
	oSection:Init()
		
	While .Not. TRA->( EOF() )
		
		If oReport:Cancel()
			Exit
		EndIf
		
		_cFilial 	:= TRA->AD5_FILIAL
		_cData		:= DtoC(StoD(TRA->AD5_DATA))
		_cTipoV	:= AllTrim(TRA->AC5_DESCRI)
		
		If .Not. Empty(AllTrim(TRA->AD5_CODCLI))
			
			DbSelectArea("SA1")
			SA1->(DbSetOrder(1))
			SA1->(DbGotop())
			If SA1->(DbSeek( xFilial("SA1") + TRA->AD5_CODCLI + TRA->AD5_LOJA))
				
				_cCGC		:= AllTrim(Transform(SA1->A1_CGC,Iif(Len(AllTrim(SA1->A1_CGC))==11,"@R 999.999.999-99","@R 99.999.999/9999-99")))
				_cRazaoS	:= AllTrim(SA1->A1_NOME)
				_cNReduz	:= AllTrim(SA1->A1_NREDUZ)
				
				If AllTrim(SA1->A1_XCTOCLI) == "M"
					_cCtoCli := "Mensal"
				ElseIf AllTrim(SA1->A1_XCTOCLI) == "B"
					_cCtoCli := "Bimestral"
				ElseIf AllTrim(SA1->A1_XCTOCLI) == 'T'
					_cCtoCli := "Trimestral"
				Else
					_cCtoCli := "Não Infor."
				EndIf
				
			EndIf
			
			SA1->(DbCloseArea())
			
		Else
			
			DbSelectArea("SUS")
			SUS->(DbSetOrder(1))
			SUS->(DbGotop())
			If SUS->(DbSeek( xFilial("SUS") + TRA->AD5_PROSPE + TRA->AD5_LOJPRO))
				_cCGC		:= AllTrim(Transform(SUS->US_CGC,Iif(Len(AllTrim(SUS->US_CGC))==11,"@R 999.999.999-99","@R 99.999.999/9999-99")))
				_cRazaoS	:= AllTrim(SUS->US_NOME)
				_cNReduz	:= AllTrim(SUS->US_NREDUZ)
			EndIf
			
			_cCtoCli := "Prospect"
			
			SA1->(DbCloseArea())
			
		EndIf
		
		_cVend	 := AllTrim(TRA->A3_NOME)
		
		If Empty(TRA->ADZ_XVEND)
			_cVistAc := "Não"
		Else
			_cVistAc := "Sim"
		EndIf
		
		_cHrIni := TRA->AD5_XRHINI + ":00"
		_cHrFim := TRA->AD5_XRHFIM + ":00"
		
		_cAcomp 	:= TRA->ADZ_XVEND
		_cDuracao	:= Elaptime(_cHrIni,_cHrFim)
		_cProduto	:= TRA->ADZ_DESCRI
		_cProp	 	:= TRA->ADZ_PROP
		
		oSection:Cell("_cFilial"):SetValue(_cFilial)
		oSection:Cell("_cData"):SetValue(_cData)
		oSection:Cell("_cTipoV"):SetValue(_cTipoV)
		oSection:Cell("_cCGC"):SetValue(_cCGC)
		oSection:Cell("_cRazaoS"):SetValue(_cRazaoS)
		oSection:Cell("_cNReduz"):SetValue(_cNReduz)
		oSection:Cell("_cCtoCli"):SetValue(_cCtoCli)
		oSection:Cell("_cVend"):SetValue(_cVend)
		oSection:Cell("_cVistAc"):SetValue(_cVistAc)
		oSection:Cell("_cAcomp"):SetValue(_cAcomp)
		oSection:Cell("_cDuracao"):SetValue(_cDuracao)
		oSection:Cell("_cProduto"):SetValue(_cProduto)
		oSection:Cell("_cProp"):SetValue(_cProp)
			
		oSection:PrintLine()
		oReport:IncMeter()
			
		TRA->( DbSkip() )
	End
		
	TRA->(DbCloseArea())
	oSection:Finish()
	
Return( Nil )


/** {Protheus.doc} fCriaSx1
Funcao para criacao das perguntas

@param: 	<Nil>
@author 	Jose Leite de Barros Neto
@since 	04/07/2016
@Uso: 		SFIEMT
*/
Static Function fCriaSx1( p_cPerg )
	
	Local aHelp := {}

	AAdd(aHelp, {{"Informe o Consultor Inicial"			}, {""}, {""}})
	AAdd(aHelp, {{"Informe o Consultor Inicial"			}, {""}, {""}})
	AAdd(aHelp, {{"Informe a Cliente Inicial"			}, {""}, {""}})
	AAdd(aHelp, {{"Informe a Cliente Final"				}, {""}, {""}})
	AAdd(aHelp, {{"Informe a Data da Visita Inicial"	}, {""}, {""}})
	AAdd(aHelp, {{"Informe a Data da Visita Final"		}, {""}, {""}})
	
	u_SFPUTSX1( p_cPerg, "01","Consultor de?				"  ,"","","mv_ch1","C",TamSX3("A3_COD")[1]	,0,0,"G",""						,"SA3"		,"","","mv_par01","","","","","","","","","","","","","","","","",aHelp[1,1],aHelp[1,2],aHelp[1,3],"")
	u_SFPUTSX1( p_cPerg, "02","Consultor Ate?				"  ,"","","mv_ch2","C",TamSX3("A3_COD")[1]	,0,0,"G",""						,"SA3"		,"","","mv_par02","","","","","","","","","","","","","","","","",aHelp[2,1],aHelp[2,2],aHelp[2,3],"")
	u_SFPUTSX1( p_cPerg, "03","Cliente de?					"  ,"","","mv_ch3","C",TamSX3("A1_COD")[1]	,0,0,"G",""						,"SA1"		,"","","mv_par03","","","","","","","","","","","","","","","","",aHelp[3,1],aHelp[3,2],aHelp[3,3],"")
	u_SFPUTSX1( p_cPerg, "04","Cliente Ate?				"  ,"","","mv_ch4","C",TamSX3("A1_COD")[1]	,0,0,"G",""						,"SA1"		,"","","mv_par04","","","","","","","","","","","","","","","","",aHelp[4,1],aHelp[4,2],aHelp[4,3],"")
	u_SFPUTSX1( p_cPerg, "05","Apont. Inicial?			"  ,"","","mv_ch5","D",8						 	,0,0,"G",""						,""			,"","","mv_par05","","","","","","","","","","","","","","","","",aHelp[5,1],aHelp[5,2],aHelp[5,3],"")
	u_SFPUTSX1( p_cPerg, "06","Apont. Final?				"  ,"","","mv_ch6","D",8						 	,0,0,"G",""						,""			,"","","mv_par06","","","","","","","","","","","","","","","","",aHelp[6,1],aHelp[6,2],aHelp[6,3],"")
	
Return( Nil )