#Include 'protheus.ch'
#Include 'parmtype.ch'

/*/{Protheus.doc} MT103IPC
	MT103IPC - Atualiza campos customizados no Documento de Entrada
	
	Descrição:
	Este Ponto de Entrada tem por objetivo atualizar os campos customizados 
	no Documento de Entrada e na Pré Nota de Entrada após a importação dos 
	itens do Pedido de Compras (SC7).
	
	Eventos:
	O ponto de entrada MT103IPC é chamado nas funções: NFePC2Acol() e 
	LxA103SC7ToaCols()  (MATA103X.PRX e LOCXNF2.PRW), após a carga de 
	dados do pedido de compras.
	
	Link:http://tdn.totvs.com/display/public/PROT/MT103IPC+-+Atualiza+campos+customizados+no+Documento+de+Entrada
	
@author Franklin de Brito de Oliveira
@param ExpN1, Numérico, Contém o número do item do aCols do Documento de Entrada e da Pré Nota de Entrada.
@return lRet, logico, O parâmetro lRet, somente é tratado na rotina Localizada. "LocxNF2".Em outros ambientes, este parâmetro não possui aplicação.
@since 29/09/2017

@type Function
/*/
User Function MT103IPC()
	
Local ExpN1 	:= PARAMIXB[1]
Local nPosDes   := aScan(aHeader,{|x| AllTrim(x[2]) == "D1_XDESPRO"})
Local nPosCod	:= aScan(aHeader,{|x| AllTrim(x[2]) == "D1_COD"})
Local nPosCls	:= aScan(aHeader,{|x| AllTrim(x[2]) == "D1_XCLSSC"})
Local nPosPlc	:= aScan(aHeader,{|x| AllTrim(x[2]) == "D1_XCLPLC"})
Local nPosNPed	:= aScan(aHeader,{|x| AllTrim(x[2]) == "D1_PEDIDO"})
Local nPosITPD	:= aScan(aHeader,{|x| AllTrim(x[2]) == "D1_ITEMPC"})
 
	If nPosDes > 0
		aCols[ExpN1, nPosDes] := Posicione("SB1", 1, xFilial("SB1") + aCols[ExpN1, nPosCod], "B1_DESC")
	EndIf
	
	If nPosCls > 0
		aCols[ExpN1, nPosCls] := POSICIONE("SC7",1,xFilial("SC7") + aCols[ ExpN1 , nPosNPed ] + aCols[ ExpN1 , nPosITPD ], "C7_XCLSSC" )
	EndIf
	
	If nPosPlc > 0
		aCols[ExpN1, nPosPlc] := POSICIONE("SC7",1,xFilial("SC7") + aCols[ ExpN1 , nPosNPed ] + aCols[ ExpN1 , nPosITPD ], "C7_XCLPLC" )
	EndIf

Return ( Nil )