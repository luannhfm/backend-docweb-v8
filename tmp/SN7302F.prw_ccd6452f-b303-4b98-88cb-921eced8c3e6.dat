#Include 'Protheus.ch'
#Include "Topconn.ch"

/*/{Protheus.doc} SN7302F
	Função para montagem da tela de histórico das visitas.

@author Franklin B. Oliveira
@since 22/02/2017

@type function
/*/
User Function SN7302F()

Local oModel	:= FwModelActive()
Local oDlg, oBwAD5, oBwAD7, oSayAD5, oSayAD7
Local aArea		:= GetArea()
Local aColAD5 	:= {}
Local aRowAD5 	:= {}
Local aColAD7 	:= {}
Local aRowAD7 	:= {}
Local cEntide	:= oModel:GetValue( 'MODEL_ENT_GRID', 'ADL_ENTIDA' )
Local cCodEnt	:= oModel:GetValue( 'MODEL_ENT_GRID', 'ADL_CODENT' )
Local cLojEnt	:= oModel:GetValue( 'MODEL_ENT_GRID', 'ADL_LOJENT' )
Local cTitle	:= "Histórico das visitas" 

	DEFINE MSDIALOG oDlg FROM 0,0 TO 500, 900 PIXEL TITLE cTitle
		
		oSayAD5:= TSay():New(002, 005, {|| "Apontamento de Visita:"}, oDlg, , , , , , .T., CLR_BLUE, CLR_WHITE, 200, 10)
		
		oBwAD5 := TWBrowse():New(001, 001, 440, 080, , , , oDlg, , , , , , , , , , , , , , , , , , , .T.)
		oBwAD5:SetArray(aRowAD5)
		
		Aadd(aColAD5, {"Filial"		, "", "", 030, 0, ".F.", "", "C", "", ""})
		Aadd(aColAD5, {"Vendedor"		, "", "", 150, 0, ".F.", "", "C", "", ""})
		Aadd(aColAD5, {"Data"			, "", "", 040, 0, ".F.", "", "D", "", ""})
		Aadd(aColAD5, {"Oportunidade"	, "", "", 040, 0, ".F.", "", "C", "", ""})
		Aadd(aColAD5, {"Evento"		, "", "", 060, 0, ".F.", "", "C", "", ""})
					
		oBwAD5:AddColumn(TCColumn():New(aColAD5[ 1,1], {|| aRowAD5[oBwAD5:nAt, 1]}, aColAD5[ 1,3], , , 'LEFT', aColAD5[ 1,4], .F., .F., , , , .F., ))
		oBwAD5:AddColumn(TCColumn():New(aColAD5[ 2,1], {|| aRowAD5[oBwAD5:nAt, 2]}, aColAD5[ 2,3], , , 'LEFT', aColAD5[ 2,4], .F., .F., , , , .F., ))
		oBwAD5:AddColumn(TCColumn():New(aColAD5[ 3,1], {|| aRowAD5[oBwAD5:nAt, 3]}, aColAD5[ 3,3], , , 'LEFT', aColAD5[ 3,4], .F., .F., , , , .F., ))
		oBwAD5:AddColumn(TCColumn():New(aColAD5[ 4,1], {|| aRowAD5[oBwAD5:nAt, 4]}, aColAD5[ 4,3], , , 'LEFT', aColAD5[ 4,4], .F., .F., , , , .F., ))
		oBwAD5:AddColumn(TCColumn():New(aColAD5[ 5,1], {|| aRowAD5[oBwAD5:nAt, 5]}, aColAD5[ 5,3], , , 'LEFT', aColAD5[ 5,4], .F., .F., , , , .F., ))
		
		oSayAD7:= TSay():New(100, 005, {|| "Agenda Futura:"}, oDlg, , , , , , .T., CLR_BLUE, CLR_WHITE, 200, 10)
		
		oBwAD7 := TWBrowse():New(008, 001, 440, 080, , , , oDlg, , , , , , , , , , , , , , , , , , , .T.)
		oBwAD7:SetArray(aRowAD7)
		
		Aadd(aColAD7, {"Filial"			, "", "", 030, 0, ".F.", "", "C", "", ""})
		Aadd(aColAD7, {"Vendedor"		, "", "", 150, 0, ".F.", "", "C", "", ""})
		Aadd(aColAD7, {"Data"			, "", "", 040, 0, ".F.", "", "D", "", ""})
		Aadd(aColAD7, {"Oportunidade"	, "", "", 040, 0, ".F.", "", "C", "", ""})
					
		oBwAD7:AddColumn(TCColumn():New(aColAD7[ 1,1], {|| aRowAD7[oBwAD7:nAt, 1]}, aColAD7[ 1,3], , , 'LEFT', aColAD7[ 1,4], .F., .F., , , , .F., ))
		oBwAD7:AddColumn(TCColumn():New(aColAD7[ 2,1], {|| aRowAD7[oBwAD7:nAt, 2]}, aColAD7[ 2,3], , , 'LEFT', aColAD7[ 2,4], .F., .F., , , , .F., ))
		oBwAD7:AddColumn(TCColumn():New(aColAD7[ 3,1], {|| aRowAD7[oBwAD7:nAt, 3]}, aColAD7[ 3,3], , , 'LEFT', aColAD7[ 3,4], .F., .F., , , , .F., ))
		oBwAD7:AddColumn(TCColumn():New(aColAD7[ 4,1], {|| aRowAD7[oBwAD7:nAt, 4]}, aColAD7[ 4,3], , , 'LEFT', aColAD7[ 4,4], .F., .F., , , , .F., ))
		
		@ 200,400 Button "Sair" 	Size 040,012 OF oDlg PIXEL ACTION {|| oDlg:End()} PIXEL
		
	ACTIVATE MSDIALOG oDlg ON INIT LoadRows(oBwAD5,@aRowAD5,oBwAD7,@aRowAD7,cEntide,cCodEnt,cLojEnt) CENTERED
	
	RestArea(aArea)
	
Return (Nil)

/*/{Protheus.doc} LoadRows
	Função para carregar linhas do histórico de visitas.
	
@author Franklin B. Oliveira
@since 22/02/2017
@param oBwAD5, object, Browse da AD5
@param aRowAD5, array, Array dos dados da AD5
@param oBwAD7, object, Browse da AD7
@param aRowAD7, array, Array dos dados da AD7
@param cCodEnt, characters, Código da entidade
@param cLojEnt, characters, Loja da entidade
/*/
Static Function LoadRows(oBwAD5, aRowAD5, oBwAD7, aRowAD7, cEntide, cCodEnt, cLojEnt)

Local cQuery := ""
	
	ProcRegua(0)
	IncProc()
	ProcessMessages()
	
	cQuery := "SELECT * FROM " 
	cQuery += "("
	cQuery += " SELECT AD5.AD5_FILIAL, "
	cQuery += "  AD5.AD5_VEND, "
	cQuery += "  AD5.AD5_DATA, "
	cQuery += "  UTL_RAW.CAST_TO_VARCHAR2(AD5.AD5_XRHIST) AS AD5_XRHIST, "
	cQuery += "  AD5.AD5_NROPOR, "
	cQuery += "  AD5.AD5_EVENTO, "
	cQuery += "  AC5.AC5_DESCRI, "
	cQuery += "  SA3.A3_NOME "
	cQuery += " FROM " + RetSqlName("AD5") + " AD5 "
	cQuery += " LEFT JOIN " + RetSqlName("AC5") + " AC5 ON AC5.D_E_L_E_T_ = ' '"
	cQuery += "  AND AC5.AC5_FILIAL = RPAD(SUBSTR(AD5_FILIAL, 1, 4), 8) "
	cQuery += "  AND AC5.AC5_EVENTO = AD5.AD5_EVENTO "
	cQuery += " LEFT JOIN " + RetSqlName("SA3") + " SA3 ON SA3.D_E_L_E_T_ = ' '"
	cQuery += "  AND SA3.A3_FILIAL = RPAD(SUBSTR(AD5_FILIAL, 1, 4), 8) "
	cQuery += "  AND SA3.A3_COD = AD5.AD5_VEND "
	cQuery += " WHERE AD5.D_E_L_E_T_ = ' ' "
	If cEntide == 'SA1'
		cQuery += "  AND AD5.AD5_CODCLI = '" + cCodEnt + "'"
		cQuery += "  AND AD5.AD5_LOJA = '" + cLojEnt + "'"
	ElseIf cEntide == 'SUS'
		cQuery += "  AND AD5.AD5_PROSPE = '" + cCodEnt + "'"
		cQuery += "  AND AD5.AD5_PROSPE = '" + cLojEnt + "'"
	EndIf
	cQuery += " ORDER BY AD5.AD5_DATA DESC"
	cQuery += " ) "
	cQuery += " WHERE ROWNUM <= 2 "
	
	TCQUERY cQuery NEW ALIAS "TRBAD5"
	
	While .Not. TRBAD5->(EoF())	
		aAdd(aRowAD5, {TRBAD5->AD5_FILIAL,	;
					   TRBAD5->A3_NOME,		;
					   SToD(TRBAD5->AD5_DATA),;
					   TRBAD5->AD5_NROPOR,	;
					   TRBAD5->AC5_DESCRI,  ;
					   TRBAD5->AD5_XRHIST;
					  })
		
		TRBAD5->(DbSkip())
	EndDo
	
	If ValType(oBwAD5) == "O"
		If !Empty(aRowAD5)
			oBwAD5:bHeaderClick := {|o,n| oX:=o, nX:=n, TWBOrder(oX,nX)}
			oBwAD5:cToolTip := "Linhas: (" + AllTrim(Transform(Len(aRowAD5), "@E 999,999,999")) + ") Ordem: Padrão"
			oBwAD5:bLDblClick := {||LoadHist(aRowAD5[oBwAD5:nAt,6])}
		EndIf
		oBwAD5:SetArray(aRowAD5)
		oBwAD5:Refresh()
	EndIf
	
	cQuery := "SELECT * FROM " 
	cQuery += "("
	cQuery += " SELECT AD7.AD7_FILIAL, "
	cQuery += "  AD7.AD7_VEND, "
	cQuery += "  AD7.AD7_DATA, "
	cQuery += "  AD7.AD7_NROPOR, "
	cQuery += "  SA3.A3_NOME "
	cQuery += " FROM " + RetSqlName("AD7") + " AD7 "
	cQuery += " LEFT JOIN " + RetSqlName("SA3") + " SA3 ON SA3.D_E_L_E_T_ = ' '"
	cQuery += "  AND SA3.A3_FILIAL = RPAD(SUBSTR(AD7_FILIAL, 1, 4), 8) "
	cQuery += "  AND SA3.A3_COD = AD7.AD7_VEND "
	cQuery += " WHERE AD7.D_E_L_E_T_ = ' ' "
	If cEntide == 'SA1'
		cQuery += "  AND AD7.AD7_CODCLI  =  '" + cCodEnt + "'"
		cQuery += "  AND AD7.AD7_LOJA    =  '" + cLojEnt + "'"
	ElseIf cEntide == 'SUS'
		cQuery += "  AND AD7.AD7_PROSPE  =  '" + cCodEnt + "'"
		cQuery += "  AND AD7.AD7_LOJPRO  =  '" + cLojEnt + "'"
	EndIf
	cQuery += "  AND AD7.AD7_DATA    >= '" + DToS(dDataBase) + "'"
	cQuery += " ORDER BY AD7.AD7_DATA DESC"
	cQuery += " ) "
	cQuery += " WHERE ROWNUM <= 2 "
	
	TCQUERY cQuery NEW ALIAS "TRBAD7"
	
	While .Not. TRBAD7->(EoF())	
		aAdd(aRowAD7, {TRBAD7->AD7_FILIAL,	;
					   TRBAD7->A3_NOME,		;
					   SToD(TRBAD7->AD7_DATA),;
					   TRBAD7->AD7_NROPOR	})
		
		TRBAD7->(DbSkip())
	EndDo
	
	If ValType(oBwAD7) == "O"
		If !Empty(aRowAD7)
			oBwAD7:bHeaderClick := {|o,n| oX:=o, nX:=n, TWBOrder(oX,nX)}
			oBwAD7:cToolTip := "Linhas: (" + AllTrim(Transform(Len(aRowAD7), "@E 999,999,999")) + ") Ordem: Padrão"
		EndIf
		oBwAD7:SetArray(aRowAD7)
		oBwAD7:Refresh()
	EndIf
	
	TRBAD5->(DBCloseArea())
	TRBAD7->(DBCloseArea())

Return (Nil)

/*/{Protheus.doc} LoadHist
	Montagem da tela com o histórico do apontamento.
	
@author Frnaklin B. Oliveira
@since 22/02/2017
@param cHist, characters, histórico do apontamento.
/*/
Static Function LoadHist(cHist)
Local lRet := .T.
Local oDlg
Local oGet
	
	DEFINE DIALOG oDlg FROM 0,0 TO 250,400 TITLE "Histórico do Apontamento" OF oMainWnd PIXEL
	
	@ 002,002 GROUP TO 022,200 LABEL "" OF oDlg PIXEL
	
	@ 009,004 Say "Histórico " SIZE 197,008 OF oDlg PIXEL
	
	@ 024,004 Get oGet Var cHist SIZE 198,062 MEMO OF oDlg PIXEL

	@ 108,103 BUTTON "Ok" SIZE 030,013 PIXEL OF oDlg ACTION ( oDlg:End() )
	
	oGet:lReadOnly := .T.
	
	ACTIVATE MSDIALOG oDlg

Return (lRet)