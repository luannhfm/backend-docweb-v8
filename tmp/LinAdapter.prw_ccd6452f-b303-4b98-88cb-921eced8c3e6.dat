#include 'totvs.ch'
#include 'parmtype.ch'

/*/{Protheus.doc} PrdAdapter
Classe Adapter para o serviço
@author  Sergio Salustiano
@since   01/09/2020
@version 1.0
/*/

CLASS LinAdapter FROM FWAdapterBaseV2
	METHOD New()
	METHOD GetListLin()
EndClass
//-------------------------------------------------------------------
/*/{Protheus.doc} New
Método construtor
@param cVerb, verbo HTTP utilizado
@author  Sergio Salustiano
@since   01/09/2020
@version 1.0
/*/
//-------------------------------------------------------------------
Method New( cVerb ) CLASS LinAdapter
	_Super:New( cVerb, .T. )
return
//-------------------------------------------------------------------
/*/{Protheus.doc} GetListProd
Método que retorna uma lista de produtos 
@author  Sergio Salustiano
@since   01/09/2020
@version 1.0
/*/
//-------------------------------------------------------------------
Method GetListLin( ) CLASS LinAdapter
	Local aArea 	AS ARRAY
	Local cWhere	AS CHAR
	aArea   := FwGetArea()
	AddMapFields( self )
	::SetQuery( GetQuery() )
	cWhere := " ZDI_FILIAL = '"+ FWxFilial('ZDI') +"' AND ZDI.D_E_L_E_T_ = ' '"
	::SetWhere( cWhere )
	::SetOrder( "ZDI_CODIGO" )
	//Executa a consulta, se retornar .T. tudo ocorreu conforme esperado
	If ::Execute() 
		// Gera o arquivo Json com o retorno da Query
		::FillGetResponse()
	EndIf
	FwrestArea(aArea)
RETURN
//-------------------------------------------------------------------
/*/{Protheus.doc} AddMapFields
Função para geração do mapa de campos
@param oSelf, object, Objeto da prórpia classe
@author  Sergio Salustiano
@since   01/09/2020
@version 1.0
/*/
//-------------------------------------------------------------------
Static Function AddMapFields( oSelf )
	
	oSelf:AddMapFields( 'CODE'                , 'ZDI_CODIGO'  , .T., .T., { 'ZDI_CODIGO', 'C', TamSX3( 'ZDI_CODIGO' )[1], 0 } )
	oSelf:AddMapFields( 'DESCRIPTION'	    , 'ZDI_NOME'     , .T., .F., { 'ZDI_NOME', 'C', TamSX3( 'ZDI_NOME' )[1], 0 } )	
	oSelf:AddMapFields( 'ATIVO'	            , 'ZDI_ATIVO'     , .T., .F., { 'ZDI_ATIVO', 'C', TamSX3( 'ZDI_ATIVO' )[1], 0 } )
	
	
Return 
//-------------------------------------------------------------------
/*/{Protheus.doc} GetQuery
Retorna a query usada no serviço
@param oSelf, object, Objeto da prórpia classe
@author  Sergio Salustiano
@since   01/09/2020
@version 1.0
/*/
//-------------------------------------------------------------------
Static Function GetQuery()
	Local cQuery AS CHARACTER
	
	cQuery := " SELECT #QueryFields#"
    cQuery +=   " FROM " + RetSqlName( 'ZDI' ) + " ZDI "
    cQuery += " WHERE #QueryWhere#"
	
Return cQuery