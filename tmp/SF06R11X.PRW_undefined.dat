#INCLUDE "PROTHEUS.CH"
#INCLUDE "MSGRAPHI.CH"

/*/{Protheus.doc} SF06R11X
Relatorio de Indicadores de Contas a Pagar
@author j2a.luizjunior
@since 24/03/2017
@version 1.0
@example
(examples)
@see (links_or_references)
/*/

User Function SF06R11X()

	Local oReport := Nil
	Local cPerg   := "SF06R11X"

	Private cPngDir := ""
	Private aPosPrt := {}
	Private aImg    := {}
	Private _aSelFil := {}
	Private _lTdasFil := .F.
	Private _cFilSql := ""
	
	//Walmir Junior 04/05/2020 - Preencher este parâmetro sem a primeira e ultima " ' " aspas simples, pois a rotina utiliza Embedded SQL.
	Private  _cPrf	:= SuperGetMV( 'MV_XPRFR11',, "FAT','LIQ','CMP','CAN" ) //"'FAT','LIQ','CMP','CAN','CCO'"
   
	Sleep(1000)
	
	_SetOwnerPrvt("__oDlg__",Nil)      
	__oDlg__   := MSDialog():New(01,01,800,800,"",,,.F.,,,,,,.T.,,,.T.)
	
	CriaSX1(cPerg)
	If !Pergunte(cPerg,.T.)
		Return(Nil)
	EndIf
	
	If MV_PAR02 = 1
		_aSelFil := AdmGetFil(@_lTdasFil,.F.,"SE2")
		While Len(_aSelFil) = 0
			_aSelFil := AdmGetFil(@_lTdasFil,.F.,"SE2")
		EndDo
		cParam   := GetRngFil( _aSelFil, "SE1" )
		_cFilSql := " AND CAMPO " +	cParam +" "
	EndIf
	
	oReport := ReportDef(cPerg)
	If (oReport != NIL)
		oReport:PrintDialog()	
		
		For nB := 1 To Len(aImg)	
			fErase(aImg[nB][1])
		Next nB
		
	EndIf	
	
	__oDlg__:End()		

Return

/*/{Protheus.doc} ReportDef
(long_description)
@author j2a.luizjunior
@since 24/03/2017
@version 1.0
@example
(examples)
@see (links_or_references)
/*/

Static Function ReportDef(cPerg)

	Local oReport		
	Local cNomeRel	:= "SF06R11X"
	
	Local cTitRel	:= "Relatorio de Indicador de Contas a Pagar"
	Local cDescRel	:= "Relatorio de Indicador de Contas a Pagar"

	oReport := TReport():New( cNomeRel, cTitRel, cPerg, { |oReport| ReportPrint(oReport) }, cDescRel )
	
	oReport:nFontBody := 8
	oReport:cFontBody := "Courier New"
	oReport:SetTotalInLine(.F.) 
	oReport:Page()
	oReport:ParamReadOnly(.T.)
	
	If MV_PAR01 = 1
		oReport:SetLandScape()
	Else
		oReport:SetPortrait()
	EndIf
	
	oReport:SetCustomText( {|| fGerCab(oReport) } ) // Cabecalho 
	
	If MV_PAR01 == 1
	
		oViabilidade := TRSection():New(oReport, "Indicador de Pagamento" ,{"SE2"}                                )
		 
		oNome        := TRCell():New(oViabilidade,"E2_NOMFOR"             ,"SE2"                                  )
		oNome:SetSize(60)
		
		oNum         := TRCell():New(oViabilidade,"E2_NUM"                ,"SE2"                                  )	
		oNum:SetSize(55)
		
		oTipo        := TRCell():New(oViabilidade,"E2_TIPO"               ,"SE2"                                  )  
		oTipo:SetSize(20)
		
		oValor       := TRCell():New(oViabilidade,"E2_VALOR"              ,"SE2"                                  )
		oValor:SetSize(55)
		
		oEmissao     := TRCell():New(oViabilidade,"E2_EMISSAO"            ,"SE2"                                  )
		oEmissao:SetSize(60)
		
		oVencRea     := TRCell():New(oViabilidade,"E2_VENCREA"            ,"SE2"                                  )
		oVencRea:SetSize(60)
		
		oBaixa       := TRCell():New(oViabilidade,"E2_BAIXA"              ,"SE2"                                  )
		oBaixa:SetSize(60)
		
		oDias        := TRCell():New(oViabilidade,"DIAS"                  ,"SE2","Dias Ant.","@E 99,999"          )	
		oDias:SetSize(45)
		
		oDescon      := TRCell():New(oViabilidade,"E2_DESCONT"            ,"SE2"                                  )
		oDescon:SetSize(30)	
		
		oValLiq      := TRCell():New(oViabilidade,"E5_VALOR"             ,"SE2"                                  )	
		oValLiq:SetSize(55)
		
		oSaldo       := TRCell():New(oViabilidade,"E2_SALDO"              ,"SE2"                                  )	
		oSaldo:SetSize(55)
		
		oMotBx       := TRCell():New(oViabilidade,"E5_MOTBX"              ,"SE5"                                  ) 
		oMotBx:SetSize(45)
		
		oFilOrig     := TRCell():New(oViabilidade,"E2_FILORIG"            ,"SE2"                                  )
		oFilOrig:SetSize(55)
		
		oAnt         := TRCell():New(oViabilidade,"ANTECIPADO"            ,"SE2","Antecipado","@E 99,999"         )
		oAnt:SetSize(55)
		
		oPrazo       := TRCell():New(oViabilidade,"PRAZO"                 ,"SE2","No Prazo"  ,"@E 99,999"         )
		oPrazo:SetSize(45)
		
		oAtraso      := TRCell():New(oViabilidade,"ATRASO"                ,"SE2","Em Atraso" ,"@E 99,999"         )
		oAtraso:SetSize(45)
		
		oTot := TRSection():New(oReport, "Total de Indicador de Pagamento" ,{"SE2"}                                                   )		

		oTDesc := TRCell():New(oTot,"TDESC"                 ,"SE2","Titulo"     ,"@!"                             )
		oTDesc:SetSize(20)
		
		oTVal  := TRCell():New(oTot,"TVAL"                  ,"SE2","Valor"      ,"@E 9,999,999,999,999.99"        )
		oTVal:SetSize(20)
		
		oTAnt  := TRCell():New(oTot,"TANTECIP"              ,"SE2","Antecipado" ,"@!"                             )
		oTAnt:SetSize(20)
		
		oTPraz := TRCell():New(oTot,"TPRAZO"                ,"SE2","Prazo"      ,"@!"                             )
		oTPraz:SetSize(20)
		
		oTAtra := TRCell():New(oTot,"TATRASO"               ,"SE2","Atraso"     ,"@!"                             )
		oTAtra:SetSize(20)
		
		oTot:lHeaderVisible := .T. // imprime o titulo da secao antes da impressao da secao
		oTot:SetBorder(5,1) //Borda da secao 
		oTot:SetCellBorder("BOTTOM",1,,.T.) // Setando para aparecer todas as bordas das celulas CABECALHO
		
		oPrazo := TRSection():New(oReport, "Prazo Médio de Pagamento"    ,{"SE2"}                                              )

		oPZDesc := TRCell():New(oPrazo,"PZDESC"             ,"SE2","Titulo"   ,"@!"                               )
		oPZDesc:SetSize(50)

		oPZVal  := TRCell():New(oPrazo,"PZVAL"              ,"SE2","Valor"    ,"@E 9,999,999,999,999.99"          )
		oPZVal:SetSize(20)
		
		oPrazo:lHeaderVisible := .T. // imprime o titulo da secao antes da impressao da secao
		oPrazo:SetBorder(5,1) //Borda da secao 
		oPrazo:SetCellBorder("BOTTOM",1,,.T.) // Setando para aparecer todas as bordas das celulas CABECALHO
		
	Else
	
		oIndicador   := TRSection():New(oReport, "Indicador de Pagamento" ,{"SE2"}                                )
		
		oFilial      := TRCell():New(oIndicador,"M0_FILIAL"               ,"SM0","Filial"                         )
		
		oAnt         := TRCell():New(oIndicador,"ANTECIPADO"              ,"SE2","Antecipado"        ,"@E 99,999" )
		
		oPrazo       := TRCell():New(oIndicador,"PRAZO"                   ,"SE2","No Prazo"          ,"@E 99,999" ) 
		
		oAtraso      := TRCell():New(oIndicador,"ATRASO"                  ,"SE2","Em Atraso"         ,"@E 99,999" )
		
		oTotPag      := TRCell():New(oIndicador,"TOTPAG"                  ,"SE2","Total PAGTOS"      ,"@E 99,999" )
		
		oTotal       := TRCell():New(oIndicador,"E2_VALOR"                ,"SE2","Total R$"                       )
		
		oQtdPA       := TRCell():New(oIndicador,"QTDPA"                   ,"SE2","Qtdade P.A. Paga*" ,"@E 99,999" )
		
		TRFunction():New(oIndicador:Cell("ANTECIPADO")     ,,"SUM",,,,,.F.,.T.                                    )
		TRFunction():New(oIndicador:Cell("PRAZO"     )     ,,"SUM",,,,,.F.,.T.                                    )
		TRFunction():New(oIndicador:Cell("ATRASO"    )     ,,"SUM",,,,,.F.,.T.                                    )
		TRFunction():New(oIndicador:Cell("TOTPAG"    )     ,,"SUM",,,,,.F.,.T.                                    )
		TRFunction():New(oIndicador:Cell("E2_VALOR"  )     ,,"SUM",,,,,.F.,.T.                                    )
		TRFunction():New(oIndicador:Cell("QTDPA"     )     ,,"SUM",,,,,.F.,.T.                                    )
		
	EndIf			
	
Return(oReport)

/*/{Protheus.doc} ReportPrint
(long_description)
@author j2a.luizjunior
@since 24/03/2017
@version 1.0
@example
(examples)
@see (links_or_references)
/*/

Static Function ReportPrint(oReport)

	Local cSql      := ""
	Local cAlias    := GetNextAlias()
	Local oSec1     := oReport:Section(1)
	Local oSec2     := oReport:Section(2)
	Local oSec3     := oReport:Section(3)	
	Local oFont     := TFont():New('Courier New',,-9,,.F.)
	Local lTodasFil := .F.
	Local nValLiq   := 0
	
	Local nTAnt     := 0
	Local nTPrazo   := 0
	Local nTAtra    := 0
	Local nTPAAnt   := 0 // totalizadores utilizados para titulos PA
	Local nTPAPrz   := 0 // totalizadores utilizados para titulos PA
	Local nTPAAtr   := 0 // totalizadores utilizados para titulos PA
	
	Local nMedFor   := 0	
	Local nQtdTit   := 0
	Local nValPA    := 0
	Local nAntec    := 0
	Local nPrazo    := 0
	Local nAtraz    := 0			
	Local nTotPag   := 0	

	If !Empty(MV_PAR03) .And. !Empty(MV_PAR04) 
		cSql += " AND E2_FORNECE >= '"  + MV_PAR03 + "' "
		cSql += " AND E2_LOJA    >= '"  + MV_PAR04 + "' "	
	EndIf	

	If !Empty(MV_PAR05) .And. !Empty(MV_PAR06) 
		cSql += " AND E2_FORNECE <= '"  + MV_PAR05 + "' "
		cSql += " AND E2_LOJA    <= '"  + MV_PAR06 + "' "	
	EndIf	

	If !Empty(MV_PAR07) .And. !Empty(MV_PAR08) 
		cSql += " AND E2_EMISSAO BETWEEN '"  + DToS(MV_PAR07) + "' AND '" + DToS(MV_PAR08) + "'"			
	EndIf		

	If !Empty(MV_PAR09) .And. !Empty(MV_PAR10) 
		cSql += " AND E2_VENCTO BETWEEN '"  + DToS(MV_PAR09) + "' AND '" + DToS(MV_PAR10) + "'"			
	EndIf
		
	cSql += StrTran(_cFilSql,"CAMPO","E2_FILIAL")
	
	If MV_PAR01 == 1	

		cSql := "%"+cSql+"%"		
	
		BeginSql Alias cAlias
	
			SELECT E2_NOMFOR,
			       E2_NUM,
			       E2_PREFIXO,
			       E2_TIPO,
			       E2_VALOR,
			       E2_EMISSAO,
			       E2_VENCREA,
			       E2_BAIXA,
			       E2_DESCONT,
			       E2_SALDO,
			       E5_MOTBX,
			       E2_FILORIG,
			       E5_VALOR	             
			FROM       %Table:SE2% E2
			INNER JOIN %Table:SE5% E5
			ON E5.%NOTDEL%
			AND E5_FILIAL  = E2_FILIAL                                                                                              
			AND E5_PREFIXO = E2_PREFIXO
			AND E5_NUMERO  = E2_NUM
			AND E5_PARCELA = E2_PARCELA
			AND E5_TIPO    = E2_TIPO
			AND E5_CLIFOR  = E2_FORNECE
			AND E5_LOJA	   = E2_LOJA 
			AND E5_MOTBX NOT IN ( %Exp:_cPrf%  )
			AND E5_SITUACA NOT IN ('C', 'E', 'X')
			AND E5_TIPODOC NOT IN ( 'RA', 'ES',  'TE', 'EC', 'JR', 'MT', 'PR') //'PA','TR','CH',
			AND E2_TIPO NOT IN ( 'RA', 'ES',  'TE', 'EC', 'JR', 'MT', 'PR')
			%Exp:cSql%
			WHERE	E2.%NOTDEL%
			AND NOT (E2_VENCREA = E2_BAIXA AND E2_VENCREA = E2_EMISSAO)
	
		EndSql	
		
		oReport:SetMeter(70)
		While !(cAlias)->(Eof())
			
			oReport:IncMeter() // incrementa regua
				
			nQtdTit++
	
			oSec1:Init()
		
			oSec1:Cell("E2_NOMFOR") :SetValue((cAlias)->E2_NOMFOR)		
			oSec1:Cell("E2_NUM")    :SetValue((cAlias)->E2_NUM)
			oSec1:Cell("E2_TIPO")   :SetValue((cAlias)->E2_TIPO)
			oSec1:Cell("E2_VALOR")  :SetValue((cAlias)->E2_VALOR)
			oSec1:Cell("E2_EMISSAO"):SetValue(DToC(SToD((cAlias)->E2_EMISSAO)))
			oSec1:Cell("E2_VENCREA"):SetValue(DToC(SToD((cAlias)->E2_VENCREA)))
			oSec1:Cell("E2_BAIXA")  :SetValue(DToC(SToD((cAlias)->E2_BAIXA)))		
			oSec1:Cell("DIAS")      :SetValue( SToD((cAlias)->E2_VENCREA) - SToD((cAlias)->E2_EMISSAO))		
			oSec1:Cell("E2_DESCONT"):SetValue((cAlias)->E2_DESCONT)
			oSec1:Cell("E5_VALOR")  :SetValue((cAlias)->E5_VALOR)
			oSec1:Cell("E2_SALDO")  :SetValue((cAlias)->E2_SALDO)
			oSec1:Cell("E5_MOTBX")  :SetValue((cAlias)->E5_MOTBX)
			oSec1:Cell("E2_FILORIG"):SetValue((cAlias)->E2_FILORIG)
			
			If SToD((cAlias)->E2_BAIXA) < SToD((cAlias)->E2_VENCREA) .AND. (cAlias)->E2_TIPO <> "PA "
				nAnt    := 1
			Else
				nAnt    := 0
			EndIf
			oSec1:Cell("ANTECIPADO"):SetValue(nAnt)
			
			If SToD((cAlias)->E2_BAIXA) = SToD((cAlias)->E2_VENCREA) .AND. (cAlias)->E2_TIPO <> "PA "
				nPrazo   := 1
				nValPraz := (cAlias)->E5_VALOR
			Else
				nPrazo   := 0
			EndIf	
			oSec1:Cell("PRAZO"):SetValue(nPrazo)
			
			If SToD((cAlias)->E2_BAIXA) > SToD((cAlias)->E2_VENCREA) .AND. (cAlias)->E2_TIPO <> "PA "
				nAtraso   := 1
				nValAtraz := (cAlias)->E5_VALOR
			Else
				nAtraso   := 0
			EndIf	
			oSec1:Cell("ATRASO"):SetValue(nAtraso)
			
			oSec1:Printline()
			
			If oReport:Cancel()
				Return(Nil)
			EndIf
			
			If AllTrim((cAlias)->E2_TIPO) == "PA"
				nValPA += (cAlias)->E5_VALOR	//E2_VALOR
				nTPAAnt += nAnt
				nTPAPrz += nPrazo
				nTPAAtr += nAtraso
			Else
				nTAnt   += nAnt
				nTPrazo += nPrazo
				nTAtra  += nAtraso
			EndIf
			
			nValLiq += (cAlias)->E5_VALOR
			
			(cAlias)->(DbSkip())
		EndDo
		
		(cAlias)->(DbCloseArea())
		
		oSec1:Finish()
		
		oReport:SkipLine()
		
		oReport:ThinLine()
		
		oReport:SkipLine()
		
		cConta   := "21010201001"
		nSalAnt  := SaldoCT7Fil(cConta,MV_PAR07,"01","1","CTBR400",,,_aSelFil,,lTodasFil)
		nMedFor  := SaltoAtu(nSalAnt[6])
		nValCom  := ValComPer()
		nValVar  := ( nMedFor / nValCom ) * 360
		
		//-> quadro 1
		//		oReport:Say(oReport:Row(),012,"QTDE Titu. - " + AllTrim(cValToChar(nQtdTit))                                                     ,oFont)
		nPerAnt := Round((nTAnt   / nQtdTit) * 100,2)
		nPerPra := Round((nTPrazo / nQtdTit) * 100,2)
		nPerAtr := Round((nTAtra  / nQtdTit) * 100,2)
		//		oReport:Say(oReport:Row(),400,AllTrim(cValToChar(nPerAnt))+"%" + Space(05) + AllTrim(cValToChar(nPerPra))+"%" + Space(05) + AllTrim(cValToChar(nPerAtr))+"%",oFont)
		
		//-> quadro 2
		//		oReport:Say(oReport:Row(),1002,"Variação (A) / (B) X 360        Dias - " + cValToChar(round(nValVar,0))                                   ,oFont)
		//		oReport:SkipLine()
		
		// verifico se tem espaço suficiente para mostrar o quadro com o resumo e caso não tenha pula pagina
		If oReport:Row() > (oReport:PageHeight() - 1000)
			// pula pagina
			oReport:SetRow(oReport:PageHeight() + 100)
			oReport:PrintText("")
		EndIf
		
		//oReport:PrintText("Total de Indicador de Pagamento")
		oSec2:Init()
		cDesc := "Total - "
		oSec2:Cell("TDESC")   :SetValue(cDesc)
		oSec2:Cell("TVAL")    :SetValue(nValLiq)
		oSec2:Cell("TANTECIP"):SetValue(AllTrim(cValToChar(nTAnt)))
		oSec2:Cell("TPRAZO")  :SetValue(AllTrim(cValToChar(nTPrazo)))
		oSec2:Cell("TATRASO") :SetValue(AllTrim(cValToChar(nTAtra)))
		oSec2:Printline()
		
		cDesc := "P.A.  - "
		oSec2:Cell("TDESC")   :SetValue(cDesc)
		oSec2:Cell("TVAL")    :SetValue(nValPA)
		oSec2:Cell("TANTECIP"):SetValue(AllTrim(cValToChar(nTPAAnt)))
		oSec2:Cell("TPRAZO")  :SetValue(AllTrim(cValToChar(nTPAPrz)))
		oSec2:Cell("TATRASO") :SetValue(AllTrim(cValToChar(nTPAAtr)))
		oSec2:Cell("TANTECIP"):Disable()
		oSec2:Cell("TPRAZO")  :Disable()
		oSec2:Cell("TATRASO") :Disable()
		oSec2:Printline()
		oSec2:Cell("TANTECIP"):Enable()
		oSec2:Cell("TPRAZO")  :Enable()
		oSec2:Cell("TATRASO") :Enable()

		cDesc  := "QTDE Titu. - "
		oSec2:Cell("TDESC"):SetValue(cDesc + AllTrim(cValToChar(nQtdTit)))
		
		oSec2:Cell("TVAL")    :SetValue()
		oSec2:Cell("TANTECIP"):SetValue()
		oSec2:Cell("TPRAZO")  :SetValue()
		oSec2:Cell("TATRASO") :SetValue()		
		oSec2:Printline()
		
		oSec2:Finish()
		
		oReport:SkipLine()
		
		oReport:SkipLine()
		
		//oReport:PrintText("Prazo Médio de Pagamento")
		
		oSec3:Init()

		cDesc := "Valor do Periodo Conta Fornecedor R$ - "
		oSec3:Cell("PZDESC"):SetValue(cDesc)
		oSec3:Cell("PZVAL") :SetValue(nMedFor)
		oSec3:Printline()
		
		cDesc := "Valor das Compras no Periodo      R$ - "
		oSec3:Cell("PZDESC"):SetValue(cDesc)
		oSec3:Cell("PZVAL") :SetValue(nValCom)
		oSec3:Printline()
		
		cDesc := "Variação (A) / (B) X 360        Dias - " + cValToChar(round(nValVar,0))
		oSec3:Cell("PZDESC"):SetValue(cDesc)
		oSec3:Cell("PZVAL") :SetValue()
		oSec3:Printline()
		
		oSec3:Finish()
		
	Else
	
		aConsolida := Consolida(cSql, oReport)
		
		For nH := 1 To Len(aConsolida)
	
			oSec1:Init()
			
			oSec1:Cell("M0_FILIAL") :SetValue(SubStr(aConsolida[nH][1],1,5))
			oSec1:Cell("ANTECIPADO"):SetValue(aConsolida[nH][2])		
			oSec1:Cell("PRAZO")     :SetValue(aConsolida[nH][3])
			oSec1:Cell("ATRASO")    :SetValue(aConsolida[nH][4])
			oSec1:Cell("TOTPAG")    :SetValue(aConsolida[nH][5])
			oSec1:Cell("E2_VALOR")  :SetValue(aConsolida[nH][6])
			oSec1:Cell("QTDPA")     :SetValue(aConsolida[nH][7])
			
			nAntec  += aConsolida[nH][2]
			nPrazo  += aConsolida[nH][3]
			nAtraz  += aConsolida[nH][4]			
			nTotPag += aConsolida[nH][5]
		
			oSec1:Printline()
			
			If oReport:Cancel()
				Return(Nil)
			EndIf	
			
		Next nH		
		
		oSec1:Finish()
		
		ImpGrf(@oReport,nAntec,nPrazo,nAtraz,nTotPag,"")		
	
	EndIf	
	
Return

/*/{Protheus.doc} SaltoAtu
(long_description)
@author j2a.luizjunior
@since 11/05/2017
@version 1.0
@example
(examples)
@see (links_or_references)
/*/

Static Function SaltoAtu(pSalAnt) 

	Local cConta  := '21010201001'
	Local cAlSal  := GetNextalias()
	Local nSalAnt := pSalAnt
	Local nValCre := 0
	Local nValDeb := 0
	Local nSalAtu := 0
	Local nMedfor := 0
	
	_cFSql := StrTran(_cFilSql,"CAMPO","CT2_FILIAL")
	_cFSql := "%"+_cFSql+"%"
	
	BeginSql Alias cAlSal
	
		SELECT CT2_FILIAL,
		       CT2_DEBITO,
		       CT2_CREDIT,
		       CT2_VALOR
		FROM %Table:CT2%
		WHERE %NOTDEL%
		%Exp:_cFSql%
		AND CT2_DATA   BETWEEN %Exp:DToS(MV_PAR07)% AND %Exp:DToS(MV_PAR08)%
		AND (CT2_DEBITO = %Exp:cConta% OR CT2_CREDIT = %Exp:cConta%)
	
	EndSql
	
	While !(cAlSal)->(Eof())
	
		If AllTrim((cAlSal)->CT2_DEBITO) == "21010201001"
			nValCre += (cAlSal)->CT2_VALOR			
		ElseIf AllTrim((cAlSal)->CT2_CREDIT) == "21010201001"
			nValDeb += (cAlSal)->CT2_VALOR
		EndIf
	
		(cAlSal)->(DbSkip())
	EndDo
	
	(cAlSal)->(DbCloseArea())
	
	nSalAtu	:= ( nSalAnt + nValCre ) - nValDeb
	
	nMedFor := ( nSalAtu + nSalAnt ) / 2

Return nMedFor

/*/{Protheus.doc} ValComPer
(long_description)
@author j2a.luizjunior
@since 11/05/2017
@version 1.0
@example
(examples)
@see (links_or_references)
/*/

Static Function ValComPer

	Local cAlCom  := GetNextAlias()
	Local nValCom := 0
	
	_cFSql := StrTran(_cFilSql,"CAMPO","F1_FILIAL")
	_cFSql := "%"+_cFSql+"%"
	
	BeginSql Alias cAlCom	
		
		SELECT F1_VALBRUT 
		FROM  %Table:SF1%
		WHERE %NOTDEL%
		%Exp:_cFSql%
		AND   F1_EMISSAO BETWEEN %Exp:DToS(MV_PAR07)% AND %Exp:DToS(MV_PAR08)%
		AND   F1_STATUS  = 'A'
		AND   F1_ESPECIE IN ('NF','NFS','NFCE')
	
	EndSql
	
	While !(cAlCom)->(Eof())
		
		nValCom += (cAlCom)->F1_VALBRUT
		
		(cAlCom)->(DbSkip())
	EndDo
	
	(cAlCom)->(DbCloseArea())	

Return nValCom

/*/{Protheus.doc} Consolida
(long_description)
@author j2a.luizjunior
@since 12/05/2017
@version 1.0
@example
(examples)
@see (links_or_references)
/*/

Static Function Consolida(pSql,oReport)

	Local cSql    := pSql
	Local nValLiq := 0
	
	Local nTAnt   := 0
	Local nTPrazo := 0
	Local nTAtra  := 0
	Local nTPAAnt := 0
	Local nTPAPrz := 0
	Local nTPAAtr := 0
	 	
	Local nQtdPA  := 0
	Local cFil    := ""
	Local aFilial := {}
	Local cAlCon  := GetNextAlias()
	Local aRet    := {} 
	Local _aAreaSM0 := SM0->(GetArea()) 		
	
	DbSelectArea("SM0")
	DbGoTop()
	While !SM0->(Eof()) .And. SM0->M0_CODIGO == cEmpAnt
	
		If !(SubStr(SM0->M0_CODFIL,1,4) $ cFil)
			cFil += SubStr(SM0->M0_CODFIL,1,4)
			aAdd(aFilial,{AllTrim(SM0->M0_FILIAL),SM0->M0_CODFIL})
		EndIf 
		
		SM0->(DbSkip())
	EndDo
	
	RestArea(_aAreaSM0)
	
	cSql := "%"+cSql+"%"
		
	For nK := 1 To Len(aFilial)
	
		cFilCon := SubStr(aFilial[nK][2],1,4) + "%"
		
		BeginSql Alias cAlCon
		
			SELECT E2_NOMFOR,
			       E2_NUM,
			       E2_PREFIXO,
			       E2_TIPO,
			       E2_VALOR,
			       E2_EMISSAO,
			       E2_VENCREA,
			       E2_BAIXA,
			       E2_DESCONT,
			       E2_SALDO,
			       E5_MOTBX,
			       E2_FILORIG,
			       E5_VALOR,
			       E2_FILIAL	             
			FROM       %Table:SE2% E2
			INNER JOIN %Table:SE5% E5
			ON E5.%NOTDEL%
			AND E5_FILIAL  = E2_FILIAL
			AND E5_PREFIXO = E2_PREFIXO
			AND E5_NUMERO  = E2_NUM
			AND E5_PARCELA = E2_PARCELA
			AND E5_TIPO    = E2_TIPO
			AND E5_CLIFOR  = E2_FORNECE
			AND E5_LOJA	   = E2_LOJA 
			AND E5_MOTBX NOT IN ( %Exp:_cPrf% )
			AND E5_SITUACA NOT IN ('C', 'E', 'X')
			AND E5_TIPODOC NOT IN ('RA', 'ES', 'TE', 'EC', 'JR', 'MT', 'PR')//'TR', 'CH','PA'******
			AND E2_TIPO NOT IN ('RA', 'ES', 'TE', 'EC', 'JR', 'MT', 'PR')
			%Exp:cSql%
			AND E2_FILIAL  LIKE %Exp:cFilCon%
			WHERE E2.%NOTDEL%
			AND NOT (E2_VENCREA = E2_BAIXA AND E2_VENCREA = E2_EMISSAO)
		EndSql
	
		While !(cAlCon)->(Eof())
			oReport:IncMeter() // incrementa regua
			
			If SToD((cAlCon)->E2_BAIXA) < SToD((cAlCon)->E2_VENCREA)
				nAnt    := 1				
			Else
				nAnt    := 0
			EndIf
				
			If SToD((cAlCon)->E2_BAIXA) = SToD((cAlCon)->E2_VENCREA)
				nPrazo   := 1
			Else
				nPrazo   := 0
			EndIf	
			
			If SToD((cAlCon)->E2_BAIXA) > SToD((cAlCon)->E2_VENCREA) .AND. (cAlCon)->E2_TIPO <> "PA "
				nAtraso   := 1
			Else
				nAtraso   := 0
			EndIf	

			If AllTrim((cAlCon)->E2_TIPO) == "PA"
				nQtdPA++
				nTPAAnt += nAnt
				nTPAPrz += nPrazo
				nTPAAtr += nAtraso
			Else
				nTAnt   += nAnt
				nTPrazo += nPrazo
				nTAtra  += nAtraso 	
			EndIf			
			
			nValLiq += (cAlCon)->E5_VALOR
	
			(cAlCon)->(DbSkip())
		EndDo
			
		(cAlCon)->(DbCloseArea())
		
		nTotPag := nTAnt + nTPrazo + nTAtra
		
		aAdd(aRet,{aFilial[nK][1],nTAnt,nTPrazo,nTAtra,nTotPag,nValLiq,nQtdPA})
		
		nValLiq := 0
		nTAnt   := 0
		nTPrazo := 0
		nTAtra  := 0
		nTotPag := 0 
		nQtdPA  := 0		
			
	Next nK				

Return aRet

/*/{Protheus.doc} ImpGrf
(long_description)
@author j2a.luizjunior
@since 12/05/2017
@version 1.0
@example
(examples)
@see (links_or_references)
/*/

Static Function ImpGrf(oReport,pAntec,pPrazo,pAtraz,pTotPag,pDescRat)

	Local nAntec   := pAntec
	Local nPrazo   := pPrazo
	Local nAtraz   := pAtraz
	Local nTotPag  := pTotPag
	
	Local nPerAnt  := 0
	Local nPerPraz := 0
	Local nPerAtra := 0
	
	Local cDescRat := pDescRat
	Local oImpGra  := SFXX03X():New()
	Local cPngDir  := GetTempPath()+Replace(DToS(dDatabase)+ time(),':','')+".png"  
	
	Sleep(1000)

	cTipo := "B" 
	
	nPerAnt  := (nAntec / nTotPag) * 100
	
	nPerPraz := (nPrazo / nTotPag) * 100
	 
	nPerAtra := (nAtraz / nTotPag) * 100

	oImpGra:CHARTPRTH(@oReport,__oDlg__,cPngDir,cTipo,nPerAnt,nAntec,nPerPraz,nPrazo,nPerAtra,nAtraz,cDescRat)	
    FreeObj(oImpGra)
    	
Return

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³funcao que monta o cabeçalho para o relatório		 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

/*/{Protheus.doc} fGerCab
Funcao a ser chamada para gerar um cabeçalho customizado no TReport
@author j2a.caiolima
@since 01/09/2017
@version 1.0
@param oReport, objeto, TReport
@return array, cabeçalho customizado
/*/
Static Function fGerCab(oReport)
Local _aRet
Local _cPeriodo := "Periodo apuração: " + DToC(MV_PAR09) + " até " + DToC(MV_PAR10)

_aRet := {	"__LOGOEMP__" + space(10) + "Folha:" + AllTrim(cValToChar(oReport:Page())),;
			"SF06R11X" + space(10)+ oReport:Title() + space(10) + "Dt.Ref:" + DToC(dDataBase),;
			"Hora:" + Time() + space(10)+ _cPeriodo + space(10) + "Emissão:" + DToC(Date()),;
			""}

Return _aRet

/*/{Protheus.doc} CriaSX1
(long_description)
@author j2a.luizjunior
@since 24/03/2017
@version 1.0
@example
(examples)
@see (links_or_references)
/*/

Static Function CriaSX1(cPerg)

	Local aHelp := {}

	AAdd(aHelp, {{"Filial De"        } ,{""},{""}})//01
	AAdd(aHelp, {{"Filial Ate"       } ,{""},{""}})//02
	AAdd(aHelp, {{"Fornecedor De"    } ,{""},{""}})//03
	AAdd(aHelp, {{"Loja De"          } ,{""},{""}})//04
	AAdd(aHelp, {{"Fornecedor Ate"   } ,{""},{""}})//05
	AAdd(aHelp, {{"Loja Ate"         } ,{""},{""}})//06	
	AAdd(aHelp, {{"Emissao De"       } ,{""},{""}})//07
	AAdd(aHelp, {{"Emissao Ate"      } ,{""},{""}})//08
	AAdd(aHelp, {{"Vencimento Ate"   } ,{""},{""}})//09
	AAdd(aHelp, {{"Vencimento Ate"   } ,{""},{""}})//10
	AAdd(aHelp, {{"Tipo Relatorio"   } ,{""},{""}})//11
	
	u_SFPUTSX1(cPerg,"01","Tipo"             ,"","","mv_ch1"  ,"C",01,00,00,"C","",""     ,"","","mv_par01","Analitico","","","","Sintetico","","","","","","","","","","","" ,aHelp[11,1] ,aHelp[11,2] ,aHelp[11,3] ,"")
	u_SFPUTSX1(cPerg,"02","Seleciona filiais","","","mv_ch2"  ,"C",01,00,00,"C","",""     ,"","","mv_par02","Sim"      ,"","","","Não"      ,"","","","","","","","","","","")
	u_SFPUTSX1(cPerg,"03","Fornecedor De"    ,"","","mv_ch3"  ,"C",09,00,00,"G","","SA2A" ,"","","mv_par03",""         ,"","","",""         ,"","","","","","","","","","","" ,aHelp[3,1]  ,aHelp[3,2]  ,aHelp[3,3]  ,"")
	u_SFPUTSX1(cPerg,"04","Loja De"          ,"","","mv_ch4"  ,"C",04,00,00,"G","",""     ,"","","mv_par04",""         ,"","","",""         ,"","","","","","","","","","","" ,aHelp[4,1]  ,aHelp[4,2]  ,aHelp[4,3]  ,"")
	u_SFPUTSX1(cPerg,"05","Fornecedor Ate"   ,"","","mv_ch5"  ,"C",09,00,00,"G","","SA2A" ,"","","mv_par05",""         ,"","","",""         ,"","","","","","","","","","","" ,aHelp[5,1]  ,aHelp[5,2]  ,aHelp[5,3]  ,"")
	u_SFPUTSX1(cPerg,"06","Loja Ate"         ,"","","mv_ch6"  ,"C",04,00,00,"G","",""     ,"","","mv_par06",""         ,"","","",""         ,"","","","","","","","","","","" ,aHelp[6,1]  ,aHelp[6,2]  ,aHelp[6,3]  ,"")
	u_SFPUTSX1(cPerg,"07","Emissao De"       ,"","","mv_ch7"  ,"D",08,00,00,"G","",""     ,"","","mv_par07",""         ,"","","",""         ,"","","","","","","","","","","" ,aHelp[7,1]  ,aHelp[7,2]  ,aHelp[7,3]  ,"")
	u_SFPUTSX1(cPerg,"08","Emissao Ate"      ,"","","mv_ch8"  ,"D",08,00,00,"G","",""     ,"","","mv_par08",""         ,"","","",""         ,"","","","","","","","","","","" ,aHelp[8,1]  ,aHelp[8,2]  ,aHelp[8,3]  ,"")
	u_SFPUTSX1(cPerg,"09","Vencimento De"    ,"","","mv_ch9"  ,"D",08,00,00,"G","",""     ,"","","mv_par09",""         ,"","","",""         ,"","","","","","","","","","","" ,aHelp[9,1]  ,aHelp[9,2]  ,aHelp[9,3]  ,"")
	u_SFPUTSX1(cPerg,"10","Vencimento Ate"   ,"","","mv_ch10" ,"D",08,00,00,"G","",""     ,"","","mv_par10",""         ,"","","",""         ,"","","","","","","","","","","" ,aHelp[10,1] ,aHelp[10,2] ,aHelp[10,3] ,"")
	
Return
