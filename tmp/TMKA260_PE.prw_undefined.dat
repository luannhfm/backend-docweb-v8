#Include "protheus.ch"
#Include "FWMVCDef.ch"

User Function TMKA260()	
Local aParam     := PARAMIXB
Local xRet       := .T.
Local oObj       := ''
Local cIdPonto   := ''
Local cIdModel   := ''
Local lIsGrid    := .F.

	If aParam <> NIL
		oObj       := aParam[1]
		cIdPonto   := aParam[2]
		cIdModel   := aParam[3]
		lIsGrid    := ( Len( aParam ) > 3 )

		If cIdPonto == "MODELVLDACTIVE" //Na ativação do modelo.

		ElseIf cIdPonto == 'MODELPRE'	//Antes da alteração de qualquer campo do modelo. 
			
		ElseIf cIdPonto == 'MODELPOS'	//Na validação total do modelo.

		ElseIf cIdPonto == 'FORMPRE'	//Antes da alteração de qualquer campo do formulário. 

		ElseIf cIdPonto == 'FORMPOS'	//Na validação total do formulário. 

		ElseIf cIdPonto == 'FORMLINEPRE'	//Antes da alteração da linha do formulário FWFORMGRID. 

		ElseIf cIdPonto == 'FORMLINEPOS'	//Na validação total da linha do formulário FWFORMGRID. 
			
		ElseIf cIdPonto == 'MODELCOMMITTTS'	//Após a gravação total do modelo e dentro da transação.

		ElseIf cIdPonto == 'MODELCOMMITNTTS'	//Após a gravação total do modelo e fora da transação.
			Processa({|| fRegOper(oObj) }, "Aguarde...", "Registrando operação para controle de integrações...",.F.)
		ElseIf cIdPonto == 'FORMCOMMITTTSPRE'	//Antes da gravação da tabela do formulário.

		ElseIf cIdPonto == 'FORMCOMMITTTSPOS'	//Após a gravação da tabela do formulário.

		ElseIf cIdPonto == 'MODELCANCEL'
		
		ElseIf cIdPonto == 'BUTTONBAR'	//Para a inclusão de botões na ControlBar.
			xRet := { {'Log Integração', '', { || U_SF73A04F( xFilial("SUS") +;
			 	oObj:GetValue("SUSMASTER", "US_COD") + oObj:GetValue("SUSMASTER", "US_LOJA") ) }, '' } }
		EndIf
	EndIf

Return(xRet)

Static Function fRegOper(oMdlSUS)
Local aPrimKey	:= {}
Local cChavePri := xFilial("SUS")
Local cJSONData	:= ""
Local lRet		:= .T.
Local nX		:= 0
Local nOper		:= oMdlSUS:GetOperation()
	
	If (nOper == MODEL_OPERATION_INSERT) .Or. (nOper == MODEL_OPERATION_UPDATE)
		aPrimKey := oMdlSUS:GetPrimaryKey()
		cJSONData :=  oMdlSUS:GetJSONData()
		For nX := 1 To Len(aPrimKey)
			cChavePri += oMdlSUS:GetValue("SUSMASTER", aPrimKey[nX])
		Next nX
		U_F99A01S1(FunName(), cChavePri, cJSONData)
	EndIf
	
Return lRet