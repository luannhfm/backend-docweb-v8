#Include "Protheus.ch"
#Include "Report.ch"

////////////////////////////////////////////////////////////////////////////
//±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±//
//±± Função: SICOMR25       Autor: Caio Santos         Data: 19.01.2012 ±±//
//±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±//
//±± Descrição: Relatorio de Lotes      								±±//
//±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±//
////////////////////////////////////////////////////////////////////////////
User Function SICOMR25()

Local oReport
                                                                                             
u_SFPUTSX1("XSICOMR25","01","Filial de:" ,"","","mv_ch01","C",8,0,,"G",,,,,"mv_par01",,,,,)
u_SFPUTSX1("XSICOMR25","02","Filial at?","","","mv_ch02","C",8,0,,"G",,,,,"mv_par02",,,,,)
u_SFPUTSX1("XSICOMR25","03","Edital de:" ,"","","mv_ch03","C",15,0,,"G",,"CO1",,,"mv_par03",,,,,)
u_SFPUTSX1("XSICOMR25","04","Edital at?","","","mv_ch04","C",15,0,,"G",,"CO1",,,"mv_par04",,,,,)

If Pergunte("XSICOMR25",.T.)
	oReport := ReportDef()
	oReport:PrintDialog()
EndIf

Return

////////////////////////////////////////////////////////////////////////////
//±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±//
//±± Função: ReportDef      Autor: Caio Santos         Data: 19.01.2012 ±±//
//±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±//
//±± Descrição: Relatorio de Lotes (Layout)								±±//
//±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±//
////////////////////////////////////////////////////////////////////////////
Static Function ReportDef()

Local oReport

Local oSecCO1
Local oSecCO2
Local oSecSB1
Local oSecCO3
Local oSecSB2

Local oBreakCO1
Local oBreakCO2

oReport := TReport():New("SICOMR25","Relatório de Lotes","XSICOMR25",{|oReport| ReportPrint(oReport)})

	oSecCO1 := TRSection():New(oReport,"EDT",{})
		TRCell():New(oSecCO1,"CO1_FILIAL","EDITAL","Filial")
		TRCell():New(oSecCO1,"CO1_CODEDT","EDITAL","Edital")
		TRCell():New(oSecCO1,"CO1_NUMPRO","EDITAL","Nr Processo")
		
		oSecCO2 := TRSection():New(oSecCO1,"LTE",{})
			TRCell():New(oSecCO2,"CO2_XNUMLO","EDITAL","Lote")
			TRCell():New(oSecCO2,"CO2_XVLMIN","EDITAL","Valor Minimo")
			
			oSecSB1 := TRSection():New(oSecCO2,"PRD",{})
				TRCell():New(oSecSB1,"B1_COD","EDITAL","Produto")
				TRCell():New(oSecSB1,"B1_DESC","EDITAL","Descricao")
				
			oSecCO3 := TRSection():New(oSecCO2,"PRT",{},)
				TRCell():New(oSecCO3,"CO3_CODIGO","FRLOTE","Participante")
				TRCell():New(oSecCO3,"CO3_NOME","FRLOTE","Nome")
				TRCell():New(oSecCO3,"CO3_VLTOT","FRLOTE","Valor do Lote")
				
		oSecSB2 := TRSection():New(oSecCO1,"PSL",{})
			TRCell():New(oSecSB2,"CO2_CODPRO","PSLOTE","Produtos sem Lote")
			TRCell():New(oSecSB2,"B1_DESC","PSLOTE","Descricao")
			
	oBreakFil := TrBreak():New(oSecCO1,oSecCO1:Cell("CO1_FILIAL"),"Edital",.F.,"FILBRK",.T.)
	oBreakEdt := TrBreak():New(oSecCO1,oSecCO1:Cell("CO1_CODEDT"),"Edital",.F.,"EDTBRK",.T.)
	oBreakPro := TrBreak():New(oSecCO1,oSecCO1:Cell("CO1_NUMPRO"),"Edital",.F.,"PROBRK",.T.)
	
	oBreakLte := TrBreak():New(oSecCO2,oSecCO2:Cell("CO2_XNUMLO"),"Lote",.F.,"LTEBRK",.F.)
	
Return (oReport)

////////////////////////////////////////////////////////////////////////////
//±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±//
//±± Função: ReportPrint    Autor: Caio Santos         Data: 19.01.2012 ±±//
//±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±//
//±± Descrição: Relatorio de Lotes (Impressao)          				±±//
//±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±//
////////////////////////////////////////////////////////////////////////////
Static Function ReportPrint(oReport)

Local oSecCO1 := oReport:Section(1)
Local oSecCO2 := oReport:Section(1):Section(1)
Local oSecSB1 := oReport:Section(1):Section(1):Section(1)
Local oSecCO3 := oReport:Section(1):Section(1):Section(2)
Local oSecSB2 := oReport:Section(1):Section(2)

Local cWhereEdt := ""
Local cWhereFrn := ""
Local cWherePsl := ""

Local cCO1Fil := ""
Local cCO1Cod := ""
Local cCO1Num := ""

Local cCO2Fil := ""
Local cCO2Cod := ""
Local cCO2Num := ""
Local cCO2Lte := ""

Local cJoinCO3 := ""
Local cCampoCO3 := ""

Local cFilialDe := mv_par01
Local cFilialAte := mv_par02
Local cEditalDe := mv_par03
Local cEditalAte := mv_par04

If !Empty(cFilialDe)
	cWhereEdt += " AND CO1_FILIAL >= '" + cFilialDe + "' "
EndIf
If !Empty(cFilialAte)
	cWhereEdt += " AND CO1_FILIAL <= '" + cFilialAte + "' "
EndIf
If !Empty(cEditalDe)
	cWhereEdt += " AND CO1_CODEDT >= '" + cEditalDe + "' "
EndIf
If !Empty(cEditalAte)
	cWhereEdt += " AND CO1_CODEDT <= '" + cEditalAte + "' "
EndIf

cWhereEdt := "%"+cWhereEdt+"%"

//Query Edital, Lote e Produto
BEGIN REPORT QUERY oSecCO1
	BeginSQL Alias "EDITAL"
		SELECT 	CO1_FILIAL, CO1_CODEDT, CO1_NUMPRO, CO1_MODALI,
				CO2_FILIAL, CO2_CODEDT, CO2_NUMPRO, CO2_XNUMLO, CO2_XVLMIN, CO2_CODPRO,
				    B1_COD, B1_DESC
		FROM %Table:CO1% CO1
		JOIN %Table:CO2% CO2 ON CO2_FILIAL = CO1_FILIAL AND CO1_CODEDT = CO2_CODEDT AND CO1_NUMPRO = CO2_NUMPRO
		JOIN %Table:SB1% SB1 ON B1_FILIAL = %xFilial:SB1% AND CO2_CODPRO = B1_COD
		WHERE CO1.%NotDel% AND CO2.%NotDel% AND SB1.%NotDel% %Exp:cWhereEdt%
		GROUP BY CO1_FILIAL, CO1_CODEDT, CO1_NUMPRO, CO1_MODALI,
				 CO2_FILIAL, CO2_CODEDT, CO2_NUMPRO, CO2_XNUMLO, CO2_XVLMIN, CO2_CODPRO,
		             B1_COD, B1_DESC
		ORDER BY CO1_FILIAL, CO1_CODEDT, CO1_NUMPRO, CO2_XNUMLO, CO2_CODPRO
	EndSQL	
END REPORT QUERY oSecCO1

//IMPRESSAO TREPORT
     
EDITAL->(dbGoTop())
//EDITAL
While !EDITAL->(EOF())

	If oReport:Cancel()
		Exit
	EndIf
	
	oSecCO1:Init()
	oSecCO1:Printline()
	cCO1Fil := EDITAL->CO1_FILIAL
	cCO1Cod := EDITAL->CO1_CODEDT
	cCO1Num := EDITAL->CO1_NUMPRO
		
	//LOTE
	While (!EDITAL->(EOF())) .And. (EDITAL->(CO1_FILIAL+CO1_CODEDT+CO1_NUMPRO) == cCO1Fil+cCO1Cod+cCO1Num)

		If Empty(CO2_XNUMLO)
			EDITAL->(dbSkip())
			Loop
		EndIf
					
		oSecCO2:Init()
		oSecCO2:Printline()
		cCO2Fil := EDITAL->CO2_FILIAL
		cCO2Cod := EDITAL->CO2_CODEDT
		cCO2Num := EDITAL->CO2_NUMPRO
		cCO2Lte := EDITAL->CO2_XNUMLO
				
		//PRODUTO
		oSecSB1:Init()
		While (!EDITAL->(EOF())) .And. (EDITAL->(CO2_FILIAL+CO2_CODEDT+CO2_NUMPRO+CO2_XNUMLO) == cCO2Fil+cCO2Cod+cCO2Num+cCO2Lte)
			
			oSecSB1:PrintLine()
			EDITAL->(dbSkip())
			
		EndDo
		oSecSB1:Finish()
		
		//Query Fornecedores por Lote
		cWhereFrn := " CO2_FILIAL = '" + cCO2Fil + "' "
		cWhereFrn += " AND CO2_CODEDT = '" + cCO2Cod + "' "
		cWhereFrn += " AND CO2_NUMPRO = '" + cCO2Num + "' "
		cWhereFrn += " AND CO2_XNUMLO = '" + cCO2Lte + "' "
		cWhereFrn := "%"+cWhereFrn+"%"
		
		BeginSQL Alias "DEFPRT"
			SELECT CO1_MODALI
			FROM %Table:CO1% CO1 
			WHERE CO1_FILIAL = %Exp:cCO2Fil% AND CO1_CODEDT = %Exp:cCO2Cod% AND CO1_NUMPRO = %Exp:cCO2Num%
		EndSQL
		
		If AllTrim(DEFPRT->CO1_MODALI) == "LL"
			oSecCO3:aCell[1]:SetTitle("Cliente")
			cJoinCO3 := " JOIN " + RetSQLName("SA1") + " SA1 ON CO3_CODIGO = A1_COD AND A1_FILIAL = '" + xFilial("SA1") + "' "
			cCampoCO3 := " A1_NOME"
		Else
			oSecCO3:aCell[1]:SetTitle("Fornecedor")
			cJoinCO3 := " JOIN " + RetSQLName("SA2") + " SA2 ON CO3_CODIGO = A2_COD AND A2_FILIAL = '" + xFilial("SA2") + "' "
			cCampoCO3 := " A2_NOME"
		EndIf
		
		DEFPRT->(dbCloseArea())
		
		cJoinCO3 := "%"+cJoinCO3+"%"		
		cCampoCO3 := "%"+cCampoCO3+"%"
		
		BEGIN REPORT QUERY oSecCO3
			BeginSQL Alias "FRLOTE"
				SELECT CO2_FILIAL, CO2_CODEDT, CO2_NUMPRO, CO2_XNUMLO,
			 		   CO3_FILIAL, CO3_CODEDT, CO3_NUMPRO, CO3_CODIGO, SUM(CO3_VLUNIT) AS CO3_VLTOT, CO3_TIPO,
			 		   %Exp:cCampoCO3% AS CO3_NOME
				FROM %Table:CO2% CO2
				JOIN %Table:CO3% CO3 ON CO3_FILIAL = CO2_FILIAL AND CO2_CODEDT = CO3_CODEDT AND CO2_NUMPRO = CO3_NUMPRO AND CO2_CODPRO = CO3_CODPRO
				%Exp:cJoinCO3%
				WHERE %Exp:cWhereFrn%
				GROUP BY CO2_FILIAL, CO2_CODEDT, CO2_NUMPRO, CO2_XNUMLO,
						 CO3_FILIAL, CO3_CODEDT, CO3_NUMPRO, CO3_CODIGO, CO3_TIPO, %Exp:cCampoCO3%
				ORDER BY CO2_FILIAL, CO2_CODEDT, CO2_NUMPRO, CO2_XNUMLO, CO3_CODIGO, %Exp:cCampoCO3%
			EndSQL
		END REPORT QUERY oSecCO3
		FRLOTE->(dbGoTop())
		//FORNECEDOR POR LOTE
		oSecCO3:Init()
		While !FRLOTE->(EOF())
		
			oSecCO3:PrintLine()
			FRLOTE->(dbSkip())
			
		EndDo
		oSecCO3:Finish()
		
		oSecCO2:Finish()
					
	EndDo
                                  
	//Query produtos s/ lote           
	cWherePsl := " CO2_FILIAL = '" + cCO1Fil + "' "		
	cWherePsl += " AND CO2_CODEDT = '" + cCO1Cod + "' "
	cWherePsl += " AND CO2_NUMPRO = '" + cCO1Num + "' "
	cWherePsl := "%"+cWherePsl+"%"
	BEGIN REPORT QUERY oSecSB2	
		BeginSQL Alias "PSLOTE"
			SELECT CO2_FILIAL, CO2_CODEDT, CO2_NUMPRO, CO2_CODPRO, CO2_XNUMLO, B1_DESC
			FROM %Table:CO2% CO2
			JOIN %Table:SB1% SB1 ON B1_FILIAL = %xFilial:SB1% AND CO2_CODPRO = B1_COD
			WHERE %Exp:cWherePsl% AND CO2_XNUMLO = %Exp:""%
			ORDER BY CO2_FILIAL, CO2_CODEDT, CO2_NUMPRO, CO2_CODPRO, CO2_XNUMLO
		EndSQL
	END REPORT QUERY oSecSB2
	PSLOTE->(dbGoTop())
	//PRODUTOS POR LOTE
	oSecSB2:Init()
	While !PSLOTE->(EOF())
	
		oSecSB2:PrintLine()
		PSLOTE->(dbSkip())
		
	EndDo
	oSecSB2:Finish()
				
	oSecCO1:Finish()
	
EndDo

EDITAL->(dbCloseArea())
FRLOTE->(dbCloseArea())
PSLOTE->(dbCloseArea())

Return