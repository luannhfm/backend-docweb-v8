#include "totvs.ch"
#include "restful.ch"

//-------------------------------------------------------------------
/*/{Protheus.doc} Centros de Custo
Declaracao do GET de Centros de Custo
@author Walmir Junior
@since 05/10/2020
@version 1.0
/*/
//-------------------------------------------------------------------
WSRESTFUL centrocusto DESCRIPTION 'endpoint Centros de Custo - VENUS ' FORMAT "application/json,text/html"
    WSDATA Page              AS INTEGER OPTIONAL
    WSDATA aQueryString      AS ARRAY    OPTIONAL
    
 	WSMETHOD GET CentroCusto;
	    DESCRIPTION "Retorna uma lista de Centros de Custo";
	    WSSYNTAX "/api/centrocusto" ;
        PATH "/api/centrocusto" ;
	    PRODUCES APPLICATION_JSON
 	
END WSRESTFUL

//-------------------------------------------------------------------
/*/{Protheus.doc} GET LancContabil
MÃ©todo GET CentroCusto
@author Walmir Junior
@since 05/10/2020
@version 1.0
/*/
//-------------------------------------------------------------------
WSMETHOD GET CentroCusto QUERYPARAM Page WSREST centrocusto
Return getCentroCusto(self)

//-------------------------------------------------------------------
/*/{Protheus.doc} GET getCentroCusto
Funcao para tratamento da requisicao GET
@author Walmir Junior
@since 05/10/2020
@version 1.0
/*/
//-------------------------------------------------------------------
Static Function getCentroCusto( oWS )
   Local lRet  as logical
   Local oCentC as object
   DEFAULT oWS:Page      := 1  
   lRet        := .T.
   
   oCentC := CentroCustoAdapter():new( 'GET' )
   oCentC:setPage(oWS:Page)
   oCentC:setPageSize(10)
   oCentC:setUrlFilter(oWS:aQueryString)
   oCentC:GetListCentroCusto()
   //Se tudo ocorreu bem, retorna os dados via Json
   If oCentC:lOk
       oWS:SetResponse(EncodeUTF8(oCentC:getJSONResponse(), "cp1252"))
   Else
   //Ou retorna o erro encontrado durante o processamento
       SetRestFault(oCentC:GetCode(),oCentC:GetMessage())
       lRet := .F.
   EndIf
   //faz a desalocacao de objetos e arrays utilizados
   oCentC:DeActivate()
   oCentC := nil
   
Return lRet
