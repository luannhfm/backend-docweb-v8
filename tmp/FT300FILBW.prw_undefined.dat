#Include 'Protheus.ch'

/*/{Protheus.doc} FT300FILBW
Ponto de Entrada utilizado para Filtra oportunidade de venda no BROWSE de oportunidades(FATA300)

@author 	Jose Leite de Barros Neto
@since 	24/07/2015
@version 	1.0

@return 	_xFiltro, Expressão ADVPL contendo uma condição de filtro (Caractere), se retornar diferente de caracter o filtro sera desconsiderado.

@see 
http://tdn.totvs.com/display/public/mp/FT300FILBW+-+Filtra+oportunidade+de+venda+no+BROWSE+de+oportunidades
/*/
User Function FT300FILBW()
	
	Local _aAreaAtu	:= GetArea()
	Local _xFiltro	
	
	If cFilAnt $ '01MT0001/02MT0001/03MT0001/04MT0001/05MT0001'
		_xFiltro	:= 1 //Retorna um valor diferente de caracter, o filtro Sera desconsiderado
	Else
		DbSelectArea('SA3')
		SA3->(DbSetOrder(7)) //A3_FILIAL+A3_CODUSR
		SA3->(DbGoTop())
		If SA3->(DbSeek(FwxFilial("SA3") + __cUserId))
			If SA3->A3_XSUPERV == 'S' .OR. SA3->A3_XVENDTA == 'S'
				_xFiltro := 'AD1->AD1_FILIAL == cFilAnt'
				//Se for pela area de trabalho
				If FUNNAME() == "FATA320"
					If ADL->ADL_ENTIDA == 'SA1'
						_xFiltro += ' .AND. AD1->AD1_CODCLI == ADL->ADL_CODENT .AND. AD1->AD1_LOJCLI == ADL->ADL_LOJENT'
					Else
					 	_xFiltro += ' .AND. AD1->AD1_PROSPE == ADL->ADL_CODENT .AND. AD1->AD1_LOJPRO == ADL->ADL_LOJENT'
					EndIf
				EndIf
			Else
				_xFiltro := 'AD1->AD1_FILIAL == cFilAnt .AND. AD1->AD1_VEND == SA3->A3_COD'
				//Se for pela area de trabalho
				If FUNNAME() == "FATA320"
					If ADL->ADL_ENTIDA == 'SA1'
						_xFiltro += ' .AND. AD1->AD1_CODCLI == ADL->ADL_CODENT .AND. AD1->AD1_LOJCLI == ADL->ADL_LOJENT'
					Else
					 	_xFiltro += ' .AND. AD1->AD1_PROSPE == ADL->ADL_CODENT .AND. AD1->AD1_LOJPRO == ADL->ADL_LOJENT' 
					EndIf
				EndIf
			EndIf
		Else
			_xFiltro := 'AD1->AD1_FILIAL == cFilAnt'
		EndIf
		
	EndIf 
	
	RestArea(_aAreaAtu)

Return( _xFiltro )