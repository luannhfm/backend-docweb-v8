#Include "Protheus.ch"

/*/{Protheus.doc} MA110BUT
@description Manipula botões nas solicitações de compras
@Obs Criacao de interface para visualizar o conteudo completo do campo Justificacao de Compra.
@type User Function
@author Carlos A. Queiroz
@since 28/02/2013
@version 11.0
@param PARAMIXB[1], Numérico,			Parâmetro numérico contendo a opção de operação da rotina selecionada nOpc: 2 - Visualização; 3 - Inclusão; 4 - Alteração; 5 - Exclusão
@param PARAMIXB[2], Array of Record,	Vetor contendo os botões carregados pela rotina.	
@return aRet, array, Retorno contendo os botões que deverão ser utilizados pelo programa de Solicitação de Compras.
@see http://tdn.totvs.com/pages/releaseview.action?pageId=6085722
@history 18/02/2019, Alan Teles de Oliveira, estrutura do fonte e Protheus doc.
/*/
User Function MA110BUT()

	Local aRet := aClone(PARAMIXB[2]) 

	aadd(aRet, {"BUDGET", {|| u_fM10JUST()}, "Just.Compras", "Consulta Just.Compras"}) //"Consulta Aprovacao"

Return aRet


/*/{Protheus.doc} fM10JUST
@description Interface contendo o campo justificativa de compra.
@type User Function
@author Carlos A. Queiroz
@since 27/02/2013
@version 11.0
@history 18/02/2019, Alan Teles de Oliveira, alterado variavel cJustCom para _cJustCom
/*/
User Function fM10JUST()

	Local cObserv  	:= ""
	Local cTexto   	:= ""
	Local cFileLog 	:= ""
	Local oMemo		:= Nil
	Local oFont		:= Nil

	cTexto += Replicate( "-", 65 ) +chr(13)+chr(10)
	cTexto += Replicate( "*", 65 ) +chr(13)+chr(10)
	cTexto += "   Conteúdo do campo Justificativa de Compras "+chr(13)+chr(10)
	cTexto += Replicate( "*", 65 ) +chr(13)+chr(10)
	cTexto += Replicate( "-", 65 ) + CRLF + CRLF + CRLF

	for i:=1 to len(_cJustCom)

		cObserv += substr(_cJustCom,i,1)

		if i % 80 == 0
			cObserv += chr(13)+chr(10)
		endif

	next i

	If !Empty(alltrim(cObserv))

		cTexto += cObserv

		cFileLog := MemoWrite( CriaTrab( , .F. ) + ".log", cTexto )

		Define Font oFont Name "Arial" Size 8, 12 

		Define MsDialog oDlg Title "Totvs" From 3, 0 to 340, 725 Pixel

		@ 5, 5 Get oMemo Var cTexto Memo When .F. Size 352, 145 Of oDlg Pixel //FONT oDlg:oFont COLOR CLR_BLACK,CLR_HGRAY 
		oMemo:bRClicked := { || AllwaysTrue() }
		oMemo:oFont     := oFont 

		Define SButton From 153, 275 Type  1 Action oDlg:End() Enable Of oDlg Pixel // Apaga

		Activate MsDialog oDlg Center

	EndIf

Return .T.
