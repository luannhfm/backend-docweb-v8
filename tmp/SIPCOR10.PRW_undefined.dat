#INCLUDE "PROTHEUS.ch"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ SIPCOR10     ºAutor  ³Microsiga         º Data ³ 09/05/12  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Relatório Oficial: Quadro Comparativo Receita/Receita      º±±
±±º          ³ Identificação CNI: RORC035/RORC037                         º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ P11 - SISTEMA INDUSTRIA                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

User Function SIPCOR10(aConfig2)
Local aArea  	:= GetArea()
Local nCont 	:= 1000
Local _nTotal   := 0
Local _aDados   := {}
Local _nSaldo1G := 0
Local _nSaldo2G := 0
Private oBrGray := TBrush():New("",CLR_GRAY)
Private oPrint
Private _nRow	:= 8000
Private nTamForm	:= 3000 //Tamanho formulario
Private lFirst		:= .f.
Private nPag		:= 0
Private cCadastro 	:= OemToAnsi("Quadro Orçamentário por Programática")
Private oFont07		:= TFont():New("Courier",7,7,.T.,.F.,5,.T.,5,.T.,.F.)
Private oFont07b	:= TFont():New("Courier",7,7,.T.,.T.,5,.T.,5,.T.,.F.)
Private oFont10		:= TFont():New("Courier",9,10,.T.,.F.,5,.T.,5,.T.,.F.)
Private oFont10b	:= TFont():New("Courier",9,10,.T.,.T.,5,.T.,5,.T.,.F.)
Private oFont12b	:= TFont():New("Courier",9,12,.T.,.T.,5,.T.,5,.T.,.F.)
Private oFont12 	:= TFont():New("Courier",9,12,.T.,.F.,5,.T.,5,.T.,.F.)
// Parametros:
Private _dDataIni := aConfig2[1]
Private _dDataFim := aConfig2[2]
Private _nMoeda   := Val(aConfig2[3])
Private _cCfgCubo := aConfig2[4]
Private _cModelo  := aConfig2[5]
Private _cCubo    := Posicione("AL4",1, xFilial("AL4")+ _cCfgCubo, "AL4_CONFIG")
Private _oStruCub

// Busca estrutura do cubo selecionado para chegagem
IF Empty(_cCubo)
	Aviso("Cubo","Cubo inválido ou não informado.",{"Voltar"})
	Return()
ENDIF

// Busca estrutura do cubo selecionado para chegagem
_oStruCub := PcoStructCube( _cCubo,_cCfgCubo )

// Validação do Cubo
// DIMENSÃO ESPERADA = PROGRAMÁTICA(CV0) + QUALQUER CHAVE +TIPO DE SALDO

IF _oStruCub:ACHAVE[1] <> "CV0_CODIGO"
	Aviso("Cubo","Primeiro nível do cubo deve ser alguma ENTIDADE GERENCIAL.",{"Voltar"})
	Return()
ENDIF

IF _oStruCub:ACHAVE[Len(_oStruCub:ACHAVE)] <> "AL2->AL2_TPSALD"
	Aviso("Cubo","Último nível do cubo deve ser TIPO DE SALDO.",{"Voltar"})
	Return()
ENDIF

IF Len( _aDados := SIPCO10Cub() ) == 0
	Aviso("Aviso","Não existem dados a serem exibidos.",{"Sair"})
	Return()
ENDIF

oPrint:= TMSPrinter():New(cCadastro)

oPrint:Setup()
oPrint:SetPortrait()
oPrint:StartPage()

SIPCO10Cab(aConfig2)
lFirst := .t.

_nRow := 870
_nLin := 1

For i := 1 to Len(_aDados)
	
	If _nLin > 43
		oPrint:EndPage()
		oPrint:StartPage()
		SIPCO10Cab(aConfig2)
		_nLin := 1
		_nRow := 870
	EndIf
	
	oPrint:Say(_nRow,0070,_aDados[i][1],IIF(_aDados[i][5]=="1",oFont07b,oFont07))
	oPrint:Say(_nRow,0380,_aDados[i][2],IIF(_aDados[i][5]=="1",oFont07b,oFont07))
	oPrint:Say(_nRow,1200,Transform(_aDados[i][3],"@E 999,999,999,999.99"),IIF(_aDados[i][5]=="1",oFont07b,oFont07))
	oPrint:Say(_nRow,1500,Transform(_aDados[i][4],"@E 999,999,999,999.99"),IIF(_aDados[i][5]=="1",oFont07b,oFont07))
	oPrint:Say(_nRow,1800,Transform(IIF(_aDados[i][4]-_aDados[i][3]<0,0,_aDados[i][4]-_aDados[i][3]),"@E 999,999,999,999.99"),IIF(_aDados[i][5]=="1",oFont07b,oFont07))
	oPrint:Say(_nRow,2100,Transform(IIF(_aDados[i][4]-_aDados[i][3]>0,0,Abs(_aDados[i][4]-_aDados[i][3])),"@E 999,999,999,999.99"),IIF(_aDados[i][5]=="1",oFont07b,oFont07))
	
	_nSaldo1G += IIF(_aDados[i][5]=="2",_aDados[i][3],0)
	_nSaldo2G += IIF(_aDados[i][5]=="2",_aDados[i][4],0)
	
	_nRow += 50
	_nLin++
	
Next

// Box do Rodape
aCoord:={3000,060,3070,2380}
oPrint:FillRect(aCoord,oBrGray)
oPrint:Say(3020,700,"TOTAL",oFont07b,,CLR_WHITE)

// Imprime total geral
oPrint:Say(3020,1200,Transform(_nSaldo1G,"@E 999,999,999,999.99"),oFont07b,,CLR_WHITE)
oPrint:Say(3020,1500,Transform(_nSaldo2G,"@E 999,999,999,999.99"),oFont07b,,CLR_WHITE)
oPrint:Say(3020,1800,Transform(IIF(_nSaldo2G-_nSaldo1G<0,0,_nSaldo2G-_nSaldo1G),"@E 999,999,999,999.99"),oFont07b,,CLR_WHITE)
oPrint:Say(3020,2100,Transform(IIF(_nSaldo2G-_nSaldo1G>0,0,Abs(_nSaldo2G-_nSaldo1G)),"@E 999,999,999,999.99"),oFont07b,,CLR_WHITE)

oPrint:Preview()
oPrint:End()

Return()

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ SIPCO10Cab ³ Autor ³ Microsiga           ³ Data ³ Abr/2012 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Imprime cabecalho e trata quebra de pagina.                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ P11 - SISTEMA INDUSTRIA                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function SIPCO10Cab(aConfig2)
Local _cBitMap   := "LOGO_OFICIAL.BMP"
Local _cDR       := SIPCO10UF()
Private oFont13b := TFont():New("Courier",9,13,.T.,.T.,5,.T.,5,.T.,.F.)
Private oFont15b := TFont():New("Courier",9,16,.T.,.T.,5,.T.,5,.T.,.F.)

oPrint:SayBitmap(100,1100,_cBitMap,0200,0200) // Tem que estar no diretorio system

IF _cModelo$("1/2") // SESI
	oPrint:Say(360,060,PadC( "SESI - SERVIÇO SOCIAL DA INDÚSTRIA", 104 ),oFont10b)
ELSE // SENAI
	oPrint:Say(360,060,PadC( "SENAI - SERVIÇO NACIONAL DE APRENDIZAGEM INDUSTRIAL", 104 ),oFont10b)
ENDIF
oPrint:Say(430,060,PadC( _cDR, 104 ),oFont10b)

IF _cModelo$("1/3") // Receita
	oPrint:Say(500,060,PadC( "QUADRO COMPARATIVO DA RECEITA ORÇADA COM A ARRECADADA", 104 ),oFont10b)
	oPrint:Say(570,060,PadC( "EXERCÍCIO DE "+Left(Dtos(_dDataIni),4), 104 ),oFont10b)
ELSE
	oPrint:Say(500,060,PadC( "QUADRO COMPARATIVO DA DESPESA AUTORIZADA COM A", 104 ),oFont10b)
	oPrint:Say(570,060,PadC( "REALIZADA POR PROGRAMA DE TRABALHO", 104 ),oFont10b)
	oPrint:Say(640,060,PadC( "EXERCÍCIO DE "+Left(Dtos(_dDataIni),4), 104 ),oFont10b)
ENDIF

// Box do Cabeçalho
oPrint:Box(700,060,850,360 )
oPrint:Box(700,360,850,1180 )
oPrint:Box(700,1180,850,1480 )
oPrint:Box(700,1480,850,1780 )
oPrint:Box(700,1780,775,2380 )
oPrint:Box(775,1780,850,2080 )
oPrint:Box(775,2080,850,2380 )

oPrint:Box(850,0060,3000,0360 )
oPrint:Box(850,0360,3000,1180 )
oPrint:Box(850,1180,3000,1480 )
oPrint:Box(850,1480,3000,1780 )
oPrint:Box(850,1780,3000,2080 )
oPrint:Box(850,2080,3000,2380 )

oPrint:Say(755,0120,"CÓDIGOS",oFont10b)
oPrint:Say(755,0640,"TÍTULOS",oFont10b)
IF _cModelo$("1/3") // Receita
	oPrint:Say(755,1270,"ORÇADA",oFont10b)
	oPrint:Say(755,1520,"ARRECADADA",oFont10b)
ELSE
	oPrint:Say(755,1220,"AUTORIZADA",oFont10b)
	oPrint:Say(755,1530,"REALIZADA",oFont10b)
ENDIF
oPrint:Say(715,1980,"VARIAÇÕES",oFont10b)
oPrint:Say(790,1830,"PARA MAIS",oFont10b)
oPrint:Say(790,2120,"PARA MENOS",oFont10b)

Return()

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³SIPCO10Cub  ºAutor  ³Microsiga         º Data ³  09/12/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Processamento do Cubo                                      º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ P11 - SISTEMA INDUSTRIA                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function SIPCO10Cub()
Local _aRet     := {}
Local aFiltro   := {}
Local aCfgAux   := {}
Local aItCfgBlq := {}
Private aCfgCub := {}
Private COD_CUBO  := _cCubo

aCuboCfg := {}

aAdd(aCuboCfg, { 1  ,"Config.Cubo Serie 1",Space(LEN(AL3->AL3_CODIGO))		  ,"@!" 	 ,''  ,"AL3" ,"" ,25 ,.F. })
aAdd(aCuboCfg, { 1  ,"Config.Cubo Serie 2",Space(LEN(AL3->AL3_CODIGO))		  ,"@!" 	 ,''  ,"AL3" ,"" ,25 ,.F. })

If ParamBox(aCuboCfg, "Configuracao de Cubos", aCfgCub,/*bOk*/,/*aButtons*/,/*lCentered*/,/*nPosx*/,/*nPosy*/, /*oDlgWizard*/, /*cArqParam*/,,.T.) //"Configuracao de Cubos"
	
	// Processa saldo 1 - Orçada
	aProcessa := PcoRunCube(_cCubo, 1, "U__PCO10Sld", aCfgCub[1] ,1,.F.,.T.,aFiltro,aFiltro,.T.,aCfgAux,/*lProcessa*/,/*lVerAcesso*/,/*lForceNoSint*/,aItCfgBlq,/*aFiltCfg*/,/*cArqAKT*/,/*lLimpArqAKT*/,/*lVisao*/,.T./*lBloqueio*/)
	
	For i := 1 to Len(aProcessa)
		
		IF aProcessa[i][4] == "CV0" // Operacao
			
			_aEstrut := SIPCO10Niv(aProcessa[i][1],aProcessa[i][2][1])
			
			For y := 1 to Len(_aEstrut)
				IF (_nPos := aScan(_aRet,{|x| Alltrim(x[1]) == Alltrim(_aEstrut[y,1])})) == 0
					Aadd(_aRet,{_aEstrut[y,1],_aEstrut[y,2],_aEstrut[y,4],0,_aEstrut[y,3]})
				ELSE
					_aRet[_nPos,3] += _aEstrut[y,4]
				ENDIF
			Next
			
		ENDIF
		
	Next
	
	// Processa saldo 2 - Arrecadada
	aProcessa := PcoRunCube(_cCubo, 1, "U__PCO10Sld", aCfgCub[2] ,1,.F.,.T.,aFiltro,aFiltro,.T.,aCfgAux,/*lProcessa*/,/*lVerAcesso*/,/*lForceNoSint*/,aItCfgBlq,/*aFiltCfg*/,/*cArqAKT*/,/*lLimpArqAKT*/,/*lVisao*/,.T./*lBloqueio*/)
	
	For i := 1 to Len(aProcessa)
		
		IF aProcessa[i][4] == "CV0" // Operacao
			
			_aEstrut := SIPCO10Niv(aProcessa[i][1],aProcessa[i][2][1])
			
			For y := 1 to Len(_aEstrut)
				IF (_nPos := aScan(_aRet,{|x| Alltrim(x[1]) == Alltrim(_aEstrut[y,1])})) == 0
					Aadd(_aRet,{_aEstrut[y,1],_aEstrut[y,2],0,_aEstrut[y,4],_aEstrut[y,3]})
				ELSE
					_aRet[_nPos,4] += _aEstrut[y,4]
				ENDIF
			Next
			
		ENDIF
		
	Next
	
ENDIF

Return(_aRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³_PCO10Sld  ºAutor  ³Microsiga          º Data ³  09/12/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Retorna saldo para o cubo                                  º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ P11 - SISTEMA INDUSTRIA                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

User Function _PCO10Sld(cConfig,cChave)
Local aRetSld := {}
Local nSldIni := 0
Local nSldFim := 0
Local _nRet   := 0

aPeriodos := PcoRetPer( _dDataIni/*dIniPer*/, _dDataFim/*dFimPer*/, "1" /*cTipoPer*/, .F. /*lAcumul*/)

For ny := 1 to Len(aPeriodos)
	
	dIni := CtoD(Subs(aPeriodos[ny],1,10))
	dFim := CtoD(Subs(aPeriodos[ny],14))
	
	aRetIni := PcoRetSld(cConfig,cChave,dIni-1)
	nCrdIni := aRetIni[1, _nMoeda]
	nDebIni := aRetIni[2, _nMoeda]
	
	aRetFim := PcoRetSld(cConfig,cChave,dFim)
	nCrdFim := aRetFim[1, _nMoeda]
	nDebFim := aRetFim[2, _nMoeda]
	
	nSldIni := nCrdIni-nDebIni
	nMovCrd := nCrdFim-nCrdIni
	nMovDeb := nDebFim-nDebIni
	nMovPer := nMovCrd-nMovDeb
	
	_nRet += nMovPer
	
Next

Return {_nRet}

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³SIPCO10Niv  ºAutor  ³Microsiga         º Data ³  26/09/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Retorna niveis sinteticos da entidade                      º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ P11 - SISTEMA INDUSTRIA                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function SIPCO10Niv(_cEntidade,_nValor)
Local _aRetNiv := {}
Local _cPlano  := StrTran(_oStruCub:ACHAVER[1],"AKD->AKD_ENT","") // Numero da Entidade
Local _lCont   := .f.

CV0->(dbSetOrder(1))
CV0->(dbSeek(XFilial("CV0")+_cPlano+_cEntidade))

// Entidade analitica
Aadd(_aRetNiv,{CV0->CV0_CODIGO,CV0->CV0_DESC,CV0->CV0_CLASSE,_nValor})

IF !Empty(CV0->CV0_ENTSUP)
	_lCont := .t.
ENDIF

While _lCont
	
	// Procura proximo nivel
	CV0->(dbSeek(XFilial("CV0")+_cPlano+CV0->CV0_ENTSUP))
	
	// Adiciona novo nivel
	Aadd(_aRetNiv,{CV0->CV0_CODIGO,CV0->CV0_DESC,CV0->CV0_CLASSE,_nValor})
	
	IF Empty(CV0->CV0_ENTSUP)
		_lCont := .f.
	ENDIF
	
	Skip()
End

Return( aSort(_aRetNiv,,,{| x,y | x[1] < y[1]}) )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³SIPCO10UF  ºAutor  ³Microsiga          º Data ³  28/09/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Retorna nome do regional                                   º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ P11 - SISTEMA INDUSTRIA                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function SIPCO10UF()
Local _cRet := ""

Do Case
	Case SM0->M0_ESTCOB == "RS" // 01
		_cRet := "DEPARTAMENTO REGIONAL DO ESTADO DO RIO GRANDE DO SUL"
	Case SM0->M0_ESTCOB == "PR" // 02
		_cRet := "DEPARTAMENTO REGIONAL DO ESTADO DO PARANÁ"
	Case SM0->M0_ESTCOB == "SC" // 03
		_cRet := "DEPARTAMENTO REGIONAL DO ESTADO DE SANTA CATARINA"
	Case SM0->M0_ESTCOB == "MS" // 04
		_cRet := "DEPARTAMENTO REGIONAL DO ESTADO DO MATO GROSSO DO SUL"
	Case SM0->M0_ESTCOB == "MT" // 05
		_cRet := "DEPARTAMENTO REGIONAL DO ESTADO DO MATO GROSSO"
	Case SM0->M0_ESTCOB == "RO" // 06
		_cRet := "DEPARTAMENTO REGIONAL DO ESTADO DE RONDÔNIA"
	Case SM0->M0_ESTCOB == "AC" // 07
		_cRet := "DEPARTAMENTO REGIONAL DO ESTADO DO ACRE"
	Case SM0->M0_ESTCOB == "AM" // 08
		_cRet := "DEPARTAMENTO REGIONAL DO ESTADO DO AMAZONAS"
	Case SM0->M0_ESTCOB == "RR" // 09
		_cRet := "DEPARTAMENTO REGIONAL DO ESTADO DE RORAINA"
	Case SM0->M0_ESTCOB == "AP" // 10
		_cRet := "DEPARTAMENTO REGIONAL DO ESTADO DO AMAPÁ"
	Case SM0->M0_ESTCOB == "TO" // 11
		_cRet := "DEPARTAMENTO REGIONAL DO ESTADO DO TOCANTINS"
	Case SM0->M0_ESTCOB == "PA" // 12
		_cRet := "DEPARTAMENTO REGIONAL DO ESTADO DO PARÁ"
	Case SM0->M0_ESTCOB == "PI" // 13
		_cRet := "DEPARTAMENTO REGIONAL DO ESTADO DO PIAUÍ"
	Case SM0->M0_ESTCOB == "CE" // 14
		_cRet := "DEPARTAMENTO REGIONAL DO ESTADO DO CEARÁ"
	Case SM0->M0_ESTCOB == "MA" // 15
		_cRet := "DEPARTAMENTO REGIONAL DO ESTADO DO MARANHÃO"
	Case SM0->M0_ESTCOB == "BA" // 16
		_cRet := "DEPARTAMENTO REGIONAL DO ESTADO DA BAHIA"
	Case SM0->M0_ESTCOB == "PE" // 17
		_cRet := "DEPARTAMENTO REGIONAL DO ESTADO DO PERNAMBUCO"
	Case SM0->M0_ESTCOB == "RN" // 18
		_cRet := "DEPARTAMENTO REGIONAL DO ESTADO DO RIO GRANDE DE NORTE"
	Case SM0->M0_ESTCOB == "PB" // 19
		_cRet := "DEPARTAMENTO REGIONAL DO ESTADO DA PARAÍBA"
	Case SM0->M0_ESTCOB == "SE" // 20
		_cRet := "DEPARTAMENTO REGIONAL DO ESTADO DO SERGIPE"
	Case SM0->M0_ESTCOB == "AL" // 21
		_cRet := "DEPARTAMENTO REGIONAL DO ESTADO DE ALOGOAS"
	Case SM0->M0_ESTCOB == "MG" // 22
		_cRet := "DEPARTAMENTO REGIONAL DO ESTADO DE MINAS GERAIS"
	Case SM0->M0_ESTCOB == "ES" // 23
		_cRet := "DEPARTAMENTO REGIONAL DO ESTADO DO ESPÍRITO SANTO"
	Case SM0->M0_ESTCOB == "RJ" // 24
		_cRet := "DEPARTAMENTO REGIONAL DO ESTADO DO RIO DE JANEIRO"
	Case SM0->M0_ESTCOB == "GO" // 25
		_cRet := "DEPARTAMENTO REGIONAL DO ESTADO DE GOIÁS"
	Case SM0->M0_ESTCOB == "DF" // 26
		_cRet := "DEPARTAMENTO REGIONAL DO ESTADO DO DISTRITO FEDERAL"
	Case SM0->M0_ESTCOB == "SP" // 27
		_cRet := "DEPARTAMENTO REGIONAL DO ESTADO DE SÃO PAULO"
	OtherWise
		_cRet := "ENTIDADE NACIONAL DO SISTEMA INDÚSTRIA"
EndCase

Return(_cRet)
