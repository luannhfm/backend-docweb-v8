#Include "Protheus.ch"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³VALIDA    ºAutor  ³DOIT           º Data ³  12/03/2014      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³  Função para validar obrigatoriedade do Centro de Custo    º±±
±±º          ³  por filial                                                º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

User Function ValidaObrig()

Local _cFiliais := GetnewPar("MV_FILOBG","01MT0001")
Local _lRet     := .T.
Local _lPEl     := IsInCallStack("U_MT140LOK")
Local _lPEt     := IsInCallStack("U_MT140TOK")
Local _cUO      := ""
Local _n        := 0    
Local _nPosCC   := AScan(aHeader, { |x| Alltrim(x[2]) = 'D1_CC'})
Local _cErro    := "/ "

If CFILANT $ _cFiliais //Valida obrigatoriedade na Filial
	If _lPEt                      
		For _n:= 1 To Len(aCols)
			If aCols[_n][Len(aHeader)+1] == .F. //Trata linha deletada
				_cUO := IIf(_lPEt, aCols[_n][_nPosCC], M->D1_CC )
			   IF Alltrim(_cUO) == ""
			   	_cErro += StrZero(_n,TamSx3("D1_ITEM")[01]) + " / "
			      _lRet := .F.
			   Endif
			Endif                   
		Next _n            
		If _lRet == .F. 
			MsgAlert("Obrigatório informar o Campo " +AllTrim(RetTitle("D1_CC")) + CHR(13) + CHR(10) +;
						"Linha(s) : " + _cErro)              
		Endif
	Else
		If aCols[oGetDados:oBrowse:nAt][Len(aHeader)+1] == .F. //Trata linha deletada
			_cUO := IIf(_lPEl, aCols[oGetDados:oBrowse:nAt][_nPosCC], M->D1_CC )
		   IF Alltrim(_cUO) == ""
		   	MsgAlert("Obrigatório informar o Campo " +AllTrim(RetTitle("D1_CC")))
		      _lRet := .F.
		   Endif
		Endif                   
	Endif	
Endif
	
Return(_lRet)