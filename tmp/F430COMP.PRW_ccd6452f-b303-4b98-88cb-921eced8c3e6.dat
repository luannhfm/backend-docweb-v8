#Include "Protheus.ch"
#Include "TopConn.ch"

#DEFINE IDCNAB 			1
#DEFINE AUTENTICACAO 	2
#DEFINE BANCO			3
#DEFINE AGENCIA			4
#DEFINE CONTA			5

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³F430COMP  ºAutor  ³Felipe Alves        º Data ³  30/01/13   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³                                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

User Function F430COMP()

Local aArea  := SaveArea1({"SA2","SE2","SE5"})
Local lRet   := .T.
Local nI     := 0
Local _cBanco	:= ""
Local _cAgencia	:= ""
Local _cConta	:= ""

dbSelectArea("SA2")
SA2->(dbSetOrder(1))
  
dbSelectArea("SE2")
SE2->(dbSetOrder(13))
 
dbSelectArea("SE5")
SE5->(DbSetOrder(7))
  
For nI := 1 To Len(_aInfoCNAB)
	
	//---------------------------------
    //-- Grava só se tiver autenticação
    //---------------------------------
    If !(Empty(_aInfoCNAB[nI][AUTENTICACAO]))
    
    	SE2->(dbSeek(_aInfoCNAB[nI,IDCNAB]))

		If SE2->(Found())
			    		
     		SE5->(dbSeek(SE2->(E2_FILIAL + E2_PREFIXO + E2_NUM + E2_PARCELA + E2_TIPO + E2_FORNECE + E2_LOJA)))
       		
       		If SE5->(Found())
       		
       			//-------------------------------------
				//-- Retorna o Banco/Agencia/Conta que 
				//-- foi realizado o DOC|TED
				//-------------------------------------
       			GetBanco(_aInfoCNAB[nI],@_cBanco,@_cAgencia,@_cConta) 

				//-------------------------------------
				//-- Grava Informaçõs CNAB no SE5
				//-------------------------------------
       			RecLock("SE5", .F.)
        		SE5->E5_XAUTENT  := _aInfoCNAB[nI][AUTENTICACAO]
        		SE5->E5_XBCOPAG	 := _cBanco 
				SE5->E5_XAGEPAG  := _cAgencia 
				SE5->E5_XCTAPAG  := _cConta
	        	SE5->(MsUnlock())

      		Endif
		
		EndIf

	EndIf

Next nI
  
RestArea1(aArea)
Return(lRet)


//---------------------------------------------------------------
/*/{Protheus.doc} GetBanco
Retorna os dados bancário do Favoreciso

@author Allan da Silva Faria
@since 06/07/2016
@version 1.0
@param _aCNAB	, Array 	, Dados do Retorno CNAB
@param _cBanco	, Caracter	, Passado por referncia o banco
@param _cAgencia, Caracter	, Passado por referncia o agencia
@param _cConta	, Caracter	, Passado por referncia o conta
@return Nil
/*/
//----------------------------------------------------------------
Static Function GetBanco(_aCNAB,_cBanco,_cAgencia,_cConta) 

//--------------------------------------
//-- Pega do arquivo retorno CNAB
//--------------------------------------	
If Len(_aCNAB)>2
	
	_cBanco	  := _aCNAB[BANCO	]
	_cAgencia := _aCNAB[AGENCIA	]
	_cConta	  := _aCNAB[CONTA	]

Else
	
	//----------------------------------------------------
	//-- Caso não dos dados no título, pega do fornecedor
	//----------------------------------------------------	
	If (.Not. Empty(SE2->E2_XBANCO) .AND. .Not. Empty(SE2->E2_XAGENC) .AND. .Not. Empty(SE2->E2_XNUMCON)) 
		
		//--------------------------------------
		//-- Pega o que está no títulos a pagar
		//--------------------------------------		
		_cBanco	  := SE2->E2_XBANCO
		_cAgencia := SE2->E2_XAGENC
		_cConta	  := SE2->E2_XNUMCON
	
	Else
		
		//---------------------------------
		//-- Pega o que está no fornecedor
		//---------------------------------
		_cBanco	  := SA2->A2_BANCO
		_cAgencia := SA2->A2_AGENCIA
		_cConta	  := SA2->A2_NUMCON
	
	EndIf
	
EndIf

Return Nil