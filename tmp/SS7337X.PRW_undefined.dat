#INCLUDE "PROTHEUS.CH"
#INCLUDE "TOPCONN.CH"
#INCLUDE "RWMAKE.CH"
#INCLUDE "FONT.CH"
#INCLUDE "COLORS.CH"
#INCLUDE "PARMTYPE.CH"
#INCLUDE "FWMBROWSE.CH"
#INCLUDE "FWMVCDEF.CH"
#INCLUDE "FWCOMMAND.CH"
/*
--------------------------------------------------------------------------------
{Protheus.doc} <SS7337X>
 37 - Relatório de Vigencia de contratos   

@author<Antonio Dantas>
@since<14/04/2015>
@version<1.00>
@return<Nil>
@example<Nil>
@see<Nil>
--------------------------------------------------------------------------------
*/
User Function SS7337X()
Local _oReport			:= Nil     
Private _lCancel			:= .f.  
Private _cPerg 			:= ""
Private _cAlias			:= GetNextAlias()   
Private _cAceAll   		:= SuperGetMv("MV_XACEALL",,"")   
Private _lFilialDR 		:= .f.
//+------------------------------------------------------------------+
//| Verifica se o operador esta logado na filail DR que tem acesso   |
//| a todas as filiais, ou somente na filial em que esta logada.     |
//+------------------------------------------------------------------+
_cPerg	:= FunName()			//-- Define o Grupo de Perguntas   
//+------------------------------------------------------------------+
//|   Interface de impressao                                         |
//+------------------------------------------------------------------+
_oReport := ReportDef()
If !_lCancel
	_oReport:PrintDialog()
Endif	    
Return


/*
--------------------------------------------------------------------------------
{Protheus.doc} <ReportDef>
 37 - Relatório de Vigencia de contratos      

@author<Antonio Dantas>
@since<14/04/2015>
@version<1.00>
@receive<Nil>
@return<Nil>
@example<Nil>
@see<Nil>
--------------------------------------------------------------------------------
*/
Static Function ReportDef()
Local _cTitulo		:= "37 - Relatório de Vigencia de contratos"
Local _cSubTit		:= ""  
Local _oReport		:= Nil
//--
Local _oDataFIM		:= Nil
Local _oContrato		:= Nil
//+------------------------------------------------------------------+
//| Carrega as perguntas relativas aos parametros de Impressao       |
//|    Mv_par01 - Vigencia DE ?                                      |
//|    Mv_par02 - Vigencia ATE ?                                     |
//|    Mv_par03 - Cliente DE ?                                       |
//|    Mv_par04 - Cliente ATE ?                                      |
//|    Mv_par05 - Produto DE ?                                       |
//|    Mv_par06 - Produto ATE ?                                      |
//|    Mv_par07 - Grupo de Prod. DE ?                                |
//|    Mv_par08 - Grupo de Prod. ATE ?                               |
//|    Mv_par09 - Data Ass Proposta DE ?                             |
//|    Mv_par10 - Data Ass Proposta ATE ?                            |
//|    Mv_par11 - Data Ass Contrato DE ?                             |
//|    Mv_par12 - Data Ass Contrato ATE ?                            |
//|    Mv_par13 - Quebra por Data de Encerr. ?                       |
//+------------------------------------------------------------------+
xParPerg(_cPerg)      
//+------------------------------------------------------------------+
//|  Monta tela de paramentos para usuario, se cancelar sair         |
//+------------------------------------------------------------------+
If !Pergunte(_cPerg,.t.)  
	_lCancel := .T. 
   Return
Endif 
//+------------------------------------------------------------------+
//| Verifica se o operador esta logado na filail DR que tem acesso   |
//| a todas as filiais, ou somente na filial em que esta logada.     |
//+------------------------------------------------------------------+
If (cFilAnt $ _cAceAll)
	_lFilialDR := .t.
Endif 
//-- Defini o sub-titulo do Relatorio 
_cSubTit := " - Vingencia [De: "+DtoC(Mv_par01)+"Ate: "+DtoC(Mv_par02)+"] "  
//+------------------------------------------------------------------+
//|   Seleciona a tabela temporaria que sera referencia              | 
//+------------------------------------------------------------------+
FWMsgRun(, {|| u_fGet337X() }, "Processando a Consulta das Agendas e Apontamentos!", "Aguarde...")
//+------------------------------------------------------------------+
//|    Construcao do Objeto de Impressao do Relatorio                |
//+------------------------------------------------------------------+
//                    New(cReport,cTitle,uParam,bAction,cDescription,lLandscape,uTotalText,lTotalInLine,cPageTText,lPageTInLine,lTPageBreak,nColSpace)
_oReport := TReport():New(_cPerg,_cTitulo,_cPerg,{|_oReport|  ReportPrint(_oReport) },_cSubTit,.t.)
_oReport:SetLandScape() 			//-- Modo Paisagem 
//&&--  _oReport:SetPortrait() 			//-- Modo Retrato 
//+------------------------------------------------------------------+
//|    *****      S e c a o  -  1      *****                         |
//+------------------------------------------------------------------+
_oDataFIM := TRSection():New(_oReport,"Vencimento",{"ADY"} )
TRCell():New(_oDataFIM	,"DATAFIM"			,"" 		," "				,"@!"	,60		,.F.)
//&&    _oDataFIM:SetHeaderSection(.F.) 		//-- Inibe o Cabelhaço da Sessão
 
//+------------------------------------------------------------------+
//|    *****      S e c a o  -  2      *****                         |
//+------------------------------------------------------------------+
_oContrato := TRSection():New(_oReport,"Contratos",{"CN9"} )
TRCell():New(_oContrato	,"CN9_FILIAL"		,"CN9" 	,"Filial"			,"@!"	,15		,.F.)  
TRCell():New(_oContrato	,"CN9_NUMERO"		,"CN9" 	,"Contrato"		,"@!" 	,30		,.F.) 
TRCell():New(_oContrato	,"CN9_REVISA"		,"CN9" 	,"Revis"			,"@!" 	,07		,.F.)
TRCell():New(_oContrato	,"A1_CGC"			,"SA1" 	,"CGC/CPF"			,"@!"	,30		,.F.)    
TRCell():New(_oContrato	,"SA1CLIENT"		,"" 		,"Cliente"			,"@!"	,80		,.F.) 
TRCell():New(_oContrato	,"A1_MUN"			,"SA1" 	,"Cidade"			,"@!" 	,30		,.F.) 
TRCell():New(_oContrato	,"ADYVEND"			,"" 		,"Vendedor"		,"@!"	,30		,.F.) 
TRCell():New(_oContrato	,"ADZ_ITEM"		,"ADZ" 	,"Item"			,"@!"	,07		,.F.) 
TRCell():New(_oContrato	,"ADZ_PRODUT"		,"ADZ" 	,"Codigo"			,"@!" 	,20		,.F.)
TRCell():New(_oContrato	,"ADZ_DESCRI"		,"ADZ" 	,"Produto"			,"@!" 	,40		,.F.)
TRCell():New(_oContrato	,"CN9_DTINIC"		,"CN9" 	,"Dt Inicial"		,PesqPict("CN9","CN9_DTINIC") 	,TamSX3("CN9_DTINIC")[1]	,.F.) 
TRCell():New(_oContrato	,"CN9_DTFIM"		,"CN9" 	,"Dt Final"		,PesqPict("CN9","CN9_DTFIM") 	,TamSX3("CN9_DTFIM")[1]	,.F.) 
TRCell():New(_oContrato	,"AD1_DTASSI"		,"AD1" 	,"Ass Prop"		,PesqPict("AD1","AD1_DTASSI") 	,TamSX3("AD1_DTASSI")[1]	,.F.) 
TRCell():New(_oContrato	,"CN9_DTASSI"		,"CN9" 	,"Ass Contr"		,PesqPict("CN9","CN9_DTASSI") 	,TamSX3("CN9_DTASSI")[1]	,.F.)
Return _oReport

/*
+--------------------------------------------------------------------------+
 {Protheus.doc}<ReportPrint>
 32 - Clientes com e sem Atendimento   

@Author<Antonio Dsantas>
@since<14/04/2015>
@version<1.00>
@receive<Nil>
@return<Nil>
@example<Nil>
@see<Nil>
+--------------------------------------------------------------------------+
*/
Static Function ReportPrint(_oReport)
Local _oDataFIM		:= _oReport:Section(1)
Local _oContrato		:= _oReport:Section(2)
//-- 
Local _nRecs			:= 0
Local _cCNPJ			:= ""
Local _cDescCli		:= ""
Local _cDescVend		:= ""  
Local _cCtrVenc		:= ""
Local _lImpData		:= .t.
Local _lFirstTime		:= .t.
//+------------------------------------------------------------------+
//| Posiciona no inicio o Arquivo Resultado da Consulta Sql          | 
//+------------------------------------------------------------------+
dbSelectArea(_cAlias)  
(_cAlias)->(dbgotop())    
//-- Conta o numero do Registro da Tabela Temporaria
(_cAlias)->(dbEval({|| _nRecs++ }))
_oReport:SetMeter(_nRecs)
//-- 
dbSelectArea(_cAlias)  
(_cAlias)->(dbgotop())
_dDataENC	:= (_cAlias)->CN9_DTFIM
//+------------------------------------------------------------------+
//| Inicia Laco para impressao do Relatorio                          |
//+------------------------------------------------------------------+
_oContrato:Init()
_oReport:SetLeftMargin(5)		//-- Seta a Margem a partir da posição 2
Do While (_cAlias)->(!eof())   
	_oReport:IncMeter()    
	//-- 
	_cCNPJ		:= Transform((_cAlias)->A1_CGC,Iif(Len(AllTrim((_cAlias)->A1_CGC))==11,"@R 999.999.999-99","@R 99.999.999/9999-99"))
	_cDescCli	:= (_cAlias)->CN9_CLIENT+"/"+(_cAlias)->CN9_LOJACL+"-"+Substr((_cAlias)->A1_NOME,1,70)
	_cDescVend	:= Alltrim((_cAlias)->ADY_VEND)+"-"+Substr(Posicione("SA3",1,FwxFilial("SA3")+(_cAlias)->ADY_VEND,"A3_NOME"),1,20)  
	_cCtrVenc	:= "Contratos com Encerramento em: ["+Dtoc((_cAlias)->CN9_DTFIM)+"-"+Jurdow((_cAlias)->CN9_DTFIM)+" ]"
	//+------------------------------------------------------------------+
	//|	Faz Quebra e emite Cabeçalho da Data de Encerramento de Contrato. | 
	//+------------------------------------------------------------------+
	If Mv_par13 == 1
		If _lImpData  
			_oDataFIM:Init()
			If !_lFirstTime
				//-- Salta linha dupla 
				_oReport:SkipLine()
				_oReport:SkipLine()
				_oReport:SkipLine()
			Endif    
			//-- Imprime os dados do vendedor 
			_oReport:SetLeftMargin(3)		//-- Seta a Margem a partir da posição 1
			_oDataFIM:Cell( "DATAFIM"		):SetBlock({|| _cCtrVenc			})
			_oDataFIM:PrintLine()		//-- Imprime e alinha   
			_lImpData := .f.
		Endif 
		//-- 
		_oContrato:Init()
		_oReport:SetLeftMargin(5)		//-- Seta a Margem a partir da posição 5
	Endif 	
	//+------------------------------------------------------------------+
	//|  Imprime informações do Contrato                                 |
	//+------------------------------------------------------------------+
	_oContrato:Cell( "CN9_FILIAL"	):SetBlock({|| (_cAlias)->CN9_FILIAL					})  
	_oContrato:Cell( "CN9_NUMERO"	):SetBlock({|| (_cAlias)->CN9_NUMERO 					}) 
	_oContrato:Cell( "CN9_REVISA"	):SetBlock({|| (_cAlias)->CN9_REVISA 					})
	_oContrato:Cell( "A1_CGC"		):SetBlock({|| _cCNPJ									})    
	_oContrato:Cell( "SA1CLIENT"	):SetBlock({|| _cDescCli 								}) 
	_oContrato:Cell( "A1_MUN"		):SetBlock({|| (_cAlias)->A1_MUN 						}) 
	_oContrato:Cell( "ADYVEND"		):SetBlock({|| _cDescVend								}) 
	_oContrato:Cell( "ADZ_ITEM"		):SetBlock({|| (_cAlias)->ADZ_ITEM 					}) 
	_oContrato:Cell( "ADZ_PRODUT"	):SetBlock({|| (_cAlias)->ADZ_PRODUT 					})
	_oContrato:Cell( "ADZ_DESCRI"	):SetBlock({|| Substr((_cAlias)->ADZ_DESCRI,1,30) 	})
	_oContrato:Cell( "CN9_DTINIC"	):SetBlock({|| (_cAlias)->CN9_DTINIC 					}) 
	_oContrato:Cell( "CN9_DTFIM"	):SetBlock({|| (_cAlias)->CN9_DTFIM 					}) 
	_oContrato:Cell( "AD1_DTASSI"	):SetBlock({|| (_cAlias)->AD1_DTASSI 					}) 
	_oContrato:Cell( "CN9_DTASSI"	):SetBlock({|| (_cAlias)->CN9_DTASSI					})
	_oContrato:PrintLine()		//-- Imprime e alinha   
	//+------------------------------------------------------------------+
	//|  Vai para o Proximo Registro da TABELA TEMPORARIA                | 
	//|  ***Estabelece os Controles de Quebra                            |
	//+------------------------------------------------------------------+
	(_cAlias)->(dbskip()) 	
	If Mv_par13 == 1
		If (_cAlias)->CN9_DTFIM != _dDataENC
			_oDataFIM:Finish()
			_oContrato:Finish()
			_dDataENC	:= (_cAlias)->CN9_DTFIM
			_lImpData := .t.
		Endif 	
	Endif 	
	_lFirstTime := .f.
Enddo 
//+------------------------------------------------------------------+
//|     Finaliza todas as sessões por definitivo                     |
//+------------------------------------------------------------------+
_oDataFIM:Finish()
_oContrato:Finish()
//+------------------------------------------------------------------+
//|     Fecha o Arquivo de Trabalho                                  |
//+------------------------------------------------------------------+
(_cAlias)->(dbCloseArea())
//+------------------------------------------------------------------+
//|     Finaliza as sessoes                                          |
//+------------------------------------------------------------------+
Return


/*
+--------------------------------------------------------------------------+
{Protheus.doc}<fGet332X>
 Monta query que busca as matriculas.

@Author<Antonio Dsantas>
@since<10/04/2015>
@version<1.00>
@receive<Nil>
@return<Nil>
@example<Nil>
@see<Nil>
+--------------------------------------------------------------------------+
*/
User Function fGet337X()
Local _cQuery		:= "" 
_cQuery := ""
_cQuery += "Select CN9.CN9_FILIAL, CN9.CN9_NUMERO, CN9.CN9_REVISA, SA1.A1_CGC, CN9.CN9_CLIENT, "+CRLF
_cQuery += "       CN9.CN9_LOJACL, SA1.A1_NOME, SA1.A1_MUN, ADY.ADY_VEND, ADZ.ADZ_ITEM, ADZ.ADZ_PRODUT,  "+CRLF
_cQuery += "       ADZ.ADZ_DESCRI, CN9.CN9_DTINIC, CN9.CN9_DTFIM, AD1.AD1_DTASSI, CN9.CN9_DTASSI "+CRLF
_cQuery += "From "+RetSqlName("CN9")+" CN9 "+CRLF
_cQuery += "Inner Join "+RetSqlName("SA1")+" SA1 On SA1.A1_FILIAL = '"+FwxFilial("SA1")+"' "+CRLF
_cQuery += "                      And SA1.A1_COD = CN9.CN9_CLIENT "+CRLF
_cQuery += "                      And SA1.A1_LOJA = CN9.CN9_LOJACL "+CRLF
_cQuery += "Inner Join "+RetSqlName("CN1")+" CN1 On CN1.CN1_FILIAL = '"+FwxFilial("CN1")+"' "+CRLF
_cQuery += "                      And CN1.CN1_CODIGO = CN9.CN9_TPCTO  "+CRLF
_cQuery += "                      And CN1.CN1_ESPCTR = '2' "+CRLF
_cQuery += "Inner Join "+RetSqlName("ADY")+" ADY On ADY.ADY_FILIAL = CN9.CN9_FILIAL "+CRLF
_cQuery += "                      And ADY.ADY_XCONTR = CN9.CN9_NUMERO  "+CRLF
_cQuery += "                      And ADY.ADY_XREVCT = CN9.CN9_REVISA "+CRLF
_cQuery += "Inner Join "+RetSqlName("ADZ")+" ADZ ON ADZ.ADZ_FILIAL = ADY.ADY_FILIAL "+CRLF
//-- Inner nos ITENS da Proposta para filtro dos Produtos  
_cQuery += "                      And ADZ.ADZ_PROPOS = ADY.ADY_PROPOS "+CRLF
_cQuery += "                      And ADZ.ADZ_REVISA = ADY.ADY_PREVIS "+CRLF
_cQuery += "                      And ADZ.ADZ_PRODUT Between '"+Mv_par05+"' And '"+Mv_par06+"' "+CRLF
//-- Inner Nos produtos para filtro do Grupo de Produtos 
_cQuery += "Inner Join "+RetSqlName("SB1")+" SB1 On SB1.B1_FILIAL = '"+FwxFilial("SB1")+"' "+CRLF 
_cQuery += "                      And SB1.B1_COD = ADZ.ADZ_PRODUT "+CRLF
_cQuery += "                      And SB1.B1_GRUPO Between '"+Mv_par07+"' And '"+Mv_par08+"' "+CRLF
//-- Inner nas portunidades onde consta a data do aceite da Proposta DE/ATE 
_cQuery += "Inner Join "+RetSqlName("AD1")+" AD1 ON AD1.AD1_FILIAL =CN9.CN9_FILIAL "+CRLF
_cQuery += "                      And AD1.AD1_XCONTR = CN9.CN9_NUMERO "+CRLF
_cQuery += "                      And AD1.AD1_XREVCT = CN9.CN9_REVISA "+CRLF
_cQuery += "                      And AD1.AD1_DTASSI Between '"+Dtos(Mv_par09)+"' And '"+Dtos(Mv_par10)+"' "+CRLF
_cQuery += "Where CN9.D_E_L_E_T_ = ' ' "+CRLF
If _lFilialDR
	_cQuery += "And Substr(CN9.CN9_FILIAL,1,4) = '"+Substr(cFilAnt,1,4)+"' "+CRLF
Else
	_cQuery += "And CN9.CN9_FILIAL = '"+cFilAnt+"' "+CRLF
Endif 
_cQuery += "And CN9.CN9_SITUAC = '05' "+CRLF
_cQuery += "And CN9.CN9_DTINIC >= '"+Dtos(Mv_par01)+"' "+CRLF
_cQuery += "And CN9.CN9_DTFIM <= '"+Dtos(Mv_par02)+"' "+CRLF
_cQuery += "And CN9.CN9_CLIENT Between '"+Mv_par03+"' And '"+Mv_par04+"' "+CRLF
_cQuery += "And CN9.CN9_DTASSI Between '"+Dtos(Mv_par11)+"' And '"+Dtos(Mv_par12)+"' "+CRLF
If Mv_par13 == 1
	_cQuery += "Order by CN9.CN9_DTFIM, CN9.CN9_FILIAL, CN9.CN9_NUMERO, ADZ.ADZ_ITEM "+CRLF
Else
	_cQuery += "Order by CN9.CN9_FILIAL, CN9.CN9_NUMERO, ADZ.ADZ_ITEM "+CRLF
Endif 
//+------------------------------------------------------------------+
//| Grava o Resultado da Query para consulta                         |
//+------------------------------------------------------------------+
MemoWrite("C:\temp\"+FunName()+"_"+ProcName()+".TXT",_cQuery)
If Select(_cAlias)>0
	(_cAlias)->(DbCloseArea())
Endif
DbUseArea(.t.,"TOPCONN",TcGenQry(,,_cQuery),_cAlias,.t.,.t.)
//+------------------------------------------------------------------+
//| Converte os campos tipo DATA                                     |
//+------------------------------------------------------------------+
TCSETFIELD( _cAlias, "CN9_DTINIC"	,"D",8,0 ) 
TCSETFIELD( _cAlias, "CN9_DTFIM"	,"D",8,0 ) 
TCSETFIELD( _cAlias, "AD1_DTASSI"	,"D",8,0 ) 
TCSETFIELD( _cAlias, "CN9_DTASSI"	,"D",8,0 )
Return .t.





/*
--------------------------------------------------------------------------------
{Protheus.doc} <xParPerg>
 Cria as perguntas.	  

@author<Antonio Dantas>
@since<13/02/2015>
@version<1.00>
@receive<Nil>
@return<Nil>
@example<Nil>
@see<Nil>
--------------------------------------------------------------------------------
*/
Static Function xParPerg()	
Local _aPerg := {}
//*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*
//*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*
//   //-- MAPEAMENTOS DOS CAMPOS DE SX1
//   aAdd (_aPerg,{"X1_PERGUNT","X1_PERPA","X1_PERENG","MV_CHL","C",5,0,0,"X1_GSC","X1_VALID",;
//								"X1_VAR01",("X1_DEF01" e "X1_PYNE"),"X1_DEFSPA1","X1_DEFENG1","X1_CNT01",;
//								"X1_VAR02","X1_DEF02","X1_DEFSPA2","X1_DEFENG2","X1_CNT02",;
//								"X1_VAR03","X1_DEF03","X1_DEFSPA3","X1_DEFENG3","X1_CNT03",;
//								"X1_VAR04","X1_DEF04","X1_DEFSPA4","X1_DEFENG4","X1_CNT04",;
//								"X1_VAR05","X1_DEF05","X1_DEFSPA5","X1_DEFENG5","X1_CNT05",;
//								"X1_F3","X1_GRPSXG","","X1_HELP","X1_PICTURE",{"Descricao para Tela PTG"},{"Descricao para Tela EPA"},{"Descricao para Tela USA"}})
//*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*
//*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*
//+------------------------------------------------------------------+
//| Verifica se o operador esta logado na filail DR que tem acesso   |
//| a todas as filiais, ou somente na filial em que esta logada.     |
//+------------------------------------------------------------------+
//-- Mv_par01 =     Vigencia DE ?
aAdd(_aPerg, {"Vigencia DE ?","Vigencia DE ?","Vigencia DE ?","MV_CH1","D",8,0,0,"G","",;
					"MV_PAR01","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",{"Vigencia DE."},{"Vigencia DE."},{"Vigencia DE."}})

//-- Mv_par02 =     Vigencia ATE ?
aAdd(_aPerg, {"Vigencia ATE ?","Vigencia ATE ?","Vigencia ATE ?","MV_CH2","D",8,0,0,"G","",;
					"MV_PAR02","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",{"Vigencia ATE."},{"Vigencia ATE."},{"Vigencia ATE."}})

//-- Mv_par03 =     Cliente DE ?
aAdd(_aPerg, {"Cliente DE ?","Cliente DE ?","Cliente DE ?","MV_CH3","C",TamSX3("A1_COD")[1],0,0,"G","",;
					"MV_PAR03","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"SA1","","","","",{"Cliente DE."},{"Cliente DE."},{"Cliente DE."}})

//-- Mv_par04 =     Cliente ATE ?
aAdd(_aPerg, {"Cliente ATE ?","Cliente ATE ?","Cliente ATE ?","MV_CH4","C",TamSX3("A1_COD")[1],0,0,"G","",;
					"MV_PAR04","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"SA1","","","","",{"Cliente ATE."},{"Cliente ATE."},{"Cliente ATE."}})

//-- Mv_par05 =     Produto DE ?
aAdd(_aPerg, {"Produto DE ?","Produto DE ?","Produto DE ?","MV_CH5","C",TamSX3("B1_COD")[1],0,0,"G","",;
					"MV_PAR05","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"SB1","","","","",{"Produto DE."},{"Produto DE."},{"Produto DE."}})

//-- Mv_par06 =     Produto ATE ?
aAdd(_aPerg, {"Produto ATE ?","Produto ATE ?","Produto ATE ?","MV_CH6","C",TamSX3("B1_COD")[1],0,0,"G","",;
					"MV_PAR06","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"SB1","","","","",{"Produto ATE."},{"Produto ATE."},{"Produto ATE."}})

//-- Mv_par07 =     Grupo de Prod. DE ?
aAdd(_aPerg, {"Grupo de Prod. DE ?","Grupo de Prod. DE ?","Grupo de Prod. DE ?","MV_CH7","C",TamSX3("B1_GRUPO")[1],0,0,"G","",;
					"MV_PAR07","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"SBM","","","","",{"Grupo de Prod. DE."},{"Grupo de Prod. DE."},{"Grupo de Prod. DE."}})

//-- Mv_par08 =     Grupo de Prod. ATE ?
aAdd(_aPerg, {"Grupo de Prod. ATE ?","Grupo de Prod. ATE ?","Grupo de Prod. ATE ?","MV_CH8","C",TamSX3("B1_GRUPO")[1],0,0,"G","",;
					"MV_PAR08","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"SBM","","","","",{"Grupo de Prod. ATE."},{"Grupo de Prod. ATE."},{"Grupo de Prod. ATE."}})

//-- Mv_par09 =     Data Ass Proposta DE ? 
aAdd(_aPerg, {"Data Ass Proposta DE ? ","Data Ass Proposta DE ? ","Data Ass Proposta DE ? ","MV_CH9","D",8,0,0,"G","",;
					"MV_PAR09","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",{"Data Ass Proposta."},{"Data Ass Proposta."},{"Data Ass Proposta."}})

//-- Mv_par10 =     Data Ass Proposta ATE ?
aAdd(_aPerg, {"Data Ass Proposta ATE ?","Data Ass Proposta ATE ?","Data Ass Proposta ATE ?","MV_CHA","D",8,0,0,"G","",;
					"MV_PAR10","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",{"Data Ass Proposta ATE."},{"Data Ass Proposta ATE."},{"Data Ass Proposta ATE."}})

//-- Mv_par11 =     Data Ass Contrato DE ? 
aAdd(_aPerg, {"Data Ass Contrato DE ? ","Data Ass Contrato DE ? ","Data Ass Contrato DE ? ","MV_CHB","D",8,0,0,"G","",;
					"MV_PAR11","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",{"Data Ass Contrato."},{"Data Ass Contrato."},{"Data Ass Contrato."}})

//-- Mv_par12 =     Data Ass Contrato ATE ?
aAdd(_aPerg, {"Data Ass Contrato ATE ?","Data Ass Contrato ATE ?","Data Ass Contrato ATE ?","MV_CHC","D",8,0,0,"G","",;
					"MV_PAR12","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",{"Data Ass Contrato ATE."},{"Data Ass Contrato ATE."},{"Data Ass Contrato ATE."}})

//-- Mv_par13 =     Quebra por Data de Encerr. ?
aAdd(_aPerg, {"Quebra por Data de Encerr. ?","Quebra por Data de Encerr. ?","Quebra por Data de Encerr. ?","MV_CHD","N",1,0,1,"C",,;
					"MV_PAR13","Sim","Sim","Sim","",;
					"","Não","Não","Não","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",{"Quebra por Data de Encerr."},{"Quebra por Data de Encerr."},{"CQuebra por Data de Encerr."}})
//-- 
//-- 
u_SFAJUSX1(_cPerg,_aPerg) 
//-- 
Return 