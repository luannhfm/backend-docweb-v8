#Include 'Protheus.ch'
#Include 'Topconn.ch'


/*/{Protheus.doc} SS33R8S
Relatorio Fatura Analitica

@author 	Sergio Ricardo Leite Salustiano	
@since 		03/09/2014
@version 	1.0
@return 	Nil, Nulo

/*/
user function SS33R11S()
	
	Local 	oReport
	Private cPerg		:= 'SS33R11S'

	If TRepInUse()

		AjustaSx1(cPerg)
		Pergunte(cPerg,.F.)

		oReport := ReportDef(cPerg)
		oReport:PrintDialog()
		
	EndIf
			
Return( Nil )

/** {Protheus.doc} ReportDef
Funcao que define as colunas

@author: 	Sergio Ricardo Leite Salustiano	
@since: 	23/07/2014
@Uso: 		SFIEMT
*/
Static Function ReportDef(cPerg)
	
	Local oReport
	Local oSection
	Local oBreak
	Local oBreak1
	
	
	oReport := TReport():New(cPerg,"Relatorio Fatura Analitica por Credenciado",cPerg,{|oReport| PrintReport(oReport)},"Relatorio Fatura Analitica por Credenciado")
	oReport:SetLandScape()
	
	oSection:= TRSection():New(oReport,OemToAnsi(""),{"TRA"})
	
	//oCodCli:= TRCell():New(oSection, "CODIGO","TRA", "Cod. Cli.")
	//oCodCli:SetSize(10)
	
	//oLojCli:= TRCell():New(oSection, "LOJA","TRA", "Loja")
	//oLojCli:SetSize(5)
	
	oCnpj:= TRCell():New(oSection, "CNPJ","TRA", "CNPJ", "@R 99.999.999/9999-99")
	oCnpj:SetSize(20)
	
	oNomeEmp:= TRCell():New(oSection, "NOMEEMP","TRA", "Empresa")
	oNomeEmp:SetSize(45)
			
	oData:= TRCell():New(oSection, "DATA","TRA", "Data")
	oData:SetSize(12)
	
	oAtend:= TRCell():New(oSection, "ATEND","TRA", "Cod.Atend")
	oAtend:SetSize(10)
	
	oNomeTrb:= TRCell():New(oSection, "NOME","TRA", "Nome Trabalhador")
	oNomeTrb:SetSize(45)
	
	oCodProc:= TRCell():New(oSection, "CODITEM","TRA", "Cod.Item")
	oCodProc:SetSize(10)
	
	oDesProc:= TRCell():New(oSection, "DESCITEM","TRA", "Descricao Item")
	oDesProc:SetSize(25)
	
	//oVlrUnit:= TRCell():New(oSection, "VALORUNIT"  ,"TRA", "Valor R$", "@E 9,999.99")
	//oVlrUnit:SetSize(8)
	
	oCodProc:= TRCell():New(oSection, "CODCRED","TRA", "Cod.Cred.")
	oCodProc:SetSize(10)
	
	oDesProc:= TRCell():New(oSection, "CREDENC","TRA", "Credenciado")
	oDesProc:SetSize(25)
	
	oConta:= TRCell():New(oSection, "Conta"  ,"TRA", "Conta Contab", )
	oConta:SetSize(16)
	
	oItem:= TRCell():New(oSection, "ITEM"  ,"TRA", "Item Contabil.. ", )
	oItem:SetSize(16)
	
	oBreak	:= TRBreak():New(oSection,{|| (_cContax + _cItem + _cCodCre + _cCnpj)} , "Total do Item Contabil: " ,.F.)
	TRFunction():New(oSection:Cell("CODITEM"),/*cId*/,"COUNT"     ,oBreak, ,"@E 99,999",/*uFormula*/,.F.           ,.T.           ,.F. ) 
		
	//oBreak1	:= TRBreak():New(oSection, oSection:Cell("CNPJ"), "Total:",.F.)
	//TRFunction():New(oSection:Cell("VALORUNIT"),,"SUM",oBreak1,,"@E 99,999.99",,.F.,.F.,.F.)
	
	oBreak:SetPageBreak(.T.)
	
Return( oReport )
	
/** {Protheus.doc} ReportDef
Funcao que gera o relatorio

@author: 	Jose Leite de Barros Neto
@since: 	23/07/2014
@Uso: 		SFIEMT
*/
Static Function PrintReport(oReport)
	
	Local _cCodCli	:= ""
	Local _cLojCli	:= ""
	Local _cNomeEmp	:= ""
	Local _dData 		:= ""
	Local _cAtend		:= ""
	Local _cNomeTrb	:= ""
	Local _cCodProc	:= ""
	Local _cDesProc	:= ""
	Local _cCredenc := " "
	Local _nVlrUnit	:= 0
	Local oSection 	:= oReport:Section(1)
	
	Private _cCnpj		  := ""
	Private _cItem       := ""
	Private _cContax     := ""
	Private _cCodCre     := " "
	
	// -- Seta Titulo do Relatório -- //    
	oReport:SetCustomText( {||CriaCab(oReport ) })
	
	If Select("TRA") > 0
		DbSelectarea("TRA")
		DbCloseArea()
	Endif
		
	_cQuery := ""
	_cQuery += " SELECT ZZM_CODCLI, ZZM_LOJA, ZZM_CNPJ, ZZM_NOME,ZZM_DATA, ZZM_ATEND,ZZM_CODUS,ZZM_USER,ZZM_NOMTRA,ZZN_PROCED,ZZN_DESPRO, ZZN_CODCRE, ZZN_DCRED, ZZF_ITEMCC,ZZF_CONTA, ZZN_VUNIT	"
	_cQuery += "	FROM "+ RETSQLNAME("ZZM") + " ZZM "
	_cQuery += "		INNER JOIN "+ RETSQLNAME("ZZN") + " ZZN ON ZZN_FILIAL = ZZM_FILIAL AND ZZN_ATEND = ZZM_ATEND 		"
	_cQuery += "		INNER JOIN "+ RETSQLNAME("ZZF") + " ZZF ON ZZF_CODIGO = ZZN_PROCED 	"
	_cQuery += "	WHERE ZZM_FILIAL = '"+xFilial("ZZM")+"' "
	_cQuery += " 		AND ZZM.ZZM_CODCLI 	BETWEEN '"+mv_par03+"' AND '"+mv_par05+"'							"
	_cQuery += " 		AND ZZM.ZZM_LOJA 		BETWEEN '"+mv_par04+"' AND '"+mv_par06+"'							"
	_cQuery += " 		AND ZZM.ZZM_DATA 		BETWEEN '"+DtoS(mv_par01)+"' AND '"+DtoS(mv_par02)+"'			"
	_cQuery += " 		AND ZZN.ZZN_CODCRE 	BETWEEN '"+mv_par07+"' AND '"+mv_par08+"'							"
	_cQuery += " 		AND ZZM.D_E_L_E_T_ <> '*' 																	"
	_cQuery += "		AND ZZN.D_E_L_E_T_ <> '*'                                                           "
	_cQuery += "		AND ZZF.D_E_L_E_T_ <> '*' 																	"
	_cQuery += "		ORDER BY ZZM_CNPJ, ZZF_CONTA, ZZF_ITEMCC, ZZN_CODCRE                                							"
	
	TCQUERY _cQuery NEW ALIAS "TRA"
	
	DbSelectArea("TRA")
	
	DbGoTop()
	
	oReport:SetMeter(RecCount())
	oSection:Init()
	
	While .Not. TRA->( EOF() )
	
		If oReport:Cancel()
			Exit
		EndIf
		
		_cCodCli	:= AllTrim(TRA->ZZM_CODCLI)
		_cLojCli	:= AllTrim(TRA->ZZM_LOJA)
		_cCnpj		:= AllTrim(TRA->ZZM_CNPJ)
		_cNomeEmp	:= AllTrim(TRA->ZZM_NOME)
		_dData 	:= DtoC(StoD(TRA->ZZM_DATA))
		_cAtend	:= AllTrim(TRA->ZZM_ATEND)
		_cNomeTrb	:= AllTrim(TRA->ZZM_NOMTRA)
		_cCodProc	:= AllTrim(TRA->ZZN_PROCED)
		_cDesProc	:= AllTrim(TRA->ZZN_DESPRO)
		_nVlrUnit	:= TRA->ZZN_VUNIT
		_cItem     := AllTrim(TRA->ZZF_ITEMCC)
		_cContax   := AllTrim(TRA->ZZF_CONTA)
		
		_cCodCre   := AllTrim(TRA->ZZN_CODCRE)
		_cCredenc  := AllTrim(TRA->ZZN_DCRED)
		
		
		/*If(TRA->ZZF_TPEVEN = '1')
			_cTpEvent := "1 - Clinico"
		EndIf
		
		If(TRA->ZZF_TPEVEN = '2')
			_cTpEvent := "2 - Laboratorial"
		EndIf	*/
	
		//Dados
		//oSection:Cell("CODIGO"):SetValue(_cCodCli)
		//oSection:Cell("LOJA"):SetValue(_cLojCli)
		oSection:Cell("CNPJ"):SetValue(_cCnpj)
		oSection:Cell("NOMEEMP"):SetValue(_cNomeEmp)
		oSection:Cell("DATA"):SetValue(_dData)
		oSection:Cell("ATEND"):SetValue(_cAtend)
		oSection:Cell("NOME"):SetValue(_cNomeTrb)
		oSection:Cell("CODITEM"):SetValue(_cCodProc)
		oSection:Cell("DESCITEM"):SetValue(_cDesProc)
		oSection:Cell("CONTA"):SetValue(_cContax)
		oSection:Cell("ITEM"):SetValue(_cItem)
		//oSection:Cell("VALORUNIT"):SetValue(_nVlrUnit)
		oSection:Cell("CODCRED"):SetValue(_cCodCre)
		oSection:Cell("CREDENC"):SetValue(_cCredenc)
		
		oSection:PrintLine()
		oReport:IncMeter()
		
		TRA->( DbSkip() )
		
	End
	
	TRA->(DbCloseArea())
	oSection:Finish()

Return( Nil )

/*/{Protheus.doc} CriaCab
Altera o Cabeçalho do Relatorio

@author 	Sergio Ricardo Leite Salustiano
@since 		15/08/2014
@version 	1.0

@param oReport, objeto

@return aCabec, Array
/*/

Static Function CriaCab( oReport )

	Local aArea	:= GetArea()
	Local aCabec	:= {}
	Local cChar	:= chr(160)  // caracter dummy para alinhamento do cabeçalho
	Local cTitulo	:= ""


	cTitulo:= "Periodo de: " +DTOC(MV_PAR01)+" Ate "+DTOC(MV_PAR02)

          
	aCabec := {	"__LOGOEMP__" , cChar + "         " + SM0->M0_NOMECOM + "         " + cChar + RptFolha+ TRANSFORM(oReport:Page(),'999999');
		,  'SS33R11S' + cChar + "         " + "RELATORIO DE FATURA ANALITICA POR CREDENCIADO" + "         "+ cChar + RptEmiss + " " + Dtoc(dDataBase);
		, "         " + cChar +  "         " + UPPER(AllTrim(cTitulo)) +  "         "+ cChar + RptHora + " " + Time() }
		       

	RestArea( aArea )
	
Return( aCabec )

/** {Protheus.doc} AjustaSx1
Pergunta do Relatorio

@author: 	Jose Leite de Barros Neto
@since: 	03/07/2014
@Uso: 		SFIEMT
*/
Static Function AjustaSx1(cPerg)
	
	Local aHelp := {}

	AAdd(aHelp, {{"Informe a Data Inicial"		}, {""}, {""}})
	AAdd(aHelp, {{"Informe a Data Final"		}, {""}, {""}})
	AAdd(aHelp, {{"Informe o Cliente Inicial"	}, {""}, {""}})
	AAdd(aHelp, {{"Informe a Loja Inicial"		}, {""}, {""}})
	AAdd(aHelp, {{"Informe o Cliente Final"	    }, {""}, {""}})
	AAdd(aHelp, {{"Informe a Loja Final"		}, {""}, {""}})
	AAdd(aHelp, {{"Informe o Credenciado Ini"	}, {""}, {""}})
	AAdd(aHelp, {{"Informe Credenciado  Final"	}, {""}, {""}})
	
	u_SFPUTSX1(cPerg,"01","Data de..........","","","mv_ch1","D",08,00,00,"G","",""			,"","","mv_par01","","","","","","","","","","","","","","","","",aHelp[1,1],aHelp[1,2],aHelp[1,3],"")
	u_SFPUTSX1(cPerg,"02","Data Ate.........","","","mv_ch2","D",08,00,00,"G","",""			,"","","mv_par02","","","","","","","","","","","","","","","","",aHelp[2,1],aHelp[2,2],aHelp[2,3],"")
	u_SFPUTSX1(cPerg,"03","Cliente de.......","","","mv_ch3","C",08,00,00,"G","","SA1"		,"","","mv_par03","","","","","","","","","","","","","","","","",aHelp[3,1],aHelp[3,2],aHelp[3,3],"")
	u_SFPUTSX1(cPerg,"04","Loja de..........","","","mv_ch4","C",04,00,00,"G","",""			,"","","mv_par04","","","","","","","","","","","","","","","","",aHelp[4,1],aHelp[4,2],aHelp[4,3],"")
	u_SFPUTSX1(cPerg,"05","Cliente Ate......","","","mv_ch5","C",08,00,00,"G","","SA1"		,"","","mv_par05","","","","","","","","","","","","","","","","",aHelp[5,1],aHelp[5,2],aHelp[5,3],"")
	u_SFPUTSX1(cPerg,"06","Loja Ate.........","","","mv_ch6","C",04,00,00,"G","",""			,"","","mv_par06","","","","","","","","","","","","","","","","",aHelp[6,1],aHelp[6,2],aHelp[6,3],"")
	u_SFPUTSX1(cPerg,"07","Credenciado Ini..","","","mv_ch7","C",06,00,00,"G","","ZZG"		,"","","mv_par07","","","","","","","","","","","","","","","","",aHelp[7,1],aHelp[7,2],aHelp[7,3],"")
	u_SFPUTSX1(cPerg,"08","Credenciado Ate..","","","mv_ch8","C",06,00,00,"G","","ZZG"		,"","","mv_par08","","","","","","","","","","","","","","","","",aHelp[8,1],aHelp[8,2],aHelp[8,3],"")
Return( Nil )	
return