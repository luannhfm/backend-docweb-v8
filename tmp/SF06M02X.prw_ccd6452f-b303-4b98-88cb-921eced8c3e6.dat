#Include 'Protheus.ch'

/*
	Autor     : Carlos Ryve Gandini
	Data/Time : 20/02/19 as 15:13:30
	Detalhe   : Tratamento para buscar propriedades estrutura pelo SX3.
*/
Static bSx3Tam := { |cCpo| GetSx3Cache(cCpo,"X3_TAMANHO") }
Static bSx3Dec := { |cCpo| GetSx3Cache(cCpo,"X3_DECIMAL") }
Static bSx3Pic := { |cCpo| GetSx3Cache(cCpo,"X3_PICTURE") }
Static bSx3Tip := { |cCpo| GetSx3Cache(cCpo,"X3_TIPO") }

/*/{Protheus.doc} SF06M02X
	Customização ISS.NET.
	Tela para apresentar. 
	
@author Walmir Junior
@since 24/05/2017
@version 1.00

/*/
User Function SF06M02X(_aSmIns)

	Local _lRet		:=	.T.
	Local _oDlg
	Local _oGetDados
	Local _cMens	:= "Atenção, os fornecedores a seguir estão com seus cadastros pendentes de 'Insc. Municipal' ou 'Insc. Prestador'."
	Local _cMask	:= eVal(bSx3Pic,"A2_CGC") //"@R 99.999.999/9999-99"
	Local _nx := 0

	Private _aHeader := {}
	Private _aCols := {}

	AADD(_aHeader,{ OemToAnsi("CODIGO")		,"A2_COD" , eVal(bSx3Pic,"A2_COD") ,eVal(bSx3Tam,"A2_COD" ),eVal(bSx3Dec,"A2_COD" ),"","",eVal(bSx3Tip,"A2_COD" ),"",""} )
	AADD(_aHeader,{ OemToAnsi("Fornecedor")	,"A2_NOME", eVal(bSx3Pic,"A2_NOME"),eVal(bSx3Tam,"A2_NOME"),eVal(bSx3Dec,"A2_NOME"),"","",eVal(bSx3Tip,"A2_NOME"),"",""} )
	AADD(_aHeader,{ OemToAnsi("CGC")		,"A2_CGC" , eVal(bSx3Pic,"A2_CGC") ,eVal(bSx3Tam,"A2_CGC" ),eVal(bSx3Dec,"A2_CGC" ),"","",eVal(bSx3Tip,"A2_CGC" ),"",""} )
	AADD(_aHeader,{ OemToAnsi("UF")			,"A2_EST" , eVal(bSx3Pic,"A2_EST") ,eVal(bSx3Tam,"A2_EST" ),eVal(bSx3Dec,"A2_EST" ),"","",eVal(bSx3Tip,"A2_EST" ),"",""} )
	AADD(_aHeader,{ OemToAnsi("Município")	,"A2_MUN" , eVal(bSx3Pic,"A2_MUN") ,eVal(bSx3Tam,"A2_MUN" ),eVal(bSx3Dec,"A2_MUN" ),"","",eVal(bSx3Tip,"A2_MUN" ),"",""} )

	For _nx := 1 to Len(_aSmIns)
		If aScan(_aCols , {|x| AllTrim(x[3]) == AllTrim(Transform(_aSmIns[_nx][2], _cMask)) } ) == 0
			
			/*
				Autor     : Carlos Ryve Gandini
				Data/Time : 21/02/19 as 13:18:20
				Detalhe   : Tratamento buscar a picture correta.
			*/
			SA2->( dbSetOrder(3) )
			If SA2->( MsSeek( xFilial("SA2") + _aSmIns[_nx][2] ) )
				_cMask := eVal(bSx3Pic,"A2_CGC")
				_cMask := StrTran( _cMask, "%C", "" )
				
				nPosCGC := aScan( _aHeader, { |x| rTrim(x[2]) == "A2_CGC" } )
				If nPosCGC > 0 
					_aHeader[nPosCGC][3] := _cMask
				EndIf
				
			EndIf
			
			AADD(_aCols, {_aSmIns[_nx][1],_aSmIns[_nx][3], _aSmIns[_nx][2] /*Transform(_aSmIns[_nx][2], _cMask)*/, _aSmIns[_nx][4], _aSmIns[_nx][5],.F.})
		
		EndIf
	Next

	DEFINE MsDialog _oDlg Title "Cadastros Pendentes - Fornecedor" From 0,0 To 430,700 PIXEL
		
	@ 10,05		SAY _cMens	SIZE 400,7 OF _oDlg PIXEL

	_oGetDados := MsNewGetDados():New(30,05,190,350,4,,,,,,,,,,_oDlg,_aHeader,_aCols)

	@ 202, 310 BUTTON OemToAnsi("Ok")   SIZE 40 ,11  FONT _oDlg:oFont ACTION (_oDlg:End())  OF _oDlg PIXEL

	ACTIVATE MSDIALOG _oDlg CENTERED

Return _lRet
