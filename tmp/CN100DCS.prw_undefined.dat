#Include 'protheus.ch'

/*/{Protheus.doc} CN100DCS
	Ponto de Entrada que possibilita a alteração da situação do contrato. 
	Está localizado no botão de confirmação da tela de Check-List de Documentos, na alteração de situação do contrato.
	
	http://tdn.totvs.com/display/public/mp/CN100DCS+-+Tela+de+check+list

@author franklin.oliveira
@since 28/04/2016
@version 1.00
@type function

@Param PARAMIXB[1], objeto, Objeto da tela do Check-List de Documentos.
@Param PARAMIXB[2], array, Array que contém o item da tabela de Documentos - CNK.
@Param PARAMIXB[3], lógico, se .T. = Documentos liberados, .F. = Documentos bloqueados

@return ExpLCont, lógico, Se retornar .T., permite a alteração da situação do contrato. Se retornar .F., não permite a alteração da situação do Contrato
@history 17/09/2019, Franklin de Brito de Oliveira, Quando for revisão não deve validar banco de conhecimento customizado.
/*/
User Function CN100DCS()    

Local aArea		:= GetArea()

Local cDocErr	:= ""

Local ExpObDlg	:= PARAMIXB[1]
Local ExpAItDc	:= PARAMIXB[2]
Local ExpLCont	:= PARAMIXB[3]

Local nX		:= 0

	If ExpLCont .And. !( AllTrim(CN9->CN9_SITUAC) == '09' )  // 09 - Revisao
		DbSelectArea("ZG0")
		ZG0->( DbSetOrder(1) )
		
		For nX := 1 To Len (ExpAItDc)
			If !Empty(ExpAItDc[nX, 3])
				If !MsSeek(FwxFilial("ZG0") + "CNK" + ExpAItDc[nX, 3])
					cDocErr += ExpAItDc[nX, 3] + CRLF
				EndIf
			EndIf
		Next nX
		
		If !Empty(cDocErr)
			Help(" ",1,"Documentos",,"O(s) Documento(s): " + CRLF + cDocErr + " Não estão anexados ao Banco de Conhecimento.",1,0)
			ExpLCont := .F.
		EndIf
	EndIf

	RestArea(aArea)
	
Return ExpLCont