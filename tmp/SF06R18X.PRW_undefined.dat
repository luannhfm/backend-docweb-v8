#Include 'Protheus.ch'

/*/{Protheus.doc} SF06R18X
(long_description)
@author j2a.luizjunior
@since 19/10/2017
@version 1.0
@example
(examples)
@see (links_or_references)
/*/

/*/
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Data     ³ Analista      ³ Alteração                                   ³±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³20/05/2019³ Paulo Schwind ³ ValidPerg - Retirada a função PutSx1()      ³±±
±±³29/05/2019³ Paulo Schwind ³ Totalizador por Filial                      ³±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
/*/

User Function SF06R18X()

	Local   oReport
	Private _cSitTit  := ""
	// Adicionada funcao PADR() por Paulo Schwind - 20/05/2019
	Private cPerg     := Padr("SF06R18X",10) 
	Private cTitOrig  := ""
	//Walmir Junior 25/04/2018 - Alteração de escopo da variável.	
	Private cAlZF1     := GetNextAlias() 
	
    /* Trecho desabilitado - Funcao PUTSX1() descontinuada
 	If Funname(0) == "SF06R18X"	
		CriaSX1(cPerg)
		If !Pergunte(cPerg,.T.)
			Return(Nil)
		EndIf
	EndIf		
    */
    
    // Adicionado por Paulo Schwind - 20/05/2019
    ValidPerg()
	Pergunte(cPerg,.T.)
	if LastKey()=27
		Return
	Endif

	oReport  := ReportDef()
	oReport:PrintDialog()

Return

/*/{Protheus.doc} ReportDef
(long_description)
@author j2a.luizjunior
@since 19/10/2017
@version 1.0
@param cPerg, character, (Descrição do parâmetro)
@return ${return}, ${return_description}
@example
(examples)
@see (links_or_references)
/*/

Static Function ReportDef(cPerg)
	
	Local oReport
	Local cIDCelula	:= ""	
	Local cNomeRel	:= "SF06R18X"
	
	Local cTitRel	:= "Relatório de Agendamento"
	Local cDescRel	:= "Relatório de Agendamento"

	oReport := TReport():New( cNomeRel, cTitRel, cPerg, { |oReport| ReportPrint(oReport) }, cDescRel )
	
	oReport:SetLandScape()
	oReport:SetTotalInLine(.F.) 
	oReport:Page()

	_oFil	:= TRSection():New(oReport, "Filial" ,{"SE1"},,,,,,,,,,,.T.)
	TRCell():New(_oFil,"E1_FILIAL" ,(cAlZF1),,,100,,{|| FWFilName(cEmpAnt,(cAlZF1)->E1_FILIAL) })
	
	//-> Titulo X Cliente
	oTitulo := TRSection():New(oReport, "Titulo" ,{"SE1"})	
	oCGC    := TRCell():New(oTitulo,"CGC"        ,"SA1","CNPJ/CPF","@!")
	oCGC:SetSize(45)	
	oNome   := TRCell():New(oTitulo,"A1_NOME"    ,"SA1")
	oNome:SetSize(100)	
	oDDD    := TRCell():New(oTitulo,"A1_DDD"     ,"SA1")
	oDDD:SetSize(10)	
	oTel    := TRCell():New(oTitulo,"A1_TEL"     ,"SA1")
	oTel:SetSize(30)	
	oEmail  := TRCell():New(oTitulo,"A1_XEMLCON" ,"SA1")
	oEmail:SetSize(80)		
	oNum    := TRCell():New(oTitulo,"E1_NUM"     ,"SE1")
	oNum:SetSize(40)	
	oParcel := TRCell():New(oTitulo,"E1_PARCELA" ,"SE1")
	oParcel:SetSize(10)	
	oValor  := TRCell():New(oTitulo,"E1_VALOR"   ,"SE1")
	oValor:SetSize(30)		
	oVenc   := TRCell():New(oTitulo,"E1_VENCTO"  ,"SE1")
	oVenc:SetSize(20)		
	oDias   := TRCell():New(oTitulo,"DIAS"       ,"SE1","Dias Atraso","@E 99,999")	
	oDias:SetSize(30)
	oDTAge  := TRCell():New(oTitulo,"ZF1_AGENDA" ,"ZF1")
	oDTAge:SetSize(20)	
	oHist   := TRCell():New(oTitulo,"ZF1_HISTOR" ,"ZF1")
	oHist:SetSize(120)	

    TRFunction():New(oTitulo:Cell("E1_NUM")  ,,"COUNT")
    TRFunction():New(oTitulo:Cell("E1_VALOR"),,"SUM")

	oReport:SetTotalInLine(.t.)
	TReport():SetTotalText( "Sub-Total" )
	
	// Walmir Junior 25/04/2018 - Ajustes no relatório, sessão para quebrar por Filial.
	// Paulo Schwind - 29/05/2019 - Inserito acima para quebra
	//_oFil	:= TRSection():New(oReport, "Filial" ,{"SE1"},,,,,,,,,,,.T.)
	//TRCell():New(_oFil,"E1_FILIAL" ,(cAlZF1),,,100,,{|| FWFilName(cEmpAnt,(cAlZF1)->E1_FILIAL) })
Return(oReport)

/*/{Protheus.doc} ReportPrint
(long_description)
@author j2a.luizjunior
@since 19/10/2017
@version 1.0
@param oReport, objeto, (Descrição do parâmetro)
@return ${return}, ${return_description}
@example
(examples)
@see (links_or_references)F
/*/

Static Function ReportPrint(oReport)

	Local cSQL       := ""
	Local _oFil      := oReport:Section(1)
	Local oTitulo    := oReport:Section(2)
	Local cSql       := ""
	Local cTxtLinha  := ""
	Local _cFil		 := ""
    Local _aArea
    Local _cMemo      
    Local cTxtLinha := ""    	

	If IsInCallStack("U_SF06A25X")
		cSql += " AND F1.ZF1_AGENDA = '" + DToS(dDataBase) + "'"
	Else

		If !Empty(MV_PAR01) .And. !Empty(MV_PAR02) 
			cSql += " AND E1_FILIAL BETWEEN '"  + MV_PAR01 + "' AND '" + MV_PAR02 + "'"
		EndIf

		//-> De Emissao/Ate Emissao
		If !Empty(MV_PAR03) .And. !Empty(MV_PAR04) 
			cSql += " AND E1_EMISSAO BETWEEN '"  + DToS(MV_PAR03) + "' AND '" + DToS(MV_PAR04) + "'"	
		EndIf
	
		//-> De Cliente/De Loja 
		If !Empty(MV_PAR05) .And. !Empty(MV_PAR06) 
			cSql += " AND E1_CLIENTE >= '"  + MV_PAR05 + "' "
			cSql += " AND E1_LOJA    >= '"  + MV_PAR06 + "' "	
		EndIf	
			
		//-> Ate Cliente/Ate Loja
		If !Empty(MV_PAR07) .And. !Empty(MV_PAR08) 
			cSql += " AND E1_CLIENTE <= '"  + MV_PAR07 + "' "
			cSql += " AND E1_LOJA    <= '"  + MV_PAR08 + "' "	
		EndIf	
		
	EndIf
	
	cSql := "%"+cSql+"%"
	
	BeginSql Alias cAlZF1
		
		SELECT 	   E1.E1_FILIAL,
		           A1.A1_NOME,
		           A1.A1_COD,
		           A1.A1_LOJA,
		           A1.A1_PESSOA,
		           A1.A1_CGC,
		           A1.A1_DDD,
		           A1.A1_TEL,
		           A1.A1_XEMLCON,
		           //F1.ZF1_HISTOR,
		           F1.ZF1_AGENDA,
		           F1.ZF1_ACAO,
		           F1.R_E_C_N_O_,
		           E1.E1_VENCTO,
		           E1.E1_NUM,
		           E1.E1_PARCELA,
		           E1.E1_VALOR
		 FROM      %Table:SA1% A1
		INNER JOIN %Table:SE1% E1
		ON    A1.A1_COD       = E1.E1_CLIENTE 
		AND   A1.A1_LOJA      = E1.E1_LOJA
		INNER JOIN %Table:ZF1% F1 
		ON    E1.E1_FILIAL    = F1.ZF1_FILIAL 
		AND   E1.E1_CLIENTE   = F1.ZF1_CLIENT 
		AND   E1.E1_LOJA      = E1.E1_LOJA
		AND   E1.E1_PREFIXO   = F1.ZF1_PREF 
		AND   E1.E1_NUM       = F1.ZF1_NUM 
		AND   E1.E1_PARCELA   = F1.ZF1_PARC
		//Walmir Junior 25/04/2018 - Adição de critério para buscar apenas agendamentos.
		//Paulo Schwind 29/05/2019 - Alterado para buscar agendamentos pelo campo ZF1_ACAO = '1'
		//AND 	F1.ZF1_ACAO =	%Exp:'1'%
		AND 	F1.ZF1_AGENDA !=	%Exp:' '%
		AND		F1.ZF1_XJREC  =		%Exp:'F'%
		AND		F1.ZF1_XJPARE =		%Exp:'F'%
		WHERE A1.%NotDel%
		AND   E1.%NotDel%
		AND   F1.%NotDel%
		%Exp:cSql%
		ORDER BY E1.E1_FILIAL
	EndSql

	While !(cAlZF1)->(Eof())
		//Walmir Junior 25/04/2018 - Multiplicação do Resultado por -1 para tornar o numero de dias positivo.
		nDias   := dDataBase - SToD((cAlZF1)->E1_VENCTO)   //(SToD((cAlZF1)->E1_VENCTO) - dDataBase) * -1

        _cMemo := u_LoZF1hst((cAlZF1)->R_E_C_N_O_) 
		nLinhas := MLCount(_cMemo,110)	

		For nX  := 1 To nLinhas
			cTxtLinha += AllTrim(MemoLine(_cMemo,110,nX)) + " "
		Next Nx			
	
        /*
		//Walmir Junior 25/04/2018 - Tratativa para quebrar por Filial.
		If 	_cFil != (cAlZF1)->E1_FILIAL
			_oFil:Init()
			//_oFil:Cell("E1_FILIAL"):SetValue(AllTrim({|| FWFilName(cEmpAnt,(cAlZF1)->E1_FILIAL) }))
			_oFil:PrintLine()
			_oFil:Finish()
			_cFil := (cAlZF1)->E1_FILIAL
		EndIf
		*/
	
		_oFil:Init()
	    oReport:IncMeter()
		If oReport:Cancel()
			Return(Nil)
		EndIf

		_oFil:Cell("E1_FILIAL"):SetValue((cAlZF1)->E1_FILIAL+" "+FWFilName(cEmpAnt,(cAlZF1)->E1_FILIAL))
		_oFil:Printline()

		_cFil := (cAlZF1)->E1_FILIAL
		oTitulo:Init()
		
		While ! (cAlZF1)->(EOF()) .and. (cAlZF1)->E1_FILIAL == _cFil 
			oTitulo:Cell("CGC")       :SetValue(AllTrim(StrTran(Transform((cAlZF1)->A1_CGC, PicPes((cAlZF1)->A1_PESSOA)),"%C", "") ))
			oTitulo:Cell("A1_NOME")   :SetValue(AllTrim((cAlZF1)->A1_NOME                                                          ))
			oTitulo:Cell("A1_DDD")    :SetValue(AllTrim((cAlZF1)->A1_DDD                                                           ))
			oTitulo:Cell("A1_TEL")    :SetValue(AllTrim((cAlZF1)->A1_TEL                                                           ))
			oTitulo:Cell("A1_XEMLCON"):SetValue(AllTrim((cAlZF1)->A1_XEMLCON                                                       ))
			oTitulo:Cell("E1_NUM")    :SetValue(AllTrim((cAlZF1)->E1_NUM                                                           ))		
			oTitulo:Cell("E1_PARCELA"):SetValue(AllTrim((cAlZF1)->E1_PARCELA                                                       ))
			oTitulo:Cell("E1_VALOR")  :SetValue((cAlZF1)->E1_VALOR                                                                  )			
			oTitulo:Cell("E1_VENCTO") :SetValue(DToC(SToD((cAlZF1)->E1_VENCTO                                                     )))		
			oTitulo:Cell("DIAS")      :SetValue(nDias                                                                               )		
			oTitulo:Cell("ZF1_AGENDA"):SetValue(DToC(SToD((cAlZF1)->ZF1_AGENDA                                                    )))		
			oTitulo:Cell("ZF1_HISTOR"):SetValue(AllTrim(cTxtLinha                                                                  ))
		    
			oTitulo:Printline()
				
			cTxtLinha := ""		
			//oReport:SkipLine()
		
			(cAlZF1)->(DbSkip())
			//Walmir Junior 25/04/2018 - Tratativa para quebrar por Filial.
			//If 	_cFil != (cAlZF1)->E1_FILIAL
			//	_oFil:Finish()
			//EndIf
			// Paulo Schwind - IF Retirado
			
		EndDo
			
		oReport:ThinLine()
		_oFil:Finish()
		oTitulo:Finish()
		
	EndDo
	
	(cAlZF1)->(DbCloseArea())
	
	//oSec1:Finish()

Return

/*/{Protheus.doc} CriaSX1
(long_description)
@author j2a.luizjunior
@since 20/10/2017
@version 1.0
@example
(examples)
@see (links_or_references)
/*/

Static Function CriaSX1

	Local aHelp := {}
	
	//-> Texto do help em português,inglês, espanhol
	AAdd(aHelp, {{"De Filial"                }  ,{""},{""}})//21
	AAdd(aHelp, {{"Ate Filial"               }  ,{""},{""}})//22	
	AAdd(aHelp, {{"De Emissao"               }  ,{""},{""}})//11
	AAdd(aHelp, {{"Ate Emissao"              }  ,{""},{""}})//12
	AAdd(aHelp, {{"De Cliente"               }  ,{""},{""}})//01
	AAdd(aHelp, {{"De Loja"                  }  ,{""},{""}})//02	
	AAdd(aHelp, {{"Ate Cliente"              }  ,{""},{""}})//03
	AAdd(aHelp, {{"Ate Loja"                 }  ,{""},{""}})//04

	PutSX1(cPerg,"01","De Filial"   ,"","","mv_ch01","C",08,00,00,"G","","SM0","","","mv_par01","","","","","","","","","","","","","","","","",aHelp[1,1],aHelp[1,2],aHelp[1,3],"")
	PutSX1(cPerg,"02","Ate Filial"  ,"","","mv_ch02","C",08,00,00,"G","","SM0","","","mv_par02","","","","","","","","","","","","","","","","",aHelp[2,1],aHelp[2,2],aHelp[2,3],"")
	PutSX1(cPerg,"03","De Emissao"  ,"","","mv_ch03","D",08,00,00,"G","",""   ,"","","mv_par03","","","","","","","","","","","","","","","","",aHelp[3,1],aHelp[3,2],aHelp[3,3],"")
	PutSX1(cPerg,"04","Ate Emissao" ,"","","mv_ch04","D",08,00,00,"G","",""   ,"","","mv_par04","","","","","","","","","","","","","","","","",aHelp[4,1],aHelp[4,2],aHelp[4,3],"")
	PutSX1(cPerg,"05","De Cliente"  ,"","","mv_ch05","C",09,00,00,"G","","SA1","","","mv_par05","","","","","","","","","","","","","","","","",aHelp[5,1],aHelp[5,2],aHelp[5,3],"")
	PutSX1(cPerg,"06","De Loja"     ,"","","mv_ch06","C",04,00,00,"G","",""   ,"","","mv_par06","","","","","","","","","","","","","","","","",aHelp[6,1],aHelp[6,2],aHelp[6,3],"")
	PutSX1(cPerg,"07","Ate Cliente" ,"","","mv_ch07","C",09,00,00,"G","","SA1","","","mv_par07","","","","","","","","","","","","","","","","",aHelp[7,1],aHelp[7,2],aHelp[7,3],"")
	PutSX1(cPerg,"08","Ate Loja"    ,"","","mv_ch08","C",04,00,00,"G","",""   ,"","","mv_par08","","","","","","","","","","","","","","","","",aHelp[8,1],aHelp[8,2],aHelp[8,3],"")

Return



/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ValidPerg ºAutor  ³Paulo Schwind       º Data ³  21/05/2019 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ValidPerg()
	Local _aArea := GetArea()
	Local _cPerg := cPerg
	Local _aRegs:={}
	Local _nI := 0
	Local _nJ := 0

	dbSelectArea('SX1')
	dbSetOrder(1)

	Aadd(_aRegs,{_cPerg,"01","Filial De                     ","                              ","                              ","mv_ch1","C",08,0,0,"G","                                                            ","mv_par01       ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","          ","                                                            ","SM0","S","   ","          "})
	Aadd(_aRegs,{_cPerg,"02","Filial Até                    ","                              ","                              ","mv_ch2","C",08,0,0,"G","                                                            ","mv_par02       ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","          ","                                                            ","SM0","S","   ","          "})
	Aadd(_aRegs,{_cPerg,"03","Emissão De                    ","                              ","                              ","mv_ch3","D",08,0,0,"G","                                                            ","mv_par03       ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","          ","                                                            ","   ","S","   ","          "})
	Aadd(_aRegs,{_cPerg,"04","Emissao Até                   ","                              ","                              ","mv_ch4","D",08,0,0,"G","                                                            ","mv_par04       ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","          ","                                                            ","   ","S","   ","          "})
	Aadd(_aRegs,{_cPerg,"05","Cliente De                    ","                              ","                              ","mv_ch5","C",09,0,0,"G","                                                            ","mv_par05       ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","          ","                                                            ","SA1","S","   ","          "})
	Aadd(_aRegs,{_cPerg,"06","Loja De                       ","                              ","                              ","mv_ch6","C",04,0,0,"G","                                                            ","mv_par06       ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","          ","                                                            ","   ","S","   ","          "})
	Aadd(_aRegs,{_cPerg,"07","Cliente Até                   ","                              ","                              ","mv_ch7","C",09,0,0,"G","                                                            ","mv_par07       ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","          ","                                                            ","SA1","S","   ","          "})
	Aadd(_aRegs,{_cPerg,"08","Loja Até                      ","                              ","                              ","mv_ch8","C",04,0,0,"G","                                                            ","mv_par08       ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","          ","                                                            ","   ","S","   ","          "})

	For _nI:=1 to Len(_aRegs)
		If ! DbSeek(_cPerg+_aRegs[_nI,2])
			RecLock('SX1',.T.)
			For _nJ:=1 to FCount()
				If _nJ <= Len(_aRegs[_nI])
					FieldPut(_nJ,_aRegs[_nI,_nJ])
				Endif
			Next _nJ
			MsUnlock()
		Endif
	Next _nI
	RestArea(_aArea)
Return


