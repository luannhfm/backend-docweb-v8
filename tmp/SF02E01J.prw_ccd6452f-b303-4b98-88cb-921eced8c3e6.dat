#INCLUDE "PROTHEUS.CH"

/*/{Protheus.doc} SF02E01J
Rotina para validar permissao do usuario de alteracao do cadastro
de produtos (SB1) - Executado na Validacao de Usuario do campo B1_MSBLQL
@type function
@author Jose Leite de Barros Neto
@since 	05/05/2016

@return logical, Se Verdadeiro deixa alterar o campo, se Falso nao deixa.
/*/
User Function SF02E01J()
	
	Local _aArea := GetArea()
	Local _aUsrGrp := {}
	Local _cUsrAlt := __cUserId
	Local _cGrpPer := GetNewPar("MV_XALTPRO","000000")
	Local _lReturn := .F.
	Local _lGrpPer := .F.
	Local j := 1

	_aUsrGrp := UsrRetGrp( UsrRetName( _cUsrAlt ), _cUsrAlt )
	
	If IsBlind() .Or. cModulo $ "CTB" .OR. _aUsrGrp[1] == '000000'
		_lReturn 	:= .T.
	Else
		If ALTERA
			
			For j := 1 To Len(_aUsrGrp)
				If _aUsrGrp[j] $ _cGrpPer
					_lGrpPer := .T.
					Exit
				EndIf
			Next
			
			If _lGrpPer
				If M->B1_MSBLQL == '2'	//1=Sim;2=Não
					_lReturn := _lGrpPer
				else
					_lReturn := .T.
				EndIf
			Else
				_lReturn := .F.
			EndIf
			
			If .Not. _lReturn
				MsgInfo("Usuário sem permissão para realizar esta ação!","Atenção.")			
			EndIf
		
		EndIf
		
	EndIf
	
	RestArea(_aArea)
	
Return( _lReturn )
