#INCLUDE "TOTVS.CH"
#INCLUDE "RWMAKE.CH"

/*/{Protheus.doc} MT131FIL
	@description O Ponto de Entrada MT131FIL permite adicionar dados ao fazer o filtro das 
				  Solicitações de Compra que deverão gerar cotação.
	@type User Function
	@author Carlos Ryve Gandini
	@since 27/02/19
	@version 1.0
	@param param, param_type, param_descr
	@return aReturn, Array, Filtro em expressão ADVPL e SQL [ 1 ] - Expressão ADVPL [ 2 ] - Expressão SQL
	@see (http://tdn.totvs.com/display/public/PROT/TUCB70_DT_PONTO_DE_ENTRADA_MT131FIL)
	/*/
User Function MT131FIL()

	Local aArea   := GetArea()
	Local aReturn := {}
	Local cCodUs  := RetCodUsr()
	Local cCodCom := AllTrim(GetMV("MV_XCODCOM",.f.,"051"))
	Local cComAtu := Posicione( "SY1", 3, xFilial("SY1") + cCodUs, "Y1_COD" )

	cComAtu := Iif(Empty(cComAtu), "ZZZ", cComAtu)
	If cComAtu $ cCodCom
		/* Se for usuario lider do compras podera visualizar todos 				*/
		/* Permite SC sem TR													*/
		/* Filtrar TR Aprovada NS J2a											*/
		aAdd( aReturn , "( Empty(C1_XSTTR) .Or. C1_XSTTR == 'A' )" )
		aAdd( aReturn , "( C1_XSTTR = ' ' OR C1_XSTTR = 'A' )" )
	Else
		/* Se não for usuario lider do compras podera visualizar somento o seu 	*/
		/* Permite SC sem TR													*/
		/* Filtrar TR Aprovada NS J2a											*/
		aAdd( aReturn , "( Empty(C1_XSTTR) .Or. C1_XSTTR == 'A' ) .AND. (C1_CODCOMP = '"+ cComAtu +"') " )
		aAdd( aReturn , "( C1_XSTTR = ' ' OR C1_XSTTR = 'A' ) AND (C1_CODCOMP = '"+ cComAtu +"') " )
	EndIf

	RestArea(aArea)

Return( aReturn )
