#INCLUDE "PROTHEUS.CH"
#INCLUDE "TOPCONN.CH"
#INCLUDE "RWMAKE.CH"
#INCLUDE "FONT.CH"
#INCLUDE "COLORS.CH"
#INCLUDE "PARMTYPE.CH"
#INCLUDE "FWMBROWSE.CH"
#INCLUDE "FWMVCDEF.CH"
#INCLUDE "FWCOMMAND.CH"
/*
--------------------------------------------------------------------------------
{Protheus.doc} <SS7332X>
 32 - Clientes com e sem Atendimento   

@author<Antonio Dantas>
@since<14/04/2015>
@version<1.00>
@return<Nil>
@example<Nil>
@see<Nil>
--------------------------------------------------------------------------------
*/
User Function SS7332X()
Local _oReport			:= Nil     
Private _lCancel			:= .f.  
Private _cPerg 			:= ""
Private _cAlias			:= GetNextAlias()   
Private _cAceAll   		:= SuperGetMv("MV_XACEALL",,"")       
//+------------------------------------------------------------------+
//| Verifica se o operador esta logado na filail DR que tem acesso   |
//| a todas as filiais, ou somente na filial em que esta logada.     |
//+------------------------------------------------------------------+
_cPerg	:= FunName()			//-- Define o Grupo de Perguntas   
//+------------------------------------------------------------------+
//|   Interface de impressao                                         |
//+------------------------------------------------------------------+
_oReport := ReportDef()
If !_lCancel
	_oReport:PrintDialog()
Endif	    
Return


/*
--------------------------------------------------------------------------------
{Protheus.doc} <ReportDef>
 32 - Clientes com e sem Atendimento   

@author<Antonio Dantas>
@since<14/04/2015>
@version<1.00>
@receive<Nil>
@return<Nil>
@example<Nil>
@see<Nil>
--------------------------------------------------------------------------------
*/
Static Function ReportDef()
Local _cTitulo		:= "32 - Clientes com e sem Atendimento"
Local _cSubTit		:= ""  
Local _oReport		:= Nil
//--
Local _oService		:= Nil
//+------------------------------------------------------------------+
//| Carrega as perguntas relativas aos parametros de Impressao       |
//|    Mv_par01 - Filial DE?                                         |
//|    Mv_par02 - Filial ATE?                                        |
//|    Mv_par03 - Polo de Venda DE?                                  |
//|    Mv_par04 - Polo de Venda ATE?                                 |
//|    Mv_par05 - Vendedor DE?                                       |
//|    Mv_par06 - Vendedor ATE?                                      |
//|    Mv_par07 - Cliente DE?                                        |
//|    Mv_par08 - Cliente ATE?                                       |
//|    Mv_par09 - Produto DE?                                        |
//|    Mv_par10 - Produto ATE?                                       |
//|    Mv_par11 - Data da Visita DE ?                                |
//|    Mv_par12 - Data da Visita ATE ?                               |
//|    Mv_par13 - Unidade Executora DE?                              |
//|    Mv_par14 - Unidade Executora ATE?                             |
//|    Mv_par15 - Com/Sem Atendimento ?                              |
//|    Mv_par16 - Com Atendimento ÚNICO ?                            |
//|    Mv_par17 - Pertence ao TOP 500 ?                              |
//|    Mv_par18 - Pertence ao TOP 250 ?                              |
//|    Mv_par19 - Pertence ao TOP 20 ?                               |
//+------------------------------------------------------------------+
xParPerg(_cPerg)      
//+------------------------------------------------------------------+
//|  Monta tela de paramentos para usuario, se cancelar sair         |
//+------------------------------------------------------------------+
If !Pergunte(_cPerg,.t.)  
	_lCancel := .T. 
   Return
Endif 
//+------------------------------------------------------------------+
//| Verifica se o operador esta logado na filail DR que tem acesso   |
//| a todas as filiais, ou somente na filial em que esta logada.     |
//+------------------------------------------------------------------+
If !(cFilAnt $ _cAceAll)
	Mv_par01	:= cFilAnt
	Mv_par02	:= cFilAnt
Endif 
//-- Defini o sub-titulo do Relatorio 
_cSubTit := " - Periodo [De: "+DtoC(Mv_par11)+"Ate: "+DtoC(Mv_par12)+"] "  
//+------------------------------------------------------------------+
//|   Seleciona a tabela temporaria que sera referencia              | 
//+------------------------------------------------------------------+
FWMsgRun(, {|| u_fGet332X() }, "Processando a Consulta das Agendas e Apontamentos!", "Aguarde...")
//+------------------------------------------------------------------+
//|    Construcao do Objeto de Impressao do Relatorio                |
//+------------------------------------------------------------------+
//                    New(cReport,cTitle,uParam,bAction,cDescription,lLandscape,uTotalText,lTotalInLine,cPageTText,lPageTInLine,lTPageBreak,nColSpace)
_oReport := TReport():New(_cPerg,_cTitulo,_cPerg,{|_oReport|  ReportPrint(_oReport) },_cSubTit,.t.)
_oReport:SetLandScape() 			//-- Modo Paisagem 
//&&--  _oReport:SetPortrait() 			//-- Modo Retrato 
//+------------------------------------------------------------------+
//|    *****      S e c a o  -  1      *****                         |
//+------------------------------------------------------------------+
_oService := TRSection():New(_oReport,"Atendimentos",{"SA3"} )
TRCell():New(_oService	,"AD5_FILIAL"		,"AD5" 	,"Filial"			,"@!"	,20		,.F.) 
TRCell():New(_oService	,"A1_CGC"			,"SA1" 	,"CGC/CPF"			,"@!"  ,30		,.F.)    
TRCell():New(_oService	,"A1_NOME"			,"SA1" 	,"Nome"			,"@!"	,80 	,.F.)   
TRCell():New(_oService	,"A1_XTOPJUR"		,"SA1" 	,"T-20"			,"@!"  ,8		,.F.) 
TRCell():New(_oService	,"A1_XTOPDN"		,"SA1" 	,"T-250"			,"@!" 	,8		,.F.) 
TRCell():New(_oService	,"A1_XTOPUF"		,"SA1" 	,"T-500"			,"@!"	,8		,.F.) 
If Mv_par16 == 2		//-- Mv_par16 - Com Atendimento ÚNICO ?                             
	TRCell():New(_oService	,"SA3VEND"			,"SA3" 	,"Representante"	,"@!"	,30		,.F.) 
	TRCell():New(_oService	,"AD5UNID"			,"AD5" 	,"Unid.Executora"	,"@!"	,15		,.F.) 
	TRCell():New(_oService	,"AD5PROD"			,"AD5" 	,"Produto"			,"@!"	,90		,.F.) 
Endif 
TRCell():New(_oService	,"AD5_DATA"		,"AD5" 	,"Data"			,"@!"	,10		,.F.)  
Return _oReport

/*
+--------------------------------------------------------------------------+
 {Protheus.doc}<ReportPrint>
 32 - Clientes com e sem Atendimento   

@Author<Antonio Dsantas>
@since<14/04/2015>
@version<1.00>
@receive<Nil>
@return<Nil>
@example<Nil>
@see<Nil>
+--------------------------------------------------------------------------+
*/
Static Function ReportPrint(_oReport)
Local _oService		:= _oReport:Section(1)
Local _nRecs		:= 0
Local _nQtdeCtr 	:= 0
Local _cCNPJ		:= ""
Local _cDescCli		:= ""
Local _cDescVend	:= ""  
Local _cDescUnid	:= "" 
Local _cDescProd	:= ""
//+------------------------------------------------------------------+
//| Posiciona no inicio o Arquivo Resultado da Consulta Sql          | 
//+------------------------------------------------------------------+
dbSelectArea(_cAlias)  
(_cAlias)->(dbgotop())    
//-- Conta o numero do Registro da Tabela Temporaria
(_cAlias)->(dbEval({|| _nRecs++ }))
_oReport:SetMeter(_nRecs)
//-- 
dbSelectArea(_cAlias)  
(_cAlias)->(dbgotop())
//+------------------------------------------------------------------+
//| Inicia Laco para impressao do Relatorio                          |
//+------------------------------------------------------------------+
_oService:Init()
_oReport:SetLeftMargin(3)		//-- Seta a Margem a partir da posição 2
Do While (_cAlias)->(!eof())   
	_oReport:IncMeter()    
	//-- Mv_par16 - Com Atendimento ÚNICO
	_nQtdeCtr := 0
	If Mv_par16 == 1 
		_nQtdeCtr := fQtdeCtr(Mv_par01,Mv_par02, Mv_par03,Mv_par04,(_cAlias)->AD5_CODCLI,(_cAlias)->AD5_LOJA,Mv_par11,Mv_par12)
		If _nQtdeCtr > 1 
			(_cAlias)->(dbskip()) 	
			Loop  
		Endif 
	Endif 
	_cCNPJ		:= Transform((_cAlias)->A1_CGC,Iif(Len(AllTrim((_cAlias)->A1_CGC))==11,"@R 999.999.999-99","@R 99.999.999/9999-99"))
	_cDescCli	:= (_cAlias)->AD5_CODCLI+"/"+(_cAlias)->AD5_LOJA+"-"+Substr((_cAlias)->A1_NOME,1,60)
	_cDescVend	:= Alltrim((_cAlias)->AD5_VEND)+"-"+Substr(Posicione("SA3",1,FwxFilial("SA3")+(_cAlias)->AD5_VEND,"A3_NOME"),1,20)  
	_cDescUnid	:= Alltrim((_cAlias)->AD6_XUNEXE)+"-"+Posicione("SM0",1,cEmpAnt+(_cAlias)->AD6_XUNEXE,"M0_FILIAL") 
	_cDescProd	:= Alltrim((_cAlias)->AD6_CODPRO)+"-"+Substr(Posicione("SB1",1,FwxFilial("SB1")+(_cAlias)->AD6_CODPRO,"B1_DESC"),1,70)
	//+------------------------------------------------------------------+
	//|  Imprime a IDENTIFICAÇÃO DO VENDEDOR                             |
	//+------------------------------------------------------------------+
	_oService:Cell( "AD5_FILIAL"	):SetBlock({|| (_cAlias)->AD5_FILIAL  			}) 
	_oService:Cell( "A1_CGC"			):SetBlock({|| _cCNPJ				 			})    
	_oService:Cell( "A1_NOME"		):SetBlock({|| _cDescCli				 			})   
	_oService:Cell( "A1_XTOPJUR"	):SetBlock({|| Iif((_cAlias)->A1_XTOPJUR="S","Sim","Não")	}) 
	_oService:Cell( "A1_XTOPDN"		):SetBlock({|| Iif((_cAlias)->A1_XTOPDN="S","Sim","Não")	}) 
	_oService:Cell( "A1_XTOPUF"		):SetBlock({|| Iif((_cAlias)->A1_XTOPUF="S","Sim","Não")	}) 
	If Mv_par16 == 2		//-- Mv_par16 - Com Atendimento ÚNICO ?                             
		_oService:Cell( "SA3VEND"		):SetBlock({|| _cDescVend  					}) 
		//-- _oService:Cell( "AD5UNID"		):SetBlock({|| _cDescUnid 					})
		_oService:Cell( "AD5UNID"		):SetBlock({|| (_cAlias)->AD6_XUNEXE		})
		_oService:Cell( "AD5PROD"		):SetBlock({|| _cDescProd 					}) 
	Endif 
	_oService:Cell( "AD5_DATA"		):SetBlock({|| Dtoc((_cAlias)->AD5_DATA)  					})  
	_oService:PrintLine()		//-- Imprime a Linha 
	//+------------------------------------------------------------------+
	//|  Vai para o Proximo Registro da TABELA TEMPORARIA                | 
	//|  ***Estabelece os Controles de Quebra                            |
	//+------------------------------------------------------------------+
	(_cAlias)->(dbskip()) 	
Enddo 
//+------------------------------------------------------------------+
//|     Finaliza todas as sessões por definitivo                     |
//+------------------------------------------------------------------+
_oService:Finish()
//+------------------------------------------------------------------+
//|     Fecha o Arquivo de Trabalho                                  |
//+------------------------------------------------------------------+
(_cAlias)->(dbCloseArea())
//+------------------------------------------------------------------+
//|     Finaliza as sessoes                                          |
//+------------------------------------------------------------------+
Return


/*
+--------------------------------------------------------------------------+
{Protheus.doc}<fGet332X>
 Monta query que busca as matriculas.

@Author<Antonio Dsantas>
@since<10/04/2015>
@version<1.00>
@receive<Nil>
@return<Nil>
@example<Nil>
@see<Nil>
+--------------------------------------------------------------------------+
*/
User Function fGet332X()
Local _cQuery		:= "" 
_cQuery := ""
_cQuery += "Select "+CRLF
_cQuery += "AD5.AD5_FILIAL, AD5.AD5_VEND, AD5.AD5_DATA, AD5.AD5_SEQUEN, AD5.AD5_CODCLI, AD5.AD5_LOJA, "+CRLF
_cQuery += "SA1.A1_CGC, SA1.A1_NOME, SA1.A1_XTOPJUR, SA1.A1_XTOPDN, SA1.A1_XTOPUF, SA1.A1_PESSOA, "+CRLF
_cQuery += "AD6.AD6_ITEM, AD6.AD6_CODPRO, AD6.AD6_QUANT, AD6.AD6_VLUNIT, AD6.AD6_TOTAL, AD6.AD6_XUNEXE, "+CRLF
_cQuery += "AD1.AD1_NROPOR, AD1.AD1_STATUS, AD1.AD1_DTASSI, "+CRLF
//+------------------------------------------------------------------+
//| Antonio Dantas                                        09/02/2019 |
//| Substituicao do bloco abaixo para adequacao do relatorio a nova  |
//| tabela de Agenda (Atividades) [AOF] que substitui a tabela [ADF].|
//+------------------------------------------------------------------+
//--&& _cQuery += "AD7.AD7_TOPICO "+CRLF
_cQuery += "AOF.AOF_ASSUNT "+CRLF
_cQuery += "From "+RetSqlName("AD5")+" AD5 "+CRLF
_cQuery += "Inner Join "+RetSqlName("AD6")+" AD6 on AD6.D_E_L_E_T_ = ' ' "+CRLF  
_cQuery += "             And AD6.AD6_FILIAL = AD5.AD5_FILIAL "+CRLF 
_cQuery += "             And AD6.AD6_VEND = AD5.AD5_VEND "+CRLF 
_cQuery += "             And AD6.AD6_DATA = AD5.AD5_DATA "+CRLF
_cQuery += "             And AD6.AD6_SEQUEN = AD5.AD5_SEQUEN "+CRLF
_cQuery += "             And AD6.AD6_CODPRO BetWeen '"+Mv_par09+"' And '"+Mv_par10+"' "+CRLF
_cQuery += "             And AD6.AD6_XUNEXE BetWeen '"+Mv_par13+"' And '"+Mv_par14+"' "+CRLF
_cQuery += "Inner Join "+RetSqlName("AD1")+" AD1 on AD1.D_E_L_E_T_ = ' ' "+CRLF 
_cQuery += "             And AD1.AD1_FILIAL = AD5.AD5_FILIAL "+CRLF 
_cQuery += "             And AD1.AD1_NROPOR = AD5.AD5_NROPOR "+CRLF
//-- Mv_par15 =     Com/Sem Atendimento 
//+------------------------------------------------------------------+
//| Antonio Dantas                                        09/02/2019 |
//| Ajuste do bloco abaixo para implementar os novos STATUS para     |
//| proposta.                                                        |
//+------------------------------------------------------------------+
//--&&  _cQuery += "             And AD1.AD1_STATUS "+Iif(Mv_par15 == 1,"=","<>")+" '9' "+CRLF
If MV_PAR15 == 1
	_cQuery += "             And AD1.AD1_STATUS in ('9','E','F') "+CRLF
Else
	_cQuery += "             And AD1.AD1_STATUS not in ('9','E','F') "+CRLF
Endif
//+------------------------------------------------------------------+
//| Antonio Dantas                                        09/02/2019 |
//| Substituicao do bloco abaixo para adequacao do relatorio a nova  |
//| tabela de Agenda (Atividades) [AOF] que substitui a tabela [ADF].|
//+------------------------------------------------------------------+
//--&&  _cQuery += "Inner Join "+RetSqlName("AD7")+" AD7 on AD7.D_E_L_E_T_ = ' ' "+CRLF 
//--&&  _cQuery += "             And AD7.AD7_FILIAL = AD5.AD5_FILIAL "+CRLF 
//--&&  _cQuery += "             And AD7.AD7_NROPOR = AD5.AD5_NROPOR "+CRLF
_cQuery += "Inner Join "+RetSqlName("AOF")+" AOF on AOF.D_E_L_E_T_ = ' ' "+CRLF 
_cQuery += "             And AOF.AOF_FILIAL = AD5.AD5_FILIAL "+CRLF 
_cQuery += "             And AOF_ENTIDA = 'AD1' "+CRLF
_cQuery += "             And Substr(AOF.AOF_CHAVE,1,"+Alltrim(Str(TamSX3("AD5_NROPOR")[1]))+") = Substr(AD5.AD5_NROPOR,1,"+Alltrim(Str(TamSX3("AD5_NROPOR")[1]))+") "+CRLF 
_cQuery += "Inner Join "+RetSqlName("SA1")+" SA1 on SA1.D_E_L_E_T_ = ' ' "+CRLF
_cQuery += "             And SA1.A1_FILIAL = '"+FwxFilial("SA1")+"' "+CRLF
_cQuery += "             And SA1.A1_COD = AD5.AD5_CODCLI "+CRLF
_cQuery += "             And SA1.A1_LOJA = AD5.AD5_LOJA "+CRLF
//-- Mv_par15 =     Com/Sem Atendimento 
If Mv_par15 == 2 
	_cQuery += "             And SA1.A1_PESSOA = 'J' "+CRLF
Endif 
/*	03-06-15 - Jose Leite - CSI
	Ajuste na pergunta para informar se quer trazer todos os clientes
	SIM/NAO/TODOS
*/
//Inicio
If Mv_par17 <> 3
	//-- TOP 500 
	_cQuery += "             And SA1.A1_XTOPUF in ("+Iif(Mv_par17=1,"'S'","' ','N'")+") "+CRLF
EndIf

If Mv_par18 <> 3 
	//-- TOP 250
	_cQuery += "             And SA1.A1_XTOPDN in ("+Iif(Mv_par18=1,"'S'","' ','N'")+") "+CRLF
EndIf 

If Mv_par19 <> 3
	//-- TOP 20
	_cQuery += "             And SA1.A1_XTOPJUR in ("+Iif(Mv_par19=1,"'S'","' ','N'")+") "+CRLF
EndIf 
//Fim
_cQuery += "Where AD5.D_E_L_E_T_ = ' ' "+CRLF 
_cQuery += "And AD5.AD5_FILIAL BetWeen '"+Mv_par01+"' And '"+Mv_par02+"' "+CRLF
//--&& _cQuery += "And Substr(AD5.AD5_FILIAL,1,4) = '"+Substr(cFilAnt,1,4)+"' "+CRLF
_cQuery += "And AD5.AD5_XPOLO BetWeen '"+Mv_par03+"' And '"+Mv_par04+"' "+CRLF
_cQuery += "And AD5.AD5_VEND  BetWeen '"+Mv_par05+"' And '"+Mv_par06+"' "+CRLF
_cQuery += "And AD5.AD5_CODCLI BetWeen '"+Mv_par07+"' And '"+Mv_par08+"' "+CRLF
_cQuery += "And AD5.AD5_DATA BetWeen  '"+Dtos(Mv_par11)+"' And '"+Dtos(Mv_par12)+"' "+CRLF
_cQuery += "Order By AD5.AD5_FILIAL, AD5.AD5_DATA, AD5.AD5_SEQUEN, AD5.AD5_CODCLI, AD5.AD5_LOJA, AD6.AD6_CODPRO "+CRLF
//+------------------------------------------------------------------+
//| Grava o Resultado da Query para consulta                         |
//+------------------------------------------------------------------+
MemoWrite("C:\temp\"+FunName()+"_"+ProcName()+".TXT",_cQuery)
If Select(_cAlias)>0
	(_cAlias)->(DbCloseArea())
Endif
DbUseArea(.t.,"TOPCONN",TcGenQry(,,_cQuery),_cAlias,.t.,.t.)
//+------------------------------------------------------------------+
//| Converte os campos tipo DATA                                     |
//+------------------------------------------------------------------+
TCSETFIELD( _cAlias, "AD5_DATA"		,"D",8,0 )
TCSETFIELD( _cAlias, "AD1_DTASSI"	,"D",8,0 )
Return .t.



/*
+--------------------------------------------------------------------------+
{Protheus.doc}<fQtdeCtr>
 Devolve o numero de Propostas Assinadas pelo CLIENTE/LOJA no perido, 
 Filial, Polo e Periodos conforme informado por parametro. 

@Author<Antonio Dsantas>
@since<10/04/2015>
@version<1.00>
@receive
<     _cFilDE (c) - Filial DE 
     _cFilATE (c) - Filial ATE
     _cPoloDE (c) - Polo de Venda DE
    _cPoloATE (c) - Polo de Venda ATE
    _cCliente (c) - Codigo do Cliente
    _cLojaCli (c) - Lola do Cliente 
     _dDataDE (d) - Data DE 
    _dDataATE (d) - Data ATE
>
@return
<      _nQdet (n) - Quantidade de Propostas Assinadas 
>
@example<Nil>
@see<Nil>
+--------------------------------------------------------------------------+
*/
Static Function fQtdeCtr(_cFilDE,_cFilATE,_cPoloDE,_cPoloATE,_cCliente,_cLojaCli,_dDataDE,_dDataATE)
Local _cQuery		:= "" 
Local _nQdet		:= 0
_cQuery := ""
_cQuery += "Select "+CRLF
_cQuery += "AD5.AD5_FILIAL, AD5.AD5_DATA, AD5.AD5_SEQUEN, AD5.AD5_CODCLI, AD5.AD5_LOJA, "+CRLF
_cQuery += "AD1.AD1_NROPOR, AD1.AD1_STATUS, AD1.AD1_DTASSI "+CRLF
_cQuery += "From "+RetSqlName("AD5")+" AD5 "+CRLF
_cQuery += "Inner Join "+RetSqlName("AD1")+" AD1 on AD1.D_E_L_E_T_ = ' ' "+CRLF 
_cQuery += "             And AD1.AD1_FILIAL = AD5.AD5_FILIAL "+CRLF 
_cQuery += "             And AD1.AD1_NROPOR = AD5.AD5_NROPOR "+CRLF
//+------------------------------------------------------------------+
//| Antonio Dantas                                        09/02/2019 |
//| Ajuste do bloco abaixo para implementar os novos STATUS para     |
//| proposta.                                                        |
//+------------------------------------------------------------------+
//--&&  _cQuery += "             And AD1.AD1_STATUS In ('3','9') "+CRLF
_cQuery += "             And AD1.AD1_STATUS In ('3','9','E','F') "+CRLF
_cQuery += "Where AD5.D_E_L_E_T_ = ' ' "+CRLF 
_cQuery += "And AD5.AD5_FILIAL BetWeen '"+_cFilDE+"' And '"+_cFilATE+"' "+CRLF
_cQuery += "And Substr(AD5.AD5_FILIAL,1,4) = '"+Substr(cFilAnt,1,4)+"' "+CRLF
_cQuery += "And AD5.AD5_XPOLO BetWeen '"+_cPoloDE+"' And '"+_cPoloATE+"' "+CRLF
_cQuery += "And AD5.AD5_CODCLI = '"+_cCliente+"' "+CRLF
_cQuery += "And AD5.AD5_LOJA = '"+_cLojaCli+"' "+CRLF
_cQuery += "And AD5.AD5_DATA BetWeen  '"+Dtos(_dDataDE)+"' And '"+Dtos(_dDataATE)+"' "+CRLF
_cQuery += "Order By AD5.AD5_FILIAL, AD5.AD5_DATA, AD5.AD5_SEQUEN, AD5.AD5_CODCLI, AD5.AD5_LOJA "+CRLF
//+------------------------------------------------------------------+
//| Grava o Resultado da Query para consulta                         |
//+------------------------------------------------------------------+
MemoWrite("C:\temp\"+FunName()+"_"+ProcName()+".TXT",_cQuery)
If Select("TMPQTCR")>0
	TMPQTCR->(DbCloseArea())
Endif
DbUseArea(.t.,"TOPCONN",TcGenQry(,,_cQuery),"TMPQTCR",.t.,.t.)
Do While TMPQTCR->(!Eof()) .and. TMPQTCR->(!Bof())
	_nQdet++
	TMPQTCR->(dbSkip())	
Enddo  
TMPQTCR->(DbCloseArea())
Return _nQdet



/*
--------------------------------------------------------------------------------
{Protheus.doc} <xParPerg>
 Cria as perguntas.	  

@author<Antonio Dantas>
@since<13/02/2015>
@version<1.00>
@receive<Nil>
@return<Nil>
@example<Nil>
@see<Nil>
--------------------------------------------------------------------------------
*/
Static Function xParPerg()	
Local _aPerg := {}
//*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*
//*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*
//   //-- MAPEAMENTOS DOS CAMPOS DE SX1
//   aAdd (_aPerg,{"X1_PERGUNT","X1_PERPA","X1_PERENG","MV_CHL","C",5,0,0,"X1_GSC","X1_VALID",;
//								"X1_VAR01",("X1_DEF01" e "X1_PYNE"),"X1_DEFSPA1","X1_DEFENG1","X1_CNT01",;
//								"X1_VAR02","X1_DEF02","X1_DEFSPA2","X1_DEFENG2","X1_CNT02",;
//								"X1_VAR03","X1_DEF03","X1_DEFSPA3","X1_DEFENG3","X1_CNT03",;
//								"X1_VAR04","X1_DEF04","X1_DEFSPA4","X1_DEFENG4","X1_CNT04",;
//								"X1_VAR05","X1_DEF05","X1_DEFSPA5","X1_DEFENG5","X1_CNT05",;
//								"X1_F3","X1_GRPSXG","","X1_HELP","X1_PICTURE",{"Descricao para Tela PTG"},{"Descricao para Tela EPA"},{"Descricao para Tela USA"}})
//*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*
//*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*
//+------------------------------------------------------------------+
//| Verifica se o operador esta logado na filail DR que tem acesso   |
//| a todas as filiais, ou somente na filial em que esta logada.     |
//+------------------------------------------------------------------+
//-- Mv_par01 =     Filial DE?
aAdd(_aPerg, {"Filial DE?","Filial DE?","Filial DE?","MV_CH1","C",TamSX3("AD5_FILIAL")[1],0,0,"G","u_fVldfil()",;
					"MV_PAR01","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"SM0EPU","","","","",{"Codigo da Filial"},{"Codigo da Filial"},{"Codigo da Filial"}})

//-- Mv_par02 =     Filial ATE?
aAdd(_aPerg, {"Filial ATE?","Filial ATE?","Filial ATE?","MV_CH2","C",TamSX3("AD5_FILIAL")[1],0,0,"G","u_fVldfil()",;
					"MV_PAR02","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"SM0EPU","","","","",{"Codigo da Filial"},{"Codigo da Filial"},{"Codigo da Filial"}})

//-- Mv_par03 =     Polo de Venda DE?
aAdd(_aPerg, {"Polo de Venda DE?","Polo de Venda DE?","Polo de Venda DE?","MV_CH3","C",TamSX3("AD7_XPOLO")[1],0,0,"G","",;
					"MV_PAR03","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"ZCD","","","","",{"Cod do Polo de Venda DE"},{"Cod do Polo de Venda DE"},{"Cod do Polo de Venda DE"}})

//-- Mv_par04 =     Polo de Venda ATE?
aAdd(_aPerg, {"Polo de Venda ATE?","Polo de Venda ATE?","Polo de Venda ATE?","MV_CH4","C",TamSX3("AD7_XPOLO")[1],0,0,"G","",;
					"MV_PAR04","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"ZCD","","","","",{"Cod do Polo de Venda ATE"},{"Cod do Polo de Venda ATE"},{"Cod do Polo de Venda ATE"}})

//-- Mv_par05 =     Vendedor DE?
aAdd(_aPerg, {"Vendedor DE?","Vendedor DE?","Vendedor DE?","MV_CH5","C",TamSX3("A3_COD")[1],0,0,"G","",;
					"MV_PAR05","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"SA3","","","","",{"Codigo do Vendedor"},{"Codigo do Vendedor"},{"Codigo do Vendedor"}})

//-- Mv_par06 =     Vendedor ATE?
aAdd(_aPerg, {"Vendedor ATE?","Vendedor ATE?","Vendedor ATE?","MV_CH6","C",TamSX3("A3_COD")[1],0,0,"G","",;
					"MV_PAR06","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"SA3","","","","",{"Codigo do Vendedor"},{"Codigo do Vendedor"},{"Codigo do Vendedor"}})

//-- Mv_par07 =     Cliente DE?
aAdd(_aPerg, {"Cliente DE?","Cliente DE?","Cliente DE?","MV_CH7","C",TamSX3("A1_COD")[1],0,0,"G","",;
					"MV_PAR07","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"SA1","","","","",{"Codigo do Cliente"},{"Codigo do Cliente"},{"Codigo do Cliente"}})

//-- Mv_par08 =     Cliente ATE?
aAdd(_aPerg, {"Cliente ATE?","Cliente ATE?","Cliente ATE?","MV_CH8","C",TamSX3("A1_COD")[1],0,0,"G","",;
					"MV_PAR08","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"SA1","","","","",{"Codigo do Cliente"},{"Codigo do Cliente"},{"Codigo do Cliente"}})     

//-- Mv_par09 =     Produto DE?
aAdd(_aPerg, {"Produto DE?","Produto DE?","Produto DE?","MV_CH9","C",TamSX3("B1_COD")[1],0,0,"G","",;
					"MV_PAR09","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"SB1","","","","",{"Codigo do Produto"},{"Codigo do Produto"},{"Codigo do Produto"}})

//-- Mv_par10 =     Produto ATE?
aAdd(_aPerg, {"Produto ATE?","Produto ATE?","Produto ATE?","MV_CHA","C",TamSX3("B1_COD")[1],0,0,"G","",;
					"MV_PAR10","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"SB1","","","","",{"Codigo do Produto"},{"Codigo do Produto"},{"Codigo do Produto"}})     

//-- Mv_par11 =     Data da Visita DE ?
aAdd(_aPerg, {"Data da Visita DE ?","Data da Visita DE ?","Data da Visita DE ?","MV_CHB","D",8,0,0,"G","",;
					"MV_PAR11","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",{"Data da Visita DE ?"},{"Data da Visita DE ?"},{"Data da Visita DE ?"}})

//-- Mv_par12 =     Data da Visita ATE ?
aAdd(_aPerg, {"Data da Visita ATE ?","Data da Visita ATE ?","Data da Visita ATE ?","MV_CHC","D",8,0,0,"G","",;
					"MV_PAR12","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",{"Data da Visita ATE ?"},{"Data da Visita ATE ?"},{"Data da Visita ATE ?"}})

//-- Mv_par13 =     Unidade Executora DE?
aAdd(_aPerg, {"Unidade Executora DE ?","Unidade Executora DE ?","Unidade Executora DE ?","MV_CHD","C",TamSX3("AD5_FILIAL")[1],0,0,"G","u_fVldfil()",;
					"MV_PAR13","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"SM0EPU","","","","",{"Unidade Executora DE?"},{"Unidade Executora DE?"},{"Unidade Executora DE?"}})

//-- Mv_par14 =     Unidade Executora ATE?
aAdd(_aPerg, {"Unidade Executora ATE?","Unidade Executora ATE?","Unidade Executora ATE?","MV_CHE","C",TamSX3("AD5_FILIAL")[1],0,0,"G","u_fVldfil()",;
					"MV_PAR14","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"SM0EPU","","","","",{"Unidade Executora"},{"Unidade Executora"},{"Unidade Executora"}})

//-- Mv_par15 =     Com/Sem Atendimento ?
aAdd(_aPerg, {"Com/Sem Atendimento ?","Com/Sem Atendimento ?","Com/Sem Atendimento ?","MV_CHF","N",1,0,1,"C",,;
					"MV_PAR15","Com Atendimento","Com Atendimento","Com Atendimento","",;
					"","Sem Atendimento","Sem Atendimento","Sem Atendimento","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",{"Com/Sem Atendimento ?"},{"Com/Sem Atendimento ?"},{"Com/Sem Atendimento ?"}})

//-- Mv_par16 =     Com Atendimento ÚNICO ?
aAdd(_aPerg, {"Com Atendimento ÚNICO ?","Com Atendimento ÚNICO ?","Com Atendimento ÚNICO ?","MV_CHG","N",1,0,1,"C",,;
					"MV_PAR16","Sim","Sim","Sim","",;
					"","Não","Não","Não","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",{"Com/Sem Atendimento ?"},{"Com/Sem Atendimento ?"},{"Com/Sem Atendimento ?"}})

//-- Mv_par17 =     Pertence ao TOP 500 ?
aAdd(_aPerg, {"Pertence ao TOP 500 ?","Pertence ao TOP 500 ?","Pertence ao TOP 500 ?","MV_CHH","N",1,0,1,"C",,;
					"MV_PAR17","Sim","Sim","Sim","",;
					"","Não","Não","Não","",;
					"","Todos","Todos","Todos","",;
					"","","","","",;
					"","","","","",;
					"","","","","",{"Pertence ao TOP 500 ?"},{"Pertence ao TOP 500 ?"},{"Pertence ao TOP 500 ?"}})

//-- Mv_par18 =     Pertence ao TOP 250 ?
aAdd(_aPerg, {"Pertence ao TOP 250 ?","Pertence ao TOP 250 ?","Pertence ao TOP 250 ?","MV_CHI","N",1,0,1,"C",,;
					"MV_PAR18","Sim","Sim","Sim","",;
					"","Não","Não","Não","",;
					"","Todos","Todos","Todos","",;
					"","","","","",;
					"","","","","",;
					"","","","","",{"Pertence ao TOP 250 ?"},{"Pertence ao TOP 250 ?"},{"Pertence ao TOP 250 ?"}})

//-- Mv_par19 =     Pertence ao TOP 20 ?
aAdd(_aPerg, {"Pertence ao TOP 20 ?","Pertence ao TOP 20 ?","Pertence ao TOP 20 ?","MV_CHJ","N",1,0,1,"C",,;
					"MV_PAR19","Sim","Sim","Sim","",;
					"","Não","Não","Não","",;
					"","Todos","Todos","Todos","",;
					"","","","","",;
					"","","","","",;
					"","","","","",{"Pertence ao TOP 20 ?"},{"Pertence ao TOP 20 ?"},{"Pertence ao TOP 20 ?"}})
u_SfAjuSX1(_cPerg,_aPerg) 
Return 