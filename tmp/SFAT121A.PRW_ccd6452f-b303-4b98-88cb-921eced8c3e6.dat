#Include 'Protheus.ch'

/*/{Protheus.doc} SFAT121A
Gatilho geral da rotina ATFA012 cadastro de ativo
@type Function
@author Alan Teles de Oliveira
@since 02/02/2019
@version 12.1.17
@return undefined, valor para campo de contra domínio.
/*/
User Function SFAT121A()

    Local cCampAtu  := ''
    Local uRet      := Nil

    Private _oModel     := FwModelActive()
    Private _oMdlSN1    := _oModel:GetModel("SN1MASTER")
    Private _oMdlSN3    := _oModel:GetModel("SN3DETAIL")

    cCampAtu := ReadVar()

    Do Case

        Case 'N1_GRUPO'     $ cCampAtu
            uRet := fGatGrupo()

        Case 'N1_DESCRIC'   $ cCampAtu
            uRet := fGatDescricao()

        Case 'N1_VLAQUIS'   $ cCampAtu
            uRet := fGatVlAqui()

        Case 'N1_AQUISIC'   $ cCampAtu
            uRet := fGatAquisi()

    EndCase

Return uRet


/*/{Protheus.doc} fGatGrupo
@description Gatilho do campo grupo
@type  Static Function
@author Alan Teles de Oliveira
@since 02/02/2019
@version 12.1.17
@return cRet, characters, valor para campo de contra domínio.
/*/
Static Function fGatGrupo()

    Local cRet  := ''
    Local nI    := 0
 
    cRet := _oMdlSN1:GetValue('N1_GRUPO')

    If .not. (IsInCallStack('ATFA240') .or. IsInCallStack('MATA103'))

        _oMdlSN1:SetValue('N1_QUANTD',  1)
        _oMdlSN1:SetValue('N1_XTPORCA', '1')

        If Select('SD1') > 0

            _oMdlSN1:SetValue('N1_VLAQUIS', SD1->D1_VUNIT)
            _oMdlSN1:SetValue('N1_XPEDIDO', SD1->D1_PEDIDO)
            _oMdlSN3:SetValue('N3_CCUSTO',  SD1->D1_CC)
            _oMdlSN3:SetValue('N3_SUBCTA',  SD1->D1_ITEMCTA)

        EndIf

        If Select('SC7') > 0
            _oMdlSN1:SetValue('N1_XSOLICI', SC7->C7_NUMSC)
        EndIf

        If Select('SE2') > 0
            _oMdlSN1:SetValue('N1_XDTPGTO', SE2->E2_VENCREA)
        EndIf

        If Select('SB1') > 0
            _oMdlSN1:SetValue('N1_XDSCPRO', SB1->B1_DESC)
        EndIf

        _oMdlSN3:SetValue('N3_AQUISIC', _oMdlSN1:GetValue('N1_AQUISIC'))
        _oMdlSN3:SetValue('N3_VORIG1',  1)
        _oMdlSN3:SetValue('N3_XDSCONT', Posicione("CT1", 1, xFilial('CT1') + _oMdlSN3:GetValue('N3_CCONTAB'), 'CT1_DESC01'))
        
        For nI := 1 to _oMdlSN3:Length()

            _oMdlSN3:GoLine(nI)
            _oMdlSN3:SetValue('N3_HISTOR', _oMdlSN1:GetValue('N1_DESCRIC'))

        Next

    EndIf

Return cRet

/*/{Protheus.doc} fGatGrupo
Gatilho do campo descrição
@type Function
@author Alan Teles de Oliveira
@since 02/02/2019
@version 12.1.17
@return characters, valor para campo de contra domínio.
/*/
Static Function fGatDescricao()

    Local cRet  := ''
    Local nI    := 0

    cRet := _oMdlSN1:GetValue('N1_DESCRIC')

    For nI := 1 to _oMdlSN3:Length()

        _oMdlSN3:GoLine(nI)
        _oMdlSN3:SetValue('N3_HISTOR', _oMdlSN1:GetValue('N1_DESCRIC'))

    Next

Return cRet

/*/{Protheus.doc} fGatGrupo
Gatilho do campo valor de aquisição
@type Function
@author Alan Teles de Oliveira
@since 02/02/2019
@version 12.1.17
@return numeric, valor para campo de contra domínio.
/*/
Static Function fGatVlAqui()

    Local nRet  := 0
    Local nI    := 0

    nRet := _oMdlSN1:GetValue('N1_VLAQUIS')

    For nI := 1 to _oMdlSN3:Length()

        _oMdlSN3:GoLine(nI)
        _oMdlSN3:SetValue('N3_VORIG1',  _oMdlSN1:GetValue('N1_VLAQUIS'))

    Next

Return nRet

/*/{Protheus.doc} fGatGrupo
Gatilho do campo aquisição
@type Function
@author Alan Teles de Oliveira
@since 02/02/2019
@version 12.1.17
@return date, valor para campo de contra domínio.
/*/
Static Function fGatAquisi()

    Local dRet  := StoD('')
    Local nI    := 0

    dRet := _oMdlSN1:GetValue('N1_AQUISIC')

    For nI := 1 to _oMdlSN3:Length()
        
        _oMdlSN3:GoLine(nI)
        _oMdlSN3:SetValue('N3_AQUISIC', _oMdlSN1:GetValue('N1_AQUISIC'))

    Next

Return dRet
