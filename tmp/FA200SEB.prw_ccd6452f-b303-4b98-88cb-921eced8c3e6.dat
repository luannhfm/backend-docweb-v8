#Include 'Protheus.ch'
#Include 'TopConn.ch'

/*/{Protheus.doc} FA200SEB
Ponto de entrada para análise das ocorrências do CNAB - permitir a execução de um tratamento diferenciado
Se T, continua o processo normalmente, se F, Pula a linha que esta sendo executada.

@type 		function
@author 	Jose Leite de Barros Neto
@since 	30/09/2016
@version 	1.0
@return 	_lRet, T ou F
@see 
http://tdn.totvs.com/pages/releaseview.action?pageId=6071390
/*/
User Function FA200SEB()
	
	Local _lRet		:= .T.
	Local _aArea		:= GetArea()
	Local _cFunName	:= AllTrim(FunName())
	Local _cXIdCnab	:= ""
	Local _cNumBco	:= ""

	If _cFunName == "FINA200" //Contas a Receber	
		
		If Select("TRA") > 0
			DbSelectarea("TRA")
			TRA->(DbCloseArea())
		EndIf
		
		_cQuery := " SELECT E1_XIDCNAB, E1_NUMBCO "
		_cQuery += " FROM " + RetSqlName("SE1")
		_cQuery += " WHERE E1_NUMBCO = '"+ cNsNum +"' OR E1_XIDCNAB = '"+ cNsNum +"'	"
		_cQuery += " AND D_E_L_E_T_ <> '*' "
		
		TCQUERY _cQuery NEW ALIAS "TRA"
		
		_cXIdCnab	:= AllTrim(TRA->E1_XIDCNAB)
		_cNumBco	:= AllTrim(TRA->E1_NUMBCO)
		
		If AllTrim(cNsNum) == _cXIdCnab .OR. AllTrim(cNsNum) == _cNumBco
			_lRet := .T.
		Else
			_lRet := .F.
		EndIf
		
		TRA->(DbCloseArea())
		
	EndIf
	
	RestArea(_aArea)

Return( _lRet )