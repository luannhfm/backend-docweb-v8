#Include 'Protheus.ch'

/*/{Protheus.doc} F402SRA
Ponto de entrada para complemento do cadastro de autonomo gerado a partir da 
integracao do modulo Financeiro (FIN) x Gestao Pessoal (GPE) para SEFIP.

@author 	JosÃ© Leite de Barros Neto
@since 		09/05/2014
@version 	1.0

@return Nil,Nulo

@example

@see
http://tdn.totvs.com/display/public/mp/F402SRA+-+Complemento+de+Cadastro+--+93287
/*/
/*/
Franklin B. Oliveira - 30/04/2018
	Adicionado o preenchimento do campo [RA_TPCONTR] conforme preenchimento, no cadastro
	de fornecedor, do campo [A2_XTPCONT].
/*/
User Function F402SRA()

	Local cEstat := U_SFGN001A(ProcName(0), "F402SRA")
	Local _aArea 	:= GetArea()
	Local _cRaNit	:= AllTrim(Posicione("SA2",3,xFilial("SA2")+SRA->RA_CIC,"A2_NIT"))
	Local _cCodFu	:= AllTrim(Posicione("SA2",3,xFilial("SA2")+SRA->RA_CIC,"A2_CODFUNC"))
	Local _cTpConTr	:= AllTrim(Posicione("SA2",3,xFilial("SA2")+SRA->RA_CIC,"A2_XTPCONT"))
	 
	
	If !PARAMIXB[1]
	
		If Val(SRA->RA_MAT) >= 900000
		
			If RecLock("SRA",.F.)
				SRA->RA_CATEG   := "13" 		//Tratamento para Autonomo
				SRA->RA_TIPOPGT := "S" 			//Tipo de Pagamento
				SRA->RA_PIS	  := _cRaNit		//Nit
				SRA->RA_NUMINSC := _cRaNit		//Nit
				SRA->RA_CODFUNC := _cCodFu		//Cod Func
				SRA->RA_TPCONTR := _cTpConTr	//Tp.Cont.Trab - 1=Indeterminado;2=Determinado;3=Intermitente
				SRA->RA_CODRET  := '0588' 		//REND TRAB SEM VINCULO EMPREGATICIO - Autonomos
				MsUnLock()
			EndIf
		
		EndIf

	EndIf
	
	RestArea(_aArea)

Return