#Include 'Protheus.ch'
#INCLUDE 'TOPCONN.CH'
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ SF06R98X ³ Autor ³ Paulo César P.Schwind ³ Data³ 17.05.2019 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Emissão de titulos com histórico de cobrança.               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Alteração ³                                                             ³±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

User Function SF06R98X()
	Local oReport     := nil
	Private cPerg     := Padr("SF06R98X",10)
    Private cTitRel

	ValidPerg()
	Pergunte(cPerg,.T.)
	if LastKey()=27
		Return
	Endif

	If MV_PAR07 == 1
		cTitRel := " - BAIXADOS "
	ElseIf MV_PAR07 == 2
		cTitRel := " - EM ABERTO "
	Endif

	oReport := RptDef(cPerg)
	oReport:PrintDialog()
Return

Static Function RptDef(cNome)
	Local oReport := Nil
	Local oSection1:= Nil
	Local oSection2:= Nil
	
	Local oBreak
	Local oFunction

	oReport := TReport():New(cNome,"Relatório de Titulos com historico de cobranca"+cTitRel,cNome,{|oReport| ReportPrint(oReport)},"Relatório de Titulos com historico de cobranca"+cTitRel,,)
	oReport:SetLandscape()

	oSection1:= TRSection():New(oReport, "Titulos a Receber"          , {"SE1"}, NIL, .F., .T.)
	
    oFilial := TRCell():New(oSection1,"E1_FILIAL" ,"SE1","Emp/Filial")
    oFilial:SetSize(50)	 

	oSection2:= TRSection():New(oReport, "Titulos com historico de cobranca", {"SE1"}, NIL, .F., .T.)
    TRCell():New(oSection2,"E1_CLIENTE","SE1")
    TRCell():New(oSection2,"E1_LOJA","SE1")
    TRCell():New(oSection2,"E1_NOMCLI","SE1")
    TRCell():New(oSection2,"E1_PREFIXO","SE1")
    TRCell():New(oSection2,"E1_NUM"	 ,"SE1")
    TRCell():New(oSection2,"E1_PARCELA","SE1")
    TRCell():New(oSection2,"E1_TIPO"   ,"SE1")
    TRCell():New(oSection2,"E1_EMISSAO","SE1")
    TRCell():New(oSection2,"E1_VENCREA","SE1")
	oDias   := TRCell():New(oSection2,"DIAS" ,"SE1","Atraso","999999")	
	oDias:SetSize(6)
    TRCell():New(oSection2,"E1_VALOR"  ,"SE1")
    TRCell():New(oSection2,"ZF1_DATA"  ,"ZF1")
    TRCell():New(oSection2,"ZF1_HORA"  ,"ZF1")
    TRCell():New(oSection2,"ZF1_AGENDA","ZF1")
    TRCell():New(oSection2,"ZF1_ORIGEM","ZF1")
    oHist := TRCell():New(oSection2,"ZF1_HISTOR","ZF1")
    oHist:SetSize(40)	 
    oUser := oHist := TRCell():New(oSection2,"ZF1_CODUSR","ZF1")
    oUser:SetSize(20)
    TRFunction():New(oSection2:Cell("E1_NUM")  ,,"COUNT")
    TRFunction():New(oSection2:Cell("E1_VALOR"),,"SUM")
    
	oReport:SetTotalInLine(.F.)
	oSection1:SetPageBreak(.F.)
	oSection2:SetTotalText(" ")
					
Return(oReport)

Static Function ReportPrint(oReport)
	Local oSection1 := oReport:Section(1)
	Local oSection2 := oReport:Section(2)
	Local cQuery    := ""
    Local nRegs     := 0
    Local cFilHis   := ""
    Local cTitHis   := ""

	cQuery := " SELECT SE1.E1_FILIAL,SE1.E1_NUM, SE1.E1_PREFIXO, SE1.E1_PARCELA, SE1.E1_BAIXA, SE1.E1_TIPO, "
   	cQuery += "        SE1.E1_CLIENTE, SE1.E1_LOJA, SE1.E1_NOMCLI, SE1.E1_EMISSAO, SE1.E1_VENCREA, SE1.E1_VALOR, SE1.E1_SALDO, "
   	cQuery += "        ZF1.ZF1_PREF, ZF1.ZF1_NUM, ZF1.ZF1_PARC, ZF1.ZF1_TIPO, ZF1.ZF1_DATA, ZF1.ZF1_HORA, ZF1.ZF1_AGENDA, "
   	cQuery += "        ZF1_ORIGEM, ZF1.ZF1_CODUSR, ZF1.R_E_C_N_O_ "
   	cQuery += " FROM "+RetSqlName("SE1")+" SE1" 
   	cQuery += "     INNER JOIN "+RetSqlName("ZF1")+" ZF1" 
   	cQuery += " ON(SE1.E1_FILIAL = ZF1.ZF1_FILIAL AND SE1.E1_CLIENTE = ZF1.ZF1_CLIENT AND SE1.E1_LOJA = SE1.E1_LOJA AND SE1.E1_PREFIXO = ZF1.ZF1_PREF AND SE1.E1_NUM = ZF1.ZF1_NUM AND SE1.E1_PARCELA = ZF1.ZF1_PARC) "
   	cQuery += " WHERE "  
   	cQuery += "	    SE1.D_E_L_E_T_ = ' ' " 
   	cQuery += "	AND ZF1.D_E_L_E_T_ = ' ' " 
   	cQuery += "	AND ZF1.ZF1_ORIGEM NOT IN('SFINA20B','FINA060T') " 
    IF MV_PAR18 = 1
   	   cQuery += "	AND SE1.E1_FILIAL >= '"+MV_PAR01+"'"
	   cQuery += "	AND SE1.E1_FILIAL <= '"+MV_PAR02+"'"
	Endif   
   	cQuery += "	AND SE1.E1_CLIENTE>= '"+MV_PAR03+"'"
   	cQuery += "	AND SE1.E1_LOJA   >= '"+MV_PAR04+"'"
   	cQuery += "	AND SE1.E1_CLIENTE<= '"+MV_PAR05+"'"
   	cQuery += "	AND SE1.E1_LOJA   <= '"+MV_PAR06+"'"
   	IF MV_PAR07 = 1
   	   cQuery += "	AND SE1.E1_SALDO <> SE1.E1_VALOR "
   	Else
   	   cQuery += "	AND SE1.E1_SALDO =  SE1.E1_VALOR "   	    
   	Endif   
   	cQuery += "	AND SE1.E1_PREFIXO>= '"+MV_PAR08+"'"
   	cQuery += "	AND SE1.E1_PREFIXO<= '"+MV_PAR09+"'"
   	cQuery += "	AND SE1.E1_NUM    >= '"+MV_PAR10+"'"
   	cQuery += "	AND SE1.E1_NUM    <= '"+MV_PAR11+"'"
   	cQuery += "	AND SE1.E1_PARCELA>= '"+MV_PAR12+"'" 
   	cQuery += "	AND SE1.E1_PARCELA<= '"+MV_PAR13+"'" 
   	cQuery += "	AND SE1.E1_EMISSAO >= '"+DTOS(MV_PAR14)+"'" 
   	cQuery += "	AND SE1.E1_EMISSAO <= '"+DTOS(MV_PAR15)+"'" 
   	cQuery += "	AND SE1.E1_VENCREA >= '"+DTOS(MV_PAR16)+"'" 
   	cQuery += "	AND SE1.E1_VENCREA <= '"+DTOS(MV_PAR17)+"'" 
   	cQuery += "	ORDER BY SE1.E1_FILIAL, SE1.E1_PREFIXO, SE1.E1_NUM, SE1.E1_PARCELA, SE1.E1_EMISSAO, ZF1.ZF1_DATA, ZF1.ZF1_HORA "

	IF Select("TRB")
		DbSelectArea("TRB")
		DbCloseArea()
	ENDIF
	
	TCQUERY cQuery NEW ALIAS "TRB"
	TcSetField("TRB","E1_EMISSAO","D",10)
	TcSetField("TRB","E1_VENCREA","D",10)
	TcSetField("TRB","ZF1_DATA"  ,"D",10)

	dbSelectArea("TRB")
    TRB->(dbEval({|| nRegs++}))
	oReport:SetMeter(nRegs)

	TRB->(dbGoTop())
	While ! TRB->(EOF())

		If oReport:Cancel()
			Exit
		EndIf

		oSection1:Init()
		oReport:IncMeter()

		IncProc("Imprimindo titulos ")
		oSection1:Cell("E1_FILIAL"):SetValue(TRB->E1_FILIAL+" "+FWFilName(cEmpAnt,TRB->E1_FILIAL))
		oSection1:Printline()

		oSection2:init()
		cFilHis := TRB->E1_FILIAL

		While ! TRB->(EOF()) .and. TRB->E1_FILIAL == cFilHis 
			oReport:IncMeter()

			IncProc("Imprimindo Titulos com historico de cobranca ")

			oSection2:Cell("E1_CLIENTE"):SetValue(TRB->E1_CLIENTE)
			oSection2:Cell("E1_LOJA"):SetValue(TRB->E1_LOJA)
			oSection2:Cell("E1_NOMCLI"):SetValue(TRB->E1_NOMCLI)
			oSection2:Cell("E1_PREFIXO"):SetValue(TRB->E1_PREFIXO)
			oSection2:Cell("E1_NUM"):SetValue(TRB->E1_NUM)
			oSection2:Cell("E1_PARCELA"):SetValue(TRB->E1_PARCELA)
			oSection2:Cell("E1_TIPO"):SetValue(TRB->E1_TIPO)
			oSection2:Cell("E1_EMISSAO"):SetValue(TRB->E1_EMISSAO)
			oSection2:Cell("E1_VENCREA"):SetValue(TRB->E1_VENCREA)
			oSection2:Cell("DIAS"):SetValue(dDataBase - TRB->E1_VENCREA)
			oSection2:Cell("E1_VALOR"):SetValue(TRB->E1_VALOR)
			oSection2:Cell("ZF1_DATA"):SetValue(TRB->ZF1_DATA)
			oSection2:Cell("ZF1_HORA"):SetValue(TRB->ZF1_HORA)
			oSection2:Cell("ZF1_ORIGEM"):SetValue(TRB->ZF1_ORIGEM)
			oSection2:Cell("ZF1_HISTOR"):SetValue(u_LoZF1hst(TRB->R_E_C_N_O_)) 
			oSection2:Cell("ZF1_CODUSR"):SetValue(UsrFullName(ZF1->ZF1_CODUSR)) 
			oSection2:Printline()

			TRB->(dbSkip())
		EndDo
		
		oReport:ThinLine()
		oSection1:Finish()
		oSection2:Finish()
	
	Enddo
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ValidPerg ºAutor  ³Paulo Schwind       º Data ³  02/05/2019 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ValidPerg()
	Local _aArea := GetArea()
	Local _cPerg := cPerg
	Local _aRegs:={}
	Local _nI := 0
	Local _nJ := 0

	dbSelectArea('SX1')
	dbSetOrder(1)

	Aadd(_aRegs,{_cPerg,"01","Filial De                     ","                              ","                              ","mv_ch1","C",08,0,0,"G","                                                            ","mv_par01       ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","          ","                                                            ","SM0","S","   ","          "})
	Aadd(_aRegs,{_cPerg,"02","Filial Até                    ","                              ","                              ","mv_ch2","C",08,0,0,"G","                                                            ","mv_par02       ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","          ","                                                            ","SM0","S","   ","          "})
	Aadd(_aRegs,{_cPerg,"03","Cliente De                    ","                              ","                              ","mv_ch3","C",09,0,0,"G","                                                            ","mv_par03       ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","          ","                                                            ","SA1","S","   ","          "})
	Aadd(_aRegs,{_cPerg,"04","Loja De                       ","                              ","                              ","mv_ch4","C",04,0,0,"G","                                                            ","mv_par04       ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","          ","                                                            ","   ","S","   ","          "})
	Aadd(_aRegs,{_cPerg,"05","Cliente Até                   ","                              ","                              ","mv_ch5","C",09,0,0,"G","                                                            ","mv_par05       ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","          ","                                                            ","SA1","S","   ","          "})
	Aadd(_aRegs,{_cPerg,"06","Loja Até                      ","                              ","                              ","mv_ch6","C",04,0,0,"G","                                                            ","mv_par06       ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","          ","                                                            ","   ","S","   ","          "})
	Aadd(_aRegs,{_cPerg,"07","Cons.Tit Baixadps             ","                              ","                              ","mv_ch7","C",01,0,1,"C","                                                            ","mv_par07       ","Sim            ","               ","               ","                                                            ","               ","Não            ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","          ","                                                            ","   ","S","   ","          "})
	Aadd(_aRegs,{_cPerg,"08","Prefixo De                    ","                              ","                              ","mv_ch8","C",03,0,0,"G","                                                            ","mv_par08       ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","          ","                                                            ","   ","S","   ","          "})
	Aadd(_aRegs,{_cPerg,"09","Prefixo Até                   ","                              ","                              ","mv_ch9","C",03,0,0,"G","                                                            ","mv_par09       ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","          ","                                                            ","   ","S","   ","          "})
	Aadd(_aRegs,{_cPerg,"10","Titulo De                     ","                              ","                              ","mv_chA","C",09,0,0,"G","                                                            ","mv_par10       ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","          ","                                                            ","   ","S","   ","          "})
	Aadd(_aRegs,{_cPerg,"11","Titulo Até                    ","                              ","                              ","mv_chB","C",09,0,0,"G","                                                            ","mv_par11       ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","          ","                                                            ","   ","S","   ","          "})
	Aadd(_aRegs,{_cPerg,"12","Parcela De                    ","                              ","                              ","mv_chC","C",03,0,0,"G","                                                            ","mv_par12       ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","          ","                                                            ","   ","S","   ","          "})
	Aadd(_aRegs,{_cPerg,"13","Parcela Até                   ","                              ","                              ","mv_chD","C",03,0,0,"G","                                                            ","mv_par13       ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","          ","                                                            ","   ","S","   ","          "})
	Aadd(_aRegs,{_cPerg,"14","Emissão De                    ","                              ","                              ","mv_chE","D",08,0,0,"G","                                                            ","mv_par14       ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","          ","                                                            ","   ","S","   ","          "})
	Aadd(_aRegs,{_cPerg,"15","Emissao Até                   ","                              ","                              ","mv_chF","D",08,0,0,"G","                                                            ","mv_par15       ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","          ","                                                            ","   ","S","   ","          "})
	Aadd(_aRegs,{_cPerg,"16","Vencimento De                 ","                              ","                              ","mv_chG","D",08,0,0,"G","                                                            ","mv_par14       ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","          ","                                                            ","   ","S","   ","          "})
	Aadd(_aRegs,{_cPerg,"17","Vencimento Até                ","                              ","                              ","mv_chH","D",08,0,0,"G","                                                            ","mv_par15       ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","          ","                                                            ","   ","S","   ","          "})
	Aadd(_aRegs,{_cPerg,"18","Considera Filial              ","                              ","                              ","mv_chI","C",01,0,1,"C","                                                            ","mv_par16       ","Sim            ","               ","               ","                                                            ","               ","Nao            ","               ","               ","                                                            ","               ","Todos          ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","          ","                                                            ","   ","S","   ","          "})

	For _nI:=1 to Len(_aRegs)
		If ! DbSeek(_cPerg+_aRegs[_nI,2])
			RecLock('SX1',.T.)
			For _nJ:=1 to FCount()
				If _nJ <= Len(_aRegs[_nI])
					FieldPut(_nJ,_aRegs[_nI,_nJ])
				Endif
			Next _nJ
			MsUnlock()
		Endif
	Next _nI
	RestArea(_aArea)
Return



User Function LoZF1hst(_pRECNO)
Local _aArea   := GetArea()
Local _mHistor := ''
Local _cTxtHst := ''
Local _nQtLnHi := 0
Local _i

DbSelectArea("ZF1")
DbGoTo(_pRECNO)
_mHistor := ZF1->ZF1_HISTOR 

_nQtLnHi := MLCount(ZF1->ZF1_HISTOR,40)	
For _i := 1 to _nQtLnHi
   _cTxtHst += AllTrim(MemoLine(_mHistor , 40 , _i )) + SPACE(1)
Next 			

RestArea(_aArea)
Return(_cTxtHst)
