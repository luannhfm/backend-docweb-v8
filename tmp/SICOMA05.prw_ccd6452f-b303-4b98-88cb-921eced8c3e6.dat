#Include 'Protheus.ch'
#Include 'TbiConn.ch'
#Include 'Ap5Mail.ch'
#Include 'TopConn.ch'
#Include 'FileIO.ch'

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³SICOMA05  ºAutor  ³Microsiga           º Data ³09/09/2011   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Envio de e-mail de cotacoes ao fornecedor                  º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

User Function SICOMA05(aParam)
Local cFuncName  := "SICOMA05"
Local cCodEmp    := ""
Local cCodFil    := ""
Local lContinua  := .F.

If ValType(aParam) == "A" .and. Len(aParam) >= 2
	If ValType(aParam[1]) =="C" .and. ValType(aParam[2]) =="C"
		cCodEmp   := aParam[1]
		cCodFil   := aParam[2]
		ConOut(cFuncName+":: Parametros Recebidos => Empresa/Filial: "+cCodEmp+"/"+cCodFil)
		lContinua := .T.
	Else
		lContinua := .F.
	EndIf
Else
	lContinua := .F.
EndIf

IF lContinua
	ConOut(cFuncName+":: Inicializacao do ambiente - Workflow PC Empresa/Filial: "+cCodEmp+"/"+cCodFil)
	WfPrepEnv(cCodEmp,cCodFil)
	
	_fEnvWF() // Processa a rotina para Envio do Workflow
	
	Reset Environment
	ConOut(cFuncName+":: Finalizacao do ambiente - Workflow PC Empresa/Filial: "+cCodEmp+"/"+cCodFil)
ENDIF

Return()

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³SICOMA05  ºAutor  ³Microsiga           º Data ³  07/02/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Envia aviso aos fornecedores                               º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ P11 - SISTEMA INDUSTRIA                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function _fEnvWF()
Local _lEnvCOT     := GetMV("SI_ENVCOT")
//Local _cURL		   := GetMv("SI_URLFOR")
Local _cURL        := GetMv("SI_URLFOR") //SuperGetMv("SI_URLFOR", .F., "Consulte o departamento de Compras", Substr(cFilant,1,4) ) // Endereco URL do Portal do Fornecedor por Empresa (Casa)

Local cFuncName  := "SICOMA05"
Private _cAliasTRB := GetNextAlias()

If _lEnvCOT
	
	//seleciona as cotacoes a serem enviadas
	_cQuery := "SELECT DISTINCT C8_NUM,C8_FORNECE,C8_LOJA,C8_VALIDA FROM "+RetSQLName("SC8")+" WHERE C8_FILIAL = '"+xFilial("SC8")+"' AND D_E_L_E_T_ = ' ' AND C8_XENVFOR <> 'T'"
	_cQuery := ChangeQuery(_cQuery)
	
	dbUseArea(.T.,"TOPCONN",TCGenQry(,,_cQuery),_cAliasTRB,.t.,.t.)
	
	TCSetField(_cAliasTRB, "C8_VALIDA", "D")
	
	(_cAliasTRB)->(dbGoTop())

	While (_cAliasTRB)->(!Eof())

		_cPara	  := Alltrim(Lower(Posicione("SA2",1,XFilial("SA2")+(_cAliasTRB)->(C8_FORNECE+C8_LOJA),"A2_EMAIL")))        
		
//		_cCodUser := Posicione("AI5",1,XFilial("AI5")+(_cAliasTRB)->(C8_FORNECE+C8_LOJA),"AI5_CODUSU")
//		ConOut(cFuncName+":: Codigo Usuario "+_cCodUser  +  " " + XFilial("AI5")+(_cAliasTRB)->(C8_FORNECE+C8_LOJA))

		AI5->(dbOrderNickName("CODFOR"))
		AI5->(dbSeek(XFilial("AI5")+(_cAliasTRB)->(C8_FORNECE+C8_LOJA)))
		_cCodUser := AI5->AI5_CODUSU                    
		ConOut(cFuncName+":: Codigo Usuario "+_cCodUser) 
		
		_cAssunto := "Cotacao "+(_cAliasTRB)->C8_NUM
		_cUser	  := Posicione("AI3",1,XFilial("AI3")+_cCodUser,"AI3_LOGIN")		
		_cPsw	  := Posicione("AI3",1,XFilial("AI3")+_cCodUser,"AI3_PSW")		

		_cWFID := SendMail(_cPara,_cAssunto,_cURL,_cUser,_cPsw,(_cAliasTRB)->C8_NUM,(_cAliasTRB)->C8_VALIDA)
		
		If Empty(_cPara) //não ha nenhum destinatario para o e-mail
			Conout("WARNING - SICOMA05 - Processo WFID: "+AllTrim(_cWFID)+" iniciado, porém não existe um destinatário para envio do Workflow.")
		EndIF
		
		//Atualiza flag como enviado
		SC8->(dbSetOrder(1))
		SC8->(dbSeek(XFilial("SC8")+(_cAliasTRB)->(C8_NUM+C8_FORNECE+C8_LOJA)))
		
		While SC8->(!Eof()) .and. SC8->C8_FILIAL == XFilial("SC8") .and. SC8->(C8_NUM+C8_FORNECE+C8_LOJA) == (_cAliasTRB)->(C8_NUM+C8_FORNECE+C8_LOJA)
			
			Reclock("SC8",.F.)
			SC8->C8_XENVFOR	:= .T.
			SC8->C8_XWFID	:= _cWFID
			SC8->(MsUnLock())
			
			SC8->(dbSkip())
		Enddo
		
		(_cAliasTRB)->(DbSkip())
	EndDo
	
	(_cAliasTRB)->(	dbCloseArea())
	FErase(_cAliasTRB+GetDBExtension())
	FErase(_cAliasTRB+OrdBagExt())
	
EndIf

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³SendMail  ºAutor  ³renato.neves        º Data ³09/09/2011   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Envia o e-mail                                             º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function SendMail(_cTo,_cSubject,_cURL,_cUser,_cPsw,_cCotacao, _cValida)
local _oProcess
Local _cWFID		:= ""
Local _cArqHtml 	:= "\WORKFLOW\SICOMA05.HTM"

_oProcess := TWFProcess():New("JOBCOT", "WF de envio de cotação ao fornecedor (SICOMA05)" )

_oProcess:NewTask('Inicio',_cArqHtml)

_oProcess:cSubject :=_cSubject
_oProcess:cTo := _cTo

_oProcess:oHtml:ValByName("LINK",_cURL)
_oProcess:oHtml:ValByName("NumCotacao",_cCotacao)
_oProcess:oHtml:ValByName("Usuario",_cUser)
_oProcess:oHtml:ValByName("Senha",_cPsw)
_oProcess:oHtml:ValByName("C8_VALIDA", DToC(_cValida) )

_cWFID := _oProcess:Start()

conout("Enviado para: " + _cTo)

_oProcess:Finish()

Return _cWFID
