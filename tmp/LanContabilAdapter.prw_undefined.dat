#include 'totvs.ch'
#include 'parmtype.ch'

/*/{Protheus.doc} LanContabilAdapter
Classe Adapter para o serviço
@author  Walmir Junior
@since   20/09/2020
@version 1.0
/*/

CLASS LanContabilAdapter FROM FWAdapterBaseV2
	METHOD New()
	METHOD GetListLanCtbil()
EndClass

//-------------------------------------------------------------------
/*/{Protheus.doc} New
Método construtor
@param cVerb, verbo HTTP utilizado
@author  Walmir Junior
@since   20/09/2020
@version 1.0
/*/
//-------------------------------------------------------------------
Method New( cVerb ) CLASS LanContabilAdapter
	_Super:New( cVerb, .T. )
return

//-------------------------------------------------------------------
/*/{Protheus.doc} GetListLanCtbil
Método que retorna uma lista de lançamentos contábeis
@author  Walmir Junior
@since   20/09/2020
@version 1.0
/*/
//-------------------------------------------------------------------
Method GetListLanCtbil() CLASS LanContabilAdapter
	Local aArea 	AS ARRAY
	Local cWhere	AS CHAR
	
    aArea   := FwGetArea()
	AddMapFields( self )
	::SetQuery( GetQuery() )

	cWhere := " D_E_L_E_T_ = ' ' AND CT2_FILIAL = '03MT' AND CT2_DATA = '20200124' AND CT2_VALOR > 0 "
	
	::SetWhere( cWhere )
	
    ::SetOrder( " CT2_FILIAL, CT2_DEBITO, CT2_CREDIT, CT2_LOTE, CT2_LINHA " )

	//Executa a consulta, se retornar .T. tudo ocorreu conforme esperado
	If ::Execute() 
		// Gera o arquivo Json com o retorno da Query
		::FillGetResponse()
	EndIf
	
    FwrestArea(aArea)

return

//-------------------------------------------------------------------
/*/{Protheus.doc} AddMapFields
Função para geração do mapa de campos
@param oSelf, object, Objeto da prórpia classe
@author  Walmir Junior
@since   20/09/2020
@version 1.0
/*/
//-------------------------------------------------------------------
Static Function AddMapFields( oSelf )
	
	oSelf:AddMapFields( 'filial'    		, 'CT2_FILIAL'  , .T., .T., { 'CT2_FILIAL'  , 'C', TamSX3( 'CT2_FILIAL' )[1] , 0 } )
	oSelf:AddMapFields( 'filial_original'	, 'CT2_FILORI'  , .T., .T., { 'CT2_FILORI'  , 'C', TamSX3( 'CT2_FILORI' )[1] , 0 } )
	oSelf:AddMapFields( 'data'      		, 'CT2_DATA'    , .T., .F., { 'CT2_DATA'    , 'C', TamSX3( 'CT2_DATA'   )[1] , 0 } )	
	oSelf:AddMapFields( 'lote'	   			, 'CT2_LOTE'    , .T., .F., { 'CT2_LOTE'    , 'C', TamSX3( 'CT2_LOTE'   )[1] , 0 } )
	oSelf:AddMapFields( 'linha'    			, 'CT2_LINHA'   , .T., .F., { 'CT2_LINHA'   , 'C', TamSX3( 'CT2_LINHA'  )[1] , 0 } )
	oSelf:AddMapFields( 'contaDeb'  		, 'CT2_DEBITO'  , .T., .F., { 'CT2_DEBITO'  , 'C', TamSX3( 'CT2_DEBITO' )[1] , 0 } )
    oSelf:AddMapFields( 'contaCred' 		, 'CT2_CREDIT'  , .T., .T., { 'CT2_CREDIT'  , 'C', TamSX3( 'CT2_CREDIT' )[1] , 0 } )
	oSelf:AddMapFields( 'cCustoDeb' 		, 'CT2_CCD'     , .T., .F., { 'CT2_CCD'     , 'C', TamSX3( 'CT2_CCD'    )[1] , 0 } )	
	oSelf:AddMapFields( 'cCustoCred'		, 'CT2_CCC'     , .T., .F., { 'CT2_CCC'     , 'C', TamSX3( 'CT2_CCC'    )[1] , 0 } )
	oSelf:AddMapFields( 'hist'      		, 'CT2_HIST'    , .T., .F., { 'CT2_HIST'    , 'C', TamSX3( 'CT2_HIST'   )[1] , 0 } )
	oSelf:AddMapFields( 'valor'     		, 'CT2_VALOR'   , .T., .F., { 'CT2_VALOR'   , 'N', TamSX3( 'CT2_VALOR'  )[1] , TamSX3( 'CT2_VALOR'  )[2]} )

return 

//------------------------------------------------------------------
/*/{Protheus.doc} GetQuery
Retorna a query usada no serviço
@param oSelf, object, Objeto da prórpia classe
@author  Walmir Junior
@since   20/09/2020
@version 1.0
/*/
//-------------------------------------------------------------------
Static Function GetQuery()
	Local cQuery AS CHARACTER
	
	cQuery := " SELECT #QueryFields#"
    cQuery +=   " FROM " + RetSqlName( 'CT2' ) + " CT2 "
    cQuery += " WHERE #QueryWhere#"

Return cQuery
