#INCLUDE "TOTVS.CH"

/*/{Protheus.doc} ChkCCD
	@description Analisa se existe amarração entre centro de custo e item contabil.
	@author walmir.junior
	@since 10/07/2017
	@version 1.0
/*/
User Function ChkCCD(_cCC, _cItC) 

	Local cAlCCD := GetNextAlias()	 
	Local cMsg := "Não existe amarração entre este Centro de Custo e Item Contábil."
	Local lRet := .F.
	Local nRegs:= 0

	BeginSql Alias cAlCCD
		
		SELECT CTA_ITEM 
		FROM   %Table:CTA%
		WHERE	CTA_CUSTO   = %Exp:_cCC%	
		AND 	CTA_ITEM    = %Exp:_cItC%		
		AND 	CTA_FILIAL  = %Exp:FWxFilial("CTA")%
		AND  	%NOTDEL% 
	    
	EndSql
	
	DbSelectArea(cAlCCD)
	(cAlCCD)->(dbGoTop())
	(cAlCCD)->(dbEval({|| nRegs++})) // Paulo Schwind - Contador de Registros
	(cAlCCD)->(dbGoTop())
	
	// Paulo Schwind - Inseri um contador de Registros 
	If nRegs > 0   //!(cAlCCD)->(Eof())
		lRet := .T.
	Else
		If lF040Auto
			AutoGrLog("FA040INC - " + cMsg)
		Else
			Aviso("Atenção", cMsg, {"Ok"})
		EndIf
	EndIf	

    (cAlCCD)->(DbCloseArea())
    
Return lRet
