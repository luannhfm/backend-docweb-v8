#Include 'Protheus.ch'

/*/{Protheus.doc} F070VMOT
@description Ponto de Entrada para validação da baixa de título do contas a receber.
@type User Function
@author Do.It
@since 29/08/2014
@version 11.0
@return lRet, logico, retorna verdadeiro se autorizado
@history 13/02/2019, Alan Teles de Oliveira, sempre que a rotina SFFNBX1A estiver na pilha validar apenas a situação diferente de 1.
@history 18/02/2019, Alan Teles de Oliveira, impeço a utilização das carteiras de cartão se a função SFFNBX1A não estiver na pilha de processamento.
/*/
User Function F070VMOT()

    Local lRet := .F.
    Local cNegMBx := GetNewPar('SF_NEGMTBX', 'BX CARTAO |CAR MASTER|CARTAOVISA')

    If !IsBlind()

        If cMotBx $ cNegMBx .and. .not. IsInCallStack('U_SFFNBX1A')

            MsgInfo('O recebimento de títulos com cartão deve ser feito na rotina de Títulos a Receber > Outras Ações > Rec. com Cartão', 'Atenção - F070VMOT')

        ElseIf IsInCallStack('U_SFFNBX1A') .or. FN022SITCB(SE1->E1_SITUACA)[1] .or. MovBcoBx(cMotBx, .T.)

            lRet := .T.

        Else

            Help(" ", 1, "FA070MOTINV")

        EndIf

    Else
        lRet := .T.
    EndIf
        
    
Return lRet
