#include 'protheus.ch'
#Include 'FWMVCDEF.CH'

User Function SF02A02F()
Local aArea	:= GetArea()
	
	DbSelectArea( "Z06" )
	Z06->( DbSetOrder(1) )
	
	If Z06->( DbSeek(xFilial("Z06") + SA1->A1_COD + SA1->A1_LOJA) ) 
		FWExecView('Atualizar', 'SF02A02F', MODEL_OPERATION_UPDATE, /*oDlg*/,/*bCloseOnOk*/,/*bOk*/,/*nPercReducao*/)
	Else
		FWExecView('Incluir', 'SF02A02F', MODEL_OPERATION_INSERT, /*oDlg*/,/*bCloseOnOk*/,/*bOk*/,/*nPercReducao*/)
	EndIf
	
	Z06->( DbCloseArea() )
	
	RestArea(aArea)

Return Nil

Static Function ModelDef()
Local oStruZ06 	:= FWFormStruct( 1, "Z06", /*bAvalCampo*/,/*lViewUsado*/ )
Local oModel

	oModel := MPFormModel():New( "F02A02FM", /*bPreValidacao*/, /*bPosValidacao*/, /*bCommit*/, /*bCancel*/  )
	
	oStruZ06:SetProperty( 'Z06_CODCLI', MODEL_FIELD_INIT, {|| SA1->A1_COD }  )
	oStruZ06:SetProperty( 'Z06_CODCLI', MODEL_FIELD_WHEN, {|| .F. }  )
	
	oStruZ06:SetProperty( 'Z06_LOJCLI', MODEL_FIELD_INIT, {|| SA1->A1_LOJA }  )
	oStruZ06:SetProperty( 'Z06_LOJCLI', MODEL_FIELD_WHEN, {|| .F. }  )

	oStruZ06:AddField("Nome", "Nome do cliente", "Z06_NOMECLI", "C", 80, 0, /*07*/,/*08*/ ,;
		/*09*/, /*10*/, FwBuildFeature( STRUCT_FEATURE_INIPAD, " SA1->A1_NOME" ), , , .T.)
			
	oModel:AddFields( "Z06MASTER", /*cOwner*/, oStruZ06, /*bPre*/, /*bPos*/, /*bLoad*/ )
	oModel:SetDescription( "Complemento dos Dados de cliente" )
	oModel:SetPrimaryKey( { "Z06_FILIAL", "Z06_CODCLI", "Z06_LOJCLI" } )
	
Return oModel

Static Function ViewDef()
Local oModel 	:= FWLoadModel( "SF02A02F" )
Local oStruZ06 	:= FWFormStruct( 2, "Z06" )
Local oView 	:= FWFormView():New()

	oView:SetModel( oModel )
	
	oStruZ06:AddField( 'Z06_NOMECLI' , '3', 'Nome', 'Nome do cliente', Nil ,;
		'C' , '@!' , NIL , '', .F. , NIL , NIL, Nil , NIL , NIL, .T. , NIL )
	
	oStruZ06:AddGroup( 'CLIENTE', 'Cadastro do cliente', '', 1 )
	oStruZ06:AddGroup( 'COMPLEMENTO', 'Complemento do cadastro', '', 2 )
	
	oStruZ06:SetProperty( '*' , MVC_VIEW_GROUP_NUMBER, 'COMPLEMENTO' )
	oStruZ06:SetProperty( 'Z06_CODCLI', MVC_VIEW_GROUP_NUMBER, 'CLIENTE' )
	oStruZ06:SetProperty( 'Z06_LOJCLI', MVC_VIEW_GROUP_NUMBER, 'CLIENTE' )
	oStruZ06:SetProperty( 'Z06_NOMECLI', MVC_VIEW_GROUP_NUMBER, 'CLIENTE' )
	
	
	oView:AddField( "VIEW_Z06", oStruZ06, "Z06MASTER" )
	
	oView:CreateHorizontalBox( "TELA", 100 )
	
	oView:SetOwnerView( "VIEW_Z06", "TELA" )
	
Return oView