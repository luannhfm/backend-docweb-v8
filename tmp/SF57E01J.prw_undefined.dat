#Include 'Protheus.ch'

/*/{Protheus.doc} SF57E01J
Função validadora de usuarios autenticados para emissao dos relatorios

@author 	José Leite de Barros Neto
@since 		08/09/2014
@version 	1.0

@param 		pFilial, Caracter, Filial Selecionada
@return 	_cCtCusto, Caracter - Retorna os Centro de Custo (UO)
/*/
User Function SF57E01J( pFilial )
	
	Local _aArea		:= GetArea()
	Local _aDados	:= {}
	Local _cCtCusto	:= ""
	Local _nCont		:= 0
	Local cMsg 		:= "" 
	
	cMsg := "Para esta Filial: "+pFilial+", o Usuário: "+AllTrim(UsrRetName(__cUserID))+" não esta amarrado na tabela ZZY - (Filial X UO X CR)" + CRLF 
	cMsg += "Favor Entrar em contato com a UNIGED."
	
	DbSelectArea("ZZY")
	DbSetOrder(1) //ZZY_FILIAL, ZZY_CODUSR
	DbGoTop()
	While .Not. ZZY->( EOF() ) 
		If ZZY->( ZZY_FILAUX ) == pFilial .And. ZZY->( ZZY_CODUSR ) == __cUserID
			_cCtCusto += AllTrim(ZZY->( ZZY_CUSTO ) + ",")
		EndIf  
		ZZY->(DbSkip())
	End
		
	If Empty(_cCtCusto)
		Alert(cMsg)
		RestArea(_aArea)
		Return(_cCtCusto)
	EndIf
	
	_aDados := StrTokArr(_cCtCusto,",")
	
	_cCtCusto := ""
	
	For _nCont := 1 To Len(_aDados)
		
		If _nCont = Len(_aDados)
			_cCtCusto += "'"+_aDados[_nCont]+"'"                 
		Else
			_cCtCusto += "'"+_aDados[_nCont]+"',"     
		EndIf 
		
	Next
	
	RestArea(_aArea)

Return( _cCtCusto )