#include "totvs.ch"
#include "restful.ch"

//-------------------------------------------------------------------
/*/{Protheus.doc} Contas Contabeis
Declaracao do GET de Contas Contabeis
@author Walmir Junior
@since 06/10/2020
@version 1.0
/*/
//-------------------------------------------------------------------
WSRESTFUL contacontabil DESCRIPTION 'endpoint Contas Contabeis - VENUS ' FORMAT "application/json,text/html"
    WSDATA Page              AS INTEGER OPTIONAL
    WSDATA aQueryString      AS ARRAY    OPTIONAL
    
 	WSMETHOD GET contacontabil;
	    DESCRIPTION "Retorna uma lista de Contas Contabeis";
	    WSSYNTAX "/api/contacontabil" ;
        PATH "/api/contacontabil" ;
	    PRODUCES APPLICATION_JSON
 	
END WSRESTFUL

//-------------------------------------------------------------------
/*/{Protheus.doc} GET LancContabil
MÃ©todo GET contacontabil
@author Walmir Junior
@since 06/10/2020
@version 1.0
/*/
//-------------------------------------------------------------------
WSMETHOD GET contacontabil QUERYPARAM Page WSREST contacontabil
Return GetListContaContabil(self)

//-------------------------------------------------------------------
/*/{Protheus.doc} GET GetListContaContabil
Funcao para tratamento da requisicao GET
@author Walmir Junior
@since 06/10/2020
@version 1.0
/*/
//-------------------------------------------------------------------
Static Function GetListContaContabil( oWS )
   Local lRet  as logical
   Local oCentC as object
   DEFAULT oWS:Page      := 1  
   lRet        := .T.
   
   oCentC := contacontabilAdapter():new( 'GET' )
   oCentC:setPage(oWS:Page)
   oCentC:setPageSize(10)
   oCentC:setUrlFilter(oWS:aQueryString)
   oCentC:GetListContaContabil()
   //Se tudo ocorreu bem, retorna os dados via Json
   If oCentC:lOk
       oWS:SetResponse(EncodeUTF8(oCentC:getJSONResponse(), "cp1252"))
   Else
   //Ou retorna o erro encontrado durante o processamento
       SetRestFault(oCentC:GetCode(),oCentC:GetMessage())
       lRet := .F.
   EndIf
   //faz a desalocacao de objetos e arrays utilizados
   oCentC:DeActivate()
   oCentC := nil
   
Return lRet
