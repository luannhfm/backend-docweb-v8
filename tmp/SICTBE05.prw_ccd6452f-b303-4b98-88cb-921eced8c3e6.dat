#Include "Protheus.Ch"
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ SICTBE05 ³ Autor ³ CADUBITSKI            ³ Data ³ Ago/2012 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ ExecBlock utilizado no LP 500. Para retorno dos valores de ³±±
±±³          ³de imposto quando mutuo.                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Devera ser passado conforme abaixo:                        ³±±
±±³          ³ cExp1:													  ³±±
±±³          ³ PCC p/ retornar o valor retido do PCC                      ³±±
±±³          ³       (E2_VRETCSL+E2_VRETPIS+E2_VRETCOF)					  ³±±
±±³          ³ ISS p/ retornar o valor retido do ISS (E2_VRETISS)         ³±±
±±³          ³ IR p/ retornar o valor retido do IRS (E2_VRETIRF)          ³±±
±±³          ³ INSS p/ retornar o valor retido do IRS (E2_INSSRET)        ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Retorna o valor de imposto retido do titulo a pagar ref.   ³±±
±±³          ³ ao mutuo conforme passado por parametro.                   ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
Exemplo:
U_SICTBE05("PCC") 
U_SICTBE05("ISS")
U_SICTBE05("IR")
U_SICTBE05("INSS")
U_SICTBE05("UO") //Centro de Custo
U_SICTBE05("CR") //Item contabil
/*/
User Function SICTBE05(cImp)

Local aArea		:= GetArea()
Local nRet		:= 0
Local cAli 		:= GetNextAlias()        
Local cFilSE1 	:= SE1->E1_FILIAL 
Local cMutuo 	:= SE1->E1_XMUTUO
Local cAtualiza := " "

If ValType(cImp)== "U" .or. cImp == NIL
	Return(nRet)
EndIf

If Alltrim(SE1->E1_PARCELA) == '1'

	If Empty(cMutuo)
		Return(nRet)
	EndIf
	
	BeginSQL Alias cAli
		SELECT 
			(E2_VRETCSL+E2_VRETPIS+E2_VRETCOF) AS PCC,
			(CASE WHEN E2_TRETISS = '1' AND E2_ISS <> 0 THEN E2_ISS ELSE 0 END) AS ISS, 
			E2_VRETIRF AS IR, 
			E2_VRETINS AS INSS,
			E2_CCD AS UO,
			E2_ITEMD AS CR

		FROM %table:SE2% SE2
	
		WHERE E2_FILIAL = %Exp:cFilSE1%
			AND SE2.%NotDel%                           
			AND E2_XMUTUO = %Exp:cMutuo%
	EndSQL
	
	//MemoWrite("\SYSTEM\SICTBE01.SQL",GetLastQuery()[2])
	
	DbSelectArea((cAli))
	
	If Select((cAli)) > 0
		cAtualiza 	:= cAli+"->"+cImp
		nRet 		:= &cAtualiza
	EndIf
	
	DbCloseArea((cAli))

EndIF

RestArea(aArea)
Return(nRet)