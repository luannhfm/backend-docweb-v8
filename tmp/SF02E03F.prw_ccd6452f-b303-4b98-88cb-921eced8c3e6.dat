#include 'protheus.ch'

/*/{Protheus.doc} SF02E03F
Função para enviar e-mail alertando os compradores da CAQC que uma nova solicitação foi aprovada.
@author Franklin Oliveira
@since 06/04/2020
@return Nil
@param cFilSc, characters, Filial da solicitação
@param cNumSc, characters, Numero da solicitação
@type user function
/*/
User Function SF02E03F(cFilSc, cNumSc)
Local aArea 	:= GetArea()
Local cComCAQC	:= AllTrim(SuperGetMv( 'MV_XCOMCAC', .F., '051' ))
Local cPara		:= AllTrim(Posicione( "SY1", 1, xFilial("SY1")+cComCAQC, "Y1_EMAIL" ))
Local cAssunto	:= ""
Local cMsg		:= ""
	cAssunto := "A solicitação " + AllTrim( cNumSc ) + " da filial " + AllTrim( cFilSc )
	cAssunto += " - " + AllTrim( FWFilialName(, cFilSc, 1) ) + " foi aprovada"
	DbSelectArea("SC1")
	DbSetOrder(1)
	If SC1->(DbSeek( cFilSc +cNumSc ))
		cMsg := "<!DOCTYPE html>"
		cMsg +="<html>"
		cMsg +="<head>"
		cMsg +="<style>"
		cMsg +="table {"
		cMsg +="	border-collapse: collapse;"
		cMsg +="}"
		cMsg +="table, th, td {"
		cMsg +="  border: 1px solid black;"
		cMsg +="}"
		cMsg +="</style>"
		cMsg +="</head>"
		cMsg +="<body>"
		
		cMsg +="<h1>" + cAssunto
		cMsg +=" e está disponivel para realizar a cotação:</h1>"
		cMsg +="<p>Justificativa: " + AllTrim(SC1->C1_XJUSTIF) + "</p>"
		cMsg +="<table>"
		cMsg +="	<tr>"
		cMsg +="		<th>Item</th>"
		cMsg +="		<th>Produto</th>"
		cMsg +="		<th>Descrição</th>"
		cMsg +="		<th>Quantidade</th>"
		cMsg +="		<th>Necessidade</th>"
		cMsg +="	</tr>"
		While !SC1->( EoF() ) .And. SC1->C1_FILIAL + SC1->C1_NUM == cFilSc + cNumSc
			cMsg +="	<tr>"
			cMsg +="		<td>" + AllTrim(SC1->C1_ITEM) + "</td>"
			cMsg +="		<td>" + AllTrim(SC1->C1_PRODUTO) + "</td>"
			cMsg +="		<td>" + AllTrim(SC1->C1_XXDESCR) + "</td>"
			cMsg +="		<td>" + Transform(SC1->C1_QUANT, X3PICTURE("C1_QUANT")) + "</td>"
			cMsg +="		<td>" + DToC(SC1->C1_DATPRF) + "</td>"
			cMsg +="	</tr>"
			SC1->( DbSkip() )
		EndDo
		cMsg +="</table>"
		cMsg +="</body>"
		cMsg +="</html>"
		//Modelo: SFEnvEmail(p_cDe, p_cPara, p_cCC, p_cCCO, p_cAssunto, p_cMsg, p_cAnexo, p_lConLe)
		U_SFEnvEmail(, cPara, , , cAssunto, cMsg, , )
	Else
		Alert("Solicitação " + cNumSc + " da filial " + cFilSc + " não foi encontrada.") 
	EndIf
	SC1->( DbCloseArea() )
	RestArea(aArea)
Return Nil