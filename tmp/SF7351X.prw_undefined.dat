#INCLUDE "PROTHEUS.CH"
#INCLUDE "TOPCONN.CH"
#INCLUDE "RWMAKE.CH"
#INCLUDE "FONT.CH"
#INCLUDE "COLORS.CH"
#INCLUDE "PARMTYPE.CH"
#INCLUDE "FWMBROWSE.CH"
#INCLUDE "FWMVCDEF.CH"
#INCLUDE "FWCOMMAND.CH"

//+==================================================================+
/*/ {Protheus.doc} SF7351X
 Modelo MVC para Cadastro de Pesquisa de Satisfação:
 Pai   : ZJR - Pesquisa de Satisfação
 Filha : ZJS - Questões da Pesquisa
 Neta  : ZJT - Opções da Questão

@author<RenatoPeixoto>
@since<09/02/2015>
@version<1.00>
@receive<Nil>
@return<Nil>
@example<Nil>
@see<Nil>
/*/
//+==================================================================+
User Function SF7351X()
Local _cAlias  	:= "ZJR"
Local _cTitle  	:= "Cadastro de Pesquisa de Satisfação"
Local _oBrowse	 	:= FWMBrowse():New()
Private aRotina	:= MenuDef()

_oBrowse:SetAlias(_cAlias)
_oBrowse:SetDescription(_cTitle)
_oBrowse:AddLegend("ZJR_MSBLQL == '2' "	, "GREEN" , "Ativo" 	   )
_oBrowse:AddLegend("ZJR_MSBLQL == '1' "	, "RED"	  , "Bloqueado"	   )
_oBrowse:Activate()

Return NIL

//+==================================================================+
/*/ {Protheus.doc} MenuDef
 Definicao do MenuDef

@author<RenatoPeixoto>
@since<09/02/2015>
@version <1.00>
@return <_aRotina (a) - Vetor com as opcoes de Menu>
@example <Nil>
@see<Nil>
/*/
//+==================================================================+
Static Function MenuDef()
Local _aRotina 		:= {}

ADD OPTION _aRotina TITLE "Pesquisar"	ACTION "PESQBRW"			OPERATION 1 ACCESS 0
ADD OPTION _aRotina TITLE "Visualizar"	ACTION "VIEWDEF.SF7351X"	OPERATION 2 ACCESS 0
ADD OPTION _aRotina TITLE "Incluir"	ACTION "VIEWDEF.SF7351X"	OPERATION 3 ACCESS 0
ADD OPTION _aRotina TITLE "Alterar"	ACTION "VIEWDEF.SF7351X"	OPERATION 4 ACCESS 0
ADD OPTION _aRotina TITLE "Excluir"	ACTION "VIEWDEF.SF7351X"	OPERATION 5 ACCESS 0
ADD OPTION _aRotina TITLE "Copiar"		ACTION "VIEWDEF.SF7351X"	OPERATION 9 ACCESS 0

Return _aRotina

//+==================================================================+
/*/ {Protheus.doc} ModelDef
 Definicao do ModelDef

@author<RenatoPeixoto>
@since<09/02/2015>
@version <1.00>
@return <>
@example <>
@see<>
/*/
//+==================================================================+
Static Function ModelDef()
Local _oStrZJR := FWFormStruct(1,"ZJR")//Cabeçalho
Local _oSZJSGR := FWFormStruct(1,"ZJS")//Agrupador
Local _oSZJTQS := FWFormStruct(1,"ZJT")//Questões
Local _oSJVSUQ := FwFormStruct(1,"ZJV")//Sub Questoes
Local _oSZJURS := FWFormStruct(1,"ZJU")//Respostas
Local _oModel   := Nil

//+==================================================================+
//|Ajustando as Estruturas 											 |
//+==================================================================+

_oModel	:= MPFormModel():New("SF7351XP",/*PreValid*/,/*Valid*/,/*Grava*/ )

_oModel:AddFields("ZJRMASTER"	, /*cOwner*/	,_oStrZJR)
_oModel:AddGrid("ZJS_AGRUP"		, "ZJRMASTER"	,_oSZJSGR)
_oModel:AddGrid("ZJT_QUEST"		, "ZJS_AGRUP"	,_oSZJTQS)
_oModel:AddGrid("ZJV_ITEMGR"    , "ZJT_QUEST"	,_oSJVSUQ)
_oModel:AddGrid("ZJU_RESPT"		, "ZJT_QUEST"	,_oSZJURS)

_oModel:SetPrimaryKey({"ZJR_FILIAL","ZJR_NUM"})

//+==================================================================+
//| Setando o relacionamento										 |
//+==================================================================+

_oModel:SetRelation("ZJS_AGRUP", {	{"ZJS_FILIAL", "xFilial('ZJS')"},;
									{"ZJS_COD", "ZJR_COD"} },ZJS->(IndexKey(1)))	//AGRUPADOR
_oModel:SetRelation("ZJT_QUEST", {	{"ZJT_FILIAL", "xFilial('ZJT')"},;
									{"ZJT_COD", "ZJR_COD"}, {"ZJT_ITEMGR", "ZJS_ITEM" }},ZJT->(IndexKey(1)))	//QUESTÕES
_oModel:SetRelation("ZJV_ITEMGR", {	{"ZJV_FILIAL", "xFilial('ZJV')"},;
									{"ZJV_AGRUP", "ZJS_ITEM"}, {"ZJV_COD", "ZJR_COD"}, {"ZJV_ITEMGR", "ZJT_ITEM"}},ZJV->(IndexKey(1)))	//SUB-QUESTAO
_oModel:SetRelation("ZJU_RESPT", {	{"ZJU_FILIAL", "xFilial('ZJU')"},;
									{"ZJU_COD", "ZJR_COD"}, {"ZJU_ITEMGR", "ZJS_ITEM"}, {"ZJU_ITEMQS", "ZJT_ITEM"}},ZJU->(IndexKey(1)))	//RESPOSTAS
_oModel:SetDescription("Cadastro de Pesquisas de Satisfação")

_oModel:GetModel("ZJRMASTER"):SetDescription("Pesquisa de Satisfação")
_oModel:GetModel("ZJS_AGRUP"):SetDescription("Pesquisa de Satisfação - Agrupadores")
_oModel:GetModel("ZJT_QUEST"):SetDescription("Pesquisa de Satisfação - Questões")
_oModel:GetModel("ZJV_ITEMGR"):SetDescription("Pesquisa de Satisfação - SubQuestões")
_oModel:GetModel("ZJU_RESPT"):SetDescription("Pesquisa de Satisfação - Respostas")

_oModel:GetModel("ZJV_ITEMGR"):SetOptional(.T.)
_oModel:GetModel("ZJU_RESPT"):SetOptional(.T.)

Return _oModel
//+==================================================================+
/*/ {Protheus.doc} ViewDef
 Definicao do ViewDef

@author<RenatoPeixoto>
@since<09/02/2015>
@version <1.00>
@return< _oView (o) - Objeto com a Definicao do Visao do Modelo  >
@example<Nil>
@see<Nil>
/*/
//+==================================================================+

Static Function ViewDef()
Local _oStrZJR	:= FWFormStruct(2,"ZJR")	//Cabeçalho
Local _oSZJSGR	:= FWFormStruct(2,"ZJS")	//Agrupador
Local _oSZJTQS	:= FWFormStruct(2,"ZJT")	//Questões
Local _oSJVSUQ	:= FWFormStruct(2,"ZJV")	//SubQuestões
Local _oSZJURS	:= FWFormStruct(2,"ZJU")	//Respostas
Local _oModel	:= FWLoadModel("SF7351X")
Local _oView	:= FWFormView():New()

//+==================================================================+
//|Ajustando as Estruturas											 |
//+==================================================================+

_oView:SetModel(_oModel)
_oView:AddField("VW_MASTER"	,_oStrZJR,	"ZJRMASTER")
_oView:AddGrid("VW_ZJS_GR"	,_oSZJSGR,	"ZJS_AGRUP"	,/*bLinePre*/,/*bLinePost*/, /*bPreVal*/,/*bPosVal*/, /*BLoad*/)
_oView:AddGrid("VW_ZJT_QS"	,_oSZJTQS,	"ZJT_QUEST"	,/*bLinePre*/,/*bLinePost*/, /*bPreVal*/,/*bPosVal*/, /*BLoad*/)
_oView:AddGrid("VW_ZJV_SQ"	,_oSJVSUQ,	"ZJV_ITEMGR",/*bLinePre*/,/*bLinePost*/, /*bPreVal*/,/*bPosVal*/, /*BLoad*/)
_oView:AddGrid("VW_ZJU_RS"	,_oSZJURS,	"ZJU_RESPT"	,/*bLinePre*/,/*bLinePost*/, /*bPreVal*/,/*bPosVal*/, /*BLoad*/)

//+==================================================================+
//|  Monta Box Horizontal - Superior                                 |
//+==================================================================+

_oView:CreateHorizontalBox("SUPERIOR",20)
_oView:CreateVerticalBox("ZJR_CABEC",100,"SUPERIOR")

//+==================================================================+
//|  Monta Box Horizontal - Centro                                   |
//+==================================================================+

_oView:CreateHorizontalBox("CENTRO",40)
_oView:CreateVerticalBox("ZJS_AGRUP",50,"CENTRO")
_oView:CreateVerticalBox("ZJT_QUEST",50,"CENTRO")

//+==================================================================+
//|  Monta Box Horizontal - Inferior - DIREITO                       |
//+==================================================================+

_oView:CreateHorizontalBox("INFERIOR",40)
_oView:CreateVerticalBox("ZJV_ITEMGR",50,"INFERIOR")

//+==================================================================+
//|  Monta Box Horizontal - Inferior - ESQUERDO                      |
//+==================================================================+

_oView:CreateVerticalBox("ZJU_RESP",50,"INFERIOR")

//+==================================================================+
//|  Mostra Box Com Suas Visões				                         |
//+==================================================================+

_oView:SetOwnerView("VW_MASTER","ZJR_CABEC")
_oView:SetOwnerView("VW_ZJS_GR","ZJS_AGRUP")
_oView:SetOwnerView("VW_ZJT_QS","ZJT_QUEST")
_oView:SetOwnerView("VW_ZJV_SQ","ZJV_ITEMGR")
_oView:SetOwnerView("VW_ZJU_RS","ZJU_RESP" )

//+==================================================================+
//|  Seta os campos que serão auto incremento	                     |
//+==================================================================+

_oView:AddIncrementField("VW_ZJS_GR","ZJS_ITEM")
_oView:AddIncrementField("VW_ZJT_QS","ZJT_ITEM")
_oView:AddIncrementField("VW_ZJV_SQ","ZJV_ITEM")
_oView:AddIncrementField("VW_ZJU_RS","ZJU_ITEM")

//+==================================================================+
//|  Ativa os títulos do Box										 |
//+==================================================================+

_oView:EnableTitleView("VW_MASTER","Pesquisa de Satisfação")
_oView:EnableTitleView("VW_ZJS_GR","Tópicos")
_oView:EnableTitleView("VW_ZJT_QS","Questões")
_oView:EnableTitleView("VW_ZJV_SQ","SubQuestões")
_oView:EnableTitleView("VW_ZJU_RS","Respostas")

Return _oView