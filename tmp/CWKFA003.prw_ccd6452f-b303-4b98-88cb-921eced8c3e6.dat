#include "TOTVS.ch"
#include "topconn.ch"
#include "tbiconn.ch"


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ CWKFA003 º Autor ³ Microsiga          º Data ³ 18/11/10    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Envio de Pedido de Compras para aprovacao                  º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MP 11 - PROJETO CNI                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

//Processa Envio do PC para Aprovação - usado somente para testes via SmartClient
//User Function EnviaPC
//	U_CWKFA003({"01","02MT0001"})
//Return
/*/
@history 18/02/2021, Franklin de Brito de Oliveira, Correção para registrar a hora de aprovação.
/*/
User Function CWKFA003(aParam,_cPc)

Local cFuncName 	:= "CWKFA003"
Local cCodEmp    	:= ""
Local cCodFil    	:= ""
Local lContinua  	:= .F.
Local nW				:= 0
Local cFunc			:= "U_CWKFA003"
Default _cPc   	:= "" //Se o ambiente estiver inicializado já é passado o código do pedido

U_Console("Inicializando o Envio Pedido de Compra para aprovacao",cFunc)

If ValType(aParam) == "A" .and. Len(aParam) >= 2
	If ValType(aParam[1]) =="C" .and. ValType(aParam[2]) =="C"
		cCodEmp   := aParam[1]
		cCodFil   := aParam[2]
		ConOut(cFuncName+":: Parametros Recebidos => Empresa/Filial: "+cCodEmp+"/"+cCodFil)
		lContinua := .T.
	Else
		lContinua := .F.
	EndIf
Else
		lContinua := .F.
EndIf

If lContinua
	if Empty(_cPC)
		ConOut(cFuncName+":: Inicializacao do ambiente - Workflow PC Empresa/Filial: "+cCodEmp+"/"+cCodFil)
		//WFPrepEnv( <cEmpresa>, <cFilial>, <cFunname>, <aTabelas> <cModulo>)
		// Alterado por Peder Munksgaard (Do.it Sistemas) - 02/09/2014
		//WfPrepEnv(cCodEmp,cCodFil,,{"SC7","SX6","SCR","SAK","SX3"})
		WfPrepEnv(cCodEmp,cCodFil,,{"SC7","SX6","SCR","SAK","SX3","SX2"})
	endif
                                 
	_WFSendPC(cCodFil,_cPC) // Processa a rotina para Envio do Workflow de Aprovacao do PC
   
	if Empty(_cPc)
		Reset Environment
		ConOut(cFuncName+":: Finalizacao do ambiente - Workflow PC Empresa/Filial: "+cCodEmp+"/"+cCodFil)
	endif
Else
	ConOut(cFuncName+":: ERRO no recebimento dos Paramentros (Empresa/Filial)!!")
	ConOut(cFuncName+"::     Tipo esperado: A |    Tipo Recebido: " + ValType(aParam))
	ConOut(cFuncName+"::  Tamanho esperado: 2 | Tamanho Recebido: " + LTrim(Str(Len(aParam))))
	If ValType(aParam) == "A"
		For nW := 1 to Len(aParam)
			ConOut(cFuncName+"::  Param["+LTrim(Str(nW))+"] -     Tipo Recebido: " + ValType(aParam[nW]))
			ConOut(cFuncName+"::  Param["+LTrim(Str(nW))+"] -  Tamanho Recebido: " + LTrim(Str(Len(aParam[nW]))))
			If ValType(aParam[nW]) ==  "C"
				ConOut(cFuncName+"::  Param["+LTrim(Str(nW))+"] - Conteudo Recebido: " + aParam[nW])
			EndIf
		Next nW
	EndIf
EndIf

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³_WFSendPC º Autor ³ Microsiga          º Data ³ 18/11/10    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Envio de Pedido de Compras para aprovacao                  º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MP 11 - PROJETO CNI                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/


Static Function _WFSendPC(_cFil,_cPC)

Local cQuery   := ""
Local _nTotal    := 0
Local _nTotICM   := 0
Local _nTotIPI   := 0
Local _nTotFRE   := 0
Local _nTotDSP   := 0
Local _nTotSEG   := 0
Local _cWfDir    := Iif ( Right(RTrim(GetNewPar("MV_WFDIRWF","workflow\")),1)=="\",;
                          GetNewPar("MV_WFDIRWF","workflow\"),;
                          GetNewPar("MV_WFDIRWF","workflow\") + "\" )
Local cHtmlModel := ""
Local cFuncName  := "_WFSendPC"
Local cWFHTTP    := Iif ( Right(RTrim(GetNewPar("MV_XWFHTTP","http://192.168.1.100:8089/workflow/")),1)=="/",;
                          GetNewPar("MV_XWFHTTP","http://192.168.1.100:8089/workflow/"),;
                          GetNewPar("MV_XWFHTTP","http://192.168.1.100:8089/workflow/") + "/" )
Local aDirHtml   := {}
Local cDirHtml   := "html\"
Local cDirPasta  := "aprovpc\"
Local cDescProd  := ""
Local i
Local cFunc		 := "_WFSendPC"
Local _cSituaca  := ""
Local lOracle	 := "ORACLE"$Upper(TCGetDB())
Local cUndNeg		:= FWUnitBusiness()
Local xNomEmp 	 := "" //Adic. Fabrica Doit SP - 06/03/2014

U_Console("Inicializando o Envio Pedido de Compra para aprovacao",cFunc)

Private oHTML
Private cItem,cCod,cDesc,cUM,nQtde,nUprc,nTot,nToler,cUComp,dEnt,cCC,cCCDesc
Private lProcesso := .f.
Private xNomFil := "" //Adic. Fabrica Doit SP - 06/03/2014

If Select("TSC7") > 0
	TSC7->(dbCloseArea())
Endif

cQuery := " select "
cQuery += "    C7_FILIAL ,C7_NUM ,min(C7_USER) as C7_USER ,min(C7_EMISSAO) as C7_EMISSAO ,min(C7_CONTATO) as C7_CONTATO "
cQuery += "   ,min(A2_NOME) as A2_NOME   ,min(A2_END) as A2_END  ,min(A2_BAIRRO) as A2_BAIRRO  ,min(A2_MUN) as A2_MUN  ,min(A2_EST) as A2_EST "
cQuery += "   ,min(E4_DESCRI) as E4_DESCRI, min(A2_CGC) as A2_CGC, min(C7_CC) as C7_CC, min(C7_NUMPR) as C7_NUMPR "
If lOracle
	cQuery += " 	,nvl(min(C1_XCODCOM),' ') as C1_XCODCOM ,nvl(min(Y1_USER),' ') as Y1_USER "
Else
	cQuery += " 	,isnull(min(C1_XCODCOM),' ') as C1_XCODCOM ,isnull(min(Y1_USER),' ') as Y1_USER "
EndIf                                                                                                 
cQuery += " from " + RetSqlName("SC7") + " SC7 "
cQuery += " inner join " + RetSqlName("SA2") + " SA2 on SA2.D_E_L_E_T_ = ' ' and A2_FILIAL = '" + xFilial("SA2") + "' and A2_COD    = C7_FORNECE and A2_LOJA = C7_LOJA "
cQuery += "  left join " + RetSqlName("SE4") + " SE4 on SE4.D_E_L_E_T_ = ' ' and E4_FILIAL = '" + xFilial("SE4") + "' and E4_CODIGO = C7_COND "
cQuery += "  left join " + RetSqlName("SC1") + " SC1 on SC1.D_E_L_E_T_ = ' ' and C1_FILIAL = C7_FILIAL  and C1_NUM    = C7_NUMSC and C1_ITEM   = C7_ITEMSC "
cQuery += "  left join " + RetSqlName("SY1") + " SY1 on SY1.D_E_L_E_T_ = ' ' and Y1_FILIAL = '" + xFilial("SY1") + "' and Y1_COD    = C1_XCODCOM "
cQuery += " where SC7.D_E_L_E_T_ = ' ' and C7_FILIAL  = '" + xFilial("SC7") + "' "
cQuery += "   and C7_WFE         = 'F' and C7_CONAPRO = 'B' "
// Condicao para identificar se trata-se de envio para primeiro ou proximo nivel
If _cPC == Nil	// primeiro nivel executado pelo Scheduler Protheus nao passa Num. do PC
  cQuery += " and C7_FILIAL      = '"+_cFil+"' "
Else			// proximos niveis executados via retorno
  cQuery += " and C7_FILIAL      = '"+_cFil+"' and C7_NUM = '"+_cPC+"' "
EndIf
cQuery += " group by C7_FILIAL ,C7_NUM "
cQuery += " order by C7_FILIAL ,C7_NUM "

TCQuery cQuery New Alias "TSC7"
TSC7->(dbGoTop())

While TSC7->(!Eof())

	U_Console("PC encontrado para processamento. Filial/PC no." + TSC7->C7_FILIAL+"/"+TSC7->C7_NUM,cFunc)
	lProcesso := .t.

	SCR->(dbSetOrder(1))
	SCR->(dbSeek(xFilial("SCR")+"PC"+TSC7->C7_NUM))

	_aLogApr  := {}

	While SCR->(!Eof()) .and. SCR->CR_FILIAL == XFilial("SCR") .and. Alltrim(SCR->CR_NUM) == Alltrim(TSC7->C7_NUM) .and. SCR->CR_TIPO == "PC"
		U_Console("Alcada encontrada para processamento(1). Filial/Num no." + SCR->CR_FILIAL+"/"+SCR->CR_NUM,cFunc)
		_cSituaca := ""
		IF SCR->CR_STATUS == "01"
			_cSituaca := OemToAnsi("Aguardando")
		ELSEIF SCR->CR_STATUS == "02"
			_cSituaca := OemToAnsi("Em Aprovacao")
		ELSEIF SCR->CR_STATUS == "03"
			_cSituaca := "Pedido Aprovado"
		ELSEIF SCR->CR_STATUS == "04"
			_cSituaca := "Pedido Bloqueado"
		ELSEIF SCR->CR_STATUS == "05"
			_cSituaca := OemToAnsi("Nivel Liberado ")
		ENDIF
		Aadd(_aLogApr,{SCR->CR_NIVEL,UsrFullName(SCR->CR_USER),_cSituaca,Dtoc(SCR->CR_DATALIB),SCR->CR_OBS})
		SCR->(dbSkip())
	Enddo

	SCR->( dbGoTop() )
	SCR->( dbSetOrder(1) )
	SCR->( dbSeek( xFilial("SCR") + "PC" + TSC7->C7_NUM ) )

	While SCR->(!Eof()) .and. SCR->CR_FILIAL == XFilial("SCR") .and. Alltrim(SCR->CR_NUM) == Alltrim(TSC7->C7_NUM) .and. SCR->CR_TIPO == "PC"
		U_Console("Alcada encontrada para processamento(2). Filial/Num no." + SCR->CR_FILIAL+"/"+SCR->CR_NUM,cFunc)

		IF SCR->CR_STATUS <> "02" // nivel 02 indica proxima que recebe e-mail
			SCR->(dbSkip())
			Loop
		ENDIF

		//Abre o HTML
		
		xNomEmp := U_retDesc("FILIAL", TSC7->C7_FILIAL) //Adic. Fabrica Doit SP - 06/03/2014		
		oProcess := TWFProcess():New( "PEDIDO", "Pedido de Compras" )
		oProcess:NewTask( "000001", _cWfDir+"aprovacaopc.htm" )  //Alterado o nome para não interferir na rotina existente em produção durante os testes - Fabrica Doit SP - 06/03/2014
		oProcess:cSubject := "Aprovação do Pedido de Compra " + TSC7->C7_NUM + " - " + xNomEmp //Alt. Fabrica Doit SP - 06/03/2014			
		oProcess:bReturn  := "U__fWFRetPC()"
		oProcess:UserSiga := TSC7->C7_USER
		oProcess:NewVersion(.T.)
		oHTML   := oProcess:oHTML

		oHtml:ValByName( "FILIAL"     , TSC7->C7_FILIAL )
		oHtml:ValByName( "EMPRESA"    , xNomFil )  //Adic. Fabrica Doit SP - 06/03/2014			
		oHtml:ValByName( "NUMPC"      , TSC7->C7_NUM )
		oHtml:ValByName( "c7_emissao" , Dtoc(StoD(TSC7->C7_EMISSAO)) )
		oHtml:ValByName( "a2_nome"    , TSC7->A2_NOME )
		oHtml:ValByName( "a2_end"     , TSC7->A2_END	)
		oHtml:ValByName( "a2_bairro"  , TSC7->A2_BAIRRO )
		oHtml:ValByName( "a2_mun"     , TSC7->A2_MUN )
		oHtml:ValByName( "a2_est"     , TSC7->A2_EST )
		oHtml:ValByName( "c7_user"    , Iif(Empty(TSC7->Y1_USER),UsrFullName(TSC7->C7_USER),UsrFullName(TSC7->Y1_USER)) )
		oHtml:ValByName( "CAPROV"     , SCR->CR_USER )
		oHtml:ValByName( "APROVADOR"  , UsrFullName(SCR->CR_USER) )
		oHtml:ValByName( "c7_contato" , TSC7->C7_CONTATO )
		oHtml:ValByName( "e4_descri"  , TSC7->E4_DESCRI )

		//Tratamento Exclusivo para a FIRJAN pois possui novos campos
		If Upper( AllTrim( Substr( TSC7->C7_FILIAL,3,2) ) ) == 'RJ'
			oHtml:ValByName( "a2_cgc"	, TSC7->A2_CGC )
			oHtml:ValByName( "c7_filial"	, TSC7->C7_FILIAL + " - " + U_retDesc("FILIAL", TSC7->C7_FILIAL) )
			oHtml:ValByName( "cc"        , TSC7->C7_CC )
			oHtml:ValByName( "desccc"	, Posicione("CTT", 1, xFilial("CTT") + TSC7->C7_CC, "CTT_DESC01") )
			oHtml:ValByName( "c7_numpr"	, TSC7->C7_NUMPR )
		Endif

		_nSubTot := 0
		_nFrete  := 0
		_nTotal  := 0

		SC7->(dbSetOrder(1))
		SC7->(dbSeek(xFilial("SC7")+Alltrim(TSC7->C7_NUM)))
		//ConOut(cFuncName+":: Processando PC Filial/No.: "+SC7->C7_FILIAL+"/"+SC7->C7_NUM)
		U_Console(cFuncName+":: Processando PC Filial/No.: "+SC7->C7_FILIAL+"/"+SC7->C7_NUM)        

		While !SC7->(Eof()) .and. SC7->C7_FILIAL == XFilial("SC7") .and. SC7->C7_NUM == Alltrim(TSC7->C7_NUM)

			//ConOut(cFuncName+":: Processando Item da PC Filial/No./Item: "+SC7->C7_FILIAL+"/"+SC7->C7_NUM+"/"+SC7->C7_ITEM)
			U_Console(cFuncName+":: Processando Item da PC Filial/No./Item: "+SC7->C7_FILIAL+"/"+SC7->C7_NUM+"/"+SC7->C7_ITEM)
			
			SB1->(dbSetOrder(1))
			SB1->(dbSeek(XFilial("SB1")+SC7->C7_PRODUTO))

			cDescProd  := Posicione("SB5",1,xFilial("SB5")+SB1->B1_COD,"B5_CEME")
			DbSelectArea("SB1")

			AAdd( (oHtml:ValByName( "prod.cItem"    )),SC7->C7_ITEM )
			AAdd( (oHtml:ValByName( "prod.cCod"     )),SC7->C7_PRODUTO )
			AAdd( (oHtml:ValByName( "prod.cDesc"    )),Iif(Empty(cDescProd),RTrim(SB1->B1_DESC),RTrim(SB1->B1_DESC) + " - " + RTrim(cDescProd)) ) // Cfme Solic.p/ Lucas Uchasky - Kley 20Mar2013
			// Comentado por Peder Munksgaard (Do.it Sistemas) em 02/09/2014
			//AAdd( (oHtml:ValByName( "prod.cNrSC"    )),SC7->C7_NUMSC )  //Adic. Fabrica Doit SP - 06/03/2014
			AAdd( (oHtml:ValByName( "prod.cUM"      )),SC7->C7_UM )
			AAdd( (oHtml:ValByName( "prod.nQuant"   )),TRANSFORM( SC7->C7_QUANT,'@E 999,999,999.99' ) )
			AAdd( (oHtml:ValByName( "prod.nVrUnit"  )),TRANSFORM( SC7->C7_PRECO,'@E 999,999,999.99' ) )
			AAdd( (oHtml:ValByName( "prod.nVrTotal" )),TRANSFORM( SC7->C7_TOTAL,'@E 999,999,999.99' ) )
			AAdd( (oHtml:ValByName( "prod.dEntrega" )),Dtoc(SC7->C7_DATPRF) )
			AAdd( (oHtml:ValByName( "prod.cObs" )),SC7->C7_OBS )
			WFSalvaID('SC7','SC7->C7_WFE', .T.)

			_nSubTot += SC7->C7_TOTAL
			_nFrete  += SC7->C7_VALFRE
			_nTotal  += SC7->(C7_TOTAL+C7_VALFRE)

			SC7->(DbSkip())
		Enddo

		oHtml:ValByName( "vlrtotal" , TRANSFORM( _nSubTot,'@E 999,999,999.99' ) )
		oHtml:ValByName( "vlrfrete" , TRANSFORM( _nFrete ,'@E 999,999,999.99' ) )
		oHtml:ValByName( "totgeral" , TRANSFORM( _nTotal ,'@E 999,999,999.99' ) )

		For i := 1 to Len(_aLogApr)
			AAdd( (oHtml:ValByName( "proc.nivel"   )),_aLogApr[i,1] )
			AAdd( (oHtml:ValByName( "proc.cApov"   )),_aLogApr[i,2] )
			AAdd( (oHtml:ValByName( "proc.cSit"    )),_aLogApr[i,3] )
			AAdd( (oHtml:ValByName( "proc.dDtLib"  )),_aLogApr[i,4] )
			AAdd( (oHtml:ValByName( "proc.cObs"    )),_aLogApr[i,5] )
		Next

		//oProcess:cTo := "APROVPC"
		oProcess:cTo := Nil

		// Verifica e cria, se necessario, o diretorio para gravacao do HTML
		aDirHtml   := Directory(_cWfDir+"emp"+cEmpAnt+"\*.*", "D",Nil,.T.)
		If aScan( aDirHtml, {|aDir| aDir[1] == Upper( Iif(Right(cDirHtml,1)=="\", Left(cDirHtml,Len(cDirHtml)-1), cDirHtml) ) } ) == 0
			If MakeDir(_cWfDir+"emp"+cEmpAnt+"\"+cDirHtml)	 == 0
				//ConOut(cFuncName+":: Diretorio dos HTML's criado com sucesso. -> "+_cWfDir+"emp"+cEmpAnt+"\"+cDirHtml )
				U_Console(cFuncName+":: Diretorio dos HTML's criado com sucesso. -> "+_cWfDir+"emp"+cEmpAnt+"\"+cDirHtml)
			Else
				//ConOut(cFuncName+":: Erro na criacao do diretorio dos HTML's! -> "+_cWfDir+"emp"+cEmpAnt+"\"+cDirHtml )
				U_Console(cFuncName+":: Erro na criacao do diretorio dos HTML's! -> "+_cWfDir+"emp"+cEmpAnt+"\"+cDirHtml )
				cDirHtml := "temp\"
			EndIf
		EndIf

		// Verifica e cria, se necessario, a pasta especifica do Workflow para gravacao do HTML
		aDirHtml   := Directory(_cWfDir+"emp"+cEmpAnt+"\"+cDirHtml+"*.*", "D",Nil,.T.)
		If aScan( aDirHtml, {|aDir| aDir[1] == Upper(Iif(Right(cDirPasta,1)=="\", Left(cDirPasta,Len(cDirPasta)-1), cDirPasta) ) } ) == 0
			If MakeDir(_cWfDir+"emp"+cEmpAnt+"\"+cDirHtml+cDirPasta) == 0
				//ConOut(cFuncName+":: Diretorio de Pasta dos HTML's criado com sucesso. -> "+_cWfDir+"emp"+cEmpAnt+"\"+cDirHtml+cDirPasta )
				U_Console(cFuncName+":: Diretorio de Pasta dos HTML's criado com sucesso. -> "+_cWfDir+"emp"+cEmpAnt+"\"+cDirHtml+cDirPasta )
			Else
				//ConOut(cFuncName+":: Erro na criacao do diretorio dos HTML's! -> "+_cWfDir+"emp"+cEmpAnt+"\"+cDirHtml+cDirPasta )
				U_Console(cFuncName+":: Erro na criacao do diretorio dos HTML's! -> "+_cWfDir+"emp"+cEmpAnt+"\"+cDirHtml+cDirPasta )
				cDirPasta := ""
			EndIf
		EndIf

		cDirHtml2  := "emp"+cEmpAnt+"\" + cDirHtml + cDirPasta
		cMailID    := oProcess:Start(_cWfDir+cDirHtml2,.T.)
		
		If File(_cWfDir+cDirHtml2+cMailID+".htm")
			//ConOut(cFuncName+":: Arquivo HTML copiado com sucesso: "+_cWfDir+cDirHtml2+cMailID+".htm" )
			U_Console(cFuncName+":: Arquivo HTML copiado com sucesso: "+_cWfDir+cDirHtml2+cMailID+".htm" )
			
			Reclock("SCR",.F.)
			SCR->CR_WF		:= "1" //--Enviado
			SCR->CR_WFID	:= cMailID  //-- Campo CR_WFID deve estar com tamanho 20
			SCR->(MSUnlock())
			
		Else
			//ConOut(cFuncName+":: ATENCAO! Arquivo HTML NAO copiado: "+_cWfDir+cDirHtml2+cMailID+".htm")
			U_Console(cFuncName+":: ATENCAO! Arquivo HTML NAO copiado: "+_cWfDir+cDirHtml2+cMailID+".htm")
		EndIf

		cHtmlModel := _cWfDir+"LinkPC.htm"
		cAssunto   := "Pedido de Compras Nr. " +TSC7->C7_NUM

		oProcess:NewTask(cAssunto, cHtmlModel)
		//ConOut(cFuncName+":: (INICIO|WFLINK)Processo: " + oProcess:fProcessID + " / Task: " + oProcess:fTaskID )
		U_Console(cFuncName+":: (INICIO|WFLINK)Processo: " + oProcess:fProcessID + " / Task: " + oProcess:fTaskID )
		oProcess:cSubject := cAssunto
		//oProcess:cTo := "gerson.soeltl@doitsistemas.com.br" //Alltrim(UsrRetMail( SCR->CR_USER ))
		oProcess:cTo := Alltrim(UsrRetMail( SCR->CR_USER ))
		U_Console(cFuncName+":: (INICIO|WFLINK)Email: " + oProcess:cTo)

		oProcess:ohtml:ValByName("CAPROVADOR",UsrRetname(SCR->CR_USER))
		
		//Adicionado após numero da SC a que filial a qual pertence. Por Walmir Junior em 14/11/2016.
		_cFilX := cFilAnt
		U_SF0205X(cEmpAnt,TSC7->C7_FILIAL)
		oProcess:ohtml:ValByName("CNUMPC",TSC7->C7_NUM + " | FILIAL: " + TSC7->C7_FILIAL + "-" + SM0->M0_FILIAL)
		U_SF0205X(cEmpAnt,_cFilX)

		oProcess:ohtml:ValByName("proc_link",StrTran(cWFHTTP+cDirHtml2+cMailID+".htm","\","/"))
		oProcess:Start()

		SCR->(dbSkip())
	Enddo
	TSC7->(dbSkip())
Enddo

TSC7->(dbCloseArea())

If !lProcesso
	//ConOut(cFuncName+":: Nao Houve Processamento (Envio)")
	U_Console(cFuncName+":: Nao Houve Processamento (Envio)")
Else
	//ConOut(cFuncName+":: Processamento (Envio) finalizado")
	U_Console(cFuncName+":: Processamento (Envio) finalizado")
EndIf

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³_fWFRetPC ºAutor  ³Microsiga           º Data ³  18/11/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Retorno da Solicitação                                     º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MP 11 - PROJETO CNI                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

User Function _fWFRetPC(oProcess)

Local cFunc		:= "U_fWFRetPC"

U_Console("Inicializando o Retorno do Pedido de Compra",cFunc)

_cFilial  := alltrim(oProcess:oHtml:RetByName("FILIAL"))
_cFilial  := IIF(Alltrim(_cFilial)=="",Xfilial("SC7"),_cFilial)
_cNumPC	  := alltrim(oProcess:oHtml:RetByName("NUMPC"))
_cObs     := alltrim(oProcess:oHtml:RetByName("OBS"))
_cAprov	  := alltrim(oProcess:oHtml:RetByName("CAPROV"))
cOpc	  := alltrim(oProcess:oHtml:RetByName("OPC"))

oProcess:Finish() // FINALIZA O PROCESSO
lLiberou := .f.

SC7->(dbSetOrder(1))
SC7->(dbSeek(_cFilial+_cNumPC))

SC1->(dbSetOrder(1))
SC1->(dbSeek(_cFilial+SC7->(C7_NUMSC+C7_ITEMSC)))

SCR->(dbSetOrder(2))
IF SCR->(dbSeek(XFilial("SCR")+"PC"+_cNumPC+Space(TamSx3("CR_NUM")[1]-Len(_cNumPC))+_cAprov))   

	If Empty(SCR->CR_DATALIB) .And. !(SCR->CR_STATUS $ "03#04#05")
	
		U_Console("Alcada Encontrada. CR_FILIAL/CR_TIPO/CR_NUM/CR_APROV: " +RTrim(SCR->CR_FILIAL)+"/"+RTrim(SCR->CR_TIPO)+"/"+RTrim(SCR->CR_NUM)+"/"+RTrim(SCR->CR_APROV),cFunc)

		lLiberou := MaAlcDoc({SCR->CR_NUM,SCR->CR_TIPO,SCR->CR_TOTAL,SCR->CR_APROV,,SC7->C7_APROV,,,,,_cObs},dDataBase,IIF(cOpc == "APROVAR",4,6))
	
		Reclock("SCR",.F.)
			SCR->CR_WF := "2"	//	Status 2 - respondido
			SCR->CR_XHRALIB := Time()
		MSUnlock()
		
		IF cOpc == "APROVAR"
			_fVerifPC(_cFilial,_cNumPC,_cObs,,_cAprov)
		ELSE
			SC7->(dbSetOrder(1))
			SC7->(dbSeek(_cFilial+_cNumPC))
		
			SC1->(dbSetOrder(1))
			SC1->(dbSeek(_cFilial+SC7->(C7_NUMSC+C7_ITEMSC)))
		
			SY1->(dbSetOrder(1))
			SY1->(dbSeek(xFilial("SY1")+SC1->C1_XCODCOM))
		
			U_Console("Enviando e-mail de notificação de Pedido Reprovado. Filial/PC no." + _cFilial+"/"+_cNumPC,cFunc)
			U_NotifWF("R",_cNumPC,"Pedido de Compra",Iif(Empty(SY1->Y1_USER),SC7->C7_USER,SY1->Y1_USER),_cObs)	// (A)provada / (R)eprovada
		    /*
			While !SC7->(Eof()) .and. SC7->C7_FILIAL == XFilial("SC7") .and. SC7->C7_NUM == Alltrim(_cNumPC)
				WFSalvaID('SC7','SC7->C7_WFE', .F.)
				SC7->(dbSkip())
			End */
		ENDIF
		
	Else

		U_Console("Aprovacao/Reprovacao do PC "+_cNumPC+" ja efetuada anteriormente. Processo nao sera executado!")	
		Return
	
	EndIf		
	
ELSE
	
	U_Console("Alcada NAO Encontrada! CR_FILIAL/CR_TIPO/CR_NUM/CR_TOTAL/CR_APROV: " + xFilial("SCR")+"/"+"PC"+"/"+_cNumPC+"/"+RTrim(_cAprov),cFunc)
	
ENDIF

Return()

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³_fVerifPC ºAutor  ³Microsiga           º Data ³  18/11/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Verifica liberação do pedido                               º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MP 11 - PROJETO CNI                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function _fVerifPC(_cFil,_cNumPC,_cObs,_cNiv,_cAprov)

Local _cQuery  := ""
Local _cArqSCR := CriaTrab(nil,.f.)
Local _cNome   := ""
Local cFunc	   := "_fVerifPC" 
Local aAreaSC7 := {}

Default _cAprov := "" //Caio.Santos - FSW - 10/02/2012 - Correcao na gravacao usuario workflow

_cNome := UsrFullName(_cAprov)

// verifica se o pedido foi totalmente liberado
_cQuery := "SELECT * FROM "+RetSqlName("SCR")+" "
_cQuery += "WHERE D_E_L_E_T_ = ' ' AND CR_FILIAL = '"+XFilial("SCR")+"' AND CR_NUM = '"+_cNumPC+"' AND CR_STATUS NOT IN ('03','05') AND CR_TIPO = 'PC' "
_cQuery := ChangeQuery(_cQuery)

dbUseArea(.T.,"TOPCONN",TCGenQry(,,_cQuery),_cArqSCR,.t.,.t.)

IF (_cArqSCR)->(Eof())

	SC7->(dbSetOrder(1))
	SC7->(dbSeek(_cFil+_cNumPC))

	SC1->(dbSetOrder(1))
	SC1->(dbSeek(_cFil+SC7->(C7_NUMSC+C7_ITEMSC)))

	SY1->(dbSetOrder(1))
	SY1->(dbSeek(xFilial("SY1")+SC1->C1_XCODCOM))

	U_Console("Enviando e-mail de notificação de Pedido Aprovado. Filial/PC no." + _cFil+"/"+_cNumPC,cFunc)
	U_NotifWF("A",_cNumPC,"Pedido de Compra",Iif(Empty(SY1->Y1_USER),SC7->C7_USER,SY1->Y1_USER),_cObs)	// (A)provada / (R)eprovada

	While SC7->(!Eof()) .and. SC7->(C7_FILIAL+C7_NUM) == _cFil+_cNumPC
		RecLock("SC7",.F.)
		SC7->C7_CONAPRO	:= "L"
		SC7->(msUnlock())

		//Caio.Santos - 11/01/13 - Req.72
		RSTSCLOG("LIB",1,_cNome)

		SC7->(dbSkip())
	EndDo
ELSE   
   
	aAreaSC7 := SC7->(GetArea())  
	
	SC7->(dbSetOrder(1))
	If SC7->(dbSeek(_cFil+_cNumPC))
		While !SC7->(Eof()) .and. SC7->C7_FILIAL == XFilial("SC7") .and. SC7->C7_NUM == Alltrim(_cNumPC)
			WFSalvaID('SC7','SC7->C7_WFE', .F.)
			SC7->(dbSkip())
		EndDo
	EndIf
	
	RestArea(aAreaSC7)
	
	_WFSendPC(_cFil,_cNumPC) // envia e-mail para o proximo aprovador
	
ENDIF

Return

//Static Function retDesc(cTipo, cCodigo)
User Function retDesc(cTipo, cCodigo)
  Local cDesc := ""
  Local aArea := {GetArea(), SM0->(GetArea())}
  Local nRecSM0 := SM0->(Recno())
  Local aUsuario := {}

  If (cTipo == "FILIAL")
    DbSelectArea("SM0")
    SM0->(DbGoTop())
    While (SM0->(!Eof()))
      If (AllTrim(cCodigo) == AllTrim(SM0->M0_CODFIL))
        cDesc := SM0->M0_NOME
        xNomFil := SM0->M0_FILIAL //Adic. Fabrica Doit SP - 06/03/2014
      Endif

      SM0->(DbSkip())
    Enddo

    SM0->(DbGoTo(nRecSM0))
  Elseif (cTipo == "SOLICIT")
    PswOrder(2)
    If PswSeek(cCodigo)
	  aUsuario := PswRet()
	  cDesc := PswRet(1)[1][4]
    EndIf
  Endif

  aEval(aArea, {|x| RestArea(x)})
Return(cDesc)
