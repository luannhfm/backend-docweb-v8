#Include 'Protheus.ch'
#Include 'FWMVCDEF.CH'

/*/{Protheus.doc} SS3308S
Cadastro de CEI

@author 	Sergio Ricardo Leite Salustiano
@since 		19/08/2014
@version 	1.0

@return Nil, Nulo
/*/
User Function SS3308S()

	Local oBrowse
	oBrowse:= FWMBrowse():New()
	oBrowse:SetAlias('ZZP')
	oBrowse:SetDescription('Cadastro de C.E.I.')
	oBrowse:Activate()
	
Return Nil

Static Function MenuDef()

	Local aRotina := {}

	ADD OPTION aRotina Title 'Visualizar' Action 'VIEWDEF.SS3308S' OPERATION 2 ACCESS 0
	ADD OPTION aRotina Title 'Incluir'    Action 'VIEWDEF.SS3308S' OPERATION 3 ACCESS 0
	ADD OPTION aRotina Title 'Alterar'    Action 'VIEWDEF.SS3308S' OPERATION 4 ACCESS 0
	ADD OPTION aRotina Title 'Excluir'    Action 'VIEWDEF.SS3308S' OPERATION 5 ACCESS 0
	ADD OPTION aRotina Title 'Imprimir'   Action 'VIEWDEF.SS3308S' OPERATION 8 ACCESS 0
	ADD OPTION aRotina Title 'Copiar'     Action 'VIEWDEF.SS3308S' OPERATION 9 ACCESS 0

Return aRotina

Static Function ModelDef()

	Local oStruZZP :=FWFormStruct(1,'ZZP')
	Local oModel
	
	oModel := MPFormModel():New('SS3308SS')
	oModel:AddFields('ZZPMASTER',/*cOwner*/,oStruZZP)
	oModel:SetDescription('Cadastro de C.E.I')
	oModel:GetModel('ZZPMASTER'):SetDescription('Cadastro de C.E.I.')
	
Return oModel

Static Function ViewDef()

	Local oModel   := FWLoadModel('SS3308S')
	Local oStruZZP := FWFormStruct(2,'ZZP')
	Local oView

	oView := FWFormView():New()
	oView:SetModel(oModel)
	oView:AddField('VIEW_ZZP', oStruZZP, 'ZZPMASTER')
	oView:CreateHorizontalBox('TELA',100)
	oView:SetOwnerView('VIEW_ZZP','TELA')
	
Return oView

/*
--------------------------------------------------------------------------------
{Protheus.doc} <fVldRot> Validação antes da salvar registro.
 
@author Walmir Junior    
@since 10/02/2019
@version<1.00>
@return
@example
@see
--------------------------------------------------------------------------------
*/
Static Function fVldRot()
Local _lRet 		:=	.T.
Local _oModel		:= FwModelActive()
Local _oStruZZP 	:= _oModel:GetModel("ZZPMASTER")
Local _cCEI			:= _oStruZZP:GetValue("ZZP_CEI")
Local _aPes			:= {7,4,1,8,5,2,1,6,3,7,4} //Algorismos EENNNNNNNNA
Local _aCEI			:= Array(13)
Local _nx			:= 0
Local _nSom			:= 0
Local _nDig			:= 0

If _cCEI $	"0000000000000#1111111111111#2222222222222#3333333333333#4444444444444#";
		 	+"5555555555555#6666666666666#7777777777777#8888888888888#9999999999999"
	Help( ,, 'ATENÇÃO',, "O CEI informado é inválido!", 1, 0,,,,,,{"Confirme a informação e informe um valor válido."})
	_lRet := .F.
/*Else
	For _nx := 1 To len(_cCEI)
		_aCEI[_nx]	:= val(SubStr(_nx,1)) * Iif(_nx <= 11,val(_aPes[_nx]),1)
		Iif _nx <= 11
			_nSom 		:= _nSom + val(SubStr(_nx,1))
		Else
			_nDig := Val(_aCEI[12] + _aCEI[13]) 
		EndIf
	Next _nx
*/	
EndIf

Return _lRet

/*
--------------------------------------------------------------------------------
{Protheus.doc} <SS3308SS> Pontos de Entrada de SS3308SS
 
@author Walmir Junior    
@since 10/02/2019
@version<1.00>
@return
@example
@see
--------------------------------------------------------------------------------
*/
User Function SS3308SS()
	Local _aPARAMIXB	:= PARAMIXB
	Local _xRet			:= Nil
	
	Do Case
		Case _aPARAMIXB[2] == "MODELPRE"				//Antes da alteracao de qualquer campo
			_xRet := .T.
		Case _aPARAMIXB[2] == "MODELPOS"				//Na validacao total do modelo
			_xRet := .T.
		Case _aPARAMIXB[2] == "FORMPRE"					//Antes da alteracao de qualquer campo de formulário
			_xRet := .T.
		Case _aPARAMIXB[2] == "FORMPOS"					//Na validação total do formulário
			_xRet := fVldRot()
		Case _aPARAMIXB[2] == "FORMLINEPRE"				//Antes da alteração da linha do formulário FWFORMGRID
			_xRet := .T.
		Case _aPARAMIXB[2] == "FORMLINEPOS"				//Na validação total da linha do formulario FWFORMGRID
			_xRet := Nil
		Case _aPARAMIXB[2] == "MODELCOMMITTTS"			//Após a gravação total do modelo e dentro da transacao
			_xRet := Nil
		Case _aPARAMIXB[2] == "MODELCOMMITNTTS"			//Após a gravação total do modelo e fora da gravação
			_xRet := Nil
		Case _aPARAMIXB[2] == "FORMCOMMITTTSPRE"		//Antes da gravação da tabela do formulário
			_xRet := Nil
		Case _aPARAMIXB[2] == "FORMCOMMITTTSPOS"		//Após a gravação da tabela do formulário
			_xRet := Nil
		Case _aPARAMIXB[2] == "FORMCANCEL"				//No cancelamento do botão
			_xRet := .T.
		Case _aPARAMIXB[2] == "MODELVLDACTIVE"			//Na ativação do modelo
			_xRet := .T.
		Case _aPARAMIXB[2] == "BUTTONBAR"				//Para inclusão de botões na ControlBar
			_xRet := {}
		Case _aPARAMIXB[2] == "MODELCANCEL"				//Na ação do botão Cancelar
			_xRet := .T.
	EndcAse
	
Return _xRet