#Include 'Protheus.ch'

User Function DRATVR()

Local aArea		:= GetArea()
Local _cMat 		:= SRA->RA_MAT
Local _cFil		:= SRA->RA_FILIAL
Local aBasica		:={}
Local _nCustFunc 	:= 0
Local _nCustEmp	:= 0
Local _VlrSal		:= IIF(SRA->RA_CATFUNC <> 'J',SRA->RA_SALARIO,M_BSETOTVR)

M_V432 := 0
M_V764	:= 0
M_V433 := 0
M_V761 := 0

/*
|----------------------------------------------------------------------------|
|																						|
|	Regra:																				|
|	1 - Funcionários Mensalistas que utilizam o SODEXO, não recalcula.			|
|	2 - Funcionários Aulistas que utilizam SODEXO, recalcula, porque			|
|		A identificação da faixa salarial deve ser pela verba 113 e				|
|		não pelo salário do RA_SALARIO.												|
|	3 - Funcioniário que não possui SODEXO, e tenha a verba						|
|		762 - TOTAL FATURA DR, irá calcular porque não é atendido				|
|		pela rotina de Cesta Basica													|
|	4 - Funcionários que possuem SODEXO podem possuir a verba 762				|
|		com isso deve ser recalculado.												|
|																						|
|----------------------------------------------------------------------------|
*/


CargaCB(@aBasica,_cFil)

IF (SRA->RA_CATFUNC = 'J' .OR. M_V762 > 0 ) //.AND. (SRA->RA_CATFUNC = 'M' .AND. SRA->RA_HRSMES >= 200)))

	If M_BSETOTVR > 0 .AND. SRA->RA_CATFUNC = 'J' .AND. M_V771 > 0
		If M_BSETOTVR  >= aBasica[1,2] .And. M_BSETOTVR  <= aBasica[1,3]	
			_nCustFunc := ( M_V771 * aBasica[1,4] ) / 100
		ElseIf M_BSETOTVR  >= aBasica[1,5] .And. M_BSETOTVR  <= aBasica[1,6]
			_nCustFunc := ( M_V771 * aBasica[1,7] ) / 100
		ElseIf M_BSETOTVR  >= aBasica[1,8] .And. M_BSETOTVR  <= aBasica[1,9]
			_nCustFunc := ( M_V771 * aBasica[1,10] ) / 100
		ElseIf M_BSETOTVR  >= aBasica[1,11] .And. M_BSETOTVR  <= aBasica[1,12]
			_nCustFunc := ( M_V771 * aBasica[1,13] ) / 100
		ElseIf M_BSETOTVR  >= aBasica[1,14] .And. M_BSETOTVR  <= aBasica[1,15]
			_nCustFunc := ( M_V771 * aBasica[1,16] ) / 100
		Endif
		
		_nCustEmp := (M_V771 -  _nCustFunc)
		M_V432 := _nCustFunc
		M_V764 := _nCustEmp  
		
		FDELPD("432",,)                
		FGERAVERBA("432",M_V432,,,,,,,,,.T.,,,,)          
		
		FDELPD("764",,)        
		FGERAVERBA("764",M_V764,,,,,,,,,.T.,,,,)                      
		
	EndIf
	
	If M_V762 > 0 
	
		If _VlrSal >= aBasica[1,2] .And. _VlrSal <= aBasica[1,3]	
			_nCustFunc := ( M_V762 * aBasica[1,4] ) / 100
		ElseIf _VlrSal >= aBasica[1,5] .And. _VlrSal <= aBasica[1,6]
			_nCustFunc := ( M_V762 * aBasica[1,7] ) / 100
		ElseIf _VlrSal >= aBasica[1,8] .And. _VlrSal <= aBasica[1,9]
			_nCustFunc := ( M_V762 * aBasica[1,10] ) / 100
		ElseIf _VlrSal >= aBasica[1,11] .And. _VlrSal <= aBasica[1,12]
			_nCustFunc := ( M_V762 * aBasica[1,13] ) / 100
		ElseIf _VlrSal >= aBasica[1,14] .And. _VlrSal <= aBasica[1,15]
			_nCustFunc := ( M_V762 * aBasica[1,16] ) / 100
		Endif
		
		_nCustEmp := (M_V762 -  _nCustFunc)
		M_V433 := _nCustFunc
		M_V761 := _nCustEmp  

		FDELPD('433',,)             
		FGERAVERBA('433',M_V433,,,,,,,,,.T.,,,,)      
		
		FDELPD('761',,)      
		FGERAVERBA('761',M_V761,,,,,,,,,.T.,,,,)        	
		
	EndIf
		
 
EndIf

/* Jose Leite - CSI - 22/10/15
	Comentado esse trecho devido aos jornalistas nao serem mensalistas, nao
	estava calculando o rateio da refeicao.
IF (M_V762 > 0 .AND. (SRA->RA_CATFUNC = 'M' .AND. SRA->RA_HRSMES < 200))
		FDELPD('433',,)             
		FGERAVERBA('433',M_V762,,,,,,,,,.T.,,,,)     
EndIf
*/
              
Return

*-------------------------------------------*
Static Function CargaCB(aBasica,_cFil)
*-------------------------------------------*

Local nValCesta := 0
Local nIniF1 := nFimF1 := nPerc1:= 0
Local nIniF2 := nFimF2 := nPerc2:= 0
Local nIniF3 := nFimF3 := nPerc3:= 0
Local nIniF4 := nFimF4 := nPerc4:= 0
Local nIniF5 := nFimF5 := nPerc5:= 0

aBasica := {}

dbSelectArea( "SRX" )
dbSelectArea( "SRC" )

If FPHIST82( _cFil , "35" , RhTamFilial(_cFil) + "1" )
	nValCesta := Val(Substr(SRX->RX_TXT,01,11))
	nIniF1    := Val(Substr(SRX->RX_TXT,12,11))
	nFimF1    := Val(Substr(SRX->RX_TXT,23,11))
	nPerc1    := Val(Substr(SRX->RX_TXT,34,07))
	FPHIST82( _cFil , "35" , RhTamFilial(_cFil) + "2" )
	nIniF2    := Val(Substr(SRX->RX_TXT,01,11))
	nFimF2    := Val(Substr(SRX->RX_TXT,12,11))
	nPerc2    := Val(Substr(SRX->RX_TXT,23,07))
	nIniF3    := Val(Substr(SRX->RX_TXT,30,11))
	nFimF3    := Val(Substr(SRX->RX_TXT,41,11))
	nPerc3    := Val(Substr(SRX->RX_TXT,52,07))
	FPHIST82( _cFil , "35" , RhTamFilial(_cFil) + "3" )
	nIniF4    := Val(Substr(SRX->RX_TXT,01,11))
	nFimF4    := Val(Substr(SRX->RX_TXT,12,11))
	nPerc4    := Val(Substr(SRX->RX_TXT,23,07))
	nIniF5    := Val(Substr(SRX->RX_TXT,30,11))
	nFimF5    := Val(Substr(SRX->RX_TXT,41,11))
	nPerc5    := Val(Substr(SRX->RX_TXT,52,07))
EndIf

If FPHIST82( _cFil , "35" , RhTamFilial(Space(FWGETTAMFILIAL)) + "1" )
	nValCesta := Val(Substr(SRX->RX_TXT,01,11))
	nIniF1    := Val(Substr(SRX->RX_TXT,12,11))
	nFimF1    := Val(Substr(SRX->RX_TXT,23,11))
	nPerc1    := Val(Substr(SRX->RX_TXT,34,07))
	dbSkip( 1 )
	FPHIST82( _cFil , "35" , RhTamFilial(Space(FWGETTAMFILIAL)) + "2" )
	nIniF2    := Val(Substr(SRX->RX_TXT,01,11))
	nFimF2    := Val(Substr(SRX->RX_TXT,12,11))
	nPerc2    := Val(Substr(SRX->RX_TXT,23,07))
	nIniF3    := Val(Substr(SRX->RX_TXT,30,11))
	nFimF3    := Val(Substr(SRX->RX_TXT,41,11))
	nPerc3    := Val(Substr(SRX->RX_TXT,52,07))
	FPHIST82( _cFil , "35" , RhTamFilial(Space(FWGETTAMFILIAL)) + "3" )
	nIniF4    := Val(Substr(SRX->RX_TXT,01,11))
	nFimF4    := Val(Substr(SRX->RX_TXT,12,11))
	nPerc4    := Val(Substr(SRX->RX_TXT,23,07))
	nIniF5    := Val(Substr(SRX->RX_TXT,30,11))
	nFimF5    := Val(Substr(SRX->RX_TXT,41,11))
	nPerc5    := Val(Substr(SRX->RX_TXT,52,07))
Endif

Aadd(aBasica,{nValCesta,nIniF1,nFimF1,nPerc1,nIniF2,nFimF2,nPerc2,nIniF3,nFimF3,nPerc3,nIniF4,nFimF4,nPerc4,nIniF5,nFimF5,nPerc5})

Return ( .T. )