#Include 'Protheus.ch'

/*/{Protheus.doc} SF05R01X
@description Relatorio de Conferencia de venda - Fechamento de caixa.
@author j2a.luizjunior
@since 28/07/2017
@version 1.0
@history 13/12/2018, Alan Teles de Oliveira, Ajuste na consulta respeitando a filial logada ou informada na pergunta organização e estrutura do fonte.	
/*/
User Function SF05R01X()

	Local cEstat 	:= U_SFGN001A(ProcName(0), "SF05R01X")
	Local cPerg   	:= ''
	Local oReport	:= Nil	
	
	Private cCaixa	:= xNumCaixa()
	
	If Empty(cCaixa)
		cPerg := "SF05R01X2"
	Else
		cPerg := "SF05R01X1"
	EndIf

	CriaSX1(cPerg)
	If .not. Pergunte(cPerg,.T.)
		Return
	EndIf

	oReport := ReportDef(cPerg)

	oReport:PrintDialog()	

Return


/*/{Protheus.doc} ReportDef
@description Função para criação da estrutura do relatório. 
@author j2a.luizjunior
@since 28/07/2017
@version 1.0
@history 13/12/2018, Alan Teles de Oliveira, Alterado estética da função e adicionado help com proposito do relatório.
@history 20/12/2018, Alan Teles de Oliveira, Criado nova seção e break.
/*/
Static Function ReportDef(p_cPerg)
	
	Local cEstat 	:= U_SFGN001A(ProcName(0), "SF05R01X")
	Local cNomeRel	:= "SF05R01X"	
	Local cTitRel	:= "Relatorio de Conferencia de venda - Fechamento de caixa"
	Local cHelp		:= 'Permite gerar um relatório contendo todos os recebidos pelo caixa no dia.'
	Local oReport	:= Nil
	Local oSec1 	:= Nil	
	Local oSec2		:= Nil
	Local oBrFor  	:= Nil
	lOCAL oBrFim	:= Nil
	
	oReport := TReport():New(cNomeRel, cTitRel, p_cPerg, {|oReport| ReportPrint(oReport)}, cHelp)
	
	oReport:SetTotalInLine(.f.)
	oReport:SetLandScape()
	oReport:DisableOrientation()
	
	//-> Forma de pagamento
	oSec1 := TRSection():New(oReport, "Forma de Pagamento", {})		

	TRCell():New(oSec1, "L1_OPERADO",, 	'Operador',, 							03)
	TRCell():New(oSec1, "A6_NOME",, 	'Nome Banco',, 							40)
	TRCell():New(oSec1, "L4_DATA",, 	'Data Orcamto',, 						10)
	TRCell():New(oSec1, "L4_NUM",, 		'Num Orcamto',, 						12)
	TRCell():New(oSec1, "L4_NUMCART",, 	'Num Cart',, 							12)
	TRCell():New(oSec1, "L4_ADMINIS",,	'Administradora',, 						40)
	TRCell():New(oSec1, "L4_VALOR",, 	'Total', 			'@R 9,999,999.99',	12)

	oSec2 := TRSection():New(oReport, "Forma de Pagamento", {},,,,,,,,,,3)

	TRCell():New(oSec2, "FORMPAG",,		'Cod. F. Pag.',, 							02)
	TRCell():New(oSec2, "DESCFORM",, 	'Forma de Pagamento',, 						25)
	TRCell():New(oSec2, "VALOR",, 		'Valor Total', 			'@E 9,999,999.99',	12)

	oBrFor  := TRBreak():New(oSec1, {|| .t.}, 'Total por forma de pagamento',, 'TOTADM')
	oBrFim  := TRBreak():New(oSec2, {|| .t.}, 'Total Geral',, 	'TOTGER')
	
	TRFunction():New(oSec1:Cell("L4_VALOR"), 	'', "SUM", oBrFor,,,, .f., .f.)
	TRFunction():New(oSec2:Cell("VALOR"), 		'', "SUM", oBrFim,,,, .f., .f.)

Return oReport


/*/{Protheus.doc} ReportPrint
@description Rotina para montagem dos dados do relatório.
@author j2a.luizjunior
@since 28/07/2017
@version 1.0
@history 13/12/2018, Alan Teles de Oliveira, Verifico se usuário e caixa caso seja permiti visualizar apenas os dados do seu caixa, alterado a query para respeitar a filial logada caso caixa ou a informada no parâmetro para demais usuários, estética e estrutura da função. 
@history 20/12/2018, Alan Teles de Oliveira, Criado segunda seção para os totalizados e alterado o break e a somatória da administradora para a forma de pagamento.

/*/
Static Function ReportPrint(p_oReport)

	Local cEstat 	:= U_SFGN001A(ProcName(0), "SF05R01X")		 
	Local cSql     	:= ""
	Local cAdminis 	:= ""  
	Local cFilOld 	:= cFilAnt		
	Local cAlTMP 	:= GetNextAlias()
	Local cFormAtu	:= ''
	Local nPosForm	:= 0
	Local nX		:= 0
	Local oSec1    	:= p_oReport:Section(1)	
	Local oSec2    	:= p_oReport:Section(2)	
	Local oBrFor	:= oSec1:GetBreak('TOTADM')
	Local oBrFim	:= oSec2:GetBreak('TOTGER')
	Local aTotVen	:= {}

	oSec1:Cell("L1_OPERADO"):SetBlock({||	(cAlTMP)->L1_OPERADO})
	oSec1:Cell("A6_NOME"):SetBlock({|| 		(cAlTMP)->A6_NOME})
	oSec1:Cell("L4_DATA"):SetBlock({|| 		(cAlTMP)->L4_DATA})
	oSec1:Cell("L4_NUM"):SetBlock({|| 		(cAlTMP)->L4_NUM})
	oSec1:Cell("L4_NUMCART"):SetBlock({|| 	(cAlTMP)->L4_NUMCART})
	oSec1:Cell("L4_ADMINIS"):SetBlock({|| 	(cAlTMP)->L4_ADMINIS})
	oSec1:Cell("L4_VALOR"):SetBlock({|| 	(cAlTMP)->L4_VALOR})
	
	oBrFor:SetBreak({|| (cAlTMP)->(Eof())})

	oSec2:Cell("FORMPAG"):SetBlock({||	aTotVen[nX][1]})
	oSec2:Cell("DESCFORM"):SetBlock({||	aTotVen[nX][2]})
	oSec2:Cell("VALOR"):SetBlock({|| 	aTotVen[nX][3]})

	oBrFim:SetBreak({|| nX > Len(aTotVen)})

	aTotVen := fFormPag()

	If Empty(cCaixa)

		If .not. Empty(MV_PAR01)
			cFilOld := MV_PAR01
		EndIf

		If !Empty(MV_PAR02) .and. !Empty(MV_PAR03) 
			cSql += " L1.L1_EMISNF BETWEEN '" + DToS(MV_PAR02) + "' AND '" + DToS(MV_PAR03) + "' AND "
		EndIf		

		If !Empty(MV_PAR04) .and. !Empty(MV_PAR05)
			cSql += " L1.L1_OPERADO BETWEEN '" + MV_PAR04 + "' AND '" + MV_PAR05 + "' AND "
		EndIf

	Else

		If !Empty(MV_PAR01) .and. !Empty(MV_PAR02) 
			cSql += " L1.L1_EMISNF BETWEEN '" + DToS(MV_PAR01) + "' AND '" + DToS(MV_PAR02) + "' AND "
		EndIf

		cSql += " L1.L1_OPERADO = '" + cCaixa + "' AND "

	EndIf

	cSql += " L1.L1_FILIAL = '" + xFilial('SL1', cFilOld) + "' AND "
	cSql := "%" + cSql + "%"
	
	If Select(cAlTMP) > 0
		(cAlTMP)->(dbCloseArea())
	EndIf

	BeginSql Alias cAlTMP

		Column L4_DATA as Date

		SELECT 
			L4.L4_DATA AS L4_DATA,
			L4.L4_NUM,
			L4.L4_FORMA,
			L4.L4_NUMCART,
			CASE 
				WHEN L4.L4_ADMINIS = ''
					THEN 'R$ - Venda a Vista' 
				WHEN L4.L4_FORMA = 'CH'
					THEN 'CH - Cheque' 
				ELSE L4.L4_ADMINIS
			END AS L4_ADMINIS,
			L4.L4_VALOR,
			L4.L4_FILIAL,
			L1.L1_OPERADO,
			COALESCE(A6.A6_NOME, ' ') AS A6_NOME
			
		FROM %Table:SL1% L1

		INNER JOIN %Table:SL4% L4 ON 
			L4.L4_FILIAL	  = %EXP:xFilial('SL4', cFilOld)% AND 
			L4.L4_NUM 		  = L1.L1_NUM AND
			TRIM(L4.L4_FORMA) != %EXP:'GT'% AND 
			L4.%NotDel%

		LEFT JOIN %Table:SA6% A6 ON 
			A6.A6_COD 		= L1.L1_OPERADO AND 
			A6.A6_FILIAL 	= %EXP:xFilial('SA6', cFilOld)% AND 
			A6.A6_AGENCIA	= %EXP:'.'% AND 
			A6.A6_NUMCON 	= %EXP:'.'% AND 
			A6.%NotDel%

		WHERE 			
			%EXP:cSql%
			L1.%NOTDEL%
			
		UNION ALL
		
		SELECT 
			L1.L1_EMISSAO AS L4_DATA,
			L1.L1_NUM AS L4_NUM,
			%EXP:'CR'% AS L4_FORMA,
			%EXP:''% AS L4_NUMCART,
			%EXP:'CR - CREDITO (NCC/RA)'% AS L4_ADMINIS,
			L1.L1_CREDITO AS L4_VALOR,
			L1.L1_FILIAL AS L4_FILIAL,
			L1.L1_OPERADO,
			COALESCE(A6.A6_NOME, ' ') AS A6_NOME
			
		FROM %TABLE:SL1% L1

		LEFT JOIN %TABLE:SA6% A6 ON 
			A6.A6_COD 		= L1.L1_OPERADO AND 
			A6.A6_FILIAL 	= %EXP:XFILIAL('SA6', cFilOld)% AND 
			A6.A6_AGENCIA	= %EXP:'.'% AND
			A6.A6_NUMCON 	= %EXP:'.'% AND
			A6.%NOTDEL%

		WHERE
			%EXP:cSql%
			L1.L1_CREDITO > 0
			AND L1.%NOTDEL%

		ORDER BY 
			L4_FORMA DESC,
			L4_ADMINIS
	
	EndSql

	//MemoWrite('C:\TEMP\SF05R01X.sql', GetLastQuery()[2])
	
	dbSelectArea(cAlTMP)

	p_oReport:SetMeter((cAlTMP)->(RecCount()))

	(cAlTMP)->(DbGoTop())

	While .not. (cAlTMP)->(Eof())	
		
		If p_oReport:Cancel()
			Exit
		EndIf				

		If cFormAtu != (cAlTMP)->L4_FORMA

			oSec1:Finish()
			
		EndIf

		nPosForm := aScan(aTotVen, {|x| x[1] == Alltrim((cAlTMP)->L4_FORMA)})

		If nPosForm = 0
			nPosForm := aScan(aTotVen, {|x| x[1] == 'OU'})
		EndIf

		aTotVen[nPosForm][3] += (cAlTMP)->L4_VALOR

		p_oReport:SetMsgPrint("Imprimindo a forma de pagamento " + Alltrim((cAlTMP)->L4_FORMA))
		p_oReport:IncMeter()
		
		oSec1:Init()
		oSec1:PrintLine()

		cFormAtu := (cAlTMP)->L4_FORMA

		(cAlTMP)->(DbSkip())	

		If cFormAtu != (cAlTMP)->L4_FORMA
			p_oReport:SkipLine()
			p_oReport:SkipLine()
		Endif

	Enddo

	If Select(cAlTMP) > 0
		(cAlTMP)->(dbCloseArea())
	EndIf

	p_oReport:SetMeter(Len(aTotVen))

	For nX := 1 To Len(aTotVen)		

		If p_oReport:Cancel()
			Exit
		EndIf

		If aTotVen[nX][3] = 0
			Loop
		EndIf

		p_oReport:SetMsgPrint("Imprimindo Totalizador " + aTotVen[nX][1] + '-' + aTotVen[nX][2])
		p_oReport:IncMeter()
		
		oSec2:Init()
		oSec2:PrintLine()	

	Next

	oSec2:Finish()	
			
	p_oReport:SkipLine()

Return


/*/{Protheus.doc} CriaSX1
@description Função para criação das perguntas (se não existirem)
@author j2a.luizjunior
@since 28/07/2017
@version 1.0
@history 13/12/2018, Alan Teles de Oliveira, Distinção de pergunta entre usuários caixas e demais usuários.
/*/
Static Function CriaSX1(p_cPerg)

	Local cEstat := U_SFGN001A(ProcName(0), "SF05R01X")
	Local aHelp := {}

	//-> Texto do help em português,inglês, espanhol
	AAdd(aHelp, {{"Filial"},		{""}, {""}}) 
	AAdd(aHelp, {{"Da  Emissao"},	{""}, {""}}) 
	AAdd(aHelp, {{"Ate Emissao"},	{""}, {""}}) 
	AAdd(aHelp, {{"Do  Caixa"},		{""}, {""}}) 
	AAdd(aHelp, {{"Ate Caixa"},		{""}, {""}}) 
	
	If p_cPerg == 'SF05R01X1'

		u_SFPUTSX1(p_cPerg, "01", "Da Emissao",		"", "", "mv_ch01", "D", 08, 00, 00, "G", "", "",	"", "", "MV_PAR01", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", aHelp[2, 1] ,aHelp[2, 2] ,aHelp[2, 3], "")
		u_SFPUTSX1(p_cPerg, "02", "Ate Emissao",	"", "", "mv_ch02", "D", 08, 00, 00, "G", "", "",	"", "", "MV_PAR02", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", aHelp[3, 1] ,aHelp[3, 2] ,aHelp[3, 3], "")

	Else

		u_SFPUTSX1(p_cPerg, "01", "Filial",			"", "", "mv_ch01", "C", 08, 00, 00, "G", "", "SM0",	"", "", "MV_PAR01", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", aHelp[1, 1] ,aHelp[1, 2] ,aHelp[1, 3], "")
		u_SFPUTSX1(p_cPerg, "02", "Da Emissao",		"", "", "mv_ch02", "D", 08, 00, 00, "G", "", "",	"", "", "MV_PAR02", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", aHelp[2, 1] ,aHelp[2, 2] ,aHelp[2, 3], "")
		u_SFPUTSX1(p_cPerg, "03", "Ate Emissao",	"", "", "mv_ch03", "D", 08, 00, 00, "G", "", "",	"", "", "MV_PAR03", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", aHelp[3, 1] ,aHelp[3, 2] ,aHelp[3, 3], "")
		u_SFPUTSX1(p_cPerg, "04", "Do Caixa",		"", "", "mv_ch04", "C", 03, 00, 00, "G", "", "23",	"", "", "MV_PAR04", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", aHelp[4, 1] ,aHelp[4, 2] ,aHelp[4, 3], "")
		u_SFPUTSX1(p_cPerg, "05", "Ate Caixa",		"", "", "mv_ch05", "C", 03, 00, 00, "G", "", "23",	"", "", "MV_PAR05", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", aHelp[5, 1] ,aHelp[5, 2] ,aHelp[5, 3], "")

	EndIf

Return


/*/{Protheus.doc} fFormPag
@description monta array com forma de pagamento que serão utilizados no totalizador
@type  Static Function
@author Alan Teles de Oliveira
@since 20/12/2018
@version 11.8
@return aRet, array, relação de forma de pagamento

/*/
Static Function fFormPag()

	Local cSimb := Substr(IIf(Empty(SuperGetMV("MV_SIMB1")), "R$", SuperGetMV("MV_SIMB1")), 1, 2)
	Local aRet := {}

	aadd(aRet, {cSimb,	Alltrim(Upper(Tabela('24', cSimb,	.F.))),	0.0})
	aadd(aRet, {'CD',  	Alltrim(Upper(Tabela('24', 'CD',	.F.))),	0.0})
	aadd(aRet, {'CC',  	Alltrim(Upper(Tabela('24', 'CC',	.F.))),	0.0})
	
	If cFilAnt = '04MT0001'
	
		aadd(aRet, {'CH',  	Alltrim(Upper(Tabela('24', 'CH',	.F.))),	0.0})
		aadd(aRet, {'BOL',  Alltrim(Upper(Tabela('24', 'BOL',	.F.))),	0.0})
		aadd(aRet, {'CR',   Alltrim(Upper(Tabela('24', 'CR',	.F.))),	0.0})
	
	EndIf
	
	aadd(aRet, {'OU',  	'OUTROS', 									0.0})

Return aRet