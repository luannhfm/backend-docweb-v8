#include 'protheus.ch'
#include 'parmtype.ch'

/*/{Protheus.doc} MT097EST
Usado para suprir a necessidade do usuário sem utilizar parâmetros
LOCALIZAÇÃO : Function A097ESTORNA - Função da Dialog que estorna a 
	liberação dos documentos com alçada.
EM QUE PONTO : O ponto se encontra no inicio da função A097ESTORNA, 
	não passa parametros e não envia retorno, usado conforme 
	necessidades do usuario para diversos fins.	
@author franklin.oliveira
@since 25/09/2019
@version 1.0
@return Nil, nulo
@see https://tdn.totvs.com/pages/releaseview.action?pageId=6085378
@type user function
/*/
User Function MT097EST()
Local _aArea	:= GetArea()
Local cFilDoc	:= SCR->CR_FILIAL
Local cNumDoc	:= PadR(SCR->CR_NUM, TamSX3("C1_NUM")[1]) 
	
	DbSelectArea('SC1')
	SC1->( DbGoTop() )
	SC1->( DbSetOrder(1) )
	
	If SC1->(DbSeek(xFilial('SC1', cFilDoc) + cNumDoc))
		While !SC1->( EoF() ) .And. ( SC1->C1_FILIAL + SC1->C1_NUM == xFilial('SC1', cFilDoc) + cNumDoc )
			//Registra o estorno da aprovação no log de processos 
			RSTSCLOG("APR", 3, cUserName)
			SC1->(dbSkip())
		EndDo
	EndIf
    
    RestArea(_aArea)
    
Return Nil