#Include 'Protheus.ch'
#Include 'Topconn.ch'

/*/{Protheus.doc} SF69E01J
Funcao responsavel por carregar o valor do contrato de acordo com a proposta comercial - CRM.

Chamada no gatilho - CN9_XOPORT

@type 		Trigger
@author 	Jose Leite de Barros Neto
@since 	27/10/2016
@version 	1.0
@return 	_nTotal, Valor total do contrato
/*/
User Function SF69E01J()
	
	Local _aArea		:= GetArea()
	Local _cOpor 	:= M->CN9_XOPORT
	Local _cRevOp 	:= M->CN9_XREVOP
	Local _cQuery	:= ""
	Local _nTotal	:= 0
	
	If Select('TRA') > 0
		DbSelectArea('TRA')
		TRA->(DbCloseArea())
	EndIf
	
	_cQuery := " SELECT ADY_OPORTU, ADY_REVISA, ADY_PROPOS, ADY_PREVIS, " 
	_cQuery += " ADZ_PRODUT, ADZ_DESCRI, ADZ_QTDVEN, ADZ_PRCVEN, ADZ_TOTAL "
	_cQuery += " FROM "+ RetSqlName("ADY") + " ADY "
	_cQuery += "  INNER JOIN "+ RetSqlName("ADZ") +" ADZ "
	_cQuery += " 		ON ADZ_FILIAL = ADY_FILIAL AND ADZ_PROPOS = ADY_PROPOS AND ADZ_REVISA = ADY_PREVIS AND ADZ.D_E_L_E_T_ <> '*' " 
	_cQuery += " WHERE ADY_FILIAL = '"+ xFilial("ADY") +"' " 
	_cQuery += " AND ADY_OPORTU = '"+ _cOpor +"' "
	_cQuery += " AND ADY_REVISA = '"+ _cRevOp +"' "
	_cQuery += " AND ADY_STATUS = 'E'	"
	_cQuery += " AND ADY.D_E_L_E_T_ <> '*' "
	
	TcQuery _cQuery New Alias 'TRA'
	
	While .Not. TRA->(Eof())
		_nTotal += TRA->ADZ_TOTAL
		TRA->(dbSkip())
	End
	
	TRA->(DbCloseArea())
	
	If _nTotal > 0
		M->CN9_VLINI := _nTotal
		M->CN9_SALDO := _nTotal
	EndIf
	
	RestArea(_aArea)

Return(_cRevOp)