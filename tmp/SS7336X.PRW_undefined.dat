#INCLUDE "PROTHEUS.CH"
#INCLUDE "TOPCONN.CH"
#INCLUDE "RWMAKE.CH"
#INCLUDE "FONT.CH"
#INCLUDE "COLORS.CH"
#INCLUDE "PARMTYPE.CH"
#INCLUDE "FWMBROWSE.CH"
#INCLUDE "FWMVCDEF.CH"
#INCLUDE "FWCOMMAND.CH"
/*
--------------------------------------------------------------------------------
{Protheus.doc} <SS7336X>
   36 - FIEMT - Especificacao de XLS Informações de visitas: Resolvemos 
   por gerar relatorio que pode ser exportao para EXCEL com todas as 
   informações solicitadas na especificação.

 PARAMETOS:
    MV_XACEALL (c) - Defini quais filiais tem acesso MASTER. Os operadores 
                     logados nas filiais identificadas neste parametro tem 
                     acesso as informações de TODAS as demais filiais.
@author<Antonio Dantas>
@since<26/05/2015>
@version<1.00>
@receive<Nil>
@return<Nil>
@example<Nil>
@see<Nil>
--------------------------------------------------------------------------------
*/
User Function 	SS7336X()
Local _oReport			:= Nil     
Private _lCancel		:= .f.  
Private _cPerg 		:= ""
Private _cAlias		:= GetNextAlias()   
Private _cAceAll   	:= SuperGetMv("MV_XACEALL",,"")       
//+------------------------------------------------------------------+
//| Verifica se o operador esta logado na filail DR que tem acesso   |
//| a todas as filiais, ou somente na filial em que esta logada.     |
//+------------------------------------------------------------------+
_cPerg	:= FunName()			//-- Define o Grupo de Perguntas   
//+------------------------------------------------------------------+
//|   Interface de impressao                                         |
//+------------------------------------------------------------------+
_oReport := ReportDef()
If !_lCancel
	_oReport:PrintDialog()
Endif	    
Return


/*
--------------------------------------------------------------------------------
{Protheus.doc} <ReportDef>
 Rotina que monta e prepara a Impressão do Relatorio. 
 
@author<Antonio Dantas>
@since<26/05/2015>
@version<1.00>
@receive<Nil>
@return<Nil>
@example<Nil>
@see<Nil>
--------------------------------------------------------------------------------
*/
Static Function ReportDef()
Local _cTitulo		:= "36 - Informações de visitas"
Local _cSubTit		:= ""  
Local _oReport		:= Nil
//--
Local _oApontam		:= Nil
//+------------------------------------------------------------------+
//| Carrega as perguntas relativas aos parametros de Impressao       |
//|     Mv_par01 - Filial DE ?                                       |
//|     Mv_par02 - Filial ATE ?                                      |
//|     Mv_par03 - Data de Apontamento DE ?                          |
//|     Mv_par04 - Data de Apontamento ATE ?                         |
//|     Mv_par05 - Unid Executora DE ?                               |
//|     Mv_par06 - Unid Executora DE ?                               |
//|     Mv_par05 - Unid do Repres DE ?                               |
//|     Mv_par06 - Unid do Repres ATE ?                              |
//|     Mv_par07 - Polo de Venda DE ?                                |
//|     Mv_par08 - Polo de Venda ATE ?                               |
//+------------------------------------------------------------------+
//xParPerg(_cPerg)
CriaSX1(_cPerg)      
//+------------------------------------------------------------------+
//|  Monta tela de paramentos para usuario, se cancelar sair         |
//+------------------------------------------------------------------+
If !Pergunte(_cPerg,.t.)  
	_lCancel := .T. 
   Return
Endif 
//+------------------------------------------------------------------+
//| Verifica se o operador esta logado na filail DR que tem acesso   |
//| a todas as filiais, ou somente na filial em que esta logada.     |
//+------------------------------------------------------------------+
If !(cFilAnt $ _cAceAll)
	Mv_par01	:= cFilAnt
	Mv_par02	:= cFilAnt
Endif 
//-- Defini o sub-titulo do Relatorio 
_cSubTit := " - Periodo [De: "+DtoC(Mv_par03)+"Ate: "+DtoC(Mv_par04)+"] "  
//+------------------------------------------------------------------+
//|   Seleciona a tabela temporaria que sera referencia              | 
//+------------------------------------------------------------------+
FWMsgRun(, {|| u_fGet336X() }, "Processando a Consulta dos Apontamentos!", "Aguarde...")
//+------------------------------------------------------------------+
//|    Construcao do Objeto de Impressao do Relatorio                |
//+------------------------------------------------------------------+
//                    New(cReport,cTitle,uParam,bAction,cDescription,lLandscape,uTotalText,lTotalInLine,cPageTText,lPageTInLine,lTPageBreak,nColSpace)
_oReport := TReport():New(_cPerg,_cTitulo,_cPerg,{|_oReport|  ReportPrint(_oReport) },_cSubTit,.t.)
_oReport:SetLandScape() 			//-- Modo Paisagem 
//&&--  _oReport:SetPortrait() 			//-- Modo Retrato 
//+------------------------------------------------------------------+
//|    *****      S e c a o  -  1      *****                         |
//+------------------------------------------------------------------+
_oApontam := TRSection():New(_oReport,"Vendedor",{"SA3"} )
TRCell():New(_oApontam,"AD5_VEND"		,"AD5" 	,"Cod Vend"			,PesqPict("AD5","AD5_VEND") 	,TamSX3("AD5_VEND")[1]		,.F.) 
TRCell():New(_oApontam,"A3_NOME"		,"SA3" 	,"Nome Vendedor"	,PesqPict("SA3","A3_NOME") 		,TamSX3("A3_NOME")[1]		,.F.) 
TRCell():New(_oApontam,"AD5_FILIAL"		,"AD5" 	,"Filial"			,PesqPict("AD5","AD5_FILIAL") 	,TamSX3("AD5_FILIAL")[1]	,.F.) 
TRCell():New(_oApontam,"AD5_DATA"		,"AD5" 	,"Dta Visita"		,PesqPict("AD5","AD5_DATA") 	,TamSX3("AD5_DATA")[1]		,.F.)
TRCell():New(_oApontam,"AD5_XRHINI"		,"AD5" 	,"HS INI"			,PesqPict("AD5","AD5_XRHINI") 	,TamSX3("AD5_XRHINI")[1]	,.F.) 
TRCell():New(_oApontam,"AD5_XRHFIM"		,"AD5" 	,"Hs FIM"			,PesqPict("AD5","AD5_XRHFIM") 	,TamSX3("AD5_XRHFIM")[1]	,.F.) 
TRCell():New(_oApontam,"AD5_SEQUEN"		,"AD5" 	,"Seguen"			,PesqPict("AD5","AD5_SEQUEN") 	,TamSX3("AD5_SEQUEN")[1]	,.F.) 
TRCell():New(_oApontam,"AD5_XDTIME"		,"AD5" 	,"Lançada em"		,PesqPict("AD5","AD5_XDTIME") 	,TamSX3("AD5_XDTIME")[1]	,.F.) 
TRCell():New(_oApontam,"AD5_NROPOR"		,"AD5" 	,"Nr Oportunid"		,PesqPict("AD5","AD5_NROPOR") 	,TamSX3("AD5_NROPOR")[1]	,.F.) 
TRCell():New(_oApontam,"A3XPOLO"		,"" 	,"Polo de Venda"	,"@!"	,40	,.F.) 
TRCell():New(_oApontam,"AD5_CODCLI"		,"AD5" 	,"Cod Cli"			,PesqPict("AD5","AD5_CODCLI") 	,TamSX3("AD5_CODCLI")[1]	,.F.) 
TRCell():New(_oApontam,"AD5_LOJA"		,"AD5" 	,"Loja"				,PesqPict("AD5","AD5_LOJA") 	,TamSX3("AD5_LOJA")[1]		,.F.)
TRCell():New(_oApontam,"A1_CGC"			,"SA1" 	,"CPF/CNPJ"			,"@!" 	,20	,.F.)
TRCell():New(_oApontam,"A1_NOME"		,"SA1" 	,"Nome"				,PesqPict("SA1","A1_NOME") 		,TamSX3("A1_NOME")[1]		,.F.) 
TRCell():New(_oApontam,"A1_NREDUZ"		,"SA1" 	,"Apelido"			,PesqPict("SA1","A1_NREDUZ") 	,TamSX3("A1_NREDUZ")[1]		,.F.) 
TRCell():New(_oApontam,"A1_CEINSS"		,"SA1" 	,"CEI/INSS"			,PesqPict("SA1","A1_CEINSS") 	,TamSX3("A1_CEINSS")[1]		,.F.) 
TRCell():New(_oApontam,"A1_MUN"			,"SA1" 	,"Cidade"			,PesqPict("SA1","A1_MUN") 		,TamSX3("A1_MUN")[1]		,.F.) 
TRCell():New(_oApontam,"A1XCNAEIN"		,"SA1" 	,"CNAE Indúst"		,"@!" 	,9	,.F.) 
TRCell():New(_oApontam,"A1XFPAS"		,"" 	,"Contrib"			,"@!" 	,9	,.F.)
TRCell():New(_oApontam,"A1_XQTEMP"		,"SA1" 	,"Qtde Empreg"		,PesqPict("SA1","A1_XQTEMP") 	,TamSX3("A1_XQTEMP")[1]		,.F.) 
TRCell():New(_oApontam,"A1_XQTDESI"		,"SA1" 	,"Qtde Emp SIGA"	,PesqPict("SA1","A1_XQTDESI") 	,TamSX3("A1_XQTDESI")[1]	,.F.) 
TRCell():New(_oApontam,"AD5_EVENTO"		,"AD5" 	,"Cod Evento"		,PesqPict("AD5","AD5_EVENTO") 	,TamSX3("AD5_EVENTO")[1]	,.F.) 
TRCell():New(_oApontam,"AC5_DESCRI"		,"AC5" 	,"Descrição"		,PesqPict("AC5","AC5_DESCRI") 	,TamSX3("AC5_DESCRI")[1]	,.F.) 
TRCell():New(_oApontam,"AD5_PROSPE"		,"AD5" 	,"Cod Prospe"		,PesqPict("AD5","AD5_PROSPE") 	,TamSX3("AD5_PROSPE")[1]	,.F.)
TRCell():New(_oApontam,"AD5_LOJPRO"		,"AD5" 	,"Loj Prospe"		,PesqPict("AD5","AD5_LOJPRO") 	,TamSX3("AD5_LOJPRO")[1]	,.F.) 
TRCell():New(_oApontam,"AD5PRONOME"		,"" 	,"Nome"				,"@!"	,40	,.F.) 
TRCell():New(_oApontam,"U5_CODCONT"		,"SU5" 	,"Cod Contato"		,PesqPict("SU5","U5_CODCONT") 	,TamSX3("U5_CODCONT")[1]	,.F.) 
TRCell():New(_oApontam,"U5_CONTAT"		,"SU5" 	,"Nome"				,PesqPict("SU5","U5_CONTAT") 	,TamSX3("U5_CONTAT")[1]		,.F.)
TRCell():New(_oApontam,"U5_FUNCAO"		,"SU5" 	,"Cod Função"		,PesqPict("SU5","U5_FUNCAO") 	,TamSX3("U5_FUNCAO")[1]		,.F.) 
TRCell():New(_oApontam,"UM_DESC"		,"SUM" 	,"Descrição"		,PesqPict("SUM","UM_DESC") 		,TamSX3("UM_DESC")[1]		,.F.) 
TRCell():New(_oApontam,"U5_EMAIL"		,"SU5" 	,"E-mail"			,PesqPict("SU5","U5_EMAIL") 	,TamSX3("U5_EMAIL")[1]		,.F.) 
TRCell():New(_oApontam,"U5_FONE"		,"SU5" 	,"Telefone"			,PesqPict("SU5","U5_FONE") 		,TamSX3("U5_FONE")[1]		,.F.)
Return _oReport

/*
+--------------------------------------------------------------------------+
 {Protheus.doc}<ReportPrint>
   Rotina de Impressão do Corpo do Relatorio. 

@author<Antonio Dantas>
@since<26/05/2015>
@version<1.00>
@receive<Nil>
@return<Nil>
@example<Nil>
@see<Nil>
+--------------------------------------------------------------------------+
*/
Static Functio ReportPrint(_oReport)
Local _oApontam		:= _oReport:Section(1)
Local _nRecs		:= 0 
Local _cCodCont		:= ""	
Local _cNomeCont	:= ""
Local _cMailCont	:= ""
Local _cFoneCont 	:= ""
Local _cCodFunc		:= ""
Local _cNomFunc		:= ""
//--
Local _cStatProp	:= "" 
Local _cStatCtr		:= "" 
Local _cDescPolo	:= "" 
Local _cCNPJ		:= ""
Local _cProspNom	:= "" 
Local _cCrtFech		:= ""
Local _cIndustr		:= ""
Local _cFPAS		:= ""
//-- Inicia o Objeto de Impressão 
_oApontam:Init()
//-- Seta a Margem a partir da posição 1
_oReport:SetLeftMargin(2)
//+------------------------------------------------------------------+
//| Posiciona no inicio o Arquivo Resultado da Consulta Sql          | 
//+------------------------------------------------------------------+
dbSelectArea(_cAlias)  
(_cAlias)->(dbgotop())    
//-- Conta o numero do Registro da Tabela Temporaria
(_cAlias)->(dbEval({|| _nRecs++ }))
_oReport:SetMeter(_nRecs)
//-- 
dbSelectArea(_cAlias)  
(_cAlias)->(dbgotop())
//+------------------------------------------------------------------+
//| Inicia Laco para impressao do Relatorio                          |
//+------------------------------------------------------------------+
Do While (_cAlias)->(!eof())   
	_oReport:IncMeter()    
	//-- CNAE Industria: Tem tratamento de Industria ?
	_cIndustr		:= Iif( (_cAlias)->A1_XCNAEIN == "S", "Sim","Não")
	//-- FPAS: Se o cliente é contribuínte ou não.
	_cFPAS			:= Iif( (_cAlias)->A1_XFPAS == "S", "Sim","Não")	
	//-- 
	_cDescPolo		:= Posicione("ZCD",1,PadR(Substr((_cAlias)->AD5_FILIAL,1,4),8)+(_cAlias)->A3_XPOLO, "ZCD_DESCRI")                                  
	_cCNPJ			:= Transform(Alltrim((_cAlias)->A1_CGC),Iif(Len(AllTrim((_cAlias)->A1_CGC))==11,"@R 999.999.999-99","@R 99.999.999/9999-99"))
	_cProspNom		:= Posicione("SUS",1,FwxFilial("SUS")+(_cAlias)->AD5_PROSPE+(_cAlias)->AD5_LOJPRO,"US_NOME")
	//+------------------------------------------------------------------+
	//|  Imprime a IDENTIFICAÇÃO DO VENDEDOR                             |
	//+------------------------------------------------------------------+
	_oApontam:Cell( "AD5_VEND"		):SetBlock({|| (_cAlias)->AD5_VEND		}) 
	_oApontam:Cell( "A3_NOME"		):SetBlock({|| (_cAlias)->A3_NOME		}) 
	_oApontam:Cell( "AD5_FILIAL"	):SetBlock({|| (_cAlias)->AD5_FILIAL	}) 
	_oApontam:Cell( "AD5_DATA"		):SetBlock({|| (_cAlias)->AD5_DATA		})
	_oApontam:Cell( "AD5_XRHINI"	):SetBlock({|| (_cAlias)->AD5_XRHINI	}) 
	_oApontam:Cell( "AD5_XRHFIM"	):SetBlock({|| (_cAlias)->AD5_XRHFIM	}) 
	_oApontam:Cell( "AD5_SEQUEN"	):SetBlock({|| (_cAlias)->AD5_SEQUEN	}) 
	_oApontam:Cell( "AD5_XDTIME"	):SetBlock({|| (_cAlias)->AD5_XDTIME	}) 
	_oApontam:Cell( "AD5_NROPOR"	):SetBlock({|| (_cAlias)->AD5_NROPOR	}) 
	//-- 
	_oApontam:Cell( "A3XPOLO"		):SetBlock({|| _cDescPolo		 		}) 
	_oApontam:Cell( "AD5_CODCLI"	):SetBlock({|| (_cAlias)->AD5_CODCLI	}) 
	_oApontam:Cell( "AD5_LOJA"		):SetBlock({|| (_cAlias)->AD5_LOJA		})
	_oApontam:Cell( "A1_CGC"		):SetBlock({|| _cCNPJ				})
	_oApontam:Cell( "A1_NOME"		):SetBlock({|| (_cAlias)->A1_NOME		}) 
	_oApontam:Cell( "A1_NREDUZ"		):SetBlock({|| (_cAlias)->A1_NREDUZ		}) 
	_oApontam:Cell( "A1_CEINSS"		):SetBlock({|| (_cAlias)->A1_CEINSS		}) 
	_oApontam:Cell( "A1_MUN"		):SetBlock({|| (_cAlias)->A1_MUN	}) 
	_oApontam:Cell( "A1XCNAEIN"		):SetBlock({|| _cIndustr				}) 
	_oApontam:Cell( "A1XFPAS"		):SetBlock({|| _cFPAS					})
	_oApontam:Cell( "A1_XQTEMP"		):SetBlock({|| (_cAlias)->A1_XQTEMP		}) 
	_oApontam:Cell( "A1_XQTDESI"	):SetBlock({|| (_cAlias)->A1_XQTDESI	}) 
	_oApontam:Cell( "AD5_EVENTO"	):SetBlock({|| (_cAlias)->AD5_EVENTO	}) 
	_oApontam:Cell( "AC5_DESCRI"	):SetBlock({|| (_cAlias)->AC5_DESCRI	}) 
	_oApontam:Cell( "AD5_PROSPE"	):SetBlock({|| (_cAlias)->AD5_PROSPE	})
	_oApontam:Cell( "AD5_LOJPRO"	):SetBlock({|| (_cAlias)->AD5_LOJPRO	}) 
	_oApontam:Cell( "AD5PRONOME"	):SetBlock({|| _cProspNom		 		}) 
	//+------------------------------------------------------------------+
	//| Identifica o Contato                                             |
	//+------------------------------------------------------------------+
	_cCodCont	:= ""	;	_cNomeCont	:= ""	;	_cMailCont	:= ""
	_cFoneCont 	:= ""	;	_cCodFunc	:= ""	;	_cNomFunc	:= ""
	//-- 
	dbSelectArea("AD9")
	AD9->(dbSetOrder(1))
	If (AD9->(dbSeek((_cAlias)->AD5_FILIAL+(_cAlias)->AD5_NROPOR,.t.)))
		dbSelectArea("SU5")
		SU5->(dbSetOrder(1))
		If (SU5->(dbSeek(FwxFilial("SU5")+AD9->AD9_CODCON,.t.)))
			_cCodCont	:= SU5->U5_CODCONT	
			_cNomeCont	:= SU5->U5_CONTAT
			_cMailCont	:= SU5->U5_EMAIL
			_cFoneCont 	:= SU5->U5_FONE
			_cCodFunc	:= SU5->U5_FUNCAO
			_cNomFunc	:= Posicione("SUM",1,FwxFilial("SUM")+SU5->U5_FUNCAO,"UM_DESC")
		Endif 	
	Endif 
	_oApontam:Cell( "U5_CODCONT"	):SetBlock({|| _cCodCont	}) 
	_oApontam:Cell( "U5_CONTAT"		):SetBlock({|| _cNomeCont	})
	_oApontam:Cell( "U5_FUNCAO"		):SetBlock({|| _cCodFunc	}) 
	_oApontam:Cell( "UM_DESC"		):SetBlock({|| _cNomFunc	}) 
	_oApontam:Cell( "U5_EMAIL"		):SetBlock({|| _cMailCont	}) 
	_oApontam:Cell( "U5_FONE"		):SetBlock({|| _cFoneCont	})
	//-- 
	_oApontam:PrintLine()		//-- Imprime e alinha   
	(_cAlias)->(dbskip()) 	
Enddo 
//+------------------------------------------------------------------+
//|     Finaliza todas as sessões por definitivo                     |
//+------------------------------------------------------------------+
_oApontam:Finish()
//+------------------------------------------------------------------+
//|     Fecha o Arquivo de Trabalho                                  |
//+------------------------------------------------------------------+
(_cAlias)->(dbCloseArea())
//+------------------------------------------------------------------+
//|     Finaliza as sessoes                                          |
//+------------------------------------------------------------------+
Return


/*
+--------------------------------------------------------------------------+
{Protheus.doc}<fGet336X>
   Rotina que cria a consulta SQL para garimpo das informações de 
   Apontamento dos Atendimento para gerar o Relatorio. 

@author<Antonio Dantas>
@since<26/05/2015>
@version<1.00>
@receive<Nil>
@return<Nil>
@example<Nil>
@see<Nil>
+--------------------------------------------------------------------------+
*/
User Function fGet336X()
Local _cQuery		:= "" 
_cQuery := ""
_cQuery += " Select  "+CRLF
_cQuery += " AD5.AD5_VEND, SA3.A3_NOME, AD5.AD5_FILIAL, AD5.AD5_DATA, AD5.AD5_XRHINI, "+CRLF 
_cQuery += " AD5.AD5_XRHFIM, AD5.AD5_SEQUEN, AD5.AD5_XDTIME, AD5.AD5_NROPOR, SA3.A3_XPOLO, "+CRLF
_cQuery += " AD5.AD5_CODCLI, AD5.AD5_LOJA, SA1.A1_CGC, SA1.A1_NOME,SA1.A1_NREDUZ, "+CRLF
_cQuery += " SA1.A1_CEINSS, SA1.A1_MUN, SA1.A1_XCNAEIN, SA1.A1_XFPAS, SA1.A1_XQTEMP, "+CRLF 
_cQuery += " SA1.A1_XQTDESI, AD5.AD5_EVENTO, AC5.AC5_DESCRI, AD5.AD5_PROSPE, AD5.AD5_LOJPRO "+CRLF 
_cQuery += " From "+RetSqlName("AD5")+" AD5 "+CRLF
_cQuery += " Inner Join "+RetSqlName("SA1")+" SA1 on SA1.D_E_L_E_T_ = ' ' "+CRLF 
_cQuery += "                       And SA1.A1_FILIAL = '"+FwxFilial("SA1")+"' "+CRLF
_cQuery += "                       And SA1.A1_COD = AD5.AD5_CODCLI "+CRLF
_cQuery += "                       And SA1.A1_LOJA = AD5.AD5_LOJA "+CRLF
_cQuery += " Inner Join "+RetSqlName("SA3")+" SA3 on SA3.D_E_L_E_T_ = ' '  "+CRLF
_cQuery += "                       And SA3.A3_FILIAL = '"+FwxFilial("SA3")+"' "+CRLF
_cQuery += "                       And SA3.A3_COD = AD5.AD5_VEND "+CRLF
_cQuery += "                       And SA3.A3_UNIDAD Between '"+Mv_par05+"' And '"+Mv_par06+"' "+CRLF
_cQuery += " Inner Join "+RetSqlName("AC5")+" AC5 on AC5.D_E_L_E_T_ = ' '  "+CRLF
_cQuery += "                       And Substr(AC5.AC5_FILIAL,1,4) = Substr(AD5.AD5_FILIAL,1,4) "+CRLF
_cQuery += "                       And AC5.AC5_EVENTO = AD5.AD5_EVENTO "+CRLF
_cQuery += " Where AD5.D_E_L_E_T_ =  ' ' "+CRLF
_cQuery += " And AD5.AD5_FILIAL BetWeen '"+Mv_par01+"' And '"+Mv_par02+"' "+CRLF
_cQuery += " And Substr(AD5.AD5_FILIAL,1,4) = '"+Substr(cFilAnt,1,4)+"' "+CRLF
_cQuery += " And AD5.AD5_DATA Between '"+Dtos(Mv_par03)+"' and '"+Dtos(Mv_par04)+"' "+CRLF
_cQuery += " And AD5_XPOLO Between '"+Mv_par07+"' and '"+Mv_par08+"'  "+CRLF
//-- _cQuery += " Order By AD5.AD5_VEND, AD5.AD5_FILIAL, AD5.AD5_DATA, AD5.AD5_XRHINI, AD5.AD5_XRHFIM ,AD5.AD5_SEQUEN "+CRLF
_cQuery += " Order By AD5.AD5_VEND, AD5.AD5_FILIAL, AD5.AD5_DATA, AD5.AD5_SEQUEN "+CRLF
//+------------------------------------------------------------------+
//| Grava o Resultado da Query para consulta                         |
//+------------------------------------------------------------------+a
MemoWrite("C:\temp\"+FunName()+"_"+ProcName()+".TXT",_cQuery)
If Select(_cAlias)>0
	(_cAlias)->(DbCloseArea())
Endif
DbUseArea(.t.,"TOPCONN",TcGenQry(,,_cQuery),_cAlias,.t.,.t.)
//+------------------------------------------------------------------+
//| Converte os campos tipo DATA                                     |
//+------------------------------------------------------------------+
TCSETFIELD( _cAlias,"AD5_DATA"	,"D",8,0 )

Return .t.


/*
--------------------------------------------------------------------------------
{Protheus.doc} <xParPerg>
 Cria as perguntas.	  

@author<Antonio Dantas>
@since<26/05/2015>
@version<1.00>
@receive<Nil>
@return<Nil>
@example<Nil>
@see<Nil>
--------------------------------------------------------------------------------
*/
Static Function xParPerg()	
Local _aPerg := {}
//*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*
//*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*
//   //-- MAPEAMENTOS DOS CAMPOS DE SX1
//   aAdd (_aPerg,{"X1_PERGUNT","X1_PERPA","X1_PERENG","MV_CHL","C",5,0,0,"X1_GSC","X1_VALID",;
//								"X1_VAR01",("X1_DEF01" e "X1_PYNE"),"X1_DEFSPA1","X1_DEFENG1","X1_CNT01",;
//								"X1_VAR02","X1_DEF02","X1_DEFSPA2","X1_DEFENG2","X1_CNT02",;
//								"X1_VAR03","X1_DEF03","X1_DEFSPA3","X1_DEFENG3","X1_CNT03",;
//								"X1_VAR04","X1_DEF04","X1_DEFSPA4","X1_DEFENG4","X1_CNT04",;
//								"X1_VAR05","X1_DEF05","X1_DEFSPA5","X1_DEFENG5","X1_CNT05",;
//								"X1_F3","X1_GRPSXG","","X1_HELP","X1_PICTURE",{"Descricao para Tela PTG"},{"Descricao para Tela EPA"},{"Descricao para Tela USA"}})
//*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*
//*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*
//+------------------------------------------------------------------+
//| Verifica se o operador esta logado na filail DR que tem acesso   |
//| a todas as filiais, ou somente na filial em que esta logada.     |
//+------------------------------------------------------------------+
//-- Mv_par01 =     Filial DE?
aAdd(_aPerg, {"Filial DE ?","Filial DE ?","Filial DE ?","MV_CH1","C",TamSX3("AD5_FILIAL")[1],0,0,"G","u_fVldfil()",;
					"MV_PAR01","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"SM0EPU","","","","",{"Codigo da Filial"},{"Codigo da Filial"},{"Codigo da Filial"}})

//-- Mv_par02 =     Filial ATE ?
aAdd(_aPerg, {"Filial ATE ?","Filial ATE ?","Filial ATE? ","MV_CH2","C",TamSX3("AD5_FILIAL")[1],0,0,"G","u_fVldfil()",;
					"MV_PAR02","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"SM0EPU","","","","",{"Codigo da Filial"},{"Codigo da Filial"},{"Codigo da Filial"}})

//-- Mv_par03 =     Data de Apontamento DE ?
aAdd(_aPerg, {"Data de Apontamento DE ?","Data de Apontamento DE ?","Data de Apontamento DE ?","MV_CH3","D",8,0,0,"G","",;
					"MV_PAR03","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",{"Data de Apontamento"},{"Data de Apontamento"},{"Data de Apontamento"}})

//-- Mv_par04 =     Data de Apontamento ATE ?
aAdd(_aPerg, {"Data de Apontamento ATE ?","Data de Apontamento ATE ?","Data de Apontamento ATE ?","MV_CH4","D",8,0,0,"G","",;
					"MV_PAR04","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",{"Data de Visita"},{"Data de Visita"},{"Data de Visita"}})

//-- Mv_par05 =     Unid do Repres DE ?
aAdd(_aPerg, {"Unid do Repres DE ?","Unid do Repres DE ?","Unid do Repres DE ?","MV_CH5","C",TamSX3("A3_UNIDAD")[1],0,0,"G","",;
					"Mv_par05","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"ADK","","","","",{"Unidade do Representante"},{"Unidade do Representante"},{"Unidade do Representante"}})

//-- Mv_par06 =     Unid do Repres ATE ?
aAdd(_aPerg, {"Unid do Repres ATE ?","Unid do Repres ATE ?","Unid do Repres ATE ?","MV_CH6","C",TamSX3("A3_UNIDAD")[1],0,0,"G","",;
					"Mv_par06","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"ADK","","","","",{"Unidade do Representante"},{"Unidade do Representante"},{"Unidade do Representante"}})

//-- Mv_par07 =     Polo de Venda DE ?
aAdd(_aPerg, {"Polo de Venda DE ?","Polo de Venda DE ?","Polo de Venda DE ?","MV_CH7","C",TamSX3("AD5_XPOLO")[1],0,0,"G","",;
					"Mv_par07","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"ADK","","","","",{"Polo de Venda"},{"Polo de Venda"},{"Polo de Venda"}})

//-- Mv_par08 =     Polo de Venda DE ?
aAdd(_aPerg, {"Polo de Venda ATE ?","Polo de Venda ATE ?","Polo de Venda ATE ?","MV_CH8","C",TamSX3("AD5_XPOLO")[1],0,0,"G","",;
					"Mv_par08","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"ADK","","","","",{"Polo de Venda"},{"Polo de Venda"},{"Polo de Venda"}})
u_SFAJUSX1(_cPerg,_aPerg) 
Return 

/*/{Protheus.doc} CriaSx1
	Função para criar as perguntas/parâmetros de relatório
	
@author Franklin B. Oliveira
@since 26/04/2018
@param cPerg, characters, grupo de perguntas 
@type function
/*/
Static Function CriaSx1(cPerg)

	Local nI		:= 0
	Local nJ		:= 0
	Local lSX1		:= .F.
	Local aSaveArea	:= GetArea()
	Local aPergs	:= {}
	Local aEstrut	:= {}
	Local aHelpEng	:= {}
	Local aHelpSpa	:= {}
	Local aHelpPor	:= {}
	
	//				X1_GRUPO   X1_ORDEM   X1_PERGUNT X1_PERSPA X1_PERENG  X1_VARIAVL X1_TIPO    X1_TAMANHO X1_DECIMAL X1_PRESEL
	//				X1_GSC     X1_VALID   X1_VAR01   X1_DEF01  X1_DEFSPA1 X1_DEFENG1 X1_CNT01   X1_VAR02   X1_DEF02
	//				X1_DEFSPA2 X1_DEFENG2 X1_CNT02   X1_VAR03  X1_DEF03   X1_fDEFSPA3 X1_DEFENG3 X1_CNT03   X1_VAR04   X1_DEF04
	// 				X1_DEFSPA4 X1_DEFENG4 X1_CNT04   X1_VAR05  X1_DEF05   X1_DEFSPA5 X1_DEFENG5 X1_CNT05   X1_F3      X1_GRPSXG X1_PYME
	
	aEstrut := { "X1_GRUPO","X1_ORDEM"  ,"X1_PERGUNT","X1_PERSPA" ,"X1_PERENG" ,"X1_VARIAVL","X1_TIPO" ,"X1_TAMANHO","X1_DECIMAL","X1_PRESEL" ,;
	"X1_GSC"    ,"X1_VALID","X1_VAR01"  ,"X1_DEF01"  ,"X1_DEFSPA1","X1_DEFENG1","X1_CNT01","X1_VAR02"  ,"X1_DEF02"  ,"X1_DEFSPA2",;
	"X1_DEFENG2","X1_CNT02","X1_VAR03"  ,"X1_DEF03"  ,"X1_DEFSPA3","X1_DEFENG3","X1_CNT03","X1_VAR04"  ,"X1_DEF04"  ,"X1_DEFSPA4",;
	"X1_DEFENG4","X1_CNT04","X1_VAR05"  ,"X1_DEF05"  ,"X1_DEFSPA5","X1_DEFENG5","X1_CNT05","X1_F3"     ,"X1_GRPSXG" ,"X1_PYME"}
	
	//-- Mv_par01 =     Filial DE?
	aAdd(aPergs,{cPerg,"01","Filial DE ?","","","MV_CH1","D",TamSX3("AD5_FILIAL")[1],00,00,"G","u_fVldfil()","MV_PAR01","","","","","","","","","","","","","","","","","","","","","","","","","SM0EPU",""})
	
	//-- Mv_par02 =     Filial ATE ?
	aAdd(aPergs,{cPerg,"02","Filial ATE ?","","","MV_CH2","D",TamSX3("AD5_FILIAL")[1],00,00,"G","u_fVldfil()","MV_PAR02","","","","","","","","","","","","","","","","","","","","","","","","","SM0EPU",""})
	
	//-- Mv_par03 =     Data de Apontamento DE ?
	aAdd(aPergs,{cPerg,"03","Data de Apontamento DE ?" ,"","","MV_CH3","D",08,00,00,"G","","MV_PAR03","","","","","","","","","","","","","","","","","","","","","","","","","",""})

	//-- Mv_par04 =     Data de Apontamento ATE ?
	aAdd(aPergs,{cPerg,"04","Data de Apontamento ATE ?","","","MV_CH4","D",08,00,00,"G","","MV_PAR04","","","","","","","","","","","","","","","","","","","","","","","","","",""})   
	
	//-- Mv_par05 =     Unid do Repres DE ?
	aAdd(aPergs,{cPerg,"05","Unid do Repres DE ?","","","MV_CH5","C",TamSX3("A3_UNIDAD")[1],00,00,"G","","mv_par05","","","","","","","","","","","","","","","","","","","","","","","","","ADK",""})
	
	//-- Mv_par06 =     Unid do Repres ATE ?
	aAdd(aPergs,{cPerg,"06","Unid do Repres ATE ?","","","MV_CH6","C",TamSX3("A3_UNIDAD")[1],00,00,"G","","mv_par06","","","","","","","","","","","","","","","","","","","","","","","","","ADK",""})
	
	//-- Mv_par07 =     Polo de Venda DE ?
	aAdd(aPergs,{cPerg,"07","Polo de Venda DE ?","","","MV_CH7","C",TamSX3("AD5_XPOLO")[1],00,00,"G","","Mv_par07","","","","","","","","","","","","","","","","","","","","","","","","","ADK",""})
	
	//-- Mv_par08 =     Polo de Venda DE ?
	aAdd(aPergs,{cPerg,"08","Polo de Venda ATE ?","","","MV_CH8","C",TamSX3("AD5_XPOLO")[1],00,00,"G","","Mv_par08","","","","","","","","","","","","","","","","","","","","","","","","","ADK",""})   
	
	ProcRegua(Len(aPergs))
	dbSelectArea("SX1")
	dbSetOrder(1)
	IF ! dbSeek(cPerg)
		For nI:= 1 To Len(aPergs)
			If !Empty(aPergs[nI][1])
				If !dbSeek(aPergs[nI,1]+aPergs[nI,2])
					lSX1 := .T.
					RecLock("SX1",.T.)
					For nJ:=1 To Len(aPergs[nI])
						If !Empty(FieldName(FieldPos(aEstrut[nJ])))
							FieldPut(FieldPos(aEstrut[nJ]),aPergs[nI,nJ])
						EndIf
					Next nJ
					MsUnLock()
					dbCommit()
					IncProc("Atualizando Perguntas de Relatorios" + "...")		//"Atualizando Perguntas de Relatorios"
				EndIf
			EndIf
		Next nI
	EndIf
	
	/*
	Inclusao do "help" 
	*/
	AADD(aHelpPor,"Codigo da Filial")
	AADD(aHelpPor,"Codigo da Filial")
	AADD(aHelpPor,"Data de Apontamento")
	AADD(aHelpPor,"Data de Visita")
	AADD(aHelpPor,"Unidade do Representante")
	AADD(aHelpPor,"Unidade do Representante")
	AADD(aHelpPor,"Polo de Venda")
	AADD(aHelpPor,"Polo de Venda")
	Aadd(aHelpSpa,"")
	Aadd(aHelpSpa,"")
	Aadd(aHelpSpa,"")
	Aadd(aHelpSpa,"")
	Aadd(aHelpSpa,"")
	Aadd(aHelpSpa,"")
	Aadd(aHelpEng,"")
	Aadd(aHelpEng,"")
	Aadd(aHelpEng,"")
	Aadd(aHelpEng,"")
	Aadd(aHelpEng,"")
	Aadd(aHelpEng,"")
	
	PutSX1Help("P." + cPerg + "01.",aHelpPor,aHelpEng,aHelpSpa,.T.)
	AHelpPor := {}
	AHelpEng := {}
	AHelpSpa := {}
	/*..*/
	AADD(aHelpPor,"Codigo da Filial")
	AADD(aHelpPor,"Codigo da Filial")
	AADD(aHelpPor,"Data de Apontamento")
	AADD(aHelpPor,"Data de Visita")
	AADD(aHelpPor,"Unidade do Representante")
	AADD(aHelpPor,"Unidade do Representante")
	AADD(aHelpPor,"Polo de Venda")
	AADD(aHelpPor,"Polo de Venda")
	Aadd(aHelpSpa,"")
	Aadd(aHelpSpa,"")
	Aadd(aHelpSpa,"")
	Aadd(aHelpSpa,"")
	Aadd(aHelpSpa,"")
	Aadd(aHelpSpa,"")
	Aadd(aHelpEng,"")
	Aadd(aHelpEng,"")
	Aadd(aHelpEng,"")
	Aadd(aHelpEng,"")
	Aadd(aHelpEng,"")
	Aadd(aHelpEng,"")
	AHelpPor := {}
	AHelpEng := {}
	AHelpSpa := {}
	/*..*/
	AADD(aHelpPor,"Codigo da Filial")
	AADD(aHelpPor,"Codigo da Filial")
	AADD(aHelpPor,"Data de Apontamento")
	AADD(aHelpPor,"Data de Visita")
	AADD(aHelpPor,"Unidade do Representante")
	AADD(aHelpPor,"Unidade do Representante")
	AADD(aHelpPor,"Polo de Venda")
	AADD(aHelpPor,"Polo de Venda")
	Aadd(aHelpSpa,"")
	Aadd(aHelpSpa,"")
	Aadd(aHelpSpa,"")
	Aadd(aHelpSpa,"")
	Aadd(aHelpSpa,"")
	Aadd(aHelpSpa,"")
	Aadd(aHelpEng,"")
	Aadd(aHelpEng,"")
	Aadd(aHelpEng,"")
	Aadd(aHelpEng,"")
	Aadd(aHelpEng,"")
	Aadd(aHelpEng,"")
	PutSX1Help("P." + cPerg + "03.",aHelpPor,aHelpEng,aHelpSpa,.T.)
	AHelpPor := {}
	AHelpEng := {}
	AHelpSpa := {} 
	
	RestArea(aSaveArea)
	
Return