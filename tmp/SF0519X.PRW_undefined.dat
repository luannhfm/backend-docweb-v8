#INCLUDE "PROTHEUS.CH"
#INCLUDE "COLORS.CH"

#DEFINE _PICTURE 13

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun?ao	 ³ CTLJ079J ³ Autor ³ JULIO STORINO         ³ Data ³ 02/12/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri?ao ³ Efetua Sangria/Entrada de Troco no Front Loja			     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

User Function SF0519X(nCheck)

Local cEstat := U_SFGN001A(ProcName(0), "SF0519X")
Local oDlgSangr, oCheck, oBtnAct, oBtnEnd, oGroup1, oGroup2, oGroup3, oGroup4
Local oCodOrigem, oCodDestin
Local oMoeda
Local cCodOrigem := xNumCaixa()
Local cCodDestin := Space(Len(cCodOrigem))
Local cMoedaCorr := GetMv("MV_MOEDA1")
Local cCampo     := ""
Local oDinheir, oCheques, oCartao, oVlrDebi, oFinanc, oConveni, oVales, oOutros
Local oMDinheir,oMCheques,oMCartao,oMVlrDebi,oMFinanc,oMConveni,oMVales,oMOutros
Local nDinheir  := nCheques  := nCartao  := nVlrDebi  := nFinanc  := nConveni  := nVales  := nOutros  := 0
Local cMDinheir := cMCheques := cMCartao := cMVlrDebi := cMFinanc := cMConveni := cMVales := cMOutros := Space(31)
Local nAltura   := 0
Local nAltura2  := 0
Local nX
Local nPosCampo := 0
Local aRet      := {} // Retorno do Ponto de Entrada
Local aMoedas   := {}
Local aSimbs    := {}
Local lRet      := .F.
Local cCaixaSup := Space(15)

Private nMoedaCorr := 1   // Moeda Corrente

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica Permissao "Sangria/Entrada de Troco" - #5 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//If !LjProfile(5, @cCaixaSup)
//"Usuario sem permissao para realizar Sangria / Entrada de Troco. Atencao"
//	MsgStop("Usuário " + AllTrim(Substr(cUsuario,7,15)) + " sem permissao para realizar Sangria / Entrada de Troco.", "Atenção")
//	Return( NIL )
//EndIf

nCheck := if(nCheck ==NIl,0,nCheck)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Se Troco o caixa destino sempre eh o operador atual³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nCheck = 2
	cCodDestin := xNumCaixa()
	cCodOrigem := Substr(GetMv("MV_CXLOJA"),1,3)
Else
	cCodDestin := Substr(GetMv("MV_CXLOJA"),1,3)
	cCodOrigem := xNumCaixa()
EndIf

If lGaveta
	
	If (!Empty(LJGetStation("PORTGAV"))) .And. (LJGetStation("PORTIF") <> LJGetStation("PORTGAV"))
		GavetaAci(nHdlGaveta, LJGetStation("PORTGAV"))
	Else
		IFGaveta(nHdlECF)
	EndIf
	
EndIf

If ExistBlock("FRT050SA")
	aRet := ExecBlock("FRT050SA",.F.,.F.,{nCheck,cCodOrigem,cCodDestin})
	nCheck		:= aRet[1]
	cCodOrigem	:= aRet[2]
	cCodDestin	:= aRet[3]
	nDinheir		:= aRet[4]
	nCheques		:= aRet[5]
	nCartao		:= aRet[6]
	nVlrDebi		:= aRet[7]
	nFinanc		:= aRet[8]
	nConveni		:= aRet[9]
	nVales		:= aRet[10]
	nOutros		:= aRet[11]
EndIf

SX5->(dbSetOrder(1)) // Primeiro indice para validacao do caixa digitado
SA6->(dbSetOrder(1)) // Primeiro indice para obtencao da Agencia/Conta para geracao do SE5

DEFINE MSDIALOG ODlgSangr FROM 1,1 TO 388+nAltura,450 TITLE "Sangria/Troco" PIXEL OF GetWndDefault()

@ 005,014 GROUP oGroup1 TO 043,067 LABEL "Tipo" COLOR CLR_HBLUE OF oDlgSangr PIXEL

@ 013,017 RADIO oCheck VAR nCheck 3D SIZE 47,10 PROMPT "Sangria","Troco" ON CHANGE FrtEnab(nCheck,@cCodOrigem,@cCodDestin,oCodOrigem,oCodDestin) OF ODlgSangr PIXEL

If !nCheck == 0
	oCheck:Disable()
Else
	nCheck := 1
EndIf

@ 005,081 GROUP oGroup2 TO 043,182 LABEL "Origem/Destino" COLOR CLR_HBLUE OF oDlgSangr PIXEL

@ 012,085 SAY "Do Caixa:" SIZE 50,10 OF ODlgSangr PIXEL
@ 012,140 MSGET oCodOrigem VAR cCodOrigem F3 "SA61LJ" WHEN nCheck == 2 SIZE 30,10 OF ODlgSangr PIXEL ;
VALID If(nCheck == 1,SX5->(dbSeek(xFilial("SX5")+"23"+cCodOrigem)),.T.) .And. SA6->(dbSeek(xFilial("SA6")+cCodOrigem) .And. If(nCheck=1,cCodOrigem == xNumCaixa(),cCodOrigem <> xNumCaixa()))
oCodOrigem:cSx1Hlp:=""

@ 027,085 SAY "Para o Caixa:" SIZE 50,10 OF ODlgSangr PIXEL
@ 027,140 MSGET oCodDestin VAR cCodDestin F3 "23" WHEN nCheck == 1 SIZE 30,10 OF ODlgSangr PIXEL ;
VALID If(nCheck == 1,.T.,SX5->(dbSeek(xFilial("SX5")+"23"+cCodDestin))) .And. SA6->(dbSeek(xFilial("SA6")+cCodDestin) .And. If(nCheck=1,cCodDestin <> xNumCaixa(),.T.))
oCodDestin:cSx1Hlp:=""

@ 046,004 GROUP oGroup3 TO 176+nAltura2,110 LABEL "Numerários" COLOR CLR_HBLUE OF oDlgSangr PIXEL
@ 046,113 GROUP oGroup4 TO 176+nAltura2,220 LABEL "Motivo" COLOR CLR_HBLUE OF oDlgSangr PIXEL

@ 055,009 SAY "Dinheiro:" SIZE 60,10 OF ODlgSangr PIXEL
@ 055,058 MSGET oDinheir VAR nDinheir Picture PesqPict("SL1","L1_DINHEIR",_PICTURE,nMoedaCorr) SIZE 50,10 OF ODlgSangr PIXEL RIGHT
oDinheir:cSx1Hlp:="L1_DINHEIR"
@ 055,117 MSGET oMDinheir VAR cMDinheir Picture "@!" SIZE 100,10 OF ODlgSangr PIXEL RIGHT

@ 070,009 SAY "Cheques:" SIZE 60,10 OF ODlgSangr PIXEL
@ 070,058 MSGET oCheques VAR nCheques Picture PesqPict("SL1","L1_CHEQUES",_PICTURE,nMoedaCorr) WHEN nCheck == 1 SIZE 50,10 OF ODlgSangr PIXEL RIGHT
oCheques:cSx1Hlp:="L1_CHEQUES"
@ 070,117 MSGET oMCheques VAR cMCheques Picture "@!" WHEN nCheck == 1 SIZE 100,10 OF ODlgSangr PIXEL RIGHT

@ 085,009 SAY "Cartão de Crédito:" SIZE 60,10 OF ODlgSangr PIXEL
@ 085,058 MSGET oCartao VAR nCartao Picture PesqPict("SL1","L1_CARTAO",_PICTURE,nMoedaCorr) WHEN nCheck == 1 SIZE 50,10 OF ODlgSangr PIXEL RIGHT
oCartao:cSx1Hlp:="L1_CARTAO"
@ 085,117 MSGET oMCartao VAR cMCartao Picture "@!" WHEN nCheck == 1 SIZE 100,10 OF ODlgSangr PIXEL RIGHT

@ 100,009 SAY "Cartão de Débito:" SIZE 60,10 OF ODlgSangr PIXEL
@ 100,058 MSGET oVlrDebi VAR nVlrDebi Picture PesqPict("SL1","L1_VLRDEBI",_PICTURE,nMoedaCorr) WHEN nCheck == 1 SIZE 50,10 OF ODlgSangr PIXEL RIGHT
oVlrDebi:cSx1Hlp:="L1_VLRDEBI"
@ 100,117 MSGET oMVlrDebi VAR cMVlrDebi Picture "@!" WHEN nCheck == 1 SIZE 100,10 OF ODlgSangr PIXEL RIGHT

@ 115,009 SAY "Financiado:" SIZE 60,10 OF ODlgSangr PIXEL
@ 115,058 MSGET oFinanc VAR nFinanc Picture PesqPict("SL1","L1_FINANC",_PICTURE,nMoedaCorr) WHEN nCheck == 1 SIZE 50,10 OF ODlgSangr PIXEL RIGHT
oFinanc:cSx1Hlp:="L1_FINANC"
@ 115,117 MSGET oMFinanc VAR cMFinanc Picture "@!" WHEN nCheck == 1 SIZE 100,10 OF ODlgSangr PIXEL RIGHT

@ 130,009 SAY "Convênio:" SIZE 60,10 OF ODlgSangr PIXEL
@ 130,058 MSGET oConveni VAR nConveni Picture PesqPict("SL1","L1_CONVENI",_PICTURE,nMoedaCorr) WHEN nCheck == 1 SIZE 50,10 OF ODlgSangr PIXEL RIGHT
oConveni:cSx1Hlp:="L1_CONVENI"
@ 130,117 MSGET oMConveni VAR cMConveni Picture "@!" WHEN nCheck == 1 SIZE 100,10 OF ODlgSangr PIXEL RIGHT

@ 145,009 SAY "Vales:" SIZE 60,10 OF ODlgSangr PIXEL
@ 145,058 MSGET oVales VAR nVales Picture PesqPict("SL1","L1_VALES",_PICTURE,nMoedaCorr) WHEN nCheck == 1 SIZE 50,10 OF ODlgSangr PIXEL RIGHT
oVales:cSx1Hlp:="L1_VALES"
@ 145,117 MSGET oMVales VAR cMVales Picture "@!" WHEN nCheck == 1 SIZE 100,10 OF ODlgSangr PIXEL RIGHT

@ 160,009 SAY "Outros:" SIZE 60,10 OF ODlgSangr PIXEL
@ 160,058 MSGET oOutros VAR nOutros Picture PesqPict("SL1","L1_OUTROS",_PICTURE,nMoedaCorr) WHEN nCheck == 1 SIZE 50,10 OF ODlgSangr PIXEL RIGHT
oOutros:cSx1Hlp:="L1_OUTROS"
@ 160,117 MSGET oMOutros VAR cMOutros Picture "@!" WHEN nCheck == 1 SIZE 100,10 OF ODlgSangr PIXEL RIGHT

If cPaisLoc != "BRA"
	@ 175,014 SAY "Moeda:" SIZE 60,10 OF ODlgSangr PIXEL  //"Moeda: "
	@ 175,074 MSCOMBOBOX oMoeda VAR cMoedaCorr ITEMS aMoedas ON CHANGE (nMoedaCorr:=oMoeda:nAt);
	SIZE 70,10 OF ODlgSangr PIXEL
EndIf

DEFINE SBUTTON oBtnAct FROM 180+nAltura2,80 TYPE 1 ENABLE ACTION (LjMsgRun("Aguarde, gravando movimentações bancárias...",,;
{|| lRet := XFrtSE5(nCheck,cCodOrigem,cCodDestin,nDinheir,nCheques,nCartao,nVlrDebi,nFinanc,nConveni,nVales,nOutros,aSimbs,,cMDinheir,cMCheques,cMCartao,cMVlrDebi,cMFinanc,cMConveni,cMVales,cMOutros)}) ,;
IIf(lRet .And. LJProfile(3) .And. ExistBlock("Frt050ST"),ExecBlock("Frt050ST",.F.,.F.,{nCheck,cCaixaSup,nDinheir,nCheques,nCartao,nVlrDebi,nFinanc,nConveni,nVales,nOutros }),),ODlgSangr:End()) OF ODlgSangr

DEFINE SBUTTON oBtnEnd FROM 180+nAltura2,115 TYPE 2 ENABLE ACTION ODlgSangr:End() OF ODlgSangr

ACTIVATE MSDIALOG ODlgSangr CENTER

Return( Nil )

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun?ao	 ³ xFrtEnab ³ Autor ³ JULIO STORINO         ³ Data ³ 02/12/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Ativa/Desativa os campos conforme a escolha do Radio Btn	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function FrtEnab(nCheck,cCodOrigem,cCodDestin,oCodOrigem,oCodDestin)

Local cEstat := U_SFGN001A(ProcName(0), "SF0519X")

If nCheck == 1
	cCodOrigem := xNumCaixa()
	cCodDestin := Substr(GetMv("MV_CXLOJA"),1,3)
	oCodOrigem:Refresh()
	oCodDestin:SetFocus()
Else
	cCodOrigem := Substr(GetMv("MV_CXLOJA"),1,3)
	cCodDestin := xNumCaixa()
	oCodDestin:Refresh()
	oCodOrigem:SetFocus()
EndIf

SysRefresh()

Return( Nil )


/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun?ao	 ³ xFrtSE5  ³ Autor ³ JULIO STORINO         ³ Data ³ 02/12/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Gera o SE5 Conforme os campos digitados na tela			     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function xFrtSE5(nCheck,cCodOrigem,cCodDestin,nDinheir,nCheques,nCartao,nVlrDebi,nFinanc,nConveni,nVales,nOutros,aSimbs,lMensa, ;
								cMDinheir,cMCheques,cMCartao,cMVlrDebi,cMFinanc,cMConveni,cMVales,cMOutros)

Local cEstat := U_SFGN001A(ProcName(0), "SF0519X")
Local aNumerarios, nNum
Local nTotal 		:= nDinheir+nCheques+nCartao+nVlrDebi+nFinanc+nConveni+nVales+nOutros
Local cSimb 		:= GetMV("MV_SIMB"+LTrim(Str(nMoedaCorr)))
Local nRet			:=0

Local _cSerie		:= GetMV("MV_LOJANF")
Local _cNumCupFis	:= Space(6)
Local _nTamCupom  := TamSx3("L1_NUMCFIS")[1]      //Quantidade de digitos do numero do cupom 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Estaremos utilizando esta função de outros programas             ³
//³portanto em determinadas ocasiões não deverei exibir mensagens   ³
//³parando o processo. Atualmente teremos no fonte LOJA340 e LOJA350³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Default lMensa 	:= .T.

If nTotal == 0 .And. lMensa
	MsgStop("Nenhum valor foi informado, nenhuma movimentação será gerada.","Atenção")
	Return (.F.)
EndIf

If lMensa
	If !MsgYesNo("Confirma gravação da(s) movimentação(ões) referente(s) a quantia de: "+cSimb+" "+Transform(nTotal,PesqPict("SL1","L1_DINHEIR",_PICTURE,nMoedaCorr)) )
		Return (.F.)
	EndIf
EndIf

aNumerarios := {	{cSimb,nDinheir,cMDinheir},;
						{"CH",nCheques,cMCheques},;
						{"CC",nCartao,cMCartao},;
						{"CD",nVlrDebi,cMVlrDebi},;
						{"FI",nFinanc,cMFinanc},;
						{"CO",nConveni,cMConveni},;
						{"VA",nVales,cMVales},;
						{"OU",nOutros,cMOutros}}

If lFiscal .And. nTotal > 0 .And. !ExistBlock("Frt050ST")

	nRet := IFSupr( nHdlECF, If(nCheck=1,3,nCheck), StrZero(nTotal,14,2))

	If nRet != 0
		MsgStop("Não foi possível registrar " + If(nCheck==1,"Sangria","Troco") + " no ECF. Operação não efetuada.", "Atenção")
		Return( .F. )
	Else
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Tenta pegar o n£mero do cupom       ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If Empty( IFPegCupom( nHdlECF, @_cNumCupFis) )
			_cNumCupFis := StrZero(Val(_cNumCupFis),_nTamCupom,0)
   	EndIf
	EndIf
EndIf

For nNum := 1 to If(nCheck==1,Len(aNumerarios),1)
	If !aNumerarios[nNum][2] == 0
		xFRTGRV(cCodOrigem,nCheck,"P",aNumerarios[nNum],aSimbs,"01",_cSerie,_cNumCupFis)
		xFRTGRv(cCodDestin,nCheck,"R",aNumerarios[nNum],aSimbs,"02",_cSerie,_cNumCupFis)
	EndIf
Next nNum

//Clama pelo recalculo do Saldo Bancario !!!
U_SF0520X()

Return( .T. )

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun?ao	 ³ xFrtGrv  ³ Autor ³ JULIO STORINO         ³ Data ³ 02/12/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Grava o Registro no SE5 conforme os parametros recebidos	  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function xFRTGrv(cCodigo,nCheck,cRecPag,aNum,aSimbs,cSeq,cSer,cNumCup)

Local cEstat := U_SFGN001A(ProcName(0), "SF0519X")
Local cCaixa 		:= xNumCaixa()
Local cNatureza 	:= If(nCheck == 1,Upper("Sangria"),Upper("Troco"))  //"Sangria","Troco"
Local cAgencia  	:= ""
Local cConta    	:= ""
Local nDecs1    	:= MsDecimais(1)

Default cSeq		:= "01"
Default cSer		:= ""
Default cNumCup	:= ""
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Efetua apenas 1 lancamento pois somente baterah o  ³
//³ Resumo de Caixa qdo os dados subirem p/ o servidor.³
//³ Sangria de Cx para Cx jah possui 2 registros,      ³
//³ um R e um P.                                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

If cCodigo == cCaixa .Or. cCodigo == Substr(GetMv("MV_CXLOJA"),1,3)
	
	SA6->(dbSeek(xFilial("SA6")+cCodigo))

	Reclock("SE5",.T.)
	SE5->E5_FILIAL		:= xFilial("SE5")
	SE5->E5_DATA		:= dDataBase
	SE5->E5_BANCO		:= SA6->A6_COD
	SE5->E5_AGENCIA	:= SA6->A6_AGENCIA
	SE5->E5_CONTA		:= SA6->A6_NUMCON
	SE5->E5_RECPAG		:= cRecPag
	SE5->E5_HISTOR		:= aNum[3]
	SE5->E5_PREFIXO	:= cSer
	SE5->E5_NUMERO		:= cNumCup
	SE5->E5_TIPODOC	:= "TR"
	SE5->E5_MOEDA		:= If(nCheck=1, aNum[1], "TC")
	SE5->E5_VALOR		:= aNum[2]
	SE5->E5_DTDIGIT	:= dDataBase
	SE5->E5_BENEF		:= Left(AllTrim(U_SF0520X(cEmpAnt,cFilAnt,3)),30)
	SE5->E5_DTDISPO	:= SE5->E5_DATA
	SE5->E5_NATUREZ	:= cNatureza
	SE5->E5_SITUA		:= "00" 	// Criar esse campo para instalacao do Front Loja
	SE5->E5_SEQ			:= cSeq	// Se nao informado, o padrao sera 01

	If SE5->(FieldPos('E5_HORAMOV')) <> 0
		SE5->E5_HORAMOV	:= Substr(Time(),1,5)
	EndIf

	If SE5->(FieldPos('E5_FILORIG')) <> 0
		SE5->E5_FILORIG	:= cFilAnt
	EndIf

	
	SE5->(dbCommit())
	SE5->(MsUnLock())
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Esta geração deve ser efetuada somente qdo chamada do Front Loja             ³
	//³pois a tabela SLI tem como única função subir os movimentos para a retaguarda|
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	//If Alltrim(Upper(FunName())) == "FRTA010"
	//	FRTGeraSLI("    ", "050", Str(SE5->(Recno()),17,0), "NOVO")
	//EndIf
	
EndIf

Return( Nil )
