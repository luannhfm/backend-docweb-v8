#INCLUDE "PROTHEUS.CH"
#INCLUDE "TOPCONN.CH"
#INCLUDE "RWMAKE.CH"
#INCLUDE "FONT.CH"
#INCLUDE "COLORS.CH"
#INCLUDE "PARMTYPE.CH"
#INCLUDE "FWMBROWSE.CH"
#INCLUDE "FWMVCDEF.CH"
#INCLUDE "FWCOMMAND.CH"
#INCLUDE "AP5MAIL.CH"

/*/{Protheus.doc} SF7352X
Selecao e envio do email referente a pesquisa de satisfação
selecionada pelo usuario.

@author    BRENO NOGUEIRA
@version   1.00
@since     15/02/2016
/*/
User Function SF7352X()

	Local   _cPerg		:= "SF7352X"
	
	Private _oBrowse	:= FWMarkBrowse():New()
	Private _cArqTrb	:= ""
	Private _aStrTrb	:= {}
	Private _cAlsTrb	:= GetNextAlias()
	Private aRotina		:= MenuDef()
	Private _cAlias
	Private _cHTML		:= ""

	fAjustPerg(_cPerg)

	If !Pergunte(_cPerg, .T.)
		Return
	EndIF

	MakeSqlExpr(_cPerg)

	//+==================================================================+
	//| Função para selecionar as Oportunidades/Propostas/Produtos		 |
	//| Disponíveis para envio do E-Mail, gerando uma Tabela Temporária. |
	//+==================================================================+
	//Processa( {|| _cAlias	:= fQryConsulta() }, "Processando...", "Consultando Oportunidades/Propostas - Encerradas")

	_oBrowse:SetAlias("AD1")
	_oBrowse:SetDescription("Envio de Pesquisa e Satisfação")

	//+==================================================================+
	//| Campo para marcação dos registros.								 |
	//+==================================================================+
	_oBrowse:SetFieldMark("AD1_OK")

	//+==================================================================+
	//| Define do Filtro padrão da rotina.								 |
	//| Status 3 e 9 e Status Diferente de "E" = enviada				 |
	//+==================================================================+
	_oBrowse:SetFilterDefault("SubStr(AD1_FILIAL,1,4) == '"+ SubStr(xFilial('AD1'),1,4) +"' .And. AD1_STATUS $ ('E/F')")

	//+==================================================================+
	//| Define a Legenda para modelo  									 |
	//+==================================================================+
	_oBrowse:AddLegend("AD1_XPESQ == '        '", "BLUE" 	, "Nao Enviada"		)
	_oBrowse:AddLegend("AD1_XPESQ == 'E       '", "GREEN"	, "Ja Enviada"		)
	_oBrowse:AddLegend("AD1_XPESQ == '2R      '", "YELLOW"	, "Segundo Envio"	)
	_oBrowse:AddLegend("AD1_XPESQ == 'C       '", "RED"	  	, "Ultimo Envio"	)

	_oBrowse:Activate()
	
	FErase(_cArqTrb)

Return

/*/ {Protheus.doc} <MenuDef>
 Definicao do MenuDef

@author<RenatoPeixoto>
@since<09/02/2015>
@version <1.00>
@return< _aRotina (a) - Vetor com as opcoes de Menu >
@example<Nil>
@see<Nil>
/*/
Static Function MenuDef()
	Local _aRotina	:= {{"Enviar Pesquisa","U_MONTAHTML()",0,1}}
Return(_aRotina)

//+==================================================================+
//|Realiza a Query de Seleção dos Registro para utilização no Browser|
//+==================================================================+
Static Function fQryConsulta()
	
	Private _cQry		:= ""
	Private _cQryAD1	:= ""
	Private _cQryADY	:= ""
	Private _cQryADZ	:= ""

	//+==================================================================+
	//| Adicionando condições da AD1									 |
	//+==================================================================+
	_cQryAD1 := ""
	_cQryAD1 += IIF(EMPTY(MV_PAR01), "", MV_PAR01+" AND ")
	_cQryAD1 += IIF(EMPTY(MV_PAR02), "", MV_PAR02+" AND ")
	_cQryAD1 += IIF(EMPTY(MV_PAR05), "", MV_PAR05+" AND ")
	_cQryAD1 += IIF(EMPTY(MV_PAR06), "", MV_PAR06+" AND ")
	_cQryAD1 += IIF(EMPTY(MV_PAR07), "", MV_PAR07+" AND ")
	_cQryAD1 += IIF(EMPTY(MV_PAR09), "", MV_PAR09+" AND ")

	_cQryAD1 += IIF(!EMPTY(MV_PAR10) .Or. !EMPTY(MV_PAR11), "(AD1_DTASSI BETWEEN '"+DToS(MV_PAR10)+"' AND '"+DToS(MV_PAR11)+"') AND ", "")
	_cQryAD1 += IIF(!EMPTY(MV_PAR12) .Or. !EMPTY(MV_PAR13), "(AD1_DATA BETWEEN   '"+DToS(MV_PAR12)+"' AND '"+DToS(MV_PAR13)+"') AND ", "")
	_cQryAD1 += IIF(!EMPTY(MV_PAR14) .Or. !EMPTY(MV_PAR15), "(AD1_DTINI BETWEEN  '"+DToS(MV_PAR14)+"' AND '"+DToS(MV_PAR15)+"') AND ", "")
	_cQryAD1 += IIF(!EMPTY(MV_PAR16) .Or. !EMPTY(MV_PAR17), "(AD1_DTFIM BETWEEN  '"+DToS(MV_PAR16)+"' AND '"+DToS(MV_PAR17)+"') AND ", "")

	//+==================================================================+
	//| Adicionando condições da ADY									 |
	//+==================================================================+
	_cQryADY := ""
	_cQryADY += IIF(EMPTY(MV_PAR03), "", MV_PAR03+" AND ")
	_cQryADY += IIF(EMPTY(MV_PAR04), "", MV_PAR04+" AND ")
	_cQryADY += IIF(EMPTY(MV_PAR08), "", MV_PAR08+" AND ")

	//+==================================================================+
	//| Adicionando condições da ADZ									 |
	//+==================================================================+
	_cQryADZ := ""
	_cQryADZ += IIF(EMPTY(MV_PAR18), "", MV_PAR18+" AND ")
	_cQryADZ += IIF(EMPTY(MV_PAR19), "", MV_PAR19+" AND ")

	//+==================================================================+
	//| Query para verificar a quantidade de registros					 |
	//+==================================================================+

	_cQry := ""
	_cQry := "SELECT "+Chr(13)+Chr(10)
	_cQry += "	COUNT(*) AS QTDREG "+Chr(13)+Chr(10)
	_cQry += "FROM "+Chr(13)+Chr(10)
	_cQry += "	"+RetSQLName("AD1")+" AD1 "+Chr(13)+Chr(10)
	_cQry += "	INNER JOIN "+RetSQLName("ADY")+" ADY ON ADY_FILIAL = AD1_FILIAL AND ADY_OPORTU = AD1_NROPOR AND ADY_REVISA = AD1_REVISA AND "+_cQryADY+" ADY.D_E_L_E_T_ <> '*' "+Chr(13)+Chr(10)
	_cQry += "	INNER JOIN "+RetSQLName("ADZ")+" ADZ ON ADZ_FILIAL = ADY_FILIAL AND ADZ_PROPOS = ADY_PROPOS AND ADZ_REVISA = ADY_PREVIS AND "+_cQryADZ+" ADZ.D_E_L_E_T_ <> '*' "+Chr(13)+Chr(10)
	_cQry += "WHERE "+Chr(13)+Chr(10)
	_cQry += "	AD1_STATUS IN ('E','F') AND "+_cQryAD1+" AD1.D_E_L_E_T_ <> '*' "+Chr(13)+Chr(10)

	MsAguarde( {|| CursorWait(), DbUseArea(.T.,"TOPCONN",TcGenQry(,,_cQry),"TMARK",.t.,.t.), CursorArrow() }, "Consultando", "Verificando a quantidade de Registros.")

	DbSelectArea("TMARK")
	TMARK->(DbGoTop())

	_cQtdReg := TMARK->QTDREG

	TMARK->(DbCloseArea())

	//+==================================================================+
	//| Selecionar os registros e enviar para a tabela temporária		 |
	//+==================================================================+
	_cQry := ""
	_cQry := "SELECT "+Chr(13)+Chr(10)
	_cQry += "	* "+Chr(13)+Chr(10)
	_cQry += "FROM "+Chr(13)+Chr(10)
	_cQry += "	"+RetSQLName("AD1")+" AD1 "+Chr(13)+Chr(10)
	_cQry += "	INNER JOIN "+RetSQLName("ADY")+" ADY ON ADY_FILIAL = AD1_FILIAL AND ADY_OPORTU = AD1_NROPOR AND ADY_REVISA = AD1_REVISA AND "+_cQryADY+" ADY.D_E_L_E_T_ = ' ' "+Chr(13)+Chr(10)
	_cQry += "	INNER JOIN "+RetSQLName("ADZ")+" ADZ ON ADZ_FILIAL = ADY_FILIAL AND ADZ_PROPOS = ADY_PROPOS AND ADZ_REVISA = ADY_PREVIS AND "+_cQryADZ+" ADZ.D_E_L_E_T_ = ' ' "+Chr(13)+Chr(10)
	_cQry += "WHERE "+Chr(13)+Chr(10)
	_cQry += "	AD1_STATUS IN ('E','F') AND "+_cQryAD1+" AD1.D_E_L_E_T_ = ' ' "+Chr(13)+Chr(10)
	_cQry += "ORDER BY "+Chr(13)+Chr(10)
	_cQry += "	AD1_FILIAL, AD1_NROPOR, AD1_REVISA "+Chr(13)+Chr(10)

	MsAguarde( {|| CursorWait(), DbUseArea(.T.,"TOPCONN",TcGenQry(,,_cQry),"TMARK",.t.,.t.), CursorArrow() }, "Consultando", "Enviando para Tabela Temporária.")

	DbSelectArea("TMARK")
	TMARK->(DbGoTop())

	//+==================================================================+
	//| Criando o Arquivo de Trabalho									 |
	//| fArqTrab()														 |
	//+==================================================================+
	ProcRegua(_cQtdReg)

	While TMARK->(!EOF())
		IncProc()
		TMARK->(DbSkip())
	EndDo
	TMARK->(DbCloseArea())

Return

//+==================================================================+
//|Criando o arquivo de trabalho 									 |
//+==================================================================+
Static Function fArqTrab()

	//+==================================================================+
	//| Campos da Oportunidade											 |
	//+==================================================================+
	aADD( _aStrTrb, {"MK_OK"	 , "C", 02, 0} )
	aADD( _aStrTrb, {"MK_NROPOR", "C", TamSX3("AD1_NROPOR")[01]	, TamSX3("AD1_NROPOR")[02]}	)
	aADD( _aStrTrb, {"MK_RVOPOR", "C", TamSX3("AD1_REVISA")[01]	, TamSX3("AD1_REVISA")[02]}	)
	aADD( _aStrTrb, {"MK_DESCRI", "C", TamSX3("AD1_DESCRI")[01]	, TamSX3("AD1_DESCRI")[02]}	)
	aADD( _aStrTrb, {"MK_DATA"  , "C", TamSX3("AD1_DATA")[01]		, TamSX3("AD1_DATA")[02]}	)
	aADD( _aStrTrb, {"MK_HORA"  , "C", TamSX3("AD1_HORA")[01]		, TamSX3("AD1_HORA")[02]}	)
	aADD( _aStrTrb, {"MK_VEND"  , "C", TamSX3("AD1_VEND")[01]		, TamSX3("AD1_VEND")[02]}	)
	aADD( _aStrTrb, {"MK_NOMVEN", "C", TamSX3("AD1_NOMVEN")[01]	, TamSX3("AD1_NOMVEN")[02]}	)
	aADD( _aStrTrb, {"MK_DTINI" , "C", TamSX3("AD1_DTINI")[01]		, TamSX3("AD1_DTINI")[02]}  )
	aADD( _aStrTrb, {"MK_DTFIM" , "C", TamSX3("AD1_DTFIM")[01]		, TamSX3("AD1_DTFIM")[02]}  )
	aADD( _aStrTrb, {"MK_CODCLI", "C", TamSX3("AD1_CODCLI")[01]	, TamSX3("AD1_CODCLI")[02]}	)
	aADD( _aStrTrb, {"MK_LOJCLI", "C", TamSX3("AD1_LOJCLI")[01]	, TamSX3("AD1_LOJCLI")[02]}	)
	aADD( _aStrTrb, {"MK_NOMCLI", "C", TamSX3("AD1_NOMCLI")[01]	, TamSX3("AD1_NOMCLI")[02]}	)
	aADD( _aStrTrb, {"MK_DTASSI", "C", TamSX3("AD1_DTASSI")[01]	, TamSX3("AD1_DTASSI")[02]}	)
	aADD( _aStrTrb, {"MK_CNTPRO", "C", TamSX3("AD1_CNTPRO")[01]	, TamSX3("AD1_CNTPRO")[02]}	)
	aADD( _aStrTrb, {"MK_NOMCNT", "C", TamSX3("AD1_NOMCNT")[01]	, TamSX3("AD1_NOMCNT")[02]}	)

	//+==================================================================+
	//| Campos da Proposta Comercial									 |
	//+==================================================================+
	aADD( _aStrTrb, {"MK_PROPOS", "C", TamSX3("ADY_PROPOS")[01], TamSX3("ADY_PROPOS")[02]} )
	aADD( _aStrTrb, {"MK_PREVIS", "C", TamSX3("ADY_PREVIS")[01], TamSX3("ADY_PREVIS")[02]} )

	//+==================================================================+
	//| Campos dos Itens da Proposta Comercial							 |
	//+==================================================================+
	aADD( _aStrTrb, {"MK_ITEM"  , "C", TamSX3("ADZ_ITEM")[01], TamSX3("ADZ_ITEM")[02]} )
	aADD( _aStrTrb, {"MK_PRODUT", "C", TamSX3("ADZ_PRODUT")[01], TamSX3("ADZ_PRODUT")[02]} )
	aADD( _aStrTrb, {"MK_DESCR" , "C", TamSX3("ADZ_DESCRI")[01], TamSX3("ADZ_DESCRI")[02]} )
	aADD( _aStrTrb, {"MK_XTURMA", "C", TamSX3("ADZ_XTURMA")[01], TamSX3("ADZ_XTURMA")[02]} )

	_cArqTrb := CriaTrab(_aStrTrb, .T.)

	DbUseArea(.T.,, _cArqTrb, "TRB", .t., .F.)

Return

/*/{Protheus.doc} MONTAHTML
Rotina que irá montar o corpo do email atraves de um layout
pré-definido pelo csi.
Este arquivo de layout deverá ser gravado na pasta system subpasta
LayoutPesq.
Exemplo : "\system\Layout_Pesq"

@author    BRENO NOGUEIRA
@version   1.00
@since     15/02/2016
/*/
User Function MONTAHTML()

	Local aArea			:= GetArea()
	Local cMarca			:= _oBrowse:Mark()
	Local _cSql			:= ""
	Local _BSqlEmail 	:= ""
	Local _NOMECOM		:= ""
	Local _cEndPub		:= GetNewPar("MV_XCRMPSE", "http://pesquisasatisfacao.sfiemt.com.br:8080/")
	
	Private _bSqlADJ	:= ""
	Private _nContador	:= 0
	Private _cAccount	:= GetMv("MV_WFMAIL")
	Private _pcOrigem	:= _cAccount
	Private _pcDestino	:= "analistaerp.csi@fiemt.com.br" //-- Comentar essas duas linhas quando em produção
	Private _pcBCC		:= "desenvolvedor.csi@fiemt.com.br" //-- Comentar essas duas linhas quando em produção
	Private _pcBody		:= ""
	Private _pcSubject	:= "Pesquisa de Satisfação - SENAI(MT)"
	Private _cIncConta	:= 0
	Private _cContas	:= ""
	Private _Cliente	:= ""
	Private _Loja		:= ""
	Private _Proposta	:= ""
	Private _Revisao	:= ""
	Private _cOporPesq := ""
	Private NomeCom 	:= ""
	Private EndEnt 		:= ""
	Private BairEnt 	:= ""
	Private CepEnt 		:= ""
	Private CidEnt 		:= ""

	AD1->(DbGoTop())
	While .Not. AD1->(EOF()) .And. SubStr(AD1->AD1_FILIAL,1,4) ==  SubStr(xFilial('AD1'),1,4) .And. AD1->AD1_STATUS $ ('E/F')
	
		If AllTrim(AD1->AD1_XPESQ) == 'E'
			AD1->(DbSkip())
			Loop
		EndIf

		//+==================================================================+
		//| Guarda variaveis para as qrys									 |
		//+==================================================================+
		_Cliente	:= AD1_CODCLI
		_Loja		:= AD1_LOJCLI
		_Proposta	:= AD1_NROPOR
		_Revisao	:= AD1_REVISA
		NomeCom	:= SM0->M0_NOMECOM
		EndEnt		:= SM0->M0_ENDENT
		BairEnt	:= SM0->M0_BAIRENT
		CepEnt		:= SM0->M0_CEPENT
		CidEnt		:= SM0->M0_CIDENT

		//+==================================================================+
		//| Pega somente os selecionados									 |
		//| Quando a proposta estiver sido enviada pela terceira vez o	     |
		//| o sistema irá ignorar o envio novamente, mesmo estando marcada   |
		//+==================================================================+
		If _oBrowse:IsMark(cMarca) .and. AD1_XPESQ <> 'C       '

			//+==================================================================+
			//| Buscar o codigo da pesquisa nos items da proposta				 |
			//+==================================================================+
			bSqlADJ := " SELECT DISTINCT ADJ.ADJ_FILIAL, ADJ.ADJ_NROPOR, ADJ.ADJ_REVISA, ADJ.ADJ_PROPOS, ADJ.ADJ_XPESQ, "
			bSqlADJ += " ADY.ADY_PREVIS, ADY.ADY_CODIGO, ADY.ADY_LOJA "
			bSqlADJ += " FROM "+RetSqlName("ADJ")+"  ADJ, "+RetSqlName("ADY")+" ADY "
			bSqlADJ += " WHERE ADJ.ADJ_FILIAL = '"+ AD1->AD1_FILIAL +"'
			bSqlADJ += " AND ADJ.ADJ_NROPOR = '"+_Proposta+"' "
			bSqlADJ += " AND ADJ.ADJ_REVISA = '"+_Revisao+"' "
			bSqlADJ += " AND ADJ.ADJ_FILIAL = ADY.ADY_FILIAL "
			bSqlADJ += " AND ADJ.ADJ_NROPOR = ADY.ADY_OPORTU "
			bSqlADJ += " AND ADJ.ADJ_REVISA = ADY.ADY_REVISA "
			bSqlADJ += " AND ADJ.ADJ_PROPOS = ADY.ADY_PROPOS "
			bSqlADJ += " AND ADJ.D_E_L_E_T_ <> '*' "
			bSqlADJ += " AND ADY.D_E_L_E_T_ <> '*' "
	
			If Select("SQLADJ") <> 0
				dbSelectArea("SQLADJ")
				dbCloseArea("SQLADJ")
			EndIf
	
			TcQuery bSqlADJ New Alias "SQLADJ"
	
			DbSelectArea("SQLADJ")
			SQLADJ->(DbGoTop())
			While .Not. SQLADJ->(EOF())
	
				//+==================================================================+
				//| Não envia se o campo ADJ_XPESQ estiver em branco.                |
				//| Este campo é preenchido no momento da gravação da proposta	      |
				//+==================================================================+
				If .Not. Empty(SQLADJ->ADJ_XPESQ)
	
					//+==================================================================+
					//| Criacao de Query para buscar os email's no cadastro de contatos  |
					//| sera considerado apenas os marcados na oportunidade se recebe ou |
					//| nao email da pesquisa											      |
					//+==================================================================+
					_BSqlEmail := " SELECT "
					_BSqlEmail += " AD1.AD1_FILIAL, AD1.AD1_NROPOR, AD1.AD1_REVISA, AD1.AD1_CODCLI, AD1.AD1_LOJCLI, "
					_BSqlEmail += " AD9.AD9_FILIAL, AD9.AD9_NROPOR, AD9.AD9_CODCON, "
					_BSqlEmail += " SU5.U5_CODCONT, SU5.U5_CONTAT, SU5.U5_EMAIL "
					_BSqlEmail += " FROM "+RetSqlName("AD1")+" AD1, "+RetSqlName("AD9")+" AD9, "+RetSqlName("SU5")+" SU5 "
					_BSqlEmail += " WHERE AD9.AD9_NROPOR = AD1.AD1_NROPOR "
					_BSqlEmail += " AND SU5.U5_CODCONT = AD9.AD9_CODCON "
					_BSqlEmail += " AND AD9.AD9_FILIAL = AD1.AD1_FILIAL "
					_BSqlEmail += " AND AD9.AD9_XREMAI = '1' "
					_BSqlEmail += " AND AD1.AD1_FILIAL = '"+ AD1->AD1_FILIAL +"'
					_BSqlEmail += " AND AD1.AD1_CODCLI = '"+_Cliente+"' "
					_BSqlEmail += " AND AD1.AD1_LOJCLI = '"+_Loja+"' "
					_BSqlEmail += " AND AD1.AD1_NROPOR = '"+_Proposta+"' "
					_BSqlEmail += " AND AD1.AD1_REVISA = '"+_Revisao+"' "
					_BSqlEmail += " AND AD1.D_E_L_E_T_ <> '*' "
					_BSqlEmail += " AND AD9.D_E_L_E_T_ <> '*' "
					_BSqlEmail += " AND SU5.D_E_L_E_T_ <> '*' "
	
					If Select("BESQL") <> 0
						DbSelectArea("BESQL")
						DbCloseArea("BESQL")
					EndIf
	
					TcQuery _BSqlEmail New Alias "BESQL"
	
					//+==================================================================+
					//| Antes da carga do html guarda todos os email para o envio		 |
					//| Pegar todos os emails dos contatos X oportunidade				 |
					//+==================================================================+
					DbSelectArea("BESQL")
					While BESQL->(!eof())
						Processa({||_cIncConta},"Aguarde buscando emails dos contatos")
						_cIncConta++
						If	_cIncConta = 1
							_cContas := Alltrim(BESQL->U5_EMAIL)
						Else
							_cContas += ',' + Alltrim(BESQL->U5_EMAIL)
						EndIf
						BESQL->(DbSkip())
					EndDo
					
					BESQL->(DbCloseArea())
	
					//+==================================================================+
					//| Faz primeiro a carga do html									 |
					//+==================================================================+
					U_CARGAHTML()
	
					Processa({||_nContador},"Enviando e-mail selecionado")
					_nContador++
	
					//+==================================================================+
					//| Atenção : Muito cuidado ao alterar o HTML						 |
					//| para não alterar bloco "usemap até </map>"						 |
					//| ele é o responsável pelo alinhamento do botão "PESQUISA"		 |
					//+==================================================================+
					_cHTML := _cHTML
					_cHTML += ' usemap="#Map"/>'
					_cHTML +='        <map name="Map" id="Map">'
					If GetEnvServer() == 'SDBPRD'
						_cHTML +='           <area shape="rect" coords="220,90,520,150" href="'+ _cEndPub +'index.aspx?idfilial='+SQLADJ->ADJ_FILIAL+'&idoportu='+SQLADJ->ADJ_NROPOR+'&idrevisa='+SQLADJ->ADJ_REVISA+'&idproposta='+SQLADJ->ADJ_PROPOS+'&idprevis='+SQLADJ->ADY_PREVIS+'&idcliente='+Alltrim(SQLADJ->ADY_CODIGO)+'&idloja='+Alltrim(SQLADJ->ADY_LOJA)+'&idpesquisa='+SQLADJ->ADJ_XPESQ+'" target="_parent" />'
					ElseIf SubStr(GetEnvServer(),1,3) == 'HML'
						_cHTML +='           <area shape="rect" coords="220,90,520,150" href="http://10.52.28.22:8080/index.aspx?idfilial='+SQLADJ->ADJ_FILIAL+'&idoportu='+SQLADJ->ADJ_NROPOR+'&idrevisa='+SQLADJ->ADJ_REVISA+'&idproposta='+SQLADJ->ADJ_PROPOS+'&idprevis='+SQLADJ->ADY_PREVIS+'&idcliente='+Alltrim(SQLADJ->ADY_CODIGO)+'&idloja='+Alltrim(SQLADJ->ADY_LOJA)+'&idpesquisa='+SQLADJ->ADJ_XPESQ+'" target="_parent" />'
					Else
						_cHTML +='           <area shape="rect" coords="220,90,520,150" href="http://10.52.28.137:80/index.aspx?idfilial='+SQLADJ->ADJ_FILIAL+'&idoportu='+SQLADJ->ADJ_NROPOR+'&idrevisa='+SQLADJ->ADJ_REVISA+'&idproposta='+SQLADJ->ADJ_PROPOS+'&idprevis='+SQLADJ->ADY_PREVIS+'&idcliente='+Alltrim(SQLADJ->ADY_CODIGO)+'&idloja='+Alltrim(SQLADJ->ADY_LOJA)+'&idpesquisa='+SQLADJ->ADJ_XPESQ+'" target="_parent" />'
					EndIf
					_cHTML +='       </map>'
					_cHTML +='   </td>'
					If GetEnvServer() == 'SDBPRD'
						_cHTML +='   <td><img src="'+ _cEndPub +'email/spacer.gif" width="1" height="336" alt="" /></td>'
					ElseIf SubStr(GetEnvServer(),1,3) == 'HML'
						_cHTML +='   <td><img src="http://10.52.28.22:8080/email/spacer.gif" width="1" height="336" alt="" /></td>'
					Else
						_cHTML +='   <td><img src="http://10.52.28.137:80/email/spacer.gif" width="1" height="336" alt="" /></td>'
					EndIf
					_cHTML +='  </tr>'
					_cHTML +='  <tr>'
					_cHTML +='   <td colspan="4" align="center" ><span class="style=text-align: center; font-family: Verdana; font-size: 16px;" style="font-family: Verdana, Geneva, Tahoma, sans-serif; font-size: 22px; font-weight: bold; font-style: normal; color: #6699FF">'+NomeCom+' </span><br />'
					_cHTML +='       <span class="style=text-align: center; font-family: Verdana; font-size: 16px;" style="font-family: Verdana, Geneva, Tahoma, sans-serif; font-size: 15px; font-weight: normal; font-style: normal; color: #6699FF">'+EndEnt+'</span><br />'
					_cHTML +='       <span class="style=text-align: center; font-family: Verdana; font-size: 16px;" style="font-family: Verdana, Geneva, Tahoma, sans-serif; font-size: 15px; font-weight: normal; font-style: normal; color: #6699FF">&nbsp;'+BairEnt+'</span><br />'
					_cHTML +='       <span class="style=text-align: center; font-family: Verdana; font-size: 16px;" style="font-family: Verdana, Geneva, Tahoma, sans-serif; font-size: 15px; font-weight: normal; font-style: normal; color: #6699FF">'+Transform(CepEnt,"@R 99.999-999")+' - '+CidEnt+'</span></td>'
					If GetEnvServer() == 'SDBPRD'
						_cHTML +='   <td colspan="3"><img name="PesquisadeSatisfacao04Fatiado_r6_c5" src="'+ _cEndPub +'email/Pesquisa_de_Satisfacao-04-Fatiado_r6_c5.jpg" width="376" height="228" id="PesquisadeSatisfacao04Fatiado_r6_c5" alt="" /></td>'
					ElseIf SubStr(GetEnvServer(),1,3) == 'HML'
						_cHTML +='   <td colspan="3"><img name="PesquisadeSatisfacao04Fatiado_r6_c5" src="http://10.52.28.22:8080/email/Pesquisa_de_Satisfacao-04-Fatiado_r6_c5.jpg" width="376" height="228" id="PesquisadeSatisfacao04Fatiado_r6_c5" alt="" /></td>'
					Else
						_cHTML +='   <td colspan="3"><img name="PesquisadeSatisfacao04Fatiado_r6_c5" src="http://10.52.28.137:80/email/Pesquisa_de_Satisfacao-04-Fatiado_r6_c5.jpg" width="376" height="228" id="PesquisadeSatisfacao04Fatiado_r6_c5" alt="" /></td>'
					EndIf
					If GetEnvServer() == 'SDBPRD'
						_cHTML +='   <td><img src="'+ _cEndPub +'email/spacer.gif" width="1" height="228" alt="" /></td>'
					ElseIf SubStr(GetEnvServer(),1,3) == 'HML'
						_cHTML +='   <td><img src="http://10.52.28.22:8080/email/spacer.gif" width="1" height="228" alt="" /></td>'
					Else
						_cHTML +='   <td><img src="http://10.52.28.137:80/email/spacer.gif" width="1" height="228" alt="" /></td>'
					EndIf
					_cHTML +='  </tr>'
					_cHTML +='</table>'
					_cHTML +='    <table class="auto-style1">'
					_cHTML +='        <tr>'
					_cHTML +='            <td align="center" style="font-family: Verdana, Geneva, Tahoma, sans-serif; font-size: 12px; font-style: normal"><a href=""></a></td>'
					_cHTML +='        </tr>'
					_cHTML +='    </table>'
					_cHTML +='</body>'
					_cHTML +='</html>'
	
					_pcBody := _cHTML
	
					//+==================================================================+
					//| Envio do email													 |
					//| Em ambiente teste e homologação todos os emails serão enviados	 |
					//| para o email definido para testes								 |
					//| após os testes e homologação pode-se trocar						 |
					//| a variável _PcDestino e no lugar informar a variavél _cContas	 |
					//+==================================================================+
					lRet := U_SFEnvEmail(,_cContas,,,_pcSubject,_pcBody,,, .F.)
					//lRet := U_SFEnvEmail(,_pcDestino,_pcBCC,,_pcSubject,_pcBody,,, .F.)
	
					//+==================================================================+
					//| Marca a pesquisa como já enviada para que o sistema				 |
					//| selecione  ela  no  envio  automatico pelo schedule	    		 |
					//| Não esquecer de configurar o SC para  esse envio do				 |
					//| email após o prazo definido pelo usuário						 |
					//+==================================================================+
					If RecLock("AD1",.F.)
						Replace AD1_XPESQ With "E"
						Replace AD1_XDTENV With Date()
						AD1->(MsUnLock())
					EndIf
	
					//+==================================================================+
					//| Caso ocorra erro na conexão com o servidor de SMTP               |
					//+==================================================================+
					If .Not. lRet
						ApMsgInfo("Não foi possível conectar ao servidor SMTP, Por favor entre em contato com o suporte...",;
							"..:: Não foi possível conectar ao SMTP ::..")
						Return .f.
					Endif
					
				//+==================================================================+
				//| Grava occorrencia para mostrar ao usuario as propostas que estão |
				//| sem a informação do código da pesquisa							 |
				//+==================================================================+
				Else
					_cOporPesq := _cOporPesq + ' | Oportunidade : '+ SQLADJ->ADJ_NROPOR + " => Proposta : " + SQLADJ->ADJ_PROPOS
				EndIf
				SQLADJ->(DbSkip())
			EndDo
			SQLADJ->(DbCloseArea())
		Endif
		AD1->(DbSkip())
	EndDo

	ApMsgAlert("Foram enviados : "+Alltrim(Str(_nContador))+" E-mail(s) ")

	If Len(_cOporPesq) > 35
	
		//+==================================================================+
		//| Caso não encontre pesquisa cadastrada na oportunidade de venda	 |
		//| o sistema grava um log para o usuário informando as propostas	 |
		//| que não possuem pesquisa cadastrada							     |
		//+==================================================================+
		ApMsgAlert("Não foi possível encontrar Pesquisa cadastrada na ..:: ABA(PRODUTOS) ::.. ,"+;
			" para a(s) seguinte(s) oportunidade(s) "+ _cOporPesq +;
			" ==>> Não será possível enviar email dessa(s) oportunidade(s), corriga o problema primeiro")
	EndIf
	
Return

/*/
{Protheus.doc} CARGAHTML
Funcao que ira ler o arquivo xml na pasta definida e efetuar a carga
na rotina MONTAXML().
O Arquivo layout deverá ficar na pasta SYSTEM do Protheus.
"\System\Layout_Pesq\Pesquisa.html"
@author Breno Nogueira
@since 15/02/2016
@version 1.0
@param
@see (links_or_references)
/*/
User Function CARGAHTML()
	
	Local cTemp		:= GetTempPath()
	Local cSystem	:= Upper(GetSrvProfString("STARTPATH",""))
	Local cOrigem	:= cSystem+"\Layout_Pesq\" //-- Busca o arquivo no diretório do sistema
	Local cArq		:= "Pesquisa.html"
	Local cFile		:= cOrigem+cArq
	Local nHandle  := 0
	Local nLinTot  := 0
	Local nLin		:= 1
	Local aDados   := {}
	Local cLinha   := ""

	//+==================================================================+
	//| Limpa a variavel para nao criar dois corpos no proximo e-mail	 |
	//+==================================================================+
	_cHTML := ""

	If !File(cFile)
		MsgInfo("O arquivo "+cFile+" não foi encontrado!","Arquivo")
		Return .F.
	Endif

	nHandle := Ft_Fuse(cFile)

	if nHandle == -1
		MsgInfo("O arquivo "+cFile+" está em uso por outro processo !","Aguarde")
		Return .F.
	EndIf

	Ft_FGoTop()
	nLinTot := Ft_FLastRec()-1
	ProcRegua(nLinTot)

	Do While !Ft_FEof()
		nLin++
		cLinha := Ft_FReadLn()
		IncProc("Carregando linha "+Alltrim(Str(nLin))+" de "+Alltrim(Str(nLinTot)))
		_cHTML += cLinha
		Ft_FSkip()
	EndDo
Return

//+==================================================================+
/*/
{Protheus.doc} fPerg
Função para criar o grupo de perguntas caso este não exitsta.
@author FOliveira
@since 11/05/2015
@version 1.0
@param _cPerg, STRING, Nome do Grupo de Perguntas
@example
(examples)
@see (links_or_references)
/*/
//+==================================================================+
Static Function fAjustPerg(_cPerg)
	
	Local _aTxt		:= {}
	Local _aHelp	:= {}

	aADD(_aTxt, "Informe neste campo o Código da Oportunidade Inicial e Final para seleção dos registros. Campo em Branco será ignorado e não participa do filtro de seleção dos registros.")
	aADD(_aTxt, "Informe neste campo o Código da Revisão da Oportunidade Inicial e Final para seleção dos registros. Campo em Branco será ignorado e não participa do filtro de seleção dos registros.")
	aADD(_aTxt, "Informe neste campo o Código da Proposta Comercial Inicial e Final para seleção dos registros. Campo em Branco será ignorado e não participa do filtro de seleção dos registros.")
	aADD(_aTxt, "Informe neste campo o Código da Revisão da Proposta Comercial Inicial e Final para seleção dos registros. Campo em Branco será ignorado e não participa do filtro de seleção dos registros.")
	aADD(_aTxt, "Informe neste campo o Código do Cliente Inicial e Final para seleção dos registros. Campo em Branco será ignorado e não participa do filtro de seleção dos registros.")
	aADD(_aTxt, "Informe neste campo o Código da Loja do Cliente Inicial e Final para seleção dos registros. Campo em Branco será ignorado e não participa do filtro de seleção dos registros.")
	aADD(_aTxt, "Informe neste campo o Código do Vendedor na Oportunidade Inicial e Final para seleção dos registros. Campo em Branco será ignorado e não participa do filtro de seleção dos registros.")
	aADD(_aTxt, "Informe neste campo o Código do Vendedor na Proposta Comercial Inicial e Final para seleção dos registros. Campo em Branco será ignorado e não participa do filtro de seleção dos registros.")
	aADD(_aTxt, "Informe neste campo o Código do Contato Inicial e Final para seleção dos registros. Campo em Branco será ignorado e não participa do filtro de seleção dos registros.")
	aADD(_aTxt, "Informe neste campo a Data da Assinatura Inicial para seleção dos registros. Campo em Branco será ignorado e não participa do filtro de seleção dos registros.")
	aADD(_aTxt, "Informe neste campo a Data da Assinatura Final para seleção dos registros. Campo em Branco será ignorado e não participa do filtro de seleção dos registros.")
	aADD(_aTxt, "Informe neste campo a Data de Inclusão da Oportunidade Inicial para seleção dos registros. Campo em Branco será ignorado e não participa do filtro de seleção dos registros.")
	aADD(_aTxt, "Informe neste campo a Data da Inclusão da Oportunidade Final para seleção dos registros. Campo em Branco será ignorado e não participa do filtro de seleção dos registros.")
	aADD(_aTxt, "Informe neste campo a Data de Inicio da Oportunidade Inicial para seleção dos registros. Campo em Branco será ignorado e não participa do filtro de seleção dos registros.")
	aADD(_aTxt, "Informe neste campo a Data de Inicio da Oportunidade Final para seleção dos registros. Campo em Branco será ignorado e não participa do filtro de seleção dos registros.")
	aADD(_aTxt, "Informe neste campo a Data de Término da Oportunidade Inicial para seleção dos registros. Campo em Branco será ignorado e não participa do filtro de seleção dos registros.")
	aADD(_aTxt, "Informe neste campo a Data de Término da Oportunidade Final para seleção dos registros. Campo em Branco será ignorado e não participa do filtro de seleção dos registros.")
	aADD(_aTxt, "Informe neste campo o Código do Produto Inicial e Final para seleção dos registros. Campo em Branco será ignorado e não participa do filtro de seleção dos registros.")
	aADD(_aTxt, "Informe neste campo o Código da Turma Inicial e Final para seleção dos registros. Campo em Branco será ignorado e não participa do filtro de seleção dos registros.")
	aADD(_aTxt, "Informe neste campo se deseja que os registro já venham marcados na tela ou não.")

	For _nT := 1 To Len(_aTxt)
		_nQtdLin	:= MlCount(_aTxt[_nT], 40)
		_aAux		:= {}
		For _nL := 1 To _nQtdLin
			aADD(_aAux, MemoLine(_aTxt[_nT], 40, _nL))
		Next
		aADD(_aHelp, {_aAux, _aAux, _aAux})
	Next _nT

	u_SFPUTSX1(_cPerg,"01","Oportunidade                  "	,	"Oportunidade                  ",	"Oportunidade                  ", "mv_ch1","C",06,00,00,"R","","AD1"	,"","","MV_PAR01","","","","AD1_NROPOR","","","","","","","","","","","","",_aHelp[01,1],_aHelp[01,2],_aHelp[01,3],"")
	u_SFPUTSX1(_cPerg,"02","Revisao Oportunidade          "	,	"Revisao Oportunidade          ",	"Revisao Oportunidade          ", "mv_ch2","C",02,00,00,"R","","   "	,"","","MV_PAR02","","","","AD1_REVISA","","","","","","","","","","","","",_aHelp[02,1],_aHelp[02,2],_aHelp[02,3],"")
	u_SFPUTSX1(_cPerg,"03","Proposta Comercial            "	,	"Proposta Comercial            ",	"Proposta Comercial            ", "mv_ch3","C",06,00,00,"R","","ADY"	,"","","MV_PAR03","","","","ADY_PROPOS","","","","","","","","","","","","",_aHelp[03,1],_aHelp[03,2],_aHelp[03,3],"")
	u_SFPUTSX1(_cPerg,"04","Revisao Proposta Comercial    "	,	"Revisao Proposta Comercial    ",	"Revisao Proposta Comercial    ", "mv_ch4","C",02,00,00,"R","","   "	,"","","MV_PAR04","","","","ADY_PREVIS","","","","","","","","","","","","",_aHelp[04,1],_aHelp[04,2],_aHelp[04,3],"")
	u_SFPUTSX1(_cPerg,"05","Código do Cliente             "	,	"Código do Cliente             ",	"Código do Cliente             ", "mv_ch5","C",09,00,00,"R","","SA1"	,"","","MV_PAR05","","","","AD1_CODCLI","","","","","","","","","","","","",_aHelp[05,1],_aHelp[05,2],_aHelp[05,3],"")
	u_SFPUTSX1(_cPerg,"06","Loja do Cliente               "	,	"Loja do Cliente               ",	"Loja do Cliente               ", "mv_ch6","C",04,00,00,"R","",""		,"","","MV_PAR06","","","","AD1_LOJCLI","","","","","","","","","","","","",_aHelp[06,1],_aHelp[06,2],_aHelp[06,3],"")
	u_SFPUTSX1(_cPerg,"07","Vendedor Oportunidade         "	,	"Vendedor Oportunidade         ",	"Vendedor Oportunidade         ", "mv_ch7","C",06,00,00,"R","","SA3"	,"","","MV_PAR07","","","","AD1_VEND  ","","","","","","","","","","","","",_aHelp[07,1],_aHelp[07,2],_aHelp[07,3],"")
	u_SFPUTSX1(_cPerg,"08","Vendedor Proposta Comercial   "	,	"Vendedor Proposta Comercial   ",	"Vendedor Proposta Comercial   ", "mv_ch8","C",06,00,00,"R","","SA3"	,"","","MV_PAR08","","","","ADY_VEND  ","","","","","","","","","","","","",_aHelp[08,1],_aHelp[08,2],_aHelp[08,3],"")
	u_SFPUTSX1(_cPerg,"09","Contato Assinatura Oportunidad"	,	"Contato Assinatura Oportunidad",	"Contato Assinatura Oportunidad", "mv_ch9","C",06,00,00,"R","","SU5"	,"","","MV_PAR09","","","","AD1_CNTPRO","","","","","","","","","","","","",_aHelp[09,1],_aHelp[09,2],_aHelp[09,3],"")
	u_SFPUTSX1(_cPerg,"10","Data Assinatura De ?          "	,	"Data Assinatura De ?          ",	"Data Assinatura De ?          ", "mv_chA","D",08,00,00,"G","",""      ,"","","MV_PAR10","","","","          ","","","","","","","","","","","","",_aHelp[10,1],_aHelp[10,2],_aHelp[10,3],"")
	u_SFPUTSX1(_cPerg,"11","Data Assinatura Ate ?         "	,	"Data Assinatura Ate ?         ",	"Data Assinatura Ate ?         ", "mv_chB","D",08,00,00,"G","",""      ,"","","MV_PAR11","","","","          ","","","","","","","","","","","","",_aHelp[11,1],_aHelp[11,2],_aHelp[11,3],"")
	u_SFPUTSX1(_cPerg,"12","Data Inclusao Oportunidade De "	,	"Data Inclusao Oportunidade De ",	"Data Inclusao Oportunidade De ", "mv_chC","D",08,00,00,"G","",""      ,"","","MV_PAR12","","","","          ","","","","","","","","","","","","",_aHelp[12,1],_aHelp[12,2],_aHelp[12,3],"")
	u_SFPUTSX1(_cPerg,"13","Data Inclusao Oportunidade Ate"	,	"Data Inclusao Oportunidade Ate",	"Data Inclusao Oportunidade Ate", "mv_chD","D",08,00,00,"G","",""      ,"","","MV_PAR13","","","","          ","","","","","","","","","","","","",_aHelp[13,1],_aHelp[13,2],_aHelp[13,3],"")
	u_SFPUTSX1(_cPerg,"14","Data Inicio Oportunidade De   "	,	"Data Inicio Oportunidade De   ",	"Data Inicio Oportunidade De   ", "mv_chE","D",08,00,00,"G","",""      ,"","","MV_PAR14","","","","          ","","","","","","","","","","","","",_aHelp[14,1],_aHelp[14,2],_aHelp[14,3],"")
	u_SFPUTSX1(_cPerg,"15","Data Inicio Oportunidade Ate  "	,	"Data Inicio Oportunidade Ate  ",	"Data Inicio Oportunidade Ate  ", "mv_chF","D",08,00,00,"G","",""      ,"","","MV_PAR15","","","","          ","","","","","","","","","","","","",_aHelp[15,1],_aHelp[15,2],_aHelp[15,3],"")
	u_SFPUTSX1(_cPerg,"16","Data Termino Oportunidade De  "	,	"Data Termino Oportunidade De  ",	"Data Termino Oportunidade De  ", "mv_chG","D",08,00,00,"G","",""      ,"","","MV_PAR16","","","","          ","","","","","","","","","","","","",_aHelp[16,1],_aHelp[16,2],_aHelp[16,3],"")
	u_SFPUTSX1(_cPerg,"17","Data Termino Oportunidade Ate "	,	"Data Termino Oportunidade Ate ",	"Data Termino Oportunidade Ate ", "mv_chH","D",08,00,00,"G","",""      ,"","","MV_PAR17","","","","          ","","","","","","","","","","","","",_aHelp[17,1],_aHelp[17,2],_aHelp[17,3],"")
	u_SFPUTSX1(_cPerg,"18","Produto da Proposta Comercial "	,	"Produto da Proposta Comercial ",	"Produto da Proposta Comercial ", "mv_chI","C",15,00,00,"R","","SB1VEN"	,"","","MV_PAR18","","","","ADZ_PRODUT","","","","","","","","","","","","",_aHelp[18,1],_aHelp[18,2],_aHelp[18,3],"")
	u_SFPUTSX1(_cPerg,"19","Turma da Proposta Comercial   "	,	"Turma da Proposta Comercial   ",	"Turma da Proposta Comercial   ", "mv_chJ","C",06,00,00,"R","",""      ,"","","MV_PAR19","","","","ADZ_XTURMA","","","","","","","","","","","","",_aHelp[19,1],_aHelp[19,2],_aHelp[19,3],"")
	u_SFPUTSX1(_cPerg,"20","Trazer os Itens Marcados ?    "	,	"Trazer os Itens Marcados ?    ",	"Trazer os Itens Marcados ?    ", "mv_chK","N",01,00,00,"C","",""      ,"","","MV_PAR20","Sim","Sim","Sim","","Não","Não","Não","","","","","","","","","",_aHelp[20,1],_aHelp[20,2],_aHelp[20,3],"")

Return