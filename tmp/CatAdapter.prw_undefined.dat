#include 'totvs.ch'
#include 'parmtype.ch'

/*/{Protheus.doc} PrdAdapter
Classe Adapter para o serviço
@author  Sergio Salustiano
@since   01/09/2020
@version 1.0
/*/

CLASS CatAdapter FROM FWAdapterBaseV2
	METHOD New()
	METHOD GetListCat()
EndClass
//-------------------------------------------------------------------
/*/{Protheus.doc} New
Método construtor
@param cVerb, verbo HTTP utilizado
@author  Sergio Salustiano
@since   01/09/2020
@version 1.0
/*/
//-------------------------------------------------------------------
Method New( cVerb ) CLASS CatAdapter
	_Super:New( cVerb, .T. )
return
//-------------------------------------------------------------------
/*/{Protheus.doc} GetListProd
Método que retorna uma lista de produtos 
@author  Sergio Salustiano
@since   01/09/2020
@version 1.0
/*/
//-------------------------------------------------------------------
Method GetListCat( ) CLASS CatAdapter
	Local aArea 	AS ARRAY
	Local cWhere	AS CHAR
	aArea   := FwGetArea()
	AddMapFields( self )
	::SetQuery( GetQuery() )
	cWhere := " ZDJ_FILIAL = '"+ FWxFilial('ZDJ') +"' AND ZDJ.D_E_L_E_T_ = ' '"
	::SetWhere( cWhere )
	::SetOrder( "ZDJ_CODIGO" )
	//Executa a consulta, se retornar .T. tudo ocorreu conforme esperado
	If ::Execute() 
		// Gera o arquivo Json com o retorno da Query
		::FillGetResponse()
	EndIf
	FwrestArea(aArea)
RETURN
//-------------------------------------------------------------------
/*/{Protheus.doc} AddMapFields
Função para geração do mapa de campos
@param oSelf, object, Objeto da prórpia classe
@author  Sergio Salustiano
@since   01/09/2020
@version 1.0
/*/
//-------------------------------------------------------------------
Static Function AddMapFields( oSelf )
	
	oSelf:AddMapFields( 'CODE'                , 'ZDJ_CODIGO'  , .T., .T., { 'ZDJ_CODIGO', 'C', TamSX3( 'ZDJ_CODIGO' )[1], 0 } )
	oSelf:AddMapFields( 'DESCRIPTION'	    , 'ZDJ_CATEG'     , .T., .F., { 'ZDJ_CATEG', 'C', TamSX3( 'ZDJ_CATEG' )[1], 0 } )	
	oSelf:AddMapFields( 'ATIVO'	            , 'ZDJ_ATIVO'     , .T., .F., { 'ZDJ_ATIVO', 'C', TamSX3( 'ZDJ_ATIVO' )[1], 0 } )
	oSelf:AddMapFields( 'LINHA'	            , 'ZDJ_LINHA'     , .T., .F., { 'ZDJ_LINHA', 'C', TamSX3( 'ZDJ_LINHA' )[1], 0 } )
	oSelf:AddMapFields( 'DESLINHA'	        , 'ZDJ_DESLIN'     , .T., .F., { 'ZDJ_DESLIN', 'C', TamSX3( 'ZDJ_DESLIN' )[1], 0 } )
Return 
//------------------------------------------------------------------
/*/{Protheus.doc} GetQuery
Retorna a query usada no serviço
@param oSelf, object, Objeto da prórpia classe
@author  Sergio Salustiano
@since   01/09/2020
@version 1.0
/*/
//-------------------------------------------------------------------
Static Function GetQuery()
	Local cQuery AS CHARACTER
	
	cQuery := " SELECT #QueryFields#"
    cQuery +=   " FROM " + RetSqlName( 'ZDJ' ) + " ZDJ "
    cQuery += " WHERE #QueryWhere#"
	
Return cQuery