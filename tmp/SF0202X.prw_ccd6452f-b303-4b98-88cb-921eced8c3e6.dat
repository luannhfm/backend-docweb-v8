#INCLUDE 'RWMAKE.CH'
#INCLUDE "TOTVS.CH"
#INCLUDE 'PROTHEUS.CH'
#INCLUDE 'TOPCONN.CH'
#INCLUDE "TBICONN.CH"
#INCLUDE "TBICODE.CH"
#INCLUDE "REPORT.CH"

/* 
-------------------------------------------------------------------------------
	{Protheus.doc}<SF0202X>
	
	 PRODUTOS FRACASSADOS 
	
	@Parametros<Nil> 
	@author<Walmir Junior> 
	@since<02/02/2016> 
	@version
		< 
		--------------------------------------------------------------------
		 Walmir Junior                                          
		 PRODUTOS EM ATA DE REGISTRO DE PREÇOS VIGENTE.
		--------------------------------------------------------------------
		>
	@receive<Nil>
	@return<Nil>
	@example<Nil> 
	@see<Nil> 
-------------------------------------------------------------------------------
*/

User Function SF0202X()

Local _cPerg 		:= "SF0202X"
Local _cTitulo		:= "Lista de Produtos em Ata de Registro de Preços Vigente" 		// Titulo
Private _oReport	:= Nil 
Private _oSection1	:= Nil 
Private _aForn		:= {}
Private _cAlias		:= GetNextAlias()

CriaSX1(_cPerg)

//--
_oReport := TReport():New("SF0202X",_cTitulo,_cPerg,{|_oReport| PrintReport(_oReport)} )

_oReport:SetLandscape()	// Orientado como retrato.
_oReport:SetEdit(.T.)
_oReport:SetTotalInLine(.F.)

//--Sessão Mapa
_oSection1 := TRSection():New(_oReport,"Produtos em Ata de Registro de Preços", {_cAlias})
//_oSection1:lHeaderSection := .F.
_oSection1:nLinesBefore := 0
_oSection1:SetTotalInLine(.T.)
_oSection1:SetReadOnly()
//_oSection1:lHeaderVisible := .F.

//TRCell():New(_oSection1,"CN9_FILIAL"	, _cALias ,"FILIAL" 		, "@!" 	,009)
TRCell():New(_oSection1,"CN9_NUMERO"	, _cALias ,"CONTRATO" 		, "@!"	,100)
TRCell():New(_oSection1,"CNA_NUMERO"  	, _cALias ,"PLANILHA"		,		,50)
TRCell():New(_oSection1,"CNA_XDESC"  	, _cALias ,"DESC PLAN"		,		,50)
//TRCell():New(_oSection1,"CNA_REVISA"  , _cALias ,"REVISAO"		,		,120)
//TRCell():New(_oSection1,"CNA_FORNEC"	, _cALias ,"COD FOR" 		, "@!"	,50	)
TRCell():New(_oSection1,"A2_NOME"  		, _cALias ,"FORNECEDOR"		,		,120)
TRCell():New(_oSection1,"CNB_PRODUT"  	, _cALias ,"COD PRD"		,		,100)
TRCell():New(_oSection1,"CNB_DESCRI"  	, _cALias ,"PRODUTO"		,		,120)
TRCell():New(_oSection1,"CNB_UM"  		, _cALias ,"UN MEDIDA"		,		,50	)
TRCell():New(_oSection1,"CNB_QUANT"  	, _cALias ,"QUANT"			, "@E 9,999,999.99"	,120)
TRCell():New(_oSection1,"CNB_SLDMED"  	, _cALias ,"SALDO"			, "@E 9,999,999.99"	,120)
TRCell():New(_oSection1,"CNB_VLUNIT"  	, _cALias ,"VLR UNIT"		,		,120)
TRCell():New(_oSection1,"CNB_VLTOT"  	, _cALias ,"VLR TOT"		,		,120)
TRCell():New(_oSection1,"SLDVLRTOT"  	, _cALias ,"SALD VLR TOT"	, "@E 9,999,999.99" ,120,,,,,"RIGHT")



_oSection1:AutoSize()
_oReport:PrintDialog()

Return Nil

/*
+----------+-----------+-------+-------------------+------+-------------+
|Programa  |PRINTREPORT|Autor  |Andre Vicente      | Data |03/11/2013   |
+----------+-----------+-------+-------------------+------+-------------+
|Descrição | Implementa a Rotina  PrintReport                           |
|          |                                                            |
+----------+------------------------------------------------------------+
|Uso       | AP                                                         |
+----------+------------------------------------------------------------+
*/
Static Function PrintReport(_oReport)

MsgRun( "Selecionando registros...", "Aguarde" ,  {|| GeraQuery() } )

_oReport:Section(1):Print()

Return

//ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
// funcao que realiza o sql do relatorio
//ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
Static Function GeraQuery()
Local _cSql := ""
Local _cFilial := IIF(EMPTY(MV_PAR01), xFilial("CN9"), StrTran( MV_PAR01, ";", "','"))
Local _cNumCN9 := IIF(EMPTY(MV_PAR02), ""			 , StrTran( MV_PAR02, ";", "','"))
//Local _cRevCN9 := IIF(EMPTY(MV_PAR03), "", MV_PAR03)

_cSql += " SELECT "+CRLF
_cSql += " 	CN9_FILIAL,"+CRLF
_cSql += " 	CN9_NUMERO,"+CRLF
_cSql += " 	CNA_FORNEC,"+CRLF
_cSql += " 	CNA_NUMERO,"+CRLF
_cSql += " 	CNA_XDESC,"+CRLF
_cSql += " 	CNA_REVISA,"+CRLF
_cSql += " 	A2_NOME,"+CRLF
_cSql += " 	CNB_ITEM,"+CRLF
_cSql += " 	CNB_PRODUT,"+CRLF
_cSql += " 	CNB_DESCRI,"+CRLF
_cSql += " 	CNB_UM,"+CRLF
_cSql += " 	CNB_QUANT,"+CRLF
_cSql += " 	CNB_SLDMED,"+CRLF
_cSql += " 	CNB_VLUNIT,	"+CRLF
_cSql += " 	CNB_VLTOT,	"+CRLF
_cSql += " 	(CNB_VLTOT - (CNB_VLUNIT * (CNB_QUANT - CNB_SLDMED))) As SLDVLRTOT	"+CRLF
_cSql += " FROM "+CRLF
_cSql += " 	"+RetSqlName("CN9")+" CN9 INNER JOIN "+CRLF
_cSql += " 	"+RetSqlName("CNA")+" CNA ON CNA.CNA_FILIAL = CN9.CN9_FILIAL AND CNA.CNA_CONTRA = CN9.CN9_NUMERO AND CNA.CNA_REVISA = CN9.CN9_REVISA AND CNA.D_E_L_E_T_ = ' ' INNER JOIN "+CRLF
_cSql += " 	"+RetSqlName("CNB")+" CNB ON CNB.CNB_FILIAL = CNA.CNA_FILIAL AND CNB.CNB_CONTRA = CNA.CNA_CONTRA AND CNB.CNB_REVISA = CNA.CNA_REVISA AND CNB.CNB_NUMERO = CNA.CNA_NUMERO AND CNB.D_E_L_E_T_ = ' ' INNER JOIN "+CRLF
_cSql += " 	"+RetSqlName("SA2")+" SA2 ON SA2.A2_COD = CNA.CNA_FORNEC AND SA2.A2_LOJA = CNA.CNA_LJFORN AND SA2.D_E_L_E_T_ = ' ' "
_cSql += " WHERE "+CRLF
_cSql += " 	CN9.D_E_L_E_T_	= ' ' AND"+CRLF
_cSql += " 	CN9.CN9_SITUAC	= '05' AND"+CRLF
_cSql += " 	CN9.CN9_XREGP	= '1' AND"+CRLF
_cSql += " 	CN9.CN9_FILIAL in ('"+_cFilial+"') "+CRLF
If !Empty(_cNumCN9)
	_cSql += " AND	CN9.CN9_NUMERO in ('"+_cNumCN9+"') "+CRLF
EndIf
_cSql += " 	Order By CN9_NUMERO,CNA_FORNEC ,CNB_ITEM "

MemoWrite("C:\Temp\SF0202X.txt" , _cSql) // escreve em arquivo de texto e se nao encontrar cria o arquivo

If Select(_cALias) > 0
	(_cALias)->(dbClosearea())
Endif

DbUseArea(.T., "TOPCONN", TCGenQry(,,ChangeQuery(_cSql)), _cALias, .F., .F.)

Return


/*
+----------+----------+-------+--------------------+------+-------------+
|Programa  |CRIASX1   |Autor  |Andre Vicente       | Data |03/11/2013   |
+----------+----------+-------+--------------------+------+-------------+
|Descrição | Cria o Grupo de perguntas para relatorio                   |
|          |                                                            |
+----------+------------------------------------------------------------+
|Uso       | AP                                                         |
+----------+------------------------------------------------------------+
*/
Static Function CriaSX1(_cPerg)
Local aHelp := {}
// Texto do help em portugues , ingles, espanhol
AAdd(aHelp, {{"Informe a(s) Filial(is)" }, {""}, {""}})
AAdd(aHelp, {{"Informe o(s) Contrato(s)"     }, {""}, {""}})
//--
u_SFPUTSX1(_cPerg,"01","Filial?"	,"","","mv_ch1"	,"C",02,00,00,"R","","SM0"		,"","","mv_par01","","","","CN9_FILIAL"	,"","","","","","","","","","","","",aHelp[1,1]	,aHelp[1,2]	,aHelp[1,3]	,"")
u_SFPUTSX1(_cPerg,"02","Contrato?"	,"","","mv_ch2" ,"C",15,00,00,"R","","CN9001"   ,"","","mv_Par02","","","",""			,"","","","","","","","","","","","",aHelp[2,1] ,aHelp[2,2] ,aHelp[2,3] ,"")
//u_SFPUTSX1(_cPerg,"03","Revisão?"	,"","","mv_ch3" ,"C",08,00,00,"G","",""   		,"","","mv_Par03","","","",""			,"","","","","","","","","","","","",aHelp[2,1] ,aHelp[2,2] ,aHelp[2,3] ,"")

Return