#INCLUDE 'PROTHEUS.CH'
#INCLUDE 'FWMVCDEF.CH'

/*/{Protheus.doc} SFSGT01J
Cadastro de Linha de Produto - SGT SENAI

@type 		function
@author 	Jose Leite de Barros Neto
@since 	18/11/2015
@version 	1.0
/*/
User Function SFSGT01J()
	
	Local oBrowse
	
	oBrowse := FWMBrowse():New()
	oBrowse:SetAlias('ZWA')
	oBrowse:SetDescription ('Cadastro de Linha de Produtos')
	oBrowse:Activate()

Return NIL

//-------------------------------------------------------------------
Static Function MenuDef()
	
	Local aRotina := {}

	ADD OPTION aRotina TITLE 'Pesquisar'  ACTION 'PesqBrw'           OPERATION 1 ACCESS 0
	ADD OPTION aRotina TITLE 'Visualizar' ACTION 'VIEWDEF.SFSGT01J'  OPERATION 2 ACCESS 0
	ADD OPTION aRotina TITLE 'Incluir'    ACTION 'VIEWDEF.SFSGT01J'  OPERATION 3 ACCESS 0
	ADD OPTION aRotina TITLE 'Alterar'    ACTION 'VIEWDEF.SFSGT01J'  OPERATION 4 ACCESS 0
	ADD OPTION aRotina TITLE 'Excluir'    ACTION 'VIEWDEF.SFSGT01J'  OPERATION 5 ACCESS 0
	ADD OPTION aRotina TITLE 'Imprimir'   ACTION 'VIEWDEF.SFSGT01J'  OPERATION 8 ACCESS 0
	ADD OPTION aRotina TITLE 'Copiar'     ACTION 'VIEWDEF.SFSGT01J'  OPERATION 9 ACCESS 0
	
Return aRotina

//-------------------------------------------------------------------
Static Function ModelDef()
	
	// Cria a estrutura a ser usada no Modelo de Dados
	Local oStruZWA := FWFormStruct(1,'ZWA', /*bAvalCampo*/,/*lViewUsado*/ )
	Local oModel

	// Cria o objeto do Modelo de Dados
	oModel := MPFormModel():New('SFSGT01M',/*bPreValidacao*/, /*bPosValidacao*/, /*bCommit*/, /*bCancel*/ )

	//Adiciona ao Modelo uma estrutura de formulario de edição por campo
	oModel:AddFields('ZWAMASTER',/*cOwner*/, oStruZWA, /*bPreValidacao*/, /*bPosValidacao*/, /*bCarga*/ )

	//Adiciona a Descrição do Modelo de Dados
	oModel:SetDescription('Cadastro de Linha de Produtos' )

	oModel:GetModel('ZWAMASTER'):SetDescription('Dados do Cadastro de Linha de Produtos')
	
	oModel:SetPrimaryKey( { "ZWA_FILIAL", "ZWA_CODIGO" } )

Return oModel

//-------------------------------------------------------------------
Static Function ViewDef()

	//Cria um objeto de Modelo de Dados baseado no ModelDef
	Local oModel := FWLoadModel('SFSGT01J')

	//Cria a estrutura a ser usada na View
	Local oStruZWA := FWFormStruct(2,'ZWA')
	Local oView
	Local cCampos := {}

	//Cria o Objeto da View
	oView := FWFormView():New()

	//Define qual o Modelo de dados será utilizado
	oView:SetModel( oModel )

	//Adiciona no View um controle do tipo FormField (Antiga Enchoice)
	oView:addField( 'VIEW_ZWA',oStruZWA, 'ZWAMASTER' )

	//Cria um BOX horizontal para receber elementos da View
	oView:CreateHorizontalBox('TELA' , 100 )

	//Relaciona o ID da View com o Box para exibição
	oView:SetOwnerView( 'VIEW_ZWA' , 'TELA' )

Return oView