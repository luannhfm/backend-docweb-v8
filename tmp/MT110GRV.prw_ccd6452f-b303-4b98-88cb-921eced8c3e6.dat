/*
 *
 * Autor..........: Peder Munksgaard (Do.it Sistemas)
 *
 * Data...........: 09/10/2014
 *
 * Descrição......: Ponto de entrada chamado ao fim da gravação de um solicitação de compras
 *                  e utilizado para manipular o campo C1_CONTA e C1_PREFIX devido a restrições
 *                  do projeto ELO.
 *
 * Função.........: u_MT110GRV()
 *
 * Partida........: A110Grava()
 *
*/

#Include "Protheus.ch"
#Include "Topconn.ch"

User Function MT110GRV()

   Local _aArea  := SC1->( GetArea() )
   Local _lAtu   := GetNewPar("MV_XXSC1CT",.T.)

   //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
   //³Gravacao da justificativa de compra da SC ³
   //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
   U_SICOMA30()

   //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
   //³Gravacao do campo C1_XCODCOMP para C1_CODCOMP  ³
   //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
   U_SICOMA16()   
   
   Reclock("SC1",.F.)
   
   Replace C1_CONTA  With C1_XXCONTA
   Replace C1_PREFIX With Left(C1_XXCONTA,TamSX3("CT1_PREFIX")[1])
   
   SC1->(MsUnlock())
   
   If _lAtu
   
      Processa({||_fProcSC1()},'Atualizando campo C1_XXCONTA...')
      
   Endif
   
   RestArea(_aArea)
   
Return NiL

/*
 *
 * Autor..........: Peder Munksgaard (Do.it Sistemas)
 *
 * Data...........: 28/11/2014
 *
 * Descrição......: Função auxiliar para preencher o campo C1_XXCONTA conforme necessidade
 *
 * Função.........: _fProcSC1()
 *
 * Partida........: u_MT110GRV()
 *
*/

Static Function _fProcSC1()

   Local _cQry := ""
   
   Begin Transaction
      
      ProcRegua(1000)
      Incproc()
      
      dbSelectArea("SX6")
      SX6->(dbSetOrder(1))
      
      If SX6->(!dbSeek(Space(TamSX3("C1_FILIAL")[1])+"MV_XXSC1CT",.T.))
      
         Reclock("SX6",.T.)
         Replace X6_FIL     With Space(TamSX3("C1_FILIAL")[1])
         Replace X6_VAR     With 'MV_XXSC1CT'
         Replace X6_TIPO    With 'L'
         Replace X6_DESCRIC With 'Atualiza campo C1_XXCONTA?'
         Replace X6_CONTEUD With '.F.'
         Replace X6_PROPRI  With 'U'
         SX6->(Msunlock())
         
      Else
      
        Reclock("SX6",.F.)
        Replace X6_CONTEUD With '.F.'
        SX6->(Msunlock())
        
      Endif
      
      _cQry := "UPDATE " + RetSqlName("SC1") + " SET C1_XXCONTA = C1_CONTA WHERE D_E_L_E_T_ <> '*' AND C1_XXCONTA <> ' '"
      
      TcSqlExec(_cQry)
      TcSqlExec("COMMIT;")
      TcRefresh(RetSqlName("SC1"))
         
   End Transaction
   
Return NiL