#Include 'Protheus.ch'
#Include 'FWMVCDef.ch'

/*/{Protheus.doc} FATA600MDL
@descritpion A finalidade do ponto de entrada FATA600MDL é possibilitar a customização do modelo (Model) da Proposta Comercial.
@obs Utilizado para adicionar um totalizador de descontos a proposta e descontinuar o ponto de entrada FT600TOT. 
@obs Fonte FATA600
@obs É executado antes do final da função ModelDef da Proposta Comercial.
@type  Function
@author Alan Teles de Oliveira
@since 23/11/2018
@version 12.1.17
@param ParamIXB[1], objeto, modelo de dados da proposta comercial
@see http://tdn.totvs.com/pages/releaseview.action?pageId=311635054
/*/
User Function FATA600MDL()

    Local oModel    := ParamIXB[1]

    oModel:AddCalc('CALC',; 
        'ADYMASTER',; 
        'ADZPRODUTO',; 
        'ADZ_VALDES',; 
        'ADZ__TOTDES',; 
        'FORMULA',; 
        {|| .T.},; 
        /*bInitValue*/,; 
        'Total Geral de Desconto',;
        {|oMdlCalc, nTotal, xValor, lSum| fCalc(oMdlCalc, nTotal, xValor, lSum)})

Return 


/*/{Protheus.doc} fCalc
@description Função utilizada no bloco bFormula.
@author alan.oliveira
@since 23/11/2018
@version 12.1.17
@return nRet, o valor total de descontos exercidos
@param p_oModel, object, o objeto do model (FwFormModel)
@param p_nTotal, numeric, o valor da atual do campo calculado
@param p_xValor, qualquer, o conteúdo do campo do submodelo Grid
@param lSum, logical, campo logico indicando se é uma execução de soma (.T.) ou subtração (.F.)
@type static function
/*/
Static Function fCalc(p_oModel, p_nTotal, p_xValor, p_lSum)

	Local oModelADZ 	:= p_oModel:GetModel('ADZPRODUTO')
	Local nX	 	    := 0
    Local nRet          := 0

    For nX := 1 to oModelADZ:GetQtdLine()
        If .not. oModelADZ:IsDeleted(nX)
            nRet += oModelADZ:GetValue('ADZ_VALDES', nX)
        EndIf
    Next 

Return nRet
