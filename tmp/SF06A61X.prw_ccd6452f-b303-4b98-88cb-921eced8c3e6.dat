#INCLUDE 'PROTHEUS.CH'


/*/{Protheus.doc} SF06A61X

@description  Rotina automática FINA060 - Inclusão de borderô

@author Diego Donatti Moura
@since 21/08/2020
@version 12.1.17

/*/
User Function SF06A61X(p_lExcAuto)

    Local cAlias    := GetNextAlias()
    Local aTitulo   := {}
    Local aBordero  := {}
    Local cBanco    := PadR(GetNewPar('MV_XBANCO'  	, '001') 	,TamSX3('A6_COD')[1])
    Local cAgencia  := PadR(GetNewPar('MV_XAGENC'	, '452')	,TamSX3('A6_AGENCIA')[1])
    Local cConta    := PadR(GetNewPar('MV_XCONTA'  	, '123873')	,TamSX3('A6_NUMCON')[1])
    Local cSituaca  := GetNewPar('MV_XSITUA', '1')
    Local cNumBor   := ""
    Local cQuery    := ""

    Private lMsErroAuto:= .F.

    DeFault p_lExcAuto := .F.

    If p_lExcAuto

        cQuery := "SELECT * FROM "+	RetSqlName("SE1") + " SE1 "
        cQuery += " WHERE D_E_L_E_T_ = ' ' "
        cQuery += "   AND E1_FILIAL = '" + xFilial("SE1") + "' "
        cQuery += "   AND E1_NUMBOR = ' '"
        cQuery += "   AND E1_NUMBCO != ' '"
        cQuery += "   AND E1_EMISSAO <= '" + DTOS(dDataBase) + "'"
        cQuery += "   AND E1_XENVBOL = '1' "
        cQuery += "   AND E1_SALDO  > 0 "
        cQuery += "   AND E1_XTITFUN != 'S' "
        cQuery += " ORDER BY "+ SqlOrder(SE1->(IndexKey()))
        cQuery := ChangeQuery(cQuery)

        If Select(cAlias) > 0
            (cAlias)->(DbCloseArea())
        Else
            DbUseArea(.T., "TOPCONN", TCGenQry(,,cQuery),cAlias, .F., .T.)
        EndIf

        //Adiciona os títulos no array
        While (cAlias)->(!Eof())

            aAdd(aTitulo,   { {"E1_FILIAL"    ,(cAlias)->E1_FILIAL  },;
                              {"E1_PREFIXO"   ,(cAlias)->E1_PREFIXO },;
                              {"E1_NUM"       ,(cAlias)->E1_NUM     },;
                              {"E1_PARCELA"   ,(cAlias)->E1_PARCELA },;
                              {"E1_TIPO"      ,(cAlias)->E1_TIPO    }})

            (cAlias)->(dbSkip())
        EndDo

    Else

        If Empty(SE1->E1_NUMBOR)

            aAdd(aTitulo,   { {"E1_FILIAL"    ,SE1->E1_FILIAL  },;
                              {"E1_PREFIXO"   ,SE1->E1_PREFIXO },;
                              {"E1_NUM"       ,SE1->E1_NUM     },;
                              {"E1_PARCELA"   ,SE1->E1_PARCELA },;
                              {"E1_TIPO"      ,SE1->E1_TIPO    }})
        
        EndIf

    EndIf

    If Len(aTitulo) > 0

        //Informações bacárias para o borderô
        aAdd(aBordero, {"AUTBANCO"   , PadR(cBanco   ,TamSX3("A6_COD")[1])      })
        aAdd(aBordero, {"AUTAGENCIA" , PadR(cAgencia ,TamSX3("A6_AGENCIA")[1])  })
        aAdd(aBordero, {"AUTCONTA"   , PadR(cConta   ,TamSX3("A6_NUMCON")[1])   })
        aAdd(aBordero, {"AUTSITUACA" , PadR(cSituaca ,TamSX3("E1_SITUACA")[1])  })
        aAdd(aBordero, {"AUTNUMBOR"  , PadR(cNumBor  ,TamSX3("E1_NUMBOR")[1])   })
   
        MSExecAuto({|a,b| FINA060(a,b)},3,{aBordero,aTitulo})
        
           //Envio manual
        If !lMsErroAuto .And. FWIsInCallStack("FINA740")

            If Aviso( "Atenção", "Deseja enviar agora o titulo para o banco?", { "Não", "Sim" }, 2 ) == 2

                U_SF06A62X(1,.F.)

            Else
                If RecLock("SEA", .F.)
                    SEA->EA_XSTATUS := "02"
                    SEA->EA_XMENSG  := "Titulo Pendente Envio Automatico"
                    SEA->(MsUnlock())
                EndIf
            EndIf

        Endif

    EndIf

Return

