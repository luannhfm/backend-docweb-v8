#Include 'Protheus.ch'
#Include 'FWMVCDEF.ch'
#Include 'TopConn.ch'

/*/{Protheus.doc} SS3304S
Cadastro de R.D.A.

@author 	Sergio Ricardo Leite Salustiano
@since 		30/05/2014
@version 	1.0

@return Nil, Nulo
/*/
User Function SS3304S()

	Local oBrowse

	oBrowse := FWMBrowse():New()
	oBrowse:SetAlias('ZZG')
	oBrowse:SetDescription('Cadastro de R.D.A.')
	oBrowse:Activate()

Return ( Nil )


Static Function MenuDef()

	Local aRotina := {}

	ADD OPTION aRotina Title 'Visualizar' Action 'VIEWDEF.SS3304S' OPERATION 2 ACCESS 0
	ADD OPTION aRotina Title 'Incluir'    Action 'VIEWDEF.SS3304S' OPERATION 3 ACCESS 0
	ADD OPTION aRotina Title 'Alterar'    Action 'VIEWDEF.SS3304S' OPERATION 4 ACCESS 0
	ADD OPTION aRotina Title 'Excluir'    Action 'VIEWDEF.SS3304S' OPERATION 5 ACCESS 0
	ADD OPTION aRotina Title 'Imprimir'   Action 'VIEWDEF.SS3304S' OPERATION 8 ACCESS 0
	ADD OPTION aRotina Title 'Copiar'     Action 'VIEWDEF.SS3304S' OPERATION 9 ACCESS 0

Return ( aRotina )

Static Function ModelDef()

	Local oStruZZG := FWFormStruct(1, 'ZZG')
	Local oStruZZI := FWFormStruct(1, 'ZZI')
	Local oModel

	oModel:= MPFormModel():New('SS3304SM', /* bPre */, { |oModel| SS3304SPOS(@oModel) })
	oModel:AddFields('ZZGMASTER',/*cOwner*/, oStruZZG)
	oModel:AddGrid('ZZIDETAIL','ZZGMASTER',oStruZZI)
	oModel:SetRelation('ZZIDETAIL',{{'ZZI_FILIAL','xFilial("ZZI")'}, {'ZZI_CODIGO','ZZG_CODIGO'}},ZZI->(IndexKey(1)))
	oModel:SetPrimaryKey({"ZZG_FILIAL","ZZG_CODIGO"})
	oModel:SetDescription('Cadastro de R.D.A.')
	oModel:GetModel('ZZGMASTER'):SetDescription('Dados Gerais do R.D.A.')
	oModel:GetModel('ZZIDETAIL'):SetDescription('Procedimetos do R.D.A.')
	oModel:GetModel( 'ZZIDETAIL' ):SetOptional( .T. )

Return ( oModel )

Static Function ViewDef()

	Local oModel   := FWLoadModel('SS3304S')
	Local oStruZZG := FWFormStruct(2, 'ZZG')
	Local oStruZZI := FWFormStruct(2, 'ZZI')
	Local oView

	oView:=FWFormView():New()
	oView:SetModel(oModel)
	oView:AddField('VIEW_ZZG', oStruZZG, 'ZZGMASTER')
	oView:AddGrid ('VIEW_ZZI', oStruZZI, 'ZZIDETAIL')
	oView:AddIncrementField( 'VIEW_ZZI', 'ZZI_ITEM' )
	oView:CreateHorizontalBox ('SUPERIOR', 60)
	oView:CreateHorizontalBox ('INFERIOR', 40)
	oView:SetOwnerView ('VIEW_ZZG', 'SUPERIOR')
	oView:SetOwnerView ('VIEW_ZZI', 'INFERIOR')
	oView:AddIncrementField('VIEW_ZZI', 'ZZI_SEQ')
	oView:EnableTitleView('VIEW_ZZI', 'DEPENDENTES')


Return ( oView )

Static Function SS3304SPOS(oModel)

Local lReturn	:= .T.
Local cCodigo	:= oModel:GetValue('ZZGMASTER', 'ZZG_CODIGO') 
Local cQuery	:= ""
Local nOper		:= oModel:GetOperation()

	If nOper == MODEL_OPERATION_INSERT
		cQuery := "SELECT MAX(ZZG_CODIGO) AS ZZG_CODIGO FROM " + RetSqlName("ZZG")
		cQuery += " WHERE D_E_L_E_T_ = ' ' " 
		cQuery += "AND ZZG_FILIAL = '" + xFilial("ZZG") + "'"
		
		If Select("TRBZZG") > 0
			TRBZZG->( DbCloseArea() )
		Endif
		
		TCQUERY cQuery NEW ALIAS "TRBZZG"
		
		If !(TRBZZG->( EoF() )) .And. TRBZZG->ZZG_CODIGO >= cCodigo
			Help( , , FunName() + "/" + ProcName(), , "O Cadastro [" + cCodigo + "] jÃ¡ existe. Realize um novo cadastro.", 1, 0)
			lReturn := .F.
		EndIf
	
		TRBZZG->( DbCloseArea() )
	
	EndIf

Return lReturn