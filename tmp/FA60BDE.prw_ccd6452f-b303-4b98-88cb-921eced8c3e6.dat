#INCLUDE 'TOTVS.CH'


/*/{Protheus.doc} FA60BDE
@description  O ponto de entrada FA60BDE será executado apos a 
gravação dos arquivos SE1/ SE5/ SEA na transferência de contas a receber

@author Diego Donatti Moura
@since 31/03/2020
@version 12.1.25
/*/
User Function FA60BDE()

    Local aGetArea := GetArea()
    Local cIdCnab  := ""
    Local cChaveID := ""
    Local aOrdSE1  := {}
    Local nOrdCNAB := 19

    If IsBlind()

        cIdCnab := GetSxENum("SE1", "E1_IDCNAB","E1_IDCNAB"+cEmpAnt,nOrdCNAB)
        cChaveID := cIdCnab

        DbSelectArea("SE1")
        aOrdSE1:= SE1->(GetArea())
        DbSetOrder(nOrdCNAB)

        While SE1->(MsSeek(cChaveID))
            cIdCnab  := GetSxENum("SE1", "E1_IDCNAB","E1_IDCNAB"+cEmpAnt,nOrdCNAB)
            cChaveID := cIdCnab
        EndDo

        SE1->(RestArea(aOrdSE1))
        If Reclock("SE1",.F.)
            SE1->E1_IDCNAB  := cIdCnab
            SE1->E1_OCORREN := "01"
            SE1->E1_XENVBOL := "3"
            MsUnlock()
        EndIf

        If FWIsInCallStack("FINA740")
            If RecLock("SEA",.F.)
                SEA->EA_XSTATUS := "01"
                SEA->EA_XMENSG  := "Titulo Pendente Envio Manual"
                SEA->EA_TRANSF  := "S"
                SEA->EA_XIDCNAB := cIdCnab
                SEA->EA_XIDBB   := SE1->E1_NUMBCO
                SEA->(MsUnlock())
            EndIf
        Else
            If RecLock("SEA",.F.)
                SEA->EA_XSTATUS := "02"
                SEA->EA_XMENSG  := "Titulo Pendente Envio Automatico"
                SEA->EA_TRANSF  := "S"
                SEA->EA_XIDCNAB := cIdCnab
                SEA->EA_XIDBB   := SE1->E1_NUMBCO
                SEA->(MsUnlock())
            EndIf
        EndIf

    EndIf

    RestArea(aGetArea)
Return
