#INCLUDE "PROTHEUS.CH"

/*/{Protheus.doc} SF06R12X
(long_description)
@author j2a.luizjunior
@since 16/05/2017
@version 1.0
@example
(examples)
@see (links_or_references)
/*/

User Function SF06R12X()

	Local oReport := Nil
	Local cPerg   := "SF06R12X"	

	CriaSX1(cPerg)
	If !Pergunte(cPerg,.T.)
		Return(Nil)
	EndIf
	
	oReport := ReportDef(cPerg)
	oReport:PrintDialog()

Return

/*/{Protheus.doc} ReportDef
(long_description)
@author j2a.luizjunior
@since 16/05/2017
@version 1.0
@param cPerg, character, (Descrição do parâmetro)
@return ${return}, ${return_description}
@example
(examples)
@see (links_or_references)
/*/

Static Function ReportDef(cPerg)

	Local oReport		
	Local cNomeRel	:= "SF06R12X"
	
	Local cTitRel	:= "Pagamentos - SESI 21669-0"
	Local cDescRel	:= "Pagamentos - SESI 21669-0"

	oReport         := TReport():New( cNomeRel, cTitRel, cPerg, { |oReport| ReportPrint(oReport) }, cDescRel )
	
	oReport:SetPortrait() 
	oReport:SetTotalInLine(.F.) 
	oReport:Page()

	oViabilidade := TRSection():New(oReport, "Posição de Fornecedor" ,{"SE2"}                                       )
		 
	oNome        := TRCell():New(oViabilidade,"E2_NOMFOR"            ,"SE2","Nome"                                  )
	oNome:SetSize(60)
		
	oNum         := TRCell():New(oViabilidade,"E2_NUM"               ,"SE2","Titulo"                                )	
	oNum:SetSize(45)

	oParcela     := TRCell():New(oViabilidade,"E2_PARCELA"           ,"SE2"                                         )	
	oParcela:SetSize(55)
		
	oTipo        := TRCell():New(oViabilidade,"E2_TIPO"              ,"SE2"                                         )  
	oTipo:SetSize(20)
		
	oValor       := TRCell():New(oViabilidade,"E2_VALOR"             ,"SE2","Vlr.Tit."                              )
	oValor:SetSize(60)
		
	oEmissao     := TRCell():New(oViabilidade,"E2_EMISSAO"           ,"SE2","DT Emis."                              )
	oEmissao:SetSize(60)
		
	oVencRea     := TRCell():New(oViabilidade,"E2_VENCREA"           ,"SE2","Vencto"                                )
	oVencRea:SetSize(60)
		
	oBaixa       := TRCell():New(oViabilidade,"E2_BAIXA"             ,"SE2"                                         )
	oBaixa:SetSize(60)
		
	oDias        := TRCell():New(oViabilidade,"DIAS"                 ,"SE2","D Ant.","@E 99,999"                    )	
	oDias:SetSize(30)
		
	oDescon      := TRCell():New(oViabilidade,"E2_DESCONT"           ,"SE2","Desc."                                 )
	oDescon:SetSize(30)	
		
	oValLiq      := TRCell():New(oViabilidade,"E2_VALLIQ"            ,"SE2"                                         )	
	oValLiq:SetSize(55)
		
	oSaldo       := TRCell():New(oViabilidade,"E2_SALDO"             ,"SE2"                                         )	
	oSaldo:SetSize(55)
		
	oMotBx       := TRCell():New(oViabilidade,"E5_MOTBX"             ,"SE5","MOT BX"                                ) 
	oMotBx:SetSize(45)
		
	oFilOrig     := TRCell():New(oViabilidade,"E2_XFILDES"           ,"SE2","Filial"                                )
	oFilOrig:SetSize(55)
		
	oAnt         := TRCell():New(oViabilidade,"ANTECIPADO"           ,"SE2","Antecip","@E 99,999"                   )
	oAnt:SetSize(55)
		
	oPrazo       := TRCell():New(oViabilidade,"PRAZO"                ,"SE2","Prazo"  ,"@E 99,999"                   )
	oPrazo:SetSize(45)
		
	oAtraso      := TRCell():New(oViabilidade,"ATRASO"               ,"SE2","Atraso" ,"@E 99,999"                   )
	oAtraso:SetSize(45)
		
	oTot := TRSection():New(oReport, "Totalizador"                   ,{"SE2"}                                       )		

	oTDesc := TRCell():New(oTot,"TDESC"                              ,"SE2","Titulo"     ,"@!"                      )
	oTDesc:SetSize(20)
		
	oTVal  := TRCell():New(oTot,"TVAL"                               ,"SE2","Valor"      ,"@E 9,999,999,999,999.99" )
	oTVal:SetSize(20)
		
	oTAnt  := TRCell():New(oTot,"TANTECIP"                           ,"SE2","Antecipado" ,"@!"                      )
	oTAnt:SetSize(20)
		
	oTPraz := TRCell():New(oTot,"TPRAZO"                             ,"SE2","Prazo"      ,"@!"                      )
	oTPraz:SetSize(20)
		
	oTAtra := TRCell():New(oTot,"TATRAZO"                            ,"SE2","Atrazo"     ,"@!"                      )
	oTAtra:SetSize(20)
		
	oPrazo := TRSection():New(oReport, "Prazo medio"                 ,{"SE2"}                                       )

	oPZDesc := TRCell():New(oPrazo,"PZDESC"                          ,"SE2","Titulo"   ,"@!"                        )
	oPZDesc:SetSize(50)

	oPZVal  := TRCell():New(oPrazo,"PZVAL"                           ,"SE2","Valor"    ,"@E 9,999,999,999,999.99"   )
	oPZVal:SetSize(20)

Return(oReport)	

/*/{Protheus.doc} ReportPrint
(long_description)
@author j2a.luizjunior
@since 16/05/2017
@version 1.0
@param oReport, objeto, (Descrição do parâmetro)
@return ${return}, ${return_description}
@example
(examples)
@see (links_or_references)
/*/

Static Function ReportPrint(oReport)

	Local cSql      := ""
	Local cAlias    := GetNextAlias()
	Local oSec1     := oReport:Section(1)	
	Local oSec2     := oReport:Section(2)
	Local oSec3     := oReport:Section(3)	
	Local oFont     := TFont():New('Courier New',,-9,,.F.)
	Local lTodasFil := .F.
	Local cConta    := GetNewPar("MV_XCONTA","")
	Local nValLiq   := 0
	Local nTAnt     := 0
	Local nTPrazo   := 0
	Local nTAtra    := 0
	Local nMedFor   := 0	
	Local nQtdTit   := 0
	Local nValPA    := 0
	Local nAntec    := 0
	Local nPrazo    := 0
	Local nAtraz    := 0			
	Local nTotPag   := 0
	
	//06/06/2017 Walmir Junior - Adição de critério para não trazer Motivo de Baixa "CCO"
	cSql += " AND E5_MOTBX != 'CCO' "	

	If !Empty(MV_PAR01) .And. !Empty(MV_PAR02) 
		cSql += " AND E2_FILIAL BETWEEN '"  + MV_PAR01 + "' AND '" + MV_PAR02 + "'"			
	EndIf

	If !Empty(MV_PAR03) .And. !Empty(MV_PAR04) 
		cSql += " AND E2_FORNECE >= '"  + MV_PAR03 + "' "
		cSql += " AND E2_LOJA    >= '"  + MV_PAR04 + "' "	
	EndIf	

	If !Empty(MV_PAR05) .And. !Empty(MV_PAR06) 
		cSql += " AND E2_FORNECE <= '"  + MV_PAR05 + "' "
		cSql += " AND E2_LOJA    <= '"  + MV_PAR06 + "' "	
	EndIf	

	If !Empty(MV_PAR07) .And. !Empty(MV_PAR08) 
		cSql += " AND E5_DATA BETWEEN '"  + DToS(MV_PAR07) + "' AND '" + DToS(MV_PAR08) + "'"			
	EndIf		

	If !Empty(MV_PAR09) .And. !Empty(MV_PAR10) 
		cSql += " AND E2_VENCTO BETWEEN '"  + DToS(MV_PAR09) + "' AND '" + DToS(MV_PAR10) + "'"			
	EndIf		
	
	If !Empty(MV_PAR11) .And. !Empty(MV_PAR12) .And. !Empty(MV_PAR13)
		cSql += " AND E5_BANCO   = '"+ MV_PAR11 +"' "
		cSql += " AND E5_AGENCIA = '"+ MV_PAR12 +"' "
		cSql += " AND E5_CONTA   = '"+ MV_PAR13 +"' "
	EndIf	

	cSql := "%"+cSql+"%"		
	
	BeginSql Alias cAlias
	
		SELECT E2_NOMFOR,
		       E2_NUM,
		       E2_PREFIXO,
		       E2_TIPO,
		       E2_VALOR,
		       E2_EMISSAO,
		       E2_VENCREA,
		       E2_BAIXA,
		       E2_DESCONT,
		       E2_SALDO,
		       E5_MOTBX,
		       E2_XFILDES,
		       E2_VALLIQ	             
		FROM       %Table:SE2% E2
		INNER JOIN %Table:SE5% E5
		ON E5.%NOTDEL%
		AND E5_FILIAL  = E2_FILIAL
		AND E5_PREFIXO = E2_PREFIXO
		AND E5_NUMERO  = E2_NUM
		AND E5_PARCELA = E2_PARCELA
		AND E5_TIPO    = E2_TIPO
		AND E2_FORNECE = E5_CLIFOR  
		AND E2_LOJA    = E5_LOJA
		AND E2_NATUREZ = E5_NATUREZ		
		AND E5_MOTBX NOT IN ('FAT','LIQ','CMP','CAN')
		%Exp:cSql%	 
		AND E2.%NOTDEL%
		
	EndSql	
	
	While !(cAlias)->(Eof())
	
		nQtdTit++
	
		oSec1:Init()
		
		oSec1:Cell("E2_NOMFOR") :SetValue((cAlias)->E2_NOMFOR)		
		oSec1:Cell("E2_NUM")    :SetValue((cAlias)->E2_NUM)
		oSec1:Cell("E2_TIPO")   :SetValue((cAlias)->E2_TIPO)
		oSec1:Cell("E2_VALOR")  :SetValue((cAlias)->E2_VALOR)
		oSec1:Cell("E2_EMISSAO"):SetValue(DToC(SToD((cAlias)->E2_EMISSAO)))
		oSec1:Cell("E2_VENCREA"):SetValue(DToC(SToD((cAlias)->E2_VENCREA)))
		oSec1:Cell("E2_BAIXA")  :SetValue(DToC(SToD((cAlias)->E2_BAIXA)))		
		oSec1:Cell("DIAS")      :SetValue( SToD((cAlias)->E2_VENCREA) - SToD((cAlias)->E2_EMISSAO))		
		oSec1:Cell("E2_DESCONT"):SetValue((cAlias)->E2_DESCONT)
		oSec1:Cell("E2_VALLIQ") :SetValue((cAlias)->E2_VALLIQ)
		oSec1:Cell("E2_SALDO")  :SetValue((cAlias)->E2_SALDO)
		oSec1:Cell("E5_MOTBX")  :SetValue((cAlias)->E5_MOTBX)
		oSec1:Cell("E2_XFILDES"):SetValue((cAlias)->E2_XFILDES)
			
		If SToD((cAlias)->E2_BAIXA) < SToD((cAlias)->E2_VENCREA)
			nAnt    := 1
			nValAnt := (cAlias)->E2_VALLIQ
		Else
			nAnt    := 0
		EndIf
		
		oSec1:Cell("ANTECIPADO"):SetValue(nAnt)
			
		If SToD((cAlias)->E2_BAIXA) = SToD((cAlias)->E2_VENCREA)
			nPrazo   := 1
			nValPraz := (cAlias)->E2_VALLIQ
		Else
			nPrazo   := 0
		EndIf	
				
		oSec1:Cell("PRAZO"):SetValue(nPrazo)
		
		If SToD((cAlias)->E2_BAIXA) > SToD((cAlias)->E2_VENCREA)
			nAtraso   := 1
			nValAtraz := (cAlias)->E2_VALLIQ
		Else
			nAtraso   := 0
		EndIf	
		
		oSec1:Cell("ATRASO"):SetValue(nAtraso)
			
		oSec1:Printline()
		
		If oReport:Cancel()
			Return(Nil)
		EndIf
		
		If AllTrim((cAlias)->E2_TIPO) == "PA"
			nValPA += (cAlias)->E2_VALLIQ
		EndIf
			
		nValLiq += (cAlias)->E2_VALLIQ
		nTAnt   += nAnt
		nTPrazo += nPrazo
		nTAtra  += nAtraso 
			
		(cAlias)->(DbSkip())
	EndDo
		
	(cAlias)->(DbCloseArea())

	oSec1:Finish()
		
	oReport:SkipLine()
		
	oReport:ThinLine()
		
	oReport:SkipLine()
		
	oReport:Say(oReport:Row(),012,"Total de Indicador de Pagamento"          ,oFont)
	oReport:Box(oReport:Row()-5,010 ,oReport:Row()+33  ,2000 )
	oReport:Box(oReport:Row()-5,010 ,oReport:Row()+200 ,2000 )

	//oReport:SkipLine()
	nRow := oReport:Row() + 222
				
	oReport:Say(nRow,012,"Prazo Médio de Pagamento"                ,oFont)
	oReport:Box(nRow-5,010,nRow + 33  ,1480)
	oReport:Box(nRow-5,010,nRow + 200 ,1480)
		
	//aSelFil := AdmGetFil(@lTodasFil)
	
	If MV_PAR01 == MV_PAR02
		aSelFil := Array(1)
		aSelFil[1] := MV_PAR01
	Else
		aSelFil := Array(2)
		aSelFil[1] := MV_PAR01
		aSelFil[2] := MV_PAR02 
	EndIf
		
	cConta   := "21010201001"
	nSalAnt  := SaldoCT7Fil(cConta,MV_PAR07,"01","1","CTBR400",,,aSelFil,,lTodasFil)
		
	nMedFor  := SaltoAtu(nSalAnt[6])
		
	nValCom  := ValComPer()
		
	//		oReport:SkipLine()
	//		oReport:SkipLine()
		
		//quadro 1
	//		oReport:Say(oReport:Row(),012,"Total - "      + AllTrim(Transfor(nValLiq,PesqPict("SE1","E1_VALOR")))                            ,oFont)
	//		oReport:Say(oReport:Row(),400,"ANTECIP. - NO PRAZO - EM ATRASO"                                                                  ,oFont)
		
		//-> quadro 2
	//		oReport:Say(oReport:Row(),1002,"Valor do Periodo Conta Fornecedor R$ - " + AllTrim(Transfor(nMedFor,PesqPict("SE1","E1_VALOR"))) ,oFont)
	//		oReport:SkipLine()
	//		oReport:SkipLine()
		
		//-> quadro 1
	//		oReport:Say(oReport:Row(),012,"P.A.  - "     + AllTrim(Transfor(nValPA,PesqPict("SE1","E1_VALOR")))                               ,oFont)
	//		oReport:Say(oReport:Row(),400,AllTrim(cValToChar(nTAnt)) + Space(08) + AllTrim(cValToChar(nTPrazo)) + Space(08) + AllTrim(cValToChar(nTAtra))                                                       ,oFont)
		
		//-> quadro 2
	//		oReport:Say(oReport:Row(),1002,"Valor das Compras no Periodo      R$ - " + AllTrim(Transfor(nValCom,PesqPict("SE1","E1_VALOR"))) ,oFont)
	//		oReport:SkipLine()
	//		oReport:SkipLine()
		
	nValVar := ( nMedFor / nValCom ) * 360
		
	//-> quadro 1
	//		oReport:Say(oReport:Row(),012,"QTDE Titu. - " + AllTrim(cValToChar(nQtdTit))                                                     ,oFont)
	nPerAnt := Round((nTAnt   / nQtdTit) * 100,2)
	nPerPra := Round((nTPrazo / nQtdTit) * 100,2)
	nPerAtr := Round((nTAtra  / nQtdTit) * 100,2)
	//	oReport:Say(oReport:Row(),400,AllTrim(cValToChar(nPerAnt))+"%" + Space(05) + AllTrim(cValToChar(nPerPra))+"%" + Space(05) + AllTrim(cValToChar(nPerAtr))+"%",oFont)
		
	//-> quadro 2
	//		oReport:Say(oReport:Row(),1002,"Variação (A) / (B) X 360        Dias - " + cValToChar(round(nValVar,0))                                   ,oFont)
	//		oReport:SkipLine()

	oSec2:Init()

	cDesc := "Total - "
	oSec2:Cell("TDESC")   :SetValue(cDesc)
	oSec2:Cell("TVAL")    :SetValue(nValLiq)
	oSec2:Cell("TANTECIP"):SetValue(AllTrim(cValToChar(nTAnt)))
	oSec2:Cell("TPRAZO")  :SetValue(AllTrim(cValToChar(nTPrazo)))
	oSec2:Cell("TATRAZO") :SetValue(AllTrim(cValToChar(nTAtra)))
	oSec2:Printline()
		
	cDesc := "P.A.  - "
	oSec2:Cell("TDESC")   :SetValue(cDesc)
	oSec2:Cell("TVAL")    :SetValue(nValPA)
	oSec2:Cell("TANTECIP"):SetValue(AllTrim(cValToChar(nPerAnt)+"%"))
	oSec2:Cell("TPRAZO")  :SetValue(AllTrim(cValToChar(nPerPra)+"%"))
	oSec2:Cell("TATRAZO") :SetValue(AllTrim(cValToChar(nPerAtr)+"%"))
	oSec2:Printline()

	cDesc  := "QTDE Titu. - "
	oSec2:Cell("TDESC"):SetValue(cDesc + AllTrim(cValToChar(nQtdTit)))
		
	oSec2:Cell("TVAL")    :SetValue()
	oSec2:Cell("TANTECIP"):SetValue()
	oSec2:Cell("TPRAZO")  :SetValue()
	oSec2:Cell("TATRAZO") :SetValue()		
	oSec2:Printline()
		
	oSec2:Finish()
		
	oReport:SkipLine()
		
	oReport:SkipLine()
		
	oSec3:Init()

	cDesc := "Valor do Periodo Conta Fornecedor R$ - "
	oSec3:Cell("PZDESC"):SetValue(cDesc)
	oSec3:Cell("PZVAL") :SetValue(nMedFor)
	oSec3:Printline()
		
	cDesc := "Valor das Compras no Periodo      R$ - "
	oSec3:Cell("PZDESC"):SetValue(cDesc)
	oSec3:Cell("PZVAL") :SetValue(nValCom)
	oSec3:Printline()
		
	cDesc := "Variação (A) / (B) X 360        Dias - " + cValToChar(round(nValVar,0))
	oSec3:Cell("PZDESC"):SetValue(cDesc)
	oSec3:Cell("PZVAL") :SetValue()
	oSec3:Printline()
		
	oSec3:Finish()

Return

/*/{Protheus.doc} SaltoAtu
(long_description)
@author j2a.luizjunior
@since 16/05/2017
@version 1.0
@param pSalAnt, ${param_type}, (Descrição do parâmetro)
@return ${return}, ${return_description}
@example
(examples)
@see (links_or_references)
/*/

Static Function SaltoAtu(pSalAnt) 

	Local cConta  := '21010201001'
	Local cAlSal  := GetNextalias()
	Local nSalAnt := pSalAnt
	Local nValCre := 0
	Local nValDeb := 0
	Local nSalAtu := 0
	Local nMedfor := 0
	
	BeginSql Alias cAlSal
	
		SELECT CT2_FILIAL,
		       CT2_DEBITO,
		       CT2_CREDIT,
		       CT2_VALOR
		FROM %Table:CT2%
		WHERE %NOTDEL%
		AND CT2_FILIAL BETWEEN %Exp:SubStr(MV_PAR01,1,4)%       AND %Exp:SubStr(MV_PAR02,1,4)%
		AND CT2_DATA   BETWEEN %Exp:DToS(MV_PAR07)% AND %Exp:DToS(MV_PAR08)%
		AND (CT2_DEBITO = %Exp:cConta% OR CT2_CREDIT = %Exp:cConta%)
	
	EndSql
	
	While !(cAlSal)->(Eof())
	
		If AllTrim((cAlSal)->CT2_DEBITO) == "21010201001"
			nValCre += (cAlSal)->CT2_VALOR			
		ElseIf AllTrim((cAlSal)->CT2_CREDIT) == "21010201001"
			nValDeb += (cAlSal)->CT2_VALOR
		EndIf
	
		(cAlSal)->(DbSkip())
	EndDo
	
	(cAlSal)->(DbCloseArea())
	
	nSalAtu	:= ( nSalAnt + nValCre ) - nValDeb
	
	nMedFor := ( nSalAtu + nSalAnt ) / 2

Return nMedFor

/*/{Protheus.doc} ValComPer
(long_description)
@author j2a.luizjunior
@since 16/05/2017
@version 1.0
@example
(examples)
@see (links_or_references)
/*/

Static Function ValComPer

	Local cAlCom  := GetNextAlias()
	Local nValCom := 0
	
	BeginSql Alias cAlCom	
		
		SELECT F1_VALBRUT 
		FROM  %Table:SF1%
		WHERE %NOTDEL%
		AND   F1_FILIAL  BETWEEN %Exp:MV_PAR01%       AND %Exp:MV_PAR02%
		AND   F1_EMISSAO BETWEEN %Exp:DToS(MV_PAR07)% AND %Exp:DToS(MV_PAR08)%
		AND   F1_STATUS = 'A'
		AND   F1_ESPECIE IN ('NF','NFS','NFCE')
	
	EndSql
	
	While !(cAlCom)->(Eof())
		
		nValCom += (cAlCom)->F1_VALBRUT
		
		(cAlCom)->(DbSkip())
	EndDo
	
	(cAlCom)->(DbCloseArea())	

Return nValCom

/*/{Protheus.doc} CriaSX1
(long_description)
@author j2a.luizjunior
@since 16/05/2017
@version 1.0
@param cPerg, character, (Descrição do parâmetro)
@return ${return}, ${return_description}
@example
(examples)
@see (links_or_references)
/*/

Static Function CriaSX1(cPerg)

	Local aHelp := {}

	AAdd(aHelp, {{"Filial De"        } ,{""},{""}})//01
	AAdd(aHelp, {{"Filial Ate"       } ,{""},{""}})//02
	AAdd(aHelp, {{"Fornecedor De"    } ,{""},{""}})//03
	AAdd(aHelp, {{"Loja De"          } ,{""},{""}})//04
	AAdd(aHelp, {{"Fornecedor Ate"   } ,{""},{""}})//05
	AAdd(aHelp, {{"Loja Ate"         } ,{""},{""}})//06	
	AAdd(aHelp, {{"Dt Movimento"     } ,{""},{""}})//07
	AAdd(aHelp, {{"Dt Movimento"     } ,{""},{""}})//08
	AAdd(aHelp, {{"Vencimento De"    } ,{""},{""}})//09
	AAdd(aHelp, {{"Vencimento Ate"   } ,{""},{""}})//10
	AAdd(aHelp, {{"Banco"            } ,{""},{""}})//11
	AAdd(aHelp, {{"Agencia"          } ,{""},{""}})//12
	AAdd(aHelp, {{"Conta"            } ,{""},{""}})//13
	
	u_SFPUTSX1(cPerg,"01","Filial De"        ,"","","mv_ch1"  ,"C",08,00,00,"G","","SM0"  ,"","","mv_par01",""         ,"","","",""         ,"","","","","","","","","","","" ,aHelp[1,1]  ,aHelp[1,2]  ,aHelp[1,3]  ,"")
	u_SFPUTSX1(cPerg,"02","Filial Ate"       ,"","","mv_ch2"  ,"C",08,00,00,"G","","SM0"  ,"","","mv_par02",""         ,"","","",""         ,"","","","","","","","","","","" ,aHelp[2,1]  ,aHelp[2,2]  ,aHelp[2,3]  ,"")
	u_SFPUTSX1(cPerg,"03","Fornecedor De"    ,"","","mv_ch3"  ,"C",09,00,00,"G","","SA2A" ,"","","mv_par03",""         ,"","","",""         ,"","","","","","","","","","","" ,aHelp[3,1]  ,aHelp[3,2]  ,aHelp[3,3]  ,"")
	u_SFPUTSX1(cPerg,"04","Loja De"          ,"","","mv_ch4"  ,"C",04,00,00,"G","",""     ,"","","mv_par04",""         ,"","","",""         ,"","","","","","","","","","","" ,aHelp[4,1]  ,aHelp[4,2]  ,aHelp[4,3]  ,"")
	u_SFPUTSX1(cPerg,"05","Fornecedor Ate"   ,"","","mv_ch5"  ,"C",09,00,00,"G","","SA2A" ,"","","mv_par05",""         ,"","","",""         ,"","","","","","","","","","","" ,aHelp[5,1]  ,aHelp[5,2]  ,aHelp[5,3]  ,"")
	u_SFPUTSX1(cPerg,"06","Loja Ate"         ,"","","mv_ch6"  ,"C",04,00,00,"G","",""     ,"","","mv_par06",""         ,"","","",""         ,"","","","","","","","","","","" ,aHelp[6,1]  ,aHelp[6,2]  ,aHelp[6,3]  ,"")
	u_SFPUTSX1(cPerg,"07","Dt Movimento De"  ,"","","mv_ch7"  ,"D",08,00,00,"G","",""     ,"","","mv_par07",""         ,"","","",""         ,"","","","","","","","","","","" ,aHelp[7,1]  ,aHelp[7,2]  ,aHelp[7,3]  ,"")
	u_SFPUTSX1(cPerg,"08","Dt Movimento Ate" ,"","","mv_ch8"  ,"D",08,00,00,"G","",""     ,"","","mv_par08",""         ,"","","",""         ,"","","","","","","","","","","" ,aHelp[8,1]  ,aHelp[8,2]  ,aHelp[8,3]  ,"")
	u_SFPUTSX1(cPerg,"09","Vencimento De"    ,"","","mv_ch9"  ,"D",08,00,00,"G","",""     ,"","","mv_par09",""         ,"","","",""         ,"","","","","","","","","","","" ,aHelp[9,1]  ,aHelp[9,2]  ,aHelp[9,3]  ,"")
	u_SFPUTSX1(cPerg,"10","Vencimento Ate"   ,"","","mv_ch10" ,"D",08,00,00,"G","",""     ,"","","mv_par10",""         ,"","","",""         ,"","","","","","","","","","","" ,aHelp[10,1] ,aHelp[10,2] ,aHelp[10,3] ,"")
	u_SFPUTSX1(cPerg,"11","Banco"            ,"","","mv_ch11" ,"C",03,00,00,"G","","SA6"  ,"","","mv_par11",""         ,"","","",""         ,"","","","","","","","","","","" ,aHelp[11,1] ,aHelp[11,2] ,aHelp[11,3] ,"")
	u_SFPUTSX1(cPerg,"12","Agencia"          ,"","","mv_ch12" ,"C",05,00,00,"G","",""     ,"","","mv_par12",""         ,"","","",""         ,"","","","","","","","","","","" ,aHelp[12,1] ,aHelp[12,2] ,aHelp[12,3] ,"")
	u_SFPUTSX1(cPerg,"13","Conta"            ,"","","mv_ch13" ,"C",10,00,00,"G","",""     ,"","","mv_par13",""         ,"","","",""         ,"","","","","","","","","","","" ,aHelp[13,1] ,aHelp[13,2] ,aHelp[13,3] ,"")
		
Return