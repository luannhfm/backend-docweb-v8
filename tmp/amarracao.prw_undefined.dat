#include "totvs.ch"
#include "restful.ch"

//-------------------------------------------------------------------
/*/{Protheus.doc} Amarrações
Declaracao do GET de Amarrações
@author Walmir Junior
@since 02/03/2021
@version 1.0
/*/
//-------------------------------------------------------------------
WSRESTFUL amarracao DESCRIPTION 'endpoint Amarracoes - VENUS ' FORMAT "application/json,text/html"
    WSDATA Page              AS INTEGER OPTIONAL
    WSDATA aQueryString      AS ARRAY    OPTIONAL
    
 	WSMETHOD GET amarracao;
	    DESCRIPTION "Retorna uma lista de Amarrações";
	    WSSYNTAX "/api/amarracao" ;
        PATH "/api/amarracao" ;
	    PRODUCES APPLICATION_JSON
 	
END WSRESTFUL

//-------------------------------------------------------------------
/*/{Protheus.doc} GET Amarração
Método GET amarracao
@author Walmir Junior
@since 02/03/2021
@version 1.0
/*/
//-------------------------------------------------------------------
WSMETHOD GET amarracao QUERYPARAM Page WSREST amarracao
Return getamarracao(self)

//-------------------------------------------------------------------
/*/{Protheus.doc} GET getAmarracao
Funcao para tratamento da requisicao GET
@author Walmir Junior
@since 02/03/2021
@version 1.0
/*/
//-------------------------------------------------------------------
Static Function getAmarracao( oWS )
   Local lRet  as logical
   Local oAmarr as object
   DEFAULT oWS:Page := 1  
   lRet             := .T.
   
   oAmarr := AmarracaoAdapter():new( 'GET' )
   oAmarr:setPage(oWS:Page)
   oAmarr:setPageSize(10)
   oAmarr:setUrlFilter(oWS:aQueryString)
   oAmarr:GetListAmarracao()
   //Se tudo ocorreu bem, retorna os dados via Json
   If oAmarr:lOk
       oWS:SetResponse(EncodeUTF8(oAmarr:getJSONResponse(), "cp1252"))
   Else
   //Ou retorna o erro encontrado durante o processamento
       SetRestFault(oAmarr:GetCode(),oAmarr:GetMessage())
       lRet := .F.
   EndIf
   //faz a desalocacao de objetos e arrays utilizados
   oAmarr:DeActivate()
   oAmarr := nil
   
Return lRet
