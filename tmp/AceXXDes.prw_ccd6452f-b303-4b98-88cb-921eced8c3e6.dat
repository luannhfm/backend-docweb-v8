/*/
=========================================================================
Autor.................: Peder Munksgaard (Do.it Sistemas)
-------------------------------------------------------------------------
Data..................: 11/07/2014
-------------------------------------------------------------------------
Descrição.............: Rotina de processamento responsável por atualizar
                        o campo B1_XXDESCR (memo/real) com as informações
                        retiradas do campo B1_DESC (caracter) quando o 
                        campo B1_CODPROC e B1_XXDESCR estiverem vazios.

                        Solicitado por Carla Barzsina.
-------------------------------------------------------------------------
Alteração.............: (dd/mm/yyyy) - Motivo
-------------------------------------------------------------------------
Partida...............: Lanctos Contábeis/Fórmulas
-------------------------------------------------------------------------
Função................: u_AceXXDes()
=========================================================================
/*/

#Include "Protheus.ch"
#Include "Rwmake.ch"
#Include "Topconn.ch"

User Function AceXXDes()

   Local _cMsg
   
   _cMsg := "Esta rotina realizará a leitura do campo B1_DESC    " + CRLF
   _cMsg += "localizado na SB1 e carregará a informação para o   " + CRLF
   _cMsg += "campo B1_XXDESCR (memo/real) caso o campo não esteja" + CRLF
   _cMsg += "preenchido e o campo B1_CODPROC esteja vazio.       " + CRLF
   _cMsg += "Deseja realmente continuar ?                        " + CRLF
   _cMsg += "                                                    " + CRLF
   _cMsg += "Caso não tenha certeza do que está fazendo entre em " + CRLF
   _cMsg += "com a UNETEC.                                       " + CRLF
   
   If MsgYesNo(OemToAnsi(_cMsg))   
      Processa({||fProcSB1()},OemToAnsi('Acertando campos B1_XXDESCR  ...'))
   Endif
   
   MsgInfo("Processamento finalizado!","[AceXXDes]")
    
Return NiL

Static Function fProcSB1()

   Local _cQry     := ''
   Local _cAliasB1 := GetNextAlias()
   Local _nX       := 0   
   Local _nRegsB1  := 0                          
   
   _cQry := " SELECT B1_CODPROC, R_E_C_N_O_ REGNOI FROM " + RetSqlName("SB1") + " B11" + CRLF
   _cQry += " WHERE B11.D_E_L_E_T_ <> '*'                                            " + CRLF
   _cQry += " AND   B11.B1_CODPROC =  ' '                                            " + CRLF
   _cQry += " ORDER BY R_E_C_N_O_                                                    " + CRLF

   _cQry := ChangeQuery(_cQry)
   
   If Select((_cAliasB1)) > 0
      (_cAliasB1)->(dbCloseArea())
   Endif
   
   TcQuery _cQry New Alias (_cAliasB1) 

   _nRegsB1 := Contar((_cAliasB1),"!Eof()")
   _nRegsC1 := SC1->(Reccount())
   _nRegsC8 := SC8->(Reccount())
   _nRegsC7 := SC7->(Reccount())
   
   (_cAliasB1)->(dbGotop())               
   
   ProcRegua(_nRegsB1+_nRegsC1+_nRegsC8+_nRegsC7)
      
   While (_cAliasB1)->(!Eof())
       
      dbSelectArea("SB1")
      SB1->(dbGoto((_cAliasB1)->REGNOI))
      
      _nX   := MLCount(SB1->B1_XXDESCR)
      
      If _nX == 0
            
         Reclock("SB1",.F.)
         Replace B1_XXDESCR With B1_DESC
         SB1->(MsUnlock())
                                    
      Endif
                  
      (_cAliasB1)->(dbSkip())
      IncProc()
      
   End

   dbSelectArea("SC1")
   SC1->(dbgotop())
   
   While SC1->(!Eof())
             
      Reclock("SC1",.F.)
      Replace C1_XXDESCR With Posicione("SB1",1,xFilial("SB1")+SC1->C1_PRODUTO,"B1_XXDESCR")
      SC1->(MsUnlock())
                                                     
      SC1->(dbSkip())
      IncProc()      
      
   End   

   dbSelectArea("SC8")
   SC8->(dbgotop())
   
   While SC8->(!Eof())
             
      Reclock("SC8",.F.)
      Replace C8_XXDESCR With Posicione("SB1",1,xFilial("SB1")+SC8->C8_PRODUTO,"B1_XXDESCR")
      SC8->(MsUnlock())
                                                     
      SC8->(dbSkip())
      IncProc()      
      
   End

   dbSelectArea("SC7")
   SC7->(dbgotop())
   
   While SC7->(!Eof())
             
      Reclock("SC7",.F.)
      Replace C7_XXDESCR With Posicione("SB1",1,xFilial("SB1")+SC7->C7_PRODUTO,"B1_XXDESCR")
      SC7->(MsUnlock())
                                                     
      SC7->(dbSkip())
      IncProc()      
      
   End      
      
Return NiL