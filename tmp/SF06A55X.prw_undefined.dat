#include "Rwmake.ch"
#include "Protheus.ch"


/*
?????????????????????????????????????????????????????????????????????????????
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±????????????????????????????????????????????????????????????????????????»±±
±±?Programa  |SF06A55X    ?Autor  ?Caio Renan          ? Data ?27/10/2011   ?±±
±±?????????????????????????????????????????????????????????????????????????±±
±±?Desc.     ?RELATORIO DE SMS ENVIADOS.                                  ?±±
±±?          ?                                                            ?±±
±±?          ?                                                            ?±±
±±?????????????????????????????????????????????????????????????????????????±±
±±?Uso       ?                                                            ?±±
±±?????????????????????????????????????????????????????????????????????????±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
?????????????????????????????????????????????????????????????????????????????
*/


User Function SF06A55X()
	Local oReport
	Private _cPicture := TM(999999999.99,14,2) // "@E 999,999,999.99"
	Private _cAlias := GetNextAlias()
	Private _oSMS
	Private _cPerg := "SF06A55X"
	Private _cRet := ""

	If FindFunction("TRepInUse") // .And. TRepInUse()
		//-- Interface de impressao
		oReport := ReportDef()
		oReport:PrintDialog()
	EndIf

Return

Static Function ReportDef()
	Local oReport
	Local oSection
	Local oBreak
	Local oCell1, oCell2, oCell3

	AjustaSX1(_cPerg)

	Pergunte(_cPerg, .F.)

	oReport := TReport():New("Cobranca-SMS","Cobranca - SMS",_cPerg,{|oReport| PrintReport(oReport)},;
		"Cobranca - SMS")
	oReport:nLeftMargin := 2
//oReport:nFontBody := 8
//oReport:nLineHeight := 45
	oReport:SetPortrait() // retrato
	oReport:oPage:SetPaperSize(9) // papel A4
	oReport:SetEnvironment(2) // local

	_oSMS := TRSection():New(oReport,"SMS" ,{_cALias} , {"Nome Cliente","Valor","Data"} )

	TRCell():New(_oSMS,"ZAP_CLIENT" ,_cALias )
	TRCell():New(_oSMS,"A1_NOME"    ,_cALias )
	TRCell():New(_oSMS,"ZAP_CELULA" ,_cALias , ,"@R +99(99)9999-9999", 16)
	TRCell():New(_oSMS,"ZAP_PREFIX" ,_cALias ,"Prefixo")
	TRCell():New(_oSMS,"ZAP_NUM"    ,_cALias ,"Numero")
	TRCell():New(_oSMS,"ZAP_PARCEL" ,_cALias ,"Parcela")
	TRCell():New(_oSMS,"E1_VALOR"   ,_cALias , ,_cPicture,14)
	TRCell():New(_oSMS,"ZAP_DATA"   ,_cALias , ,         ,  , ,{|| DToC(SToD((_cALias)->ZAP_DATA)) } )
	TRCell():New(_oSMS,"ZAP_RET"    ,_cALias ,"Status" ,         ,45, ,{|| cRet() } )

//TRCell():New(oSection,"VALOR"       ,_cALias,"Valor",,14,.F.,{|| Transform((_cALias)->VALOR,_cPicture) })
//oSection:LoadCells() // FUNCAO QUE CARREGA AS CELULAS DA SECAO A PARTIR DO DICIONARIO DE DADOS REPEITANDO NIVEL, USO, CONTEXTO...

Return(oReport)

Static Function cRet ()
	Local _cRet := ""
	Do Case
	Case (_cALias)->ZAP_RET = "0"
		_cRet := "0  -  Recebida/Reposta/Erro"
	Case (_cALias)->ZAP_RET = "2"
		_cRet := "2  -  Nao entregavel"
	Case (_cALias)->ZAP_RET = "3"
		_cRet := "3  -  Entregue"
	Case (_cALias)->ZAP_RET = "7"
		_cRet := "7  -  Nao suportada/Recusada/Falha operadora"
	Case (_cALias)->ZAP_RET = "8"
		_cRet := "8  -  Aguardando"
	Case (_cALias)->ZAP_RET = "9"
		_cRet := "9  -  Enviado"
	Endcase
Return(_cRet)
//????????????????????????????????????????????????????????
//?funcao que realiza o filtro e imprimi os dados na tela?
//????????????????????????????????????????????????????????

Static Function PrintReport(oReport)

	GeraQuery()

	_oSMS:Print()

	(_cALias)->(dbClosearea())
Return
//??????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????
// Funcao que faz o select de acordo com os parametros do relatorio.
//??????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????
Static Function GeraQuery()
	Local _cSql := ""
	Local nOrdem := _oSMS:nOrder
	_oSMS:nOrder := 0

	_cSql += " SELECT ZAP_CLIENT, A1_NOME, ZAP_CELULA, ZAP_PREFIX, ZAP_NUM, ZAP_PARCEL, E1_VALOR, ZAP_DATA, ZAP_RET "+CRLF
	_cSql += " FROM "+RetSQLName("ZAP")+" ZAP "+CRLF
	_cSql += " INNER JOIN "+RetSQLName("SA1")+" SA1 ON A1_COD=ZAP_CLIENT AND A1_LOJA=ZAP_LOJA AND SA1.D_E_L_E_T_ <> '*' "+CRLF
	_cSql += " INNER JOIN "+RetSQLName("SE1")+" SE1 ON E1_FILIAL=ZAP_FILTIT AND E1_PREFIXO=ZAP_PREFIX AND E1_NUM=ZAP_NUM AND E1_PARCELA=ZAP_PARCEL AND E1_TIPO=ZAP_TIPO AND SE1.D_E_L_E_T_ <> '*'  "+CRLF

	_cSql += " WHERE ZAP.D_E_L_E_T_ <> '*'  "+CRLF
	_cSql += " AND ZAP_DATA BETWEEN '"+DToS(MV_PAR01)+"' AND '"+DToS(MV_PAR02)+"' "+CRLF
	_cSql += " AND ZAP_CLIENT BETWEEN '"+MV_PAR03+"' AND '"+MV_PAR04+"' "+CRLF
	_cSql += " AND ZAP_FILTIT BETWEEN '"+MV_PAR05+"' AND '"+MV_PAR06+"' "+CRLF
	_cSql += " AND ZAP_RET IN ('"+StrTran(MV_PAR07,";","','")+"') "+CRLF
	Do Case
	Case MV_PAR08 = 1 // COM SUCESSO
		_cSql += " AND ZAP_ENV = '100' "+CRLF
	Case MV_PAR08 = 2 // SEM SUCESSO
		_cSql += " AND ZAP_ENV = '999' "+CRLF
	Endcase

	Do Case
	Case nOrdem = 1 // NOME DO CLIENTE
		_cSql += " ORDER BY A1_NOME "+CRLF
	Case nOrdem = 2 // VALOR
		_cSql += " ORDER BY E1_VALOR "+CRLF
	Case nOrdem = 3 // DATA
		_cSql += " ORDER BY ZAP_DATA "+CRLF
	Endcase

	If Select(_cALias) > 0
		(_cALias)->(dbClosearea())
	Endif
	DbUseArea(.T., "TOPCONN", TCGenQry(,,ChangeQuery(_cSql)), _cALias, .F., .F.)

Return
/*
?????????????????????????????????????????????????????????????????????????????
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±????????????????????????????????????????????????????????????????????????»±±
±±?Programa  ?sx1inv    ?Autor  ?caio renan          ? Data ?  05/23/09   ?±±
±±?????????????????????????????????????????????????????????????????????????±±
±±?Desc.     ?AJUSTA AS PERGUNTAS                                         ?±±
±±?????????????????????????????????????????????????????????????????????????±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
?????????????????????????????????????????????????????????????????????????????
*/
Static Function AjustaSX1(cPerg)

	LOCAL aArea    	:= GetArea()
	LOCAL aAreaDic 	:= SX1->( GetArea() )
	LOCAL aEstrut  	:= {}
	LOCAL aStruDic 	:= SX1->( dbStruct() )
	LOCAL aDados	:= {}
	LOCAL nXa       := 0
	LOCAL nXb       := 0
	LOCAL nTam1    	:= Len( SX1->X1_GRUPO )
	LOCAL nTam2    	:= Len( SX1->X1_ORDEM )

	aEstrut := { 'X1_GRUPO'  , 'X1_ORDEM'  , 'X1_PERGUNT', 'X1_PERSPA' , 'X1_PERENG' , 'X1_VARIAVL', 'X1_TIPO', ;
		'X1_TAMANHO', 'X1_DECIMAL', 'X1_PRESEL' , 'X1_GSC'    , 'X1_VALID'  , 'X1_VAR01'  , 'X1_DEF01'  , ;
		'X1_DEFSPA1', 'X1_DEFENG1', 'X1_CNT01'  , 'X1_VAR02'  , 'X1_DEF02'  , 'X1_DEFSPA2', 'X1_DEFENG2', ;
		'X1_CNT02'  , 'X1_VAR03'  , 'X1_DEF03'  , 'X1_DEFSPA3', 'X1_DEFENG3', 'X1_CNT03'  , 'X1_VAR04'  , ;
		'X1_DEF04'  , 'X1_DEFSPA4', 'X1_DEFENG4', 'X1_CNT04'  , 'X1_VAR05'  , 'X1_DEF05'  , 'X1_DEFSPA5', ;
		'X1_DEFENG5', 'X1_CNT05'  , 'X1_F3'     , 'X1_PYME'   , 'X1_GRPSXG' , 'X1_HELP'   , 'X1_PICTURE', ;
		'X1_IDFIL'   }

	aAdd( aDados, {cPerg, "01", "Data de?" 	    	,"","","mv_ch1","D",08,00,00,"G","","mv_par01","","","","","","","","","","","","","","","","","","","","","","","","","","","",""} )
	aAdd( aDados, {cPerg, "02", "Data ate?" 		,"","","mv_ch2","D",08,00,00,"G","","mv_par02","","","","","","","","","","","","","","","","","","","","","","","","","","","",""} )
	aAdd( aDados, {cPerg, "03", "Cliente de?"    	,"","","mv_ch3","C",06,00,00,"G","","mv_par03","","","","","","","","","","","","","","","","","","","","","","","","","SA1GCT","","",""} )
	aAdd( aDados, {cPerg, "04", "Cliente ate?" 	  	,"","","mv_ch4","C",06,00,00,"G","","mv_par04","","","","","","","","","","","","","","","","","","","","","","","","","SA1GCT","","",""} )
	aAdd( aDados, {cPerg, "05", "Filial de?" 		,"","","mv_ch5","C",02,00,00,"G","","mv_par05","","","","","","","","","","","","","","","","","","","","","","","","","","","",""} )
	aAdd( aDados, {cPerg, "06", "Filial ate?"	 	,"","","mv_ch6","C",02,00,00,"G","","mv_par06","","","","","","","","","","","","","","","","","","","","","","","","","","","",""} )
	aAdd( aDados, {cPerg, "07", "Status?"    	    ,"","","mv_ch7","C",06,00,00,"G","","mv_par07","","","","","","","","","","","","","","","","","","","","","","","","","","","",""} )
	aAdd( aDados, {cPerg, "08", "Enviado?"	 		,"","","mv_ch8","N",01,00,00,"C","","mv_par08","Inclusao","Inclusao","Inclusao","","","Substituicao","Substituicao","Substituicao","","","Ambos","Ambos","Ambos","","","","","","","","","","","","","","",""} )

	dbSelectArea( 'SX1' )
	SX1->( dbSetOrder( 1 ) )

	For nXa := 1 To Len( aDados )
		If !SX1->( dbSeek( PadR( aDados[nXa][1], nTam1 ) + PadR( aDados[nXa][2], nTam2 ) ) )
			RecLock( 'SX1', .T. )
			For nXb := 1 To Len( aDados[nXa] )
				If aScan( aStruDic, { |aX| PadR( aX[1], 10 ) == PadR( aEstrut[nXb], 10 ) } ) > 0
					SX1->( FieldPut( FieldPos( aEstrut[nXb] ), aDados[nXa][nXb] ) )
				EndIf
			Next nXb
			MsUnLock()
		EndIf
	Next nXa

	RestArea( aAreaDic )
	RestArea( aArea )
Return
