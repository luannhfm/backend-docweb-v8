#Include 'Protheus.ch'
#Include 'TopConn.ch'

/*/{Protheus.doc} SF73R01J
Relatorio de Convidados de Relacionamento Publico

@type 		function
@author 	Jose Leite de Barros Neto
@since 	17/05/2016
@version 	1.0
@return 	Nil, Nulo
/*/
User Function SF73R01J()
	
	Local _oReport	:= Nil
	Local _cPerg		:= FunName()
	
	If TRepInUse()
		fCriaSx1( _cPerg )
		If Pergunte( _cPerg )
			_oReport := ReportDef( _cPerg )
			_oReport:PrintDialog()
		EndIf
	EndIf
	
Return


/** {Protheus.doc} ReportDef
Funcao que define as colunas

@author: 	Jose Leite de Barros Neto
@since: 	18/05/2016
@Uso: 		SFIEMT
*/
Static Function ReportDef(p_cPerg)
	
	Local oReport	:= Nil
	Local oSection	:= Nil
	Local oSection2	:= Nil
	Local oBreak		:= Nil
	Local cTitle 	:= "Relação de Convidados" 
	
	oReport := TReport():New(p_cPerg,cTitle,p_cPerg,{|oReport| PrintReport(oReport)},cTitle)
	oReport:SetPortrait()
	oReport:cFontBody := 'Courier New'
	oReport:nFontBody := 8	
	
	oSection:= TRSection():New(oReport,OemToAnsi("Bloco"),{"TRA"})
		
	oBloco:= TRCell():New(oSection, "_cBloco","TRA", "Bloco")
	oBloco:SetSize(30)
	
	oSection2:= TRSection():New(oSection,OemToAnsi("Convidados"),{"TRA"})
		
	oOrdem:= TRCell():New(oSection2, "_cOrdem","TRA", "Ordem")
	oOrdem:SetSize(30)
	
	oNome:= TRCell():New(oSection2, "_cNome","TRA", "Nome")
	oNome:SetSize(120)
	
	oCargo:= TRCell():New(oSection2, "_cCargo","TRA", "Cargo")
	oCargo:SetSize(80)
	
	oTelefone:= TRCell():New(oSection2, "_cTel","TRA", "Telefone")
	oTelefone:SetSize(90)
	
	oEmail:= TRCell():New(oSection2, "_cEmail","TRA", "E-mail")
	oEmail:SetSize(120)
	
	oSit:= TRCell():New(oSection2, "_cSituac","TRA", "Sit")
	oSit:SetSize(16)
	
Return( oReport )


/** {Protheus.doc} ReportDef
Funcao que gera o relatorio

@author: 	Jose Leite de Barros Neto
@since: 	18/05/2016
@Uso: 		SFIEMT
*/
Static Function PrintReport(oReport)
	
	Local oSection 	:= oReport:Section(1)
	Local oSection2	:= oReport:Section(1):Section(1)
	Local _cQuery 	:= ""
	Local _cBloco	:= ""
	Local _cOrdem	:= ""
	Local _cNome		:= ""
	Local _cCargo	:= ""
	Local _cTel		:= ""
	Local _cEmail	:= ""
	Local _cSituac	:= ""
	Local _cLogo		:= GetSrvProfString("Startpath","") + "lgrlfiemt.png"
	Local _lFirst	:= .F.
	Local _nCont		:= 0
	Local _nLin		:= 30
	Local oFontA10  := TFont():New("Courier New",,10,,.F.,,,,,.F.)
	
	If Select("TRA") > 0
		DbSelectarea("TRA")
		DbCloseArea()
	Endif
	
	_cQuery := "SELECT *																				"
	_cQuery += " FROM "+ RetSqlName("ZC6")
	_cQuery += " WHERE		ZC6_CODIGO					>= '"+ mv_par01 +"'					"
	_cQuery += " 	AND 	ZC6_CODIGO 					<= '"+ mv_par02 +"'					"
	_cQuery += " 	AND 	ZC6_BLOCO 					>= '"+ mv_par03 +"'					"
	_cQuery += " 	AND 	ZC6_BLOCO 					<= '"+ mv_par04 +"'					"
	_cQuery += " 	AND 	ZC6_CARGO 					>= '"+ mv_par05 +"'					"
	_cQuery += " 	AND 	ZC6_CARGO 					<= '"+ mv_par06 +"'					"
	_cQuery += " 	AND 	SubStr(ZC6_DTANIV,5,2) 	>= '"+ SubStr(mv_par07,3,2) +"'		"
	_cQuery += " 	AND 	SubStr(ZC6_DTANIV,5,2) 	<= '"+ SubStr(mv_par08,3,2) +"'		"
	_cQuery += " 	AND 	SubStr(ZC6_DTANIV,7,2) 	>= '"+ SubStr(mv_par07,1,2) +"'		"
	_cQuery += " 	AND 	SubStr(ZC6_DTANIV,7,2) 	<= '"+ SubStr(mv_par08,1,2) +"'		"
	_cQuery += " 	AND D_E_L_E_T_ 						<> '*'										"
	_cQuery += " ORDER BY ZC6_BLOCO																	"
  	
	TCQUERY _cQuery NEW ALIAS "TRA"
		
	DbSelectArea("TRA")
	TRA->(DbGoTop())
	
	oReport:HideHeader()
	
	oReport:SetMeter(RecCount())
	
	//Imprime Logo Fiemt
	oReport:SayBitmap(_nLin,500,_cLogo, 1500, 130)
	oReport:SkipLine()
	oReport:SkipLine()
	oReport:SkipLine()
	oReport:SkipLine()
	oReport:SkipLine()

	_nLin += 140
	oReport:Say(_nLin,40,Space(55) + "Relação de Convidados", oFontA10 )
	oReport:SkipLine()
	oReport:SkipLine()
	
	_nLin += 80
	oReport:Say(_nLin,40,Space(25) + "Evento: __________________________________________________________", oFontA10 )
	oReport:SkipLine()
	oReport:SkipLine()
	
	_nLin += 80
	oReport:Say(_nLin,40,Space(25) + "Local: __________________________________________________________", oFontA10 )
	oReport:SkipLine()
	oReport:SkipLine()
	
	_nLin += 80
	oReport:Say(_nLin,40,Space(25) + "Data / Horário: __________________________________________________", oFontA10 )
	oReport:SkipLine()
	oReport:SkipLine() 
	oReport:SkipLine()
	oReport:SkipLine()
	oReport:SkipLine()
	oReport:SkipLine()
	
	While .Not. TRA->( EOF() )
		
		If oReport:Cancel()
			Exit
		EndIf
		
		oSection:Init()
		_cBloco	:= AllTrim(TRA->ZC6_DSCBLC)
		oSection:Cell("_cBloco"):SetValue(_cBloco)
		oSection:PrintLine()
		
		oSection2:Init()
		_nCont	 := 0
		
		While .Not. TRA->( EOF() ) .And. _cBloco == AllTrim(TRA->ZC6_DSCBLC)
			_nCont++
			_cOrdem	:= StrZero(_nCont,6)
			_cNome		:= AllTrim(TRA->ZC6_NOME)
			_cCargo	:= AllTrim(TRA->ZC6_DSCCRG)
			_cTel		:= AllTrim(TRA->ZC6_TELCEL) + ' / ' + AllTrim(TRA->ZC6_TELFIX)
			_cEmail	:= Lower(AllTrim(TRA->ZC6_EMAIL))
			_cSituac	:= "[ ]"
			
			oSection2:Cell("_cOrdem"):SetValue(_cOrdem)
			oSection2:Cell("_cNome"):SetValue(_cNome)
			oSection2:Cell("_cCargo"):SetValue(_cCargo)
			oSection2:Cell("_cTel"):SetValue(_cTel)
			oSection2:Cell("_cEmail"):SetValue(_cEmail)
			oSection2:Cell("_cSituac"):SetValue(_cSituac)
			oSection2:PrintLine()
			oReport:SkipLine()
			
			TRA->( DbSkip() )
			
		End	
		oSection2:Finish()
		oSection:Finish()
		oReport:IncMeter()
	End
		
	TRA->(DbCloseArea())
	
Return( Nil )


/** {Protheus.doc} fCriaSx1
Funcao para criacao das perguntas

@param: 	<Nil>
@author 	Jose Leite de Barros Neto
@since 	15/03/2016
@Uso: 		SFIEMT
*/
Static Function fCriaSx1( p_cPerg )
	
	Local aHelp := {}

	AAdd(aHelp, {{"Informe o Codigo do Relacionamento Pessoal Inicial"	}, {""}, {""}})
	AAdd(aHelp, {{"Informe o Codigo do Relacionamento Pessoal Final"		}, {""}, {""}})
	AAdd(aHelp, {{"Informe o Bloco Inicial"										}, {""}, {""}})
	AAdd(aHelp, {{"Informe o Bloco Final"											}, {""}, {""}})
	AAdd(aHelp, {{"Informe o Cargo Inicial"										}, {""}, {""}})
	AAdd(aHelp, {{"Informe o Cargo Final"											}, {""}, {""}})
	AAdd(aHelp, {{"Informe o Dia/Mes de Aniversário Inicial ; DDMM"		}, {""}, {""}})
	AAdd(aHelp, {{"Informe o Dia/Mes de Aniversário Final ; DDMM"			}, {""}, {""}})
	
	u_SFPUTSX1( p_cPerg, "01","Relacionamento Pessoal Inicial?	"  ,"","","mv_ch1","C",TamSX3("ZC6_BLOCO")[1]	,0,0,"G",""			,"ZC6"		,"","","mv_par01","","","","","","","","","","","","","","","","",aHelp[1,1],aHelp[1,2],aHelp[1,3],"")
	u_SFPUTSX1( p_cPerg, "02","Relacionamento Pessoal Final?		"  ,"","","mv_ch2","C",TamSX3("ZC6_BLOCO")[1]	,0,0,"G",""			,"ZC6"		,"","","mv_par02","","","","","","","","","","","","","","","","",aHelp[2,1],aHelp[2,2],aHelp[2,3],"")
	u_SFPUTSX1( p_cPerg, "03","Bloco Inicial?	"  							,"","","mv_ch3","C",TamSX3("ZC6_BLOCO")[1]	,0,0,"G",""			,"ZC5"		,"","","mv_par03","","","","","","","","","","","","","","","","",aHelp[3,1],aHelp[3,2],aHelp[3,3],"")
	u_SFPUTSX1( p_cPerg, "04","Bloco Final?"  							,"","","mv_ch4","C",TamSX3("ZC6_BLOCO")[1]	,0,0,"G",""			,"ZC5"		,"","","mv_par04","","","","","","","","","","","","","","","","",aHelp[4,1],aHelp[4,2],aHelp[4,3],"")	
	u_SFPUTSX1( p_cPerg, "05","Cargo Inicial?" 	 						,"","","mv_ch5","C",TamSX3("ZC6_CARGO")[1]	,0,0,"G",""			,"SUM"		,"","","mv_par05","","","","","","","","","","","","","","","","",aHelp[5,1],aHelp[5,2],aHelp[5,3],"")
	u_SFPUTSX1( p_cPerg, "06","Cargo Final?"  							,"","","mv_ch6","C",TamSX3("ZC6_CARGO")[1]	,0,0,"G",""			,"SUM"		,"","","mv_par06","","","","","","","","","","","","","","","","",aHelp[6,1],aHelp[6,2],aHelp[6,3],"")
	u_SFPUTSX1( p_cPerg, "07","Dia/Mes de Aniversário Inicial" 		,"","","mv_ch7","C",4								,0,0,"G",""			,""			,"","","mv_par07","","","","","","","","","","","","","","","","",aHelp[7,1],aHelp[7,2],aHelp[7,3],"")
	u_SFPUTSX1( p_cPerg, "08","Dia/Mes de Aniversário Final"  		,"","","mv_ch8","C",4								,0,0,"G",""			,""			,"","","mv_par08","","","","","","","","","","","","","","","","",aHelp[8,1],aHelp[8,2],aHelp[8,3],"")
	
Return( Nil )