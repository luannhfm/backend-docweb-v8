#INCLUDE "PROTHEUS.CH"

/*/{Protheus.doc} M030Inc
Este Ponto de Entrada é chamado após a inclusão dos dados do cliente no Arquivo.
@type function
@author Jose Leite de Barros Neto
@since  27/06/2014
@see http://tdn.totvs.com/pages/releaseview.action?pageId=6784136
/*/

User Function M030INC()
	
	Local _aArea 	:= GetArea()
	Local _cCodCli 	:= SA1->(A1_COD)
	Local _cLojCli	:= SA1->(A1_LOJA) 
	Local _cPessoa	:= SA1->(A1_PESSOA)
	Local _cClvl	:= ""

	If _cPessoa == "F"
		_cClvl := "CF"+_cCodCli
	Else
		_cClvl := "CJ"+_cCodCli 
	EndIf
	
	If RecLock("SA1",.F.)
		SA1->A1_CLVL := _cClvl
		SA1->(MsUnlock())
	EndIf

	IncCTH(_cClvl)
	
	fIncZDM(_cCodCli, _cLojCli)

	//Função para adicionar complemento de cliente
	fIncAI0(_cCodCli, _cLojCli)
	
	RestArea(_aArea)
	
Return( Nil )

/** {Protheus.doc} IncCTH
Funcao para incluir classe valor do cliente
@type function
@param pClvl, character, código da classe de valor
@author Jose Leite de Barros Neto
@since 27/06/2014
*/
Static Function IncCTH(pClvl)
	
	DbSelectArea("CTH")
	DbSetOrder(1) //CTH_FILIAL+CTH_CLVL
	If .Not. DbSeek(xFilial("CTH")+pClvl)
		
		If RecLock("CTH",.T.)
			Replace CTH_FILIAL	WITH xFilial("CTH")
			Replace CTH_CLVL 	WITH pClvl 						//Codigo Classe de Valor
			Replace CTH_CLASSE	WITH "2"						//Classe - 2= Analitico
			Replace CTH_DESC01	WITH AllTrim(SA1->(A1_NOME))	//Descricao - Nome do Fornecedor
			Replace CTH_DTEXIS  WITH CTOD("01/01/80")			//Data Inicio Existencia
			Replace CTH_BLOQ	WITH SA1->(A1_MSBLQL)
			MsUnlock()
		EndIf
		
	EndIf
		
Return( Nil )

Static Function fIncZDM(_cCodCli, _cLojCli)
	U_F99A01S1(FunName(), xFilial("SA1") + _cCodCli + _cLojCli,)
Return Nil

/*/{Protheus.doc} fIncAI0
	Função para incluir os dados do cliente no complemento de cliente
	@type  Function
	@author Franklin de Brito de Oliveira
	@since 17/01/2022
	@param cCodigo, charactere, código do cliente
	@param cLoja, charactere, loja do cliente
/*/
Static Function fIncAI0(cCodigo, cLoja)
	DbSelectArea("AI0")
	DbSetOrder(1)	//AI0_FILIAL+AI0_CODCLI+AI0_LOJA
	if .Not. DBSEEK( xFilial("AI0")+cCodigo+cLoja )
		if RecLock("AI0", .T.)
			Replace AI0_FILIAL WITH xFilial("AI0")
			Replace AI0_CODCLI WITH cCodigo
			Replace AI0_LOJA WITH cLoja
			Replace AI0_XTPCNP WITH "3"	//1=Agencia de Fomento;2=ICT;3=Empresa;4=Sistema Industria;
			MsUnlock()
		endif
	endif
Return NIL
