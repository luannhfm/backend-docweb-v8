#Include 'Protheus.ch'

//-------------------------------------------------
/*/{Protheus.doc} FIN460E1
Executado na canelamento da liquidação

@author Allan da Silva Faria
@since 19/04/2016
@version 1.0
/*/
//-------------------------------------------------
User Function FIN460E1()

Local _aHistCob:= Array(8)

//------------------------------------------------------
//-- Exclui Histórico de cobrança - Título Gerados.
//------------------------------------------------------
_aHistCob[1]:= SE1->E1_CLIENTE
_aHistCob[2]:= SE1->E1_LOJA
_aHistCob[3]:= SE1->E1_PREFIXO 
_aHistCob[4]:= SE1->E1_NUM
_aHistCob[5]:= SE1->E1_PARCELA
_aHistCob[6]:= SE1->E1_TIPO
_aHistCob[7]:= SE1->E1_EMISSAO
_aHistCob[8]:= "01"

//---------------------------------------
//-- Chama Função que excluir historico
//---------------------------------------
DELHISTOR(_aHistCob,"FINA460")

Return Nil

//-----------------------------------------------------
/*/{Protheus.doc} DelHistor
Deleta historico.

@author Allan da Silva Faria
@since 18/04/2016
@version 1.0
@param _aDados	, Array		, Dados do movimetação que gerou o historico
@param _cFunName, Caracter	, Nome da rotina que gerou o historico
/*/
//-----------------------------------------------------

Static Function DelHistor(_aDados,_cFunName)

	Local _aArea := ZF1->(GetArea()) 

	DEFAULT _cFunName := FunName()

	_cFunName := PADR(_cFunName,TamSX3("ZF1_ORIGEM")[1])	

	dbSelectArea("ZF1")
	ZF1->(dbSetOrder(2))
	ZF1->(dbGoTop())      //-- FILIAL - CLIENTE - LOJA - PREFIXO - NUM - PARCELA - TIPO - DATA - ORIGEM - SEQ
	If ZF1->(dbSeek(FWxFilial("ZF1")+_aDados[1]+_aDados[2]+_aDados[3]+_aDados[4]+_aDados[5]+_aDados[6]+DTOS(_aDados[7])+_cFunName+_aDados[8]))
		RecLock("ZF1",.F.)
		ZF1->(dbDelete())
		ZF1->(MsUnlock())
	EndIf

	RestArea(_aArea)

Return Nil
