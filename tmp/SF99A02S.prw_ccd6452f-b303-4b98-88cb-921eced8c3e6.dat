#Include 'Protheus.ch'
#Include 'fwmvcdef.ch'
#Include 'Topconn.ch'

/*/{Protheus.doc} SF99A02S
Cadastro de De-Para das Integrações
@type function
@author 	SERGIO RICARDO LEITE SALUSTIANO
@since 		30/11/2020
/*/

user function SF99A02S()

	Local oBrowse

	Private aRotina := MenuDef()

	//Instancio a classe FWMBrowse
	oBrowse := FWMBrowse():New()

	//Setando o Alias a ser utilizado
	oBrowse:SetAlias( 'ZDN' )

	//Descrição do Cabeçalho
	oBrowse:SetDescription('Cadastro de De-Para das Integrações')

	oBrowse:Activate()

Return( Nil )

/** {Protheus.doc} MenuDef
Funcao para Montar o Menu
@type function
@author SERGIO SALUSTIANO
@since 30/11/2020
*/
Static Function MenuDef()

	Local aRotina := {}

	aAdd( aRotina, { 'Pesquisar' , 'PesqBrw'        , 0, 1, 0, .T. } )
	aAdd( aRotina, { 'Visualizar', 'ViewDef.SF99A02S', 0, 2, 0, Nil } )
	aAdd( aRotina, { 'Incluir'   , 'ViewDef.SF99A02S', 0, 3, 0, Nil } )
	aAdd( aRotina, { 'Alterar'   , 'ViewDef.SF99A02S', 0, 4, 0, Nil } )
	aAdd( aRotina, { 'Excluir'   , 'ViewDef.SF99A02S', 0, 5, 0, Nil } )
	aAdd( aRotina, { 'Imprimir'  , 'ViewDef.SF99A02S', 0, 8, 0, Nil } )
	aAdd( aRotina, { 'Copiar'    , 'ViewDef.SF99A02S', 0, 9, 0, Nil } )

Return( aRotina )

/** {Protheus.doc} ModelDef
Funcao para Montar o Modelo de Dados
@type function
@author SERGIO SALUSTIANO
@since 30/11/2020
*/
Static Function ModelDef()

	Local oModel
	Local oStruZDNMASTER := FWFormStruct( 1, 'ZDN', { |X| .Not. (AllTrim(X) $ 'ZDN_ITEM, ZDN_SEQITE, ZDN_CMPENV, ZDN_CNPDES, ZDN_TPOPER') } )
	Local oStruZDNDETAIL := FWFormStruct( 1, 'ZDN', { |X| AllTrim(X) $ 'ZDN_ITEM, ZDN_SEQITE, ZDN_CMPENV, ZDN_CNPDES, ZDN_TPOPER' } )

	oModel := MPFormModel():New( 'SF9902SM' )

	oModel:AddFields( 'ZDNMASTER'	,				, oStruZDNMASTER )
	oModel:AddGrid('ZDNDETAIL'		, 'ZDNMASTER'	, oStruZDNDETAIL )

	oModel:SetRelation( 'ZDNDETAIL', { { 'ZDN_FILIAL', 'xFilial( "ZDN" )' }, {'ZDN_CODIGO', 'ZDN_CODIGO' }}, ZDN->( IndexKey( 1 ) ) )

	oModel:GetModel( 'ZDNDETAIL' ):SetUniqueLine( { "ZDN_SEQITE"} )

	oModel:GetModel( 'ZDNDETAIL' ):SetOptional( .T. )

	oModel:SetDescription( 'Cadastro de Integração' )

	oModel:GetModel( 'ZDNMASTER' ):SetDescription( 'Cadastro de Integração' )
	oModel:GetModel( 'ZDNDETAIL' ):SetDescription( 'Campos DE PARA' )

	oModel:SetPrimaryKey( { "ZDN_FILIAL", "ZDN_CODIGO"} )

	
Return( oModel )

/** {Protheus.doc} ViewDef
Funcao para Montar a Interface
@type function
@author SERGIO SALUSTIANO
@since 30/11/2020
*/
Static Function ViewDef()

	Local oView
	Local oModel  := FWLoadModel( 'SF99A02S' )

	Local oStruZDNMASTER := FWFormStruct( 2, 'ZDN', { |X| .Not. (AllTrim(X) $ 'ZDN_ITEM, ZDN_SEQITE, ZDN_CMPENV, ZDN_CNPDES, ZDN_TPOPER') } )
	Local oStruZDNDETAIL := FWFormStruct( 2, 'ZDN', { |X| AllTrim(X) $ 'ZDN_ITEM, ZDN_SEQITE, ZDN_CMPENV, ZDN_CNPDES, ZDN_TPOPER' } )

	oView := FWFormView():New()

	oView:SetModel( oModel )

	oView:AddField( 'VZDNMASTER', oStruZDNMASTER, 'ZDNMASTER' )
	oView:AddGrid( 'VZDNDETAIL', oStruZDNDETAIL, 'ZDNDETAIL' )
	oView:AddIncrementField( 'VZDNDETAIL', 'ZDN_COD' )
	oView:CreateHorizontalBox( 'CABECALHO', 45 )
	oView:CreateHorizontalBox( 'DETALHE', 55 )
	
	oView:SetOwnerView( 'VZDNMASTER', 'CABECALHO' )
	oView:SetOwnerView( 'VZDNDETAIL', 'DETALHE' )
	
	oView:EnableTitleView( 'VZDNMASTER' )
	oView:EnableTitleView( 'VZDNDETAIL' )
	
	oView:AddIncrementField( 'VZDNDETAIL', 'ZDN_ITEM' )
	oView:AddIncrementField( 'VZDNDETAIL', 'ZDN_SEQITE' )
	
	oView:SetCloseOnOk( {|| .t.} )

Return( oView )
