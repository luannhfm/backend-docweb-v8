#Include 'Protheus.ch'

/*/{Protheus.doc} GP010FIMPE
Ponto de entrada utilizado no Cadastro de Funcionarios (SRA), para tratamentos especificos apos 
a conclusão da gravação de todos os dados.

@author 	Jose Leite de Barros Neto
@since 	28/11/2014
@version 	1.0
@return 	Nil,Nulo

@see 
http://tdn.totvs.com/pages/releaseview.action?pageId=6079098
/*/
User Function GP010FIMPE()
	
	Local _aArea 	:= GetArea()
	Local _cCodigo	:= Substr(SRA->RA_CIC,1,9) 
	Local _cLoja		:= "00PF"
	
	If INCLUI .OR. ALTERA
		
		If .Not. Empty(AllTrim(SRA->RA_CIC))
			
			DbSelectArea("SA2")
			DbSetOrder(3)
			If .Not. DbSeek(xFilial("SA2") + SRA->RA_CIC)
				
				If Reclock("SA2",.T.)
					SA2->A2_TIPO		:= 'F'
					SA2->A2_COD 		:= _cCodigo
					SA2->A2_LOJA		:= _cLoja
					SA2->A2_NOME		:= SRA->RA_NOME
					SA2->A2_NREDUZ	:= SRA->RA_NOME
					SA2->A2_END		:= SRA->RA_ENDEREC
					SA2->A2_BAIRRO	:= SRA->RA_BAIRRO
					SA2->A2_MUN		:= SRA->RA_MUNICIP
					SA2->A2_EST		:= SRA->RA_ESTADO
					SA2->A2_COD_MUN	:= SRA->RA_CODMUN
					SA2->A2_CEP		:= SRA->RA_CEP	
					SA2->A2_MSBLQL	:= "2"	
					SA2->A2_ENDCOMP	:= SRA->RA_COMPLEM
					SA2->A2_TEL		:= SRA->RA_TELEFON
					SA2->A2_DDD		:= SRA->RA_DDDFONE
					SA2->A2_CGC		:= SRA->RA_CIC	
					SA2->A2_PFISICA	:= SRA->RA_RG	
					SA2->A2_TPESSOA	:= "PF"			
					SA2->A2_CONTATO	:= SRA->RA_NOME
					SA2->A2_NIT		:= SRA->RA_PIS
					SA2->A2_CODFUNC	:= SRA->RA_CODFUNC
					SA2->A2_CONTA	:= '21010201001'
					SA2->A2_RECINSS	:= 'S' 
					SA2->A2_CODPAIS	:= '01058'
					SA2->A2_CALCIRF	:= '1'
					SA2->( MsUnlock() )
				EndIf
			
			Else
				
				If Reclock("SA2",.F.)
					SA2->A2_NOME		:= SRA->RA_NOME
					SA2->A2_NREDUZ	:= SRA->RA_NOME
					SA2->A2_CONTATO	:= SRA->RA_NOME
					SA2->A2_END		:= SRA->RA_ENDEREC
					SA2->A2_BAIRRO	:= SRA->RA_BAIRRO
					SA2->A2_MUN		:= SRA->RA_MUNICIP
					SA2->A2_EST		:= SRA->RA_ESTADO
					SA2->A2_COD_MUN	:= SRA->RA_CODMUN
					SA2->A2_CEP		:= SRA->RA_CEP	
					SA2->A2_MSBLQL	:= "2"	
					SA2->A2_ENDCOMP	:= SRA->RA_COMPLEM
					SA2->A2_DDD		:= SRA->RA_DDDFONE
					SA2->A2_TEL		:= SRA->RA_TELEFON
					SA2->A2_NIT		:= SRA->RA_PIS
					SA2->A2_CODFUNC	:= SRA->RA_CODFUNC
					SA2->( MsUnlock() )
				EndIf
			
			EndIf
			
			If SRA->RA_FOR <> SA2->A2_COD
				If Reclock("SRA",.F.)
					SRA->RA_FOR := SA2->A2_COD				
					SRA->( MsUnlock() )
				Else
					MSGBOX("Não foi possível cadastrar o fornecedor relativo ao funcionário. Por favor comunique " +;
					"suporte CSI","Suporte","INFO")								
				EndIf
			EndIf
			
			DbSelectArea("CTH")
			DbSetOrder(1) //CTH_FILIAL+CTH_CLVL
			If .Not. DbSeek(xFilial("CTH")+"FF"+SA2->A2_COD)
				
				If RecLock("CTH",.T.)
					CTH->CTH_FILIAL	:= xFilial("CTH")
					CTH->CTH_CLVL 	:= "FF"+SA2->A2_COD 		//Codigo Classe de Valor
					CTH->CTH_CLASSE	:= "2"							//Classe - 2= Analitico
					CTH->CTH_DESC01	:= AllTrim(SA2->A2_NOME)	//Descricao - Nome do Fornecedor
					CTH->CTH_DTEXIS	:= CTOD("01/01/80")		//Data Inicio Existencia
					CTH->CTH_BLOQ	:= "2"
					CTH->( MsUnlock() )
				EndIf
				
			EndIf
			
			/* Jose Leite - 02/02/15
			If SRA->RA_CLVL <> CTH->CTH_CLVL 
				If Reclock("SRA",.F.)
					SRA->RA_CLVL := CTH->CTH_CLVL				
					SRA->( MsUnlock() )
				Else
					MSGBOX("Não foi possível cadastrar a Classe Valor relativo ao funcionário. Por favor comunique " +;
					"suporte CSI","Suporte","INFO")								
				EndIf
			EndIf
			*/
			
			If SA2->A2_CLVL <> CTH->CTH_CLVL
				If Reclock("SA2",.F.)
					SA2->A2_CLVL := CTH->CTH_CLVL				
					SA2->( MsUnlock() )
				Else
					MSGBOX("Não foi possível cadastrar a Classe Valor relativo ao fun x for. Por favor comunique " +;
					"suporte CSI","Suporte","INFO")								
				EndIf
			EndIf
			
		EndIf
		
	EndIf
	
	RestArea(_aArea)

Return( Nil )