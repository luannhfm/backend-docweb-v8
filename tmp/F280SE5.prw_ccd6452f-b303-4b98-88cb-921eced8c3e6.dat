#Include 'Protheus.ch'

//-------------------------------------------------
/*/{Protheus.doc} F280SE5
Grava informações complementareas na baixa por 
fatura

@author Allan da Silva Faria
@since 20/04/2016
@version 1.0
/*/
//-------------------------------------------------
User Function F280SE5()

Local _aArea   := SE5->(GetArea())
Local _aRecSE5 := ParamIXB[1]
Local _aHistCob:= Array(9)
Local _nn := 0

dbSelectArea("SE5")

For _nn:=1 To Len(_aRecSE5)
	SE5->(dbGoTo(_aRecSE5[_nn]))
	
	//---------------------------------------
	//-- Grava Historico de Cobranla - Baixa 
	///--------------------------------------
	If ( SE5->E5_TIPODOC == "BA" )
	
		_aHistCob[1]:= SE5->E5_CLIENTE
		_aHistCob[2]:= SE5->E5_LOJA
		_aHistCob[3]:= SE5->E5_PREFIXO 
		_aHistCob[4]:= SE5->E5_NUMERO
		_aHistCob[5]:= SE5->E5_PARCELA
		_aHistCob[6]:= SE5->E5_TIPO
		_aHistCob[7]:= STOD("//")
		_aHistCob[8]:= "TÍTULO BAIXADO POR FATURA" + CRLF 
		_aHistCob[8]+= "Número Fatura: "+SE5->E5_FATURA + CRLF 
		_aHistCob[8]+= "Valor Faturado: R$ "+Str(SE5->E5_VALOR,TamSX3("E5_VALOR")[1],TamSX3("E5_VALOR")[2])  
		_aHistCob[9]:= SE5->E5_SEQ
		
		//---------------------------------------
		//-- Chama Função que grava historico
		//---------------------------------------
		U_SF06A74X(_aHistCob,"FINA280")
	
	EndIf
	
Next _nn

RestArea(_aArea)
Return Nil
