#Include 'Protheus.ch'

/*/{Protheus.doc} FA701A01
@description Função responsável por alterar a data de validade do bilhete no venda direta. 
@type  Function
@author Alan Teles de Oliveira
@since 07/01/2019
@version 11.8
@return lRet, logico, retorna falso caso a data de validade seja superior a 30 dias ou menor que hoje.
/*/
User Function FA701A01()
    
    Local lRet := .T.
	Local aArea	:= GetArea()

    If SA6->A6_XVENDFU 

    	CriaSX1('FA701A02')
    			
		MV_PAR01 := dDataBase    
		
		If Pergunte('FA701A02',,,.T.,,.F.) .and. MV_PAR01 >= dDataBase    
		    M->LQ_DTLIM := MV_PAR01
        ElseIf MV_PAR01 < dDataBase .or. MV_PAR01 > dDataBase + 30
            
            MsgInfo('Data informada esta incorreta.', 'Atenção - FA701A01')
            
            lRet := .F.

        Else
            lRet := .F.
        EndIf
    EndIf

	RestArea(aArea)

Return lRet


/*/{Protheus.doc} CriaSX1
@description Função para criação das perguntas (se não existirem)
@author Alan Teles de Oliveira
@since 07/01/2019
@param p_cGrupo, caracter, nome da pergunta 
/*/
Static Function CriaSX1(p_cGrupo) 
 
	Local aArea	:= GetArea()
	Local nX 	:= 0
	Local aHelp	:= {}
	Local aPerg	:= {}
	
	aAdd(aHelp, {'Informe a data de validade do bilhete.'})			   	
    aAdd(aPerg, {p_cGrupo, '01', 'Data de validade?', 'MV_CH1', 'D',  8, 0, 'C', 'MV_PAR01', 'dDataBase', aHelp[1]})
	
 	DbSelectArea('SX1')
    SX1->(DbSetOrder(1))

	For nX := 1 to Len(aPerg)

		If .not. SX1->(DbSeek(PadR(aPerg[nX][01], Len(SX1->X1_GRUPO), ' ') + aPerg[nX][02]))
	
			RecLock('SX1', .T.)
				SX1->X1_GRUPO   := aPerg[nX][01]
				SX1->X1_ORDEM   := aPerg[nX][02]
				SX1->X1_PERGUNT	:= aPerg[nX][03]
				SX1->X1_VARIAVL := aPerg[nX][04]
				SX1->X1_TIPO    := aPerg[nX][05]
				SX1->X1_TAMANHO := aPerg[nX][06]
				SX1->X1_DECIMAL := aPerg[nX][07]
				SX1->X1_GSC     := aPerg[nX][08]
				SX1->X1_VAR01	:= aPerg[nX][09]
				SX1->X1_CNT01   := aPerg[nX][10]				
				
			SX1->(MsUnlock())

		EndIf
		
		If SPF_SEEK("SIGAHLP.HLP", 'P.' + aPerg[nX][01] + aPerg[nX][02] + '.', 1) < 0
			SPF_INSERT("SIGAHLP.HLP", 'P.' + aPerg[nX][01] + aPerg[nX][02] + '.',,, aPerg[nX][11][1])
		EndIf
		
	Next
    	
    RestArea(aArea)

Return 
