#INCLUDE 'RWMAKE.CH'
#INCLUDE "TOTVS.CH"
#INCLUDE 'PROTHEUS.CH'
#INCLUDE 'TOPCONN.CH'
#INCLUDE "TBICONN.CH"
#INCLUDE "TBICODE.CH"
#INCLUDE "REPORT.CH"

/* 
-------------------------------------------------------------------------------
	{Protheus.doc}<SF0203X>
	
	 PRODUTOS FRACASSADOS 
	
	@Parametros<Nil> 
	@author<Walmir Junior> 
	@since<02/02/2016> 
	@version
		< 
		--------------------------------------------------------------------
		 Walmir Junior                                          
		 Custo de Produtos em Ata de Registro de Preços.
		--------------------------------------------------------------------
		>
	@receive<Nil>
	@return<Nil>
	@example<Nil> 
	@see<Nil> 
-------------------------------------------------------------------------------
*/

User Function SF0203X()

Local _cPerg 		:= "SF0203X"
Local _cTitulo		:= "Consumo de Ata de Registro de Preços por Centro de Custo" 		// Titulo
Private _oReport	:= Nil 
Private _oSection1	:= Nil 
Private _aForn		:= {}
Private _cAlias		:= GetNextAlias()

CriaSX1(_cPerg)

//--
_oReport := TReport():New("SF0203X",_cTitulo,_cPerg,{|_oReport| PrintReport(_oReport)} )

//_oReport:SetPortrait()	// Orientado como retrato.
_oReport:SetLandscape()
_oReport:SetEdit(.T.)
_oReport:SetTotalInLine(.F.)

//--Sessão Centro de Custo
_oSection1 := TRSection():New(_oReport,"Consumo de Ata de Registro de Preços por Centro de Custo", {_cAlias})
//_oSection1:lHeaderSection := .F.
_oSection1:nLinesBefore := 0
_oSection1:SetTotalInLine(.T.)
_oSection1:SetReadOnly()
_oSection1:SetAutoSize(.T.)
//_oSection1:lHeaderVisible := .F.

//--Sessão Itens por Centro de Custos
_oSecContr := TRSection():New(_oReport,"Ata Registro Preço", {_cAlias})
//_oSection1:lHeaderSection := .F.
_oSecContr:nLinesBefore := 0
_oSecContr:SetTotalInLine(.T.)
_oSecContr:SetReadOnly()

//--Sessão Itens por Centro de Custos
_oSecItens := TRSection():New(_oReport,"Itens por Centro de Custos", {_cAlias})
//_oSection1:lHeaderSection := .F.
_oSecItens:nLinesBefore := 0
_oSecItens:SetTotalInLine(.T.)
_oSecItens:SetReadOnly()

//Célula Centro de Custo
TRCell():New(_oSection1,"CTT_CUSTO"		, _cALias ,"C CUSTO" 		, "@!"				,180)
TRCell():New(_oSection1,"CTT_DESC01"	, _cALias ,"DESC CC" 		, "@!"				,180)

//TRCell():New(_oSection1,"CN9_FILIAL"	, _cALias ,"FILIAL" 		, "@!" 	,009)
TRCell():New(_oSecContr,"CN9_NUMERO"	, _cALias ,"CONTRATO" 		, "@!"				,180)
TRCell():New(_oSecItens,"CNA_NUMERO"  	, _cALias ,"PLANILHA"		,					,100)
TRCell():New(_oSecItens,"CNA_XDESC"  	, _cALias ,"DESC PLAN"		,					,180)
//TRCell():New(_oSection1,"CNA_REVISA"  , _cALias ,"REVISAO"		,		,120)
//TRCell():New(_oSection1,"CNA_FORNEC"	, _cALias ,"COD FOR" 		, "@!"	,50	)
TRCell():New(_oSecItens,"A2_NOME"  		, _cALias ,"FORNECEDOR"		,					,120)
TRCell():New(_oSecItens,"C1_NUM"		, _cALias ,"SC" 			, "@!"				,100)
TRCell():New(_oSecItens,"CNB_PRODUT"  	, _cALias ,"COD PRD"		,					,150)
TRCell():New(_oSecItens,"CNB_DESCRI"  	, _cALias ,"PRODUTO"		,					,180)
TRCell():New(_oSecItens,"CNB_UM"  		, _cALias ,"UN MEDIDA"		,					,50	)
TRCell():New(_oSecItens,"C1_QUANT"  	, _cALias ,"QUANT SC"		, "@E 9,999,999.99"	,120)
TRCell():New(_oSecItens,"CNB_VLUNIT"  	, _cALias ,"VLR UNIT"		,					,120)
TRCell():New(_oSecItens,"VLRTOT"  		, _cALias ,"VLR TOT"		, "@E 9,999,999.99"	,120,,,,,"RIGHT")
TRCell():New(_oSecItens,"CNB_SLDMED"  	, _cALias ,"SALDO"			, "@E 9,999,999.99"	,120)
TRCell():New(_oSecItens,"SLDVLRTOT"  	, _cALias ,"SALD VLR TOT"	, "@E 9,999,999.99" ,120,,,,,"RIGHT")


_oSecItens:AutoSize()
_oReport:PrintDialog()

Return Nil

/*
+----------+-----------+-------+-------------------+------+-------------+
|Programa  |PRINTREPORT|Autor  |Andre Vicente      | Data |03/11/2013   |
+----------+-----------+-------+-------------------+------+-------------+
|Descrição | Implementa a Rotina  PrintReport                           |
|          |                                                            |
+----------+------------------------------------------------------------+
|Uso       | AP                                                         |
+----------+------------------------------------------------------------+
*/
Static Function PrintReport(_oReport)
Local _cCCusto
Local _cContr
MsgRun( "Selecionando registros...", "Aguarde" ,  {|| GeraQuery() } )

While (_cAlias)->(!EoF())
	_cCCusto := (_cAlias)->(CTT_CUSTO)
	_oReport:Section(1):Init()
	_oReport:Section(1):PrintLine()
	While (_cAlias)->(CTT_CUSTO) = _cCCusto
		_cContr := (_cAlias)->(CN9_NUMERO)
		_oReport:Section(2):Init()
		_oReport:Section(2):PrintLine()
		_oReport:Section(3):Init()
		While (_cAlias)->(CN9_NUMERO) = _cContr .And. (_cAlias)->(CTT_CUSTO) = _cCCusto
			_oReport:Section(3):PrintLine()
			(_cAlias)->(dbSkip())
		EndDo
		_oReport:Section(3):Finish()
		_oReport:Section(2):Finish()
	EndDo
	_oReport:Section(1):Finish()
EndDo
//_oReport:Section(1):Print()

Return

//ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
// funcao que realiza o sql do relatorio
//ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
Static Function GeraQuery()
Local _cSql := ""
Local _cFilial := IIF(EMPTY(MV_PAR01), xFilial("CN9"), StrTran( MV_PAR01, ";", "','"))
Local _cNumCN9 := IIF(EMPTY(MV_PAR02), ""			 , StrTran( MV_PAR02, ";", "','"))
Local _cCCusto := IIF(EMPTY(MV_PAR03), ""			 , StrTran( MV_PAR03, ";", "','"))
//Local _cRevCN9 := IIF(EMPTY(MV_PAR03), "", MV_PAR03)

_cSql += " SELECT "+CRLF
_cSql += " 	CN9_FILIAL,"+CRLF
_cSql += " 	CN9_NUMERO,"+CRLF
_cSql += " 	CTT_CUSTO,"+CRLF
_cSql += " 	CTT_DESC01,"+CRLF
_cSql += " 	C1_NUM,"+CRLF
_cSql += " 	C1_QUANT,"+CRLF
_cSql += " 	CNA_FORNEC,"+CRLF
_cSql += " 	CNA_NUMERO,"+CRLF
_cSql += " 	CNA_XDESC,"+CRLF
_cSql += " 	CNA_REVISA,"+CRLF
_cSql += " 	A2_NOME,"+CRLF
_cSql += " 	CNB_ITEM,"+CRLF
_cSql += " 	CNB_PRODUT,"+CRLF
_cSql += " 	CNB_DESCRI,"+CRLF
_cSql += " 	CNB_UM,"+CRLF
_cSql += " 	CNB_QUANT,"+CRLF
_cSql += " 	CNB_SLDMED,"+CRLF
_cSql += " 	CNB_VLUNIT,	"+CRLF
_cSql += " 	(C1_QUANT * CNB_VLUNIT)As VLRTOT,	"+CRLF
_cSql += " 	(CNB_VLTOT - (CNB_VLUNIT * (CNB_QUANT - CNB_SLDMED))) As SLDVLRTOT	"+CRLF
_cSql += " FROM "+CRLF
_cSql += " 	"+RetSqlName("CN9")+" CN9 INNER JOIN "+CRLF
_cSql += " 	"+RetSqlName("CNA")+" CNA ON CNA.CNA_FILIAL = CN9.CN9_FILIAL AND CNA.CNA_CONTRA = CN9.CN9_NUMERO AND CNA.CNA_REVISA = CN9.CN9_REVISA AND CNA.D_E_L_E_T_ = ' ' INNER JOIN "+CRLF
_cSql += " 	"+RetSqlName("CNB")+" CNB ON CNB.CNB_FILIAL = CNA.CNA_FILIAL AND CNB.CNB_CONTRA = CNA.CNA_CONTRA AND CNB.CNB_REVISA = CNA.CNA_REVISA AND CNB.CNB_NUMERO = CNA.CNA_NUMERO AND CNB.D_E_L_E_T_ = ' ' INNER JOIN "+CRLF
_cSql += " 	"+RetSqlName("SA2")+" SA2 ON SA2.A2_COD = CNA.CNA_FORNEC AND SA2.A2_LOJA = CNA.CNA_LJFORN AND SA2.D_E_L_E_T_ = ' ' INNER JOIN "+CRLF
_cSql += " 	"+RetSqlName("SC1")+" SC1 ON CN9.CN9_NUMERO = SC1.C1_XCONTPR AND CNA.CNA_NUMERO = SC1.C1_XCONTPL AND CNB.CNB_ITEM = SC1.C1_XCONTIT AND SC1.D_E_L_E_T_ = ' ' INNER JOIN "+CRLF
_cSql += " 	"+RetSqlName("CTT")+" CTT ON RTRIM(LTRIM(CTT.CTT_FILIAL)) = SUBSTR(SC1.C1_FILIAL,1, 4) AND SC1.C1_CC = CTT.CTT_CUSTO AND CTT.D_E_L_E_T_ = ' ' "+CRLF
_cSql += " WHERE "+CRLF
_cSql += " 	CN9.D_E_L_E_T_ = ' ' AND"+CRLF
_cSql += " 	CN9.CN9_SITUAC = '05' AND"+CRLF
_cSql += " 	CN9.CN9_FILIAL in ('"+_cFilial+"') AND"+CRLF
_cSql += " 	SC1.C1_QUJE = SC1.C1_QUANT AND SC1.C1_APROV = 'L' "+CRLF

If !Empty(_cCCusto)
	_cSql += " AND	CTT.CTT_CUSTO in ('"+_cCCusto+"') "+CRLF
EndIf
If !Empty(_cNumCN9)
	_cSql += " AND	CN9.CN9_NUMERO in ('"+_cNumCN9+"') "+CRLF
EndIf
_cSql += " 	Order By CTT_CUSTO, CN9_NUMERO, C1_NUM ,CNB_ITEM, CNA_FORNEC "

MemoWrite("C:\Temp\SF0203X.txt" , _cSql) // escreve em arquivo de texto e se nao encontrar cria o arquivo

If Select(_cALias) > 0
	(_cALias)->(dbClosearea())
Endif

DbUseArea(.T., "TOPCONN", TCGenQry(,,ChangeQuery(_cSql)), _cALias, .F., .F.)

Return


/*
+----------+----------+-------+--------------------+------+-------------+
|Programa  |CRIASX1   |Autor  |Andre Vicente       | Data |03/11/2013   |  
+----------+----------+-------+--------------------+------+-------------+
|Descrição | Cria o Grupo de perguntas para relatorio                   |
|          |                                                            |
+----------+------------------------------------------------------------+
|Uso       | AP                                                         |
+----------+------------------------------------------------------------+
*/
Static Function CriaSX1(_cPerg)
Local aHelp := {}
// Texto do help em portugues , ingles, espanhol
AAdd(aHelp, {{"Informe a Filial" }, {""}, {""}})
AAdd(aHelp, {{"Informe o Contrato"     }, {""}, {""}})
AAdd(aHelp, {{"Informe o Centro de Custos"     }, {""}, {""}})
//--
u_SFPUTSX1(_cPerg,"01","Filial?"			,"","","mv_ch1"	,"C",02,00,00,"R","","SM0"		,"","","mv_par01","","","","CN9_FILIAL"	,"","","","","","","","","","","","",aHelp[1,1]	,aHelp[1,2]	,aHelp[1,3]	,"")
u_SFPUTSX1(_cPerg,"02","Contrato?"			,"","","mv_ch2" ,"C",15,00,00,"R","","CN9001"   ,"","","mv_Par02","","","",""			,"","","","","","","","","","","","",aHelp[2,1] ,aHelp[2,2] ,aHelp[2,3] ,"")
u_SFPUTSX1(_cPerg,"03","Centro de Custos?"	,"","","mv_ch3" ,"C",15,00,00,"R","","CTT"   	,"","","mv_Par03","","","",""			,"","","","","","","","","","","","",aHelp[3,1] ,aHelp[3,2] ,aHelp[3,3] ,"")
//u_SFPUTSX1(_cPerg,"03","Revisão?"	,"","","mv_ch3" ,"C",08,00,00,"G","",""   		,"","","mv_Par03","","","",""			,"","","","","","","","","","","","",aHelp[2,1] ,aHelp[2,2] ,aHelp[2,3] ,"")

Return