#INCLUDE 'PROTHEUS.CH'
#INCLUDE 'RWMAKE.CH'
#INCLUDE 'FONT.CH'
#INCLUDE 'COLORS.CH' 
#INCLUDE 'TOPCONN.CH'

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ SF06A41X ³ Autor ³ Leonardo P. de Castro ³ Data ³ 05/11/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Locacao   ³                  ³Contato ³                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³Cadastro de Operadores de cobrança.                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Aplicacao ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Analista Resp.³  Data  ³                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³              ³  /  /  ³                                               ³±±
±±³              ³  /  /  ³                                               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/

User Function SF06A41X

Local _lAcesso := .F.
Private cCadastro := "Cadastro de Operadores de Cobrança"
Private aRotina := MenuDef()
Private cDelFunc := ".T." // Validacao para a exclusao. Pode-se utilizar ExecBlock
Private cString := "ZAF" 
Private _aCores := {	{"ZAF_ATIVO = 'S'", "BR_VERDE"},;
						{"ZAF_ATIVO = 'N'", "BR_VERMELHO"}}
Private _aUsers    := {} // Array com todos os usuarios do sistema CFG.
Private _aTcUsers  := {} // Array(posteriormente ordenado por nome) com todos os usuarios do sistema mas somente os dados necessarios para a tela.
Private _aAuxUsers := {} // Array com todos os usuarios do sistema mas somente os dados necessarios para a tela.
Private _aPrvFase  := {}
Private _dValida   := StoD("")

_lAcesso := U_TcSupOk()

If !_lAcesso
	Alert("Esta operação somente poderá ser realizada por um Supervisor!")
	Return()
EndIF

dbSelectArea("ZAF")
dbSetOrder(1)

mBrowse(6,1,22,75,cString,,,,,,_aCores,,,,,.F.,,)

Return()

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³MenuDef() ºAutor  ³Leonardo P de Castroº Data ³  04/11/2009 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Funcao que define o array contendo as funcoes disponeis no  º±±
±±º          ³menu principal da rotina.                                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ P10                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function MenuDef()

Local _aRet := {   	{"Pesquisar" ,"AxPesqui"  ,0,1,  ,.F.},;    		
					{"Visualizar","U_A002XVis",0,2,  ,.T.},;
					{"Incluir"   ,"U_A002XInc",0,3,  ,.T.},;
					{"Alterar"   ,"U_A002XAlt",0,4,82,.T.},;
					{"Fases Cob.","U_A002XFas",0,4,3 ,.T.},;
					{"Legenda"   ,"U_A002XLeg",0,2,  ,.T.}} 
   
Return( _aRet ) 

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ A002XInc  ³Autor  ³Leonardo P de Castro  ³ Data ³ 06/11/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Programa de Inclusao de Operadores                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ P10                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

User Function A002XInc(cAlias, nReg, nOpc)

Local _aBotao	:= {}
Local _aParam   := {}
Local _cUsuZAF	:= CriaVar("ZAF_CODUSU",.F.)       

Aadd(_aParam, {|| .T.} )
Aadd(_aParam, {|| .T.} )
Aadd(_aParam, {|| U_A002XTrans()})		// Executa dentro da transacao
Aadd(_aParam, {|| .T.} )

AAdd(_aBotao,{"BMPGROUP",{|| A002XUser(nOpc,@_cUsuZAF)},"Cadastro de Usuários","Usuarios"} )
AAdd(_aBotao,{"CLIPS",{|| U_A002XFas(cAlias, nReg, nOpc)},"Fases de Cobrança Autorizadas","Fases"} )

AxInclui(cAlias,nReg,nOpc,NIL,NIL,NIL,"U_A002XTudOk()",NIL,NIL,_aBotao,_aParam)

Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ A002XAlt  ³ Autor ³Leonardo P de Castro  ³ Data ³ 06/11/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Programa de Alteracao de Operadores                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ P10                                                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

User Function A002XAlt(cAlias, nReg, nOpc)

Local _aBotao  := {}
Local _aParam  := {}
Local _cUsuZAF := SU7->U7_CODUSU

Aadd(_aParam, {|| .T. } )
Aadd(_aParam, {|| .T. } )							 			
Aadd(_aParam, {|| U_A002XTrans()} )		// Executa dentro da transacao
Aadd(_aParam, {|| .T. } )

AAdd(_aBotao,{"BMPGROUP",{|| A002XUser(nOpc,@_cUsuZAF)},"Cadastro de Usuários","Usuarios"} )
AAdd(_aBotao,{"CLIPS",{|| U_A002XFas(cAlias, nReg, nOpc)},"Fases de Cobrança Autorizadas","Fases"} )

AxAltera(cAlias,nReg,nOpc,NIL,NIL,,NIL,"U_A002XTudOk()",NIL,NIL,_aBotao,_aParam)

Return(.T.)               

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ A002XVis  ³ Autor ³Leonardo P de Castro  ³ Data ³ 06/11/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Programa de Visuzalizacao de Operadores                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ P10                                                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

User Function A002XVis(cAlias, nReg, nOpc)

Local _aBotao  := {}
Local _cUsuZAF := ZAF->ZAF_CODUSU

AAdd(_aBotao,{"BMPGROUP",{|| A002XUser(nOpc,@_cUsuZAF)},"Cadastro de Usuários","Usuarios"} )
AAdd(_aBotao,{"CLIPS",{|| U_A002XFas(cAlias, nReg, nOpc)},"Fases de Cobrança Autorizadas","Fases"} )

AxVisual(cAlias, nReg, nOpc,,,,,_aBotao)

Return(.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍ»±±
±±³Fun‡„o    ³ A002XLeg  ³ Autor ³Leonardo P de Castro  ³ Data ³ 06/11/09 ³±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Legendas do cadastro                                       º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ P10                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

User Function A002XLeg()

BrwLegenda(cCadastro,"Legenda",	{	{"BR_VERDE"		,"Operador Ativo"},;   
									{"BR_VERMELHO"	,"Operador Não Ativo"}})


Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A002XTudOk ³ Autor ³Leonardo P de Castro  ³ Data ³ 06/11/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Validacao geral da tela de cadastro de operadores.          ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ P10                                                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

User Function A002XTudOk(nOpc)

Local _aArea := GetArea()
Local _lRet   := .T.

// Na inclusao valida se o codigo existe no banco de dados.
If INCLUI
	DbSelectArea("ZAF")
	DbSetOrder(1)
	If DbSeek(xFilial("ZAF") + M->ZAF_COD)
		Help( " ", 1, "EXISTOP" )
		_lRet := .F.
	Endif
Endif

If INCLUI .Or. (ALTERA .And. M->ZAF_CODUSU != ZAF->ZAF_CODUSU)
	DbSelectArea("ZAF")
	DbSetOrder(4)
	If DbSeek(xFilial("ZAF") + M->ZAF_CODUSU)
		Help( " ", 1, "OPER" )
		_lRet := .F.
	Endif
Endif

RestArea(_aArea)

Return(_lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³A002XUser ºAutor  ³Leonardo P de Castroº Data ³  04/11/09   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Janela para visualizacao dos usuarios do sistema.          º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ P10                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function A002XUser(_nOpc,_cUsuZAF)

Local _oDlg         
Local _oUsu    
Local _nCtrl   := 0
Local _nUsu    := 1      
Local _cNome   := "" 
Local _cNReduz := "" 

If Len(_aTcUsers) == 0
	_aUsers := AllUsers()

	For _nX := 1 To Len(_aUsers) 
		//Todos os usuarios menos o Administrador;
		If AllTrim(_aUsers[_nX,1,1]) != "000000"
			Aadd(_aAuxUsers,{_aUsers[_nX,1,4],_aUsers[_nX,1,13],_aUsers[_nX,1,12],_aUsers[_nX,1,1],_aUsers[_nX,1,2]})
		EndIF
	Next _nX

	_aTcUsers := ASort(_aAuxUsers,,,{|x, y| x[1] < y[1]})
EndIf
DEFINE MSDIALOG _oDlg TITLE "Cadastro de Usuários" FROM 00,00 TO 250,717 PIXEL OF oMainWnd //Cadastro de Usuarios

	@02,02 TO 120,325 LABEL "" PIXEL OF _oDlg
	@05,04 LISTBOX _oUsu FIELDS ;
				HEADER ;  	
				"Nome Completo","Cargo","Departamento","Código";     
				ON DbLCLICK (	_nCtrl 	 := 1,;
								_cNome	 := _aTcUsers[_oUsu:nAt,1],;
								_cNReduz := _aTcUsers[_oUsu:nAt,5],;
								_cUsuZAF := _aTcUsers[_oUsu:nAt,4],;
								_oDlg:End() );
				ON CHANGE (_nUsu := _oUsu:nAt);
	SIZE 320,115 OF _oDlg PIXEL NOSCROLL
	_oUsu:SetArray(_aTcUsers)
	_oUsu:bLine := {|| {_aTcUsers[_oUsu:nAt,1],_aTcUsers[_oUsu:nAt,2],_aTcUsers[_oUsu:nAt,3],_aTcUsers[_oUsu:nAt,4]}}	
	_oUsu:Refresh()

	If (_nOpc != 2) .AND. (_nOpc != 5) 
		DEFINE SBUTTON FROM 05,328 TYPE 1 ENABLE OF _oDlg ACTION (	_nCtrl	 := 1,;
																	_cNome	 := _aTcUsers[_oUsu:nAt,1],;
																	_cNReduz := _aTcUsers[_oUsu:nAt,5],;
																	_cUsuZAF := _aTcUsers[_oUsu:nAt,4],;
																	_oDlg:End() )
		DEFINE SBUTTON FROM 18,328 TYPE 2 ENABLE OF _oDlg ACTION _oDlg:End()
	Else
		DEFINE SBUTTON FROM 05,328 TYPE 2 ENABLE OF _oDlg ACTION _oDlg:End()	
	Endif

ACTIVATE MSDIALOG _oDlg CENTERED

If (_nCtrl == 1)
	M->ZAF_NOME	 := _cNome
	M->ZAF_NREDUZ := _cNReduz 
	M->ZAF_CODUSU := _cUsuZAF
Endif

Return(.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³ A002XFas ºAutor  ³Leonardo P de Castroº Data ³  04/11/09   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Janela amarracao de Fases e Operadores                     º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ P10                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

User Function A002XFas(cAlias, nReg, nOpc)

Local _oDlg         
Local _oFase    
Local oOk  := LoadBitmap( GetResources(), "LBOK" )
Local oNo  := LoadBitmap( GetResources(), "LBNO" )
Local _aFase := {}
Local _lTrans := Iif(nOpc=5,.F.,.T.)

//Zera variavel data valida
_dValida := StoD("")

DEFINE MSDIALOG _oDlg TITLE "Fases de Cobrança Autorizadas" FROM 00,00 TO 250,717 PIXEL OF oMainWnd

	@02,02 TO 120,325 LABEL "" PIXEL OF _oDlg
	@05,04 LISTBOX _oFase FIELDS ;
				HEADER ;  	
				" ","Código","Descrição","Dias Ven de","Dias Ven ate","Data Validade";     
				ON DbLCLICK (_aFase[_oFase:nAt,1]:=!(_aFase[_oFase:nAt,1]),;
							 U_A002XDat(_aFase[_oFase:nAt,1],_oFase:nAt,@_aFase,@_dValida),;
							 _oFase:Refresh()); 
	SIZE 320,115 OF _oDlg PIXEL NOSCROLL
	_aFase := fFasArray(_lTrans)
	_oFase:SetArray(_aFase)
	_oFase:bLine := {|| {If(_aFase[_oFase:nAT,1],oOk,oNo),_aFase[_oFase:nAt,2],;
	                     _aFase[_oFase:nAt,3],_aFase[_oFase:nAt,4],;
	                     _aFase[_oFase:nAt,5],_aFase[_oFase:nAt,6]}}	
	_oFase:Refresh()
	
	If _lTrans
		DEFINE SBUTTON FROM 05,328 TYPE 1 ENABLE OF _oDlg ACTION ( _aPrvFase := _aFase,_oDlg:End() )
    Else
		DEFINE SBUTTON FROM 05,328 TYPE 1 ENABLE OF _oDlg ACTION ( U_A002XGrv(ZAF->ZAF_COD, _aFase),_oDlg:End() )
	EndIf
	DEFINE SBUTTON FROM 18,328 TYPE 2 ENABLE OF _oDlg ACTION _oDlg:End()

ACTIVATE MSDIALOG _oDlg CENTERED

Return(.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³fFasArray ºAutor  ³Leonardo P de Castroº Data ³  04/11/09   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Funcao para carregar o Array de fases X operador           º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ P10                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function fFasArray(_lTrans)                                    

Local _aRet := {}
Local _cSql := ""
Local _nReg := 0
Local _nPos := 0   
Local _cCodZAF := Iif(_lTrans,M->ZAF_COD,ZAF->ZAF_COD)
               
_cSql := ""
_cSql += " SELECT COUNT(*) AS QTDE "
_cSql += " FROM " + RetSqlName("ZAE")
_cSql += " WHERE D_E_L_E_T_ <> '*' AND ZAE_MSBLQL = 'N'"
_cSql := ChangeQuery(_cSql)  

	If Select("tFAS") > 0
		DbSelectArea("tFAS")
		tFAS->(DbCloseArea())
	EndIF

TcQuery _cSql New Alias tFAS

_nReg := tFAS->QTDE

If _nReg > 0

	_cSql := ""
	_cSql += " SELECT * "
	_cSql += " FROM " + RetSqlName("ZAE")
	_cSql += " WHERE D_E_L_E_T_ <> '*' AND ZAE_MSBLQL = 'N'"
	_cSql += " ORDER BY ZAE_COD "
	_cSql := ChangeQuery(_cSql)  
	
		If Select("tFAS") > 0
			DbSelectArea("tFAS")
			tFAS->(DbCloseArea())
		EndIF
	
	TcQuery _cSql New Alias tFAS 
	
	//Carrego o Array com as fases ativas no momento.	
	While tFAS->(!Eof())		
		aAdd(_aRet,{.F.,;
					tFAS->ZAE_COD,;
					tFAS->ZAE_DESCRI,;
					tFAS->ZAE_PRZDE,;
					tFAS->ZAE_PRZATE,;
					StoD("//")})	
		tFAS->(DbSkip())
	EndDo
	
	//Verifico as fases amarradas atualmente
	DbSelectArea("ZAG")
	ZAG->(DbSetOrder(2))
	ZAG->(DbSeek(xFilial("ZAG") + _cCodZAF))
	
		While ZAG->(!Eof()) .And. ZAG->ZAG_CODOPE = _cCodZAF
		
			_nPos := aScan(_aRet, { |xx| xx[2] == ZAG->ZAG_CODFAS})
			
			If _nPos > 0
				_aRet[_nPos,1] := .T.
				_aRet[_nPos,6] := ZAG->ZAG_VALIDA
			EndIF			
			ZAG->(DbSkip())
		EndDo
	
Else 
	aAdd(_aRet,{.F.,"","Nào existem fases ativas! Verifique.",0,0,StoD("//")})
EndIF

Return(_aRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³fFasGrava ºAutor  ³Leonardo P de Castroº Data ³  04/11/09   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Funcao para carregar o Array de fases X operador           º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ P10                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

User Function A002XGrv(_cCodZAF, _aFasOpe)                                    

DbSelectArea("ZAG")
ZAG->(DbSetOrder(2))
ZAG->(DbSeek(xFilial("ZAG") + _cCodZAF))
    
//Deleto todos os registros gravados anteriormente.
While ZAG->(!Eof()) .And. ZAG->ZAG_CODOPE = _cCodZAF	
	If RecLock("ZAG",.F.)
			ZAG->(dbDelete())
		ZAG->(MsUnlock())
	EndIF	
	ZAG->(DbSkip())
EndDo
	
For _nX := 1 To Len(_aFasOpe)	
	//Gravo todos os registros gravados anteriormente.
	If _aFasOpe[_nX,1]
		If RecLock("ZAG",.T.)
				Replace ZAG->ZAG_FILIAL  With xFilial("ZAG") 
				Replace ZAG->ZAG_CODFAS With _aFasOpe[_nX,2]
				Replace ZAG->ZAG_CODOPE With _cCodZAF
				Replace ZAG->ZAG_VALIDA  With _aFasOpe[_nX,6]
			ZAG->(MsunLock())
		EndIF
	EndIf 
Next _nX

Return()

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A002XTrans ³ Autor ³Leonardo P de Castro  ³ Data ³ 06/11/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Rotina executada dentro da transacao                        ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ P10                                                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

User Function A002XTrans()

Local _aArea  := GetArea()
Local _lRet   := .T.
Local _lOk    := .T. 
Local _lCheck := .F.

//Nenhum dado foi carregado
If Empty(_aPrvFase)
	_lOk := .F.
Else
	//Verifico se depois de carregado, o usuario marcou algum registro
	For _nX := 1 To Len(_aPrvFase)
		If _aPrvFase[_nX,1]
			_lCheck := .T.
		EndIF	
	Next _nX
	
	If !_lCheck
		_lOk := .F.
	EndIf
EndIF

If _lOk .And. M->ZAF_ATIVO = "S"
	U_A002XGrv(M->ZAF_COD, _aPrvFase) 
	_aPrvFase := {}
Else
	MsgInfo("Nenhuma fase foi selcionada!") 
EndIF

Return(_lRet)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ A002XDat  ³ Autor ³Leonardo P de Castro  ³ Data ³ 06/11/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Exibe janela para informar a data de validade da fase       ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ P10                                                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

User Function A002XDat(_lMarca,_nAt,_aFase,_dValida)

// Variaveis Locais da Funcao
Local _dVal	  := StoD("")
Local _lCheck := .T.
Local _oVal
Local _oCheck
Local _oDlg 
Local _lClose := .F.

//So sera executado quando o usuario marcar o registro
If !_lMarca
	_aFase[_nAt,6] := StoD("")
	Return()
EndIf
     
//Se for maracao e a variavel nao estiver vazia, ja carrego a informacai armazenada
If !Empty(_dValida)
	_aFase[_nAt,6] := _dValida
	Return()
EndIf 

DEFINE MSDIALOG _oDlg TITLE "Fases de Cobrança" FROM 302,505 TO 418,743 PIXEL

	// Cria as Groups do Sistema
	@ 005,006 TO 042,111 LABEL " Data Validade " PIXEL OF _oDlg

	// Cria Componentes Padroes do Sistema
	@ 016,014 MsGet _oVal Var _dVal Size 049,009 COLOR CLR_BLACK Picture "@D 99/99/99" PIXEL OF _oDlg Valid NaoVazio(_dVal)
	@ 030,014 CheckBox _oCheck Var _lCheck Prompt "Aplicar data para todas as opções" Size 090,008 PIXEL OF _oDlg
	@ 045,080 Button "Ok" Size 030,009 PIXEL OF _oDlg ACTION(_lClose := .T., _oDlg:End())

ACTIVATE MSDIALOG _oDlg CENTERED Valid _lClose

_aFase[_nAt,6] := _dVal

If _lCheck
	_dValida := _dVal	
EndIF

Return(.T.)