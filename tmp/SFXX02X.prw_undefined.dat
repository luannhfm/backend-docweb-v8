#include "protheus.ch"
//#include "addbook.ch"
#include "tcbrowse.ch"

/*/
P_NAME -> usuario
P_PROG -> "SFXX02X"
P_TASK -> "GROUP"
P_TYPE -> nome do grupo
P_DEFS -> enderecos
/*/           

Static __cGroups

User Function SFXX02X()

	Local oDlg
	Local oBar
	Local aBtn := Array(5)
	Local cName := Space(10)
	Local oName
	Local oGroup
	Local aGroup := {}
	Local nGroup := 1
	Local oMail
	Local aMail := {}
	Local nMail := 0
	Local cSvAlias := Alias()
	Local oGroups
	Local lRet := .F.
	
	Local oFolder
	Local aPrompt := {}
	Local nOption := 1
	Local lCliente		:= VerSenha(126)
	Local lFornecedor	:= VerSenha(127)
	Local lVendedor		:= VerSenha(128)  
	Local lContato 		:= VerSenha(146)  
	Local oCliente
	Local oFornecedor
	Local oVendedor
	Local oContato
	
	Local oOrdCliente
	Local oOrdFornecedor
	Local oOrdVendedor
	Local oOrdContato
	
	Local cOrdCliente	 := ""
	Local cOrdFornecedor := ""
	Local cOrdVendedor	 := ""
	Local cOrdContato	 := ""
	
	Local aOrdCliente	 := {}
	Local aOrdFornecedor := {}
	Local aOrdVendedor	 := {}
	Local aOrdContato	 := {}
	
	Local oSrcCliente
	Local cSrcCliente
	
	Local oSrcFornecedor
	Local cSrcFornecedor
	
	Local oSrcVendedor
	Local cSrcVendedor
	
	Local oSrcContato
	Local cSrcContato
	
	Local nAlias
	Local nFolder := 1
	Local aAlias := {}
	
	Local oPanel0
	Local oPanel1
	Local oFld1P1
	Local oFld1P2
	Local oFld1P3
	Local oFld1P4
	
	Local oFld2P1
	Local oFld2P2
	Local oFld3P1
	Local oFld3P2
	Local oFld4P1
	Local oFld4P2
	Local oFld5P1
	Local oFld5P2
	
	__cGroups := ""
	
	Aadd( aPrompt, "Cat. Usuario")
	Aadd( aAlias, "")
	If lCliente
		Aadd( aPrompt, "Cat. Cliente")
		Aadd( aAlias, "SA1")
	EndIf
	If lFornecedor
		Aadd( aPrompt, "Cat. Fornecedor")
		Aadd( aAlias, "SA2")
	EndIf
	If lVendedor
		Aadd( aPrompt, "Cat. Vendendor")
		Aadd( aAlias, "SA3")
	EndIf
	
	If lContato
		Aadd( aPrompt, "Contatos")
		Aadd( aAlias, "SU5")
	EndIf
	
	OpenProfile()
	
	aGroup := LoadGroup()
	aMail := If(Empty(aGroup),aMail,LoadMail(aGroup[1]))
	nGroup := If(Empty(aGroup),0,1)
	
	DEFINE MSDIALOG oDlg TITLE /*STR0001*/ "Catálogo de Endereços" FROM 00,00 TO 370,430 PIXEL //"Address Book"
	
	DEFINE BUTTONBAR oBar 3D TOP OF oDlg
	
	DEFINE BUTTON aBtn[1] RESOURCE "NOVACELULA" OF oBar TOOLTIP /*STR0002"Incluir"*/ ; //"Incluir"
	ACTION If(EditEntry(aGroup),(aGroup := LoadGroup(),oGroup:SetItems(aGroup)),);
	WHEN (oFolder:nOption == 1)
		
	DEFINE BUTTON aBtn[2] RESOURCE "EDIT" OF oBar TOOLTIP /*STR0003"Editar"*/ ; //"Editar"
	ACTION If(nGroup <> 0,If(EditEntry(aGroup,nGroup,aMail),(aGroup := LoadGroup(),oGroup:SetItems(aGroup)),),);
	WHEN (oFolder:nOption == 1)
	
	DEFINE BUTTON aBtn[3] RESOURCE "EXCLUIR" OF oBar TOOLTIP /*STR0004"Delete"*/ ; //"Delete"
	ACTION If(nGroup <> 0,If(DelEntry(aGroup[nGroup]),(aGroup := LoadGroup(),oGroup:SetItems(aGroup)),),);
	WHEN (oFolder:nOption == 1)
	
	DEFINE BUTTON aBtn[4] RESOURCE "RESPONSA" OF oBar TOOLTIP /*STR0016"Selecionar"*/ ; //"Selecionar"
	ACTION AddGroup(oFolder:nOption,aAlias,@__cGroups,oGroups,aGroup,nGroup)
	
	DEFINE BUTTON aBtn[5] RESOURCE "OK" OF oBar TOOLTIP /*"OK"*/ ;
	ACTION If(!Empty(__cGroups),(lRet := .T.,oDlg:End()),)
	
	DEFINE BUTTON aBtn[5] RESOURCE "CANCEL" OF oBar TOOLTIP /*STR0005"Cancel"*/ ACTION oDlg:End() //"Cancel"
	
	@ 000, 000 MSPANEL oPanel0 PROMPT '' SIZE 030, 150 OF oDlg
	oPanel0:align := CONTROL_ALIGN_ALLCLIENT
	
	@ 100, 000 MSPANEL oPanel1 PROMPT '' SIZE 030, 020 OF oDlg
	oPanel1:align := CONTROL_ALIGN_BOTTOM
	
	oFolder := TFolder():New( 015, 005, aPrompt,, oPanel0, nOption,,, .T.,, __DlgWidth(oDlg)-10, __DlgHeight(oDlg)-35,)
	oFolder:bSetOption := {|| AddBkSetFocus(nOption,aBtn[1],aBtn[2],aBtn[3]) }
	oFolder:align := CONTROL_ALIGN_ALLCLIENT
	
	@ 000, 000 MSPANEL oFld1P1 PROMPT '' SIZE 030, 026 OF oFolder:aDialogs[1]
	oFld1P1:align := CONTROL_ALIGN_TOP
		
	@ 002, 005 SAY /*STR0006*/ "Digite o Nome ou Selecione na Lista:" OF oFld1P1 PIXEL SIZE 150,12 //"Type Name or Select from List:"
	@ 012, 005 GET oName VAR cName OF oFld1P1 PIXEL SIZE 80,10 VALID (nGroup := SeekGroup(cName,aGroup,nGroup),.T.)
	
	@ 000, 000 MSPANEL oFld1P2 PROMPT '' SIZE 100, 050 OF oFolder:aDialogs[1] 
	oFld1P2:align := CONTROL_ALIGN_LEFT
	
	@ 027, 005 LISTBOX oGroup VAR nGroup ITEMS aGroup OF oFld1P2 PIXEL SIZE 80,107 ;
	ON CHANGE (If(nGroup == 0,aMail := {},aMail := LoadMail(aGroup[nGroup])),oMail:SetItems(aMail))
	oGroup:align := CONTROL_ALIGN_ALLCLIENT
	
	@ 000, 000 MSPANEL oFld1P4 PROMPT '' SIZE 003, 050 OF oFolder:aDialogs[1] 
	oFld1P4:align := CONTROL_ALIGN_LEFT
		
	@ 080, 000 MSPANEL oFld1P3 PROMPT '' SIZE 080, 050 OF oFolder:aDialogs[1] 
	oFld1P3:align := CONTROL_ALIGN_ALLCLIENT
	
	@ 027, 087 LISTBOX oMail VAR nMail ITEMS aMail OF oFld1P3 PIXEL SIZE 110,107
	oMail:align := CONTROL_ALIGN_ALLCLIENT
	
	If lCliente
		
		dbSelectArea("SA1")
		nAlias := Select("SA1")
		nFolder+=1
		
		@ 000, 000 MSPANEL oFld2P1 PROMPT '' SIZE 030, 028 OF oFolder:aDialogs[nFolder]
		oFld2P1:align := CONTROL_ALIGN_ALLCLIENT
	
		@ 000, 000 MSPANEL oFld2P2 PROMPT '' SIZE 030, 028 OF oFolder:aDialogs[nFolder] 
		oFld2P2:align := CONTROL_ALIGN_BOTTOM
		
		oCliente:= TcBrowse():New( 02,02,(oFolder:nWidth/2)-6,(oFolder:nHeight/2)-50, , , , oFld2P1,,,,,,,,,,,, .F., "SA1", .T.,, .F., , ,.F. )
		oCliente:align := CONTROL_ALIGN_ALLCLIENT
		oCliente:lLineDrag	:= .T.
		oCliente:lColDrag	:= .T.
		oCliente:bLDbLClick := {|| Eval(aBtn[4]:bAction)}
		
		SX3->(dbSetOrder(1))
		SX3->(dbSeek("SA1"))
		While SX3->(!EOF()) .And. SX3->X3_ARQUIVO == "SA1"
			
			If ( __lPymeSX3 ) .And. ( SX3->X3_PYME == 'N' )
				SX3->(DbSkip())
				Loop
			EndIf
			
			If cNivel < SX3->X3_NIVEL  .Or. SX3->X3_CONTEXT == "V" .Or. !X3USO(SX3->X3_USADO) .Or. !( SX3->X3_BROWSE == "S" )
				SX3->(DbSkip())
				Loop
			EndIf
			
			oCliente:AddColumn( TCColumn():New( Trim(X3Titulo()), FieldWBlock( SX3->X3_CAMPO, nAlias ),AllTrim(SX3->X3_PICTURE),,, If(SX3->X3_TIPO=="N","RIGHT","LEFT"), CalcFieldSize(SX3->X3_TIPO,SX3->X3_TAMANHO,SX3->X3_DECIMAL,SX3->X3_PICTURE,X3Titulo()), .F., .F.,,,, .F., ) )
			
			SX3->(DbSkip())
		End
		oCliente:Gotop()
		
		SIX->(dbSetOrder(1))
		SIX->(dbSeek("SA1"))
		While SIX->(!Eof()) .And. SIX->INDICE == "SA1"
			Aadd( aOrdCliente, SIX->(SixDescricao()))
			SIX->(dbSkip())
		End
		
		SA1->(dbSetOrder(1))
		cSrcCliente := Space(Len(SA1->(Indexkey()))-2)
		
		@ 004, 005 SAY /*STR0017*/"" OF oFld2P2 PIXEL
		@ 002, 030 COMBOBOX oOrdCliente VAR cOrdCliente;
	    ITEMS aOrdCliente SIZE 120, 009 OF oFld2P2 PIXEL;
	    ON CHANGE ( SA1->(dbSetOrder(oOrdCliente:nAt)), cSrcCliente:=Space(Len(SA1->(Indexkey()))-2), oSrcCliente:Refresh() )
	 	
	 	@ 017, 005 SAY /*STR0018*/"" OF oFld2P2 PIXEL
	 	@ 015, 030 GET oSrcCliente VAR cSrcCliente SIZE 120,009 OF oFld2P2 PIXEL;
	 	ON CHANGE ( SA1->(dbSeek(xFilial()+RTrim(cSrcCliente))), oCliente:Refresh())
	 	
	EndIf
	
	If lFornecedor
		
		dbSelectArea("SA2")
		nAlias := Select("SA2")
		nFolder+=1
		
		@ 000, 000 MSPANEL oFld3P1 PROMPT '' SIZE 030, 028 OF oFolder:aDialogs[nFolder]
		oFld3P1:align := CONTROL_ALIGN_ALLCLIENT
	
		@ 000, 000 MSPANEL oFld3P2 PROMPT '' SIZE 030, 028 OF oFolder:aDialogs[nFolder] 
		oFld3P2:align := CONTROL_ALIGN_BOTTOM
		
		oFornecedor:= TcBrowse():New( 02,02,(oFolder:nWidth/2)-6,(oFolder:nHeight/2)-50, , , , oFld3P1,,,,,,,,,,,, .F., "SA2", .T.,, .F., , ,.F. )
		oFornecedor:align := CONTROL_ALIGN_ALLCLIENT
		oFornecedor:lLineDrag	:= .T.
		oFornecedor:lColDrag	:= .T.
		oFornecedor:bLDbLClick := {|| Eval(aBtn[4]:bAction)}
		
		SX3->(dbSetOrder(1))
		SX3->(dbSeek("SA2"))
		While SX3->(!EOF()) .And. SX3->X3_ARQUIVO == "SA2"
			
			If ( __lPymeSX3 ) .And. ( SX3->X3_PYME == 'N' )
				SX3->(DbSkip())
				Loop
			EndIf
			
			If cNivel < SX3->X3_NIVEL  .Or. SX3->X3_CONTEXT == "V" .Or. !X3USO(SX3->X3_USADO) .Or. !( SX3->X3_BROWSE == "S" )
				SX3->(DbSkip())
				Loop
			EndIf
			
			oFornecedor:AddColumn( TCColumn():New( Trim(X3Titulo()), FieldWBlock( SX3->X3_CAMPO, nAlias ),AllTrim(SX3->X3_PICTURE),,, If(SX3->X3_TIPO=="N","RIGHT","LEFT"), CalcFieldSize(SX3->X3_TIPO,SX3->X3_TAMANHO,SX3->X3_DECIMAL,SX3->X3_PICTURE,X3Titulo()), .F., .F.,,,, .F., ) )
			
			SX3->(DbSkip())
		End
		oFornecedor:Gotop()
		
		SIX->(dbSetOrder(1))
		SIX->(dbSeek("SA2"))
		While SIX->(!Eof()) .And. SIX->INDICE == "SA2"
			Aadd( aOrdFornecedor, SIX->(SixDescricao()))
			SIX->(dbSkip())
		End
		
		SA2->(dbSetOrder(1))
		cSrcFornecedor := Space(Len(SA2->(Indexkey()))-2)
		
		@ 004, 005 SAY /*STR0017*/"" OF oFld3P2 PIXEL
		@ 002, 030 COMBOBOX oOrdFornecedor VAR cOrdFornecedor;
	    ITEMS aOrdFornecedor SIZE 120, 009 OF oFld3P2 PIXEL;
	    ON CHANGE ( SA2->(dbSetOrder(oOrdFornecedor:nAt)), cSrcFornecedor:=Space(Len(SA2->(Indexkey()))-2), oSrcFornecedor:Refresh() )
	 	
	 	@ 017, 005 SAY /*STR0018*/"" OF oFld3P2 PIXEL
	 	@ 015, 030 GET oSrcFornecedor VAR cSrcFornecedor SIZE 120,009 OF oFld3P2 PIXEL;
	 	ON CHANGE ( SA2->(dbSeek(xFilial()+RTrim(cSrcFornecedor))), oFornecedor:Refresh())
	 	
	EndIf
	
	If lVendedor
		
		dbSelectArea("SA3")
		nAlias := Select("SA3")
		nFolder+=1
		
		@ 000, 000 MSPANEL oFld4P1 PROMPT '' SIZE 030, 028 OF oFolder:aDialogs[nFolder]
		oFld4P1:align := CONTROL_ALIGN_ALLCLIENT
	
		@ 000, 000 MSPANEL oFld4P2 PROMPT '' SIZE 030, 028 OF oFolder:aDialogs[nFolder] 
		oFld4P2:align := CONTROL_ALIGN_BOTTOM
		
		oVendedor:= TcBrowse():New( 02,02,(oFolder:nWidth/2)-6,(oFolder:nHeight/2)-50, , , , oFld4P1,,,,,,,,,,,, .F., "SA3", .T.,, .F., , ,.F. )
		oVendedor:align := CONTROL_ALIGN_ALLCLIENT
		oVendedor:lLineDrag	:= .T.
		oVendedor:lColDrag	:= .T.
		oVendedor:bLDbLClick := {|| Eval(aBtn[4]:bAction)}
		
		SX3->(dbSetOrder(1))
		SX3->(dbSeek("SA3"))
		While SX3->(!EOF()) .And. SX3->X3_ARQUIVO == "SA3"
			
			If ( __lPymeSX3 ) .And. ( SX3->X3_PYME == 'N' )
				SX3->(DbSkip())
				Loop
			EndIf
			
			If cNivel < SX3->X3_NIVEL  .Or. SX3->X3_CONTEXT == "V" .Or. !X3USO(SX3->X3_USADO) .Or. !( SX3->X3_BROWSE == "S" )
				SX3->(DbSkip())
				Loop
			EndIf
			
			oVendedor:AddColumn( TCColumn():New( Trim(X3Titulo()), FieldWBlock( SX3->X3_CAMPO, nAlias ),AllTrim(SX3->X3_PICTURE),,, If(SX3->X3_TIPO=="N","RIGHT","LEFT"), CalcFieldSize(SX3->X3_TIPO,SX3->X3_TAMANHO,SX3->X3_DECIMAL,SX3->X3_PICTURE,X3Titulo()), .F., .F.,,,, .F., ) )
			
			SX3->(DbSkip())
		End
		oVendedor:Gotop()
		
		SIX->(dbSetOrder(1))
		SIX->(dbSeek("SA3"))
		While SIX->(!Eof()) .And. SIX->INDICE == "SA3"
			Aadd( aOrdVendedor, SIX->(SixDescricao()))
			SIX->(dbSkip())
		End
		
		SA3->(dbSetOrder(1))
		cSrcVendedor := Space(Len(SA3->(Indexkey()))-2)
		
		@ 004, 005 SAY /*STR0017*/"" OF oFld4P2 PIXEL
		@ 002, 030 COMBOBOX oOrdVendedor VAR cOrdVendedor;
	    ITEMS aOrdVendedor SIZE 120, 009 OF oFld4P2 PIXEL;
	    ON CHANGE ( SA3->(dbSetOrder(oOrdVendedor:nAt)), cSrcVendedor:=Space(Len(SA3->(Indexkey()))-2), oSrcVendedor:Refresh() )
	 	
	 	@ 017, 005 SAY /*STR0018*/"" OF oFld4P2 PIXEL
	 	@ 015, 030 GET oSrcVendedor VAR cSrcVendedor SIZE 120,009 OF oFld4P2 PIXEL;
	 	ON CHANGE ( SA3->(dbSeek(xFilial()+RTrim(cSrcVendedor))), oVendedor:Refresh())
	 	
	EndIf
	
	If lContato
		
		dbSelectArea("SU5")
		nAlias := Select("SU5")
		nFolder+=1
		
		@ 000, 000 MSPANEL oFld5P1 PROMPT '' SIZE 030, 028 OF oFolder:aDialogs[nFolder]
		oFld5P1:align := CONTROL_ALIGN_ALLCLIENT
	
		@ 000, 000 MSPANEL oFld5P2 PROMPT '' SIZE 030, 028 OF oFolder:aDialogs[nFolder] 
		oFld5P2:align := CONTROL_ALIGN_BOTTOM
		
		oContato:= TcBrowse():New( 02,02,(oFolder:nWidth/2)-6,(oFolder:nHeight/2)-50, , , , oFld5P1,,,,,,,,,,,, .F., "SU5", .T.,, .F., , ,.F. )
		oContato:align := CONTROL_ALIGN_ALLCLIENT
		oContato:lLineDrag	:= .T.
		oContato:lColDrag	:= .T.
		oContato:bLDbLClick := {|| Eval(aBtn[4]:bAction)}
		
		SX3->(dbSetOrder(1))
		SX3->(dbSeek("SU5"))
		While SX3->(!EOF()) .And. SX3->X3_ARQUIVO == "SU5"
			
			If ( __lPymeSX3 ) .And. ( SX3->X3_PYME == 'N' )
				SX3->(DbSkip())
				Loop
			EndIf
			
			If cNivel < SX3->X3_NIVEL  .Or. SX3->X3_CONTEXT == "V" .Or. !X3USO(SX3->X3_USADO) .Or. !( SX3->X3_BROWSE == "S" )
				SX3->(DbSkip())
				Loop
			EndIf
			
			oContato:AddColumn( TCColumn():New( Trim(X3Titulo()), FieldWBlock( SX3->X3_CAMPO, nAlias ),AllTrim(SX3->X3_PICTURE),,, If(SX3->X3_TIPO=="N","RIGHT","LEFT"), CalcFieldSize(SX3->X3_TIPO,SX3->X3_TAMANHO,SX3->X3_DECIMAL,SX3->X3_PICTURE,X3Titulo()), .F., .F.,,,, .F., ) )
			
			SX3->(DbSkip())
		End
		oContato:Gotop()
		
		SIX->(dbSetOrder(1))
		SIX->(dbSeek("SU5"))
		While SIX->(!Eof()) .And. SIX->INDICE == "SU5"
			Aadd( aOrdContato, SIX->(SixDescricao()))
			SIX->(dbSkip())
		End
		
		SU5->(dbSetOrder(1))
		cSrcContato := Space(Len(SU5->(Indexkey()))-2)
		
		@ 004, 005 SAY /*STR0017*/"" OF oFld5P2 PIXEL
		@ 002, 030 COMBOBOX oOrdContato VAR cOrdContato;
	    ITEMS aOrdContato SIZE 120, 009 OF oFld5P2 PIXEL;
	    ON CHANGE ( SU5->(dbSetOrder(oOrdContato:nAt)), cSrcContato:=Space(Len(SU5->(Indexkey()))-2), oSrcContato:Refresh() )
	 	
	 	@ 017, 005 SAY /*STR0018*/"" OF oFld5P2 PIXEL
	 	@ 015, 030 GET oSrcContato VAR cSrcContato SIZE 120,009 OF oFld5P2 PIXEL;
	 	ON CHANGE ( SU5->(dbSeek(xFilial()+RTrim(cSrcContato))), oContato:Refresh())
	 	
	EndIf
	
	
	@ 006, 005 SAY /*STR0019*/"" PIXEL OF oPanel1
	//@171,05 SAY STR0019 PIXEL OF oPanel1
	@ 004, 030 GET oGroups VAR __cGroups READONLY PIXEL SIZE 168,10 OF oPanel1
	//@170,30 GET oGroups VAR __cGroups READONLY PIXEL SIZE 168,10 OF oPanel1
	ACTIVATE MSDIALOG oDlg CENTERED
	
	If !Empty(cSvAlias)
		DbSelectArea(cSvAlias)
	EndIf
	
Return __cGroups

Static Function EditEntry(aGroup,nGroup,aMail)

	Local i
	Local cGroup := Space(10)
	Local aList := {}
	Local nList := 1
	Local oList
	Local cMail := Space(120)
	Local lRet := .F.
	Local nLen
	
	DEFAULT nGroup := 0
	
	If nGroup <> 0
		cGroup := Padr(aGroup[nGroup],10)
		aList := aMail
	EndIf
	
	DEFINE MSDIALOG oDlg TITLE /*STR0007*/"" FROM 00,00 TO 153,260 PIXEL //"New Entry"
	
	@02,05 SAY /*STR0008*/"Nome do Grupo" PIXEL //"Group Name:"
	
	@12,05 GET cGroup PIXEL SIZE 80,10
	
	@27,05 LISTBOX oList VAR nList ITEMS aList SIZE 80,47 PIXEL
	
	@27,87 BUTTON /*STR0009*/"&Add" PIXEL SIZE 40,11 ; //"&Add"
	ACTION (cMail := AddMail(),If(!Empty(cMail),(Aadd(aList,cMail),oList:SetItems(aList)),))
	
	@39,87 BUTTON /*STR0010*/"&Remover" PIXEL SIZE 40,11 WHEN !Empty(aList) ; //"&Remove"
	ACTION If(nList > 0,(Adel(aList,nList),Asize(aList,Len(aList)-1),oList:SetItems(aList)),)
	
	@51,87 BUTTON "&OK" PIXEL SIZE 40,11 WHEN (!Empty(cGroup) .And. !Empty(aList)) ;
	ACTION If(ValEntry(nGroup,cGroup,aGroup),(lRet := .T.,oDlg:End()),)
	
	@63,87 BUTTON /*STR0011*/"&Cancelar" PIXEL SIZE 40,11 ACTION oDlg:End() //"&Cancel"
	
	ACTIVATE MSDIALOG oDlg CENTERED
	
	If lRet
		cMail := ""
		nLen := Len(aList)
		For i := 1 To nLen
			cMail += Trim(aList[i])
			If i <> nLen
				cMail += ";"
			EndIf
		Next
		
		If nGroup == 0
			WriteNewProf(__cUserID,"SFXX02X","GROUP",cGroup,cMail)
		Else
			WriteProfDef(__cUserID,"SFXX02X","GROUP",aGroup[nGroup],__cUserID,"SFXX02X","GROUP",cGroup,cMail)
		EndIf
	EndIf

Return lRet

Static Function ValEntry(nGroup,cGroup,aGroup)

	Local lRet := .T.
	Local nPos
	
	nPos := Ascan(aGroup,{|x| Trim(x) == Trim(cGroup)})
	
	If nGroup == 0
		If nPos <> 0
			MSGINFO(/*STR0012*/"Nome em uso","Atenção"/*STR0013*/) //"Group Name already exist." # "Attention"
			lRet := .F.
		EndIf
	Else
		If nGroup <> nPos
			MSGINFO(/*STR0012*/"Nome em uso","Atenção"/*STR0013*/)  //"Group Name already exist." # "Attention"
		EndIf
	EndIf

Return lRet

Static Function AddMail()

	Local oDlg
	Local cRet := Space(120)
	
	DEFINE MSDIALOG oDlg TITLE /*STR0014*/"E-mail" FROM 00,00 TO 70,300 PIXEL //"E-mail Address"
	
	@05,05 GET cRet PIXEL SIZE 142,10 VALID (!(";"$cRet) .And. ("@"$cRet) .And. !(" "$AllTrim(cRet)))
	
	@21,117 BUTTON "&OK" PIXEL SIZE 30,11 ACTION oDlg:End()
	
	ACTIVATE MSDIALOG oDlg CENTERED

Return cRet

Static Function LoadGroup()

	Local aRet := {}
	Local cWABUser := cUserName
	
	DbSelectArea("PROFALIAS")
	DbSetOrder(1)
	DbSeek(cWABUser+"SFXX02X    GROUP")
	If !Eof()
		While !Eof() .And. (PROFALIAS->P_NAME == cWABUser .And. Trim(PROFALIAS->P_PROG) == "SFXX02X" .And. Trim(PROFALIAS->P_TASK) == "GROUP")
			Aadd(aRet,PROFALIAS->P_TYPE)
			DbSkip()
		End
	Else
		DbSeek(Padr(__cUserID, 15)+"SFXX02X    GROUP")
		While !Eof() .And. (PROFALIAS->P_NAME == Padr(__cUserID, 15) .And. Trim(PROFALIAS->P_PROG) == "SFXX02X" .And. Trim(PROFALIAS->P_TASK) == "GROUP")
			Aadd(aRet,PROFALIAS->P_TYPE)
			DbSkip()
		End
	EndIf

Return aRet

Static Function LoadMail(cGroup)

	Local cMail
	Local aRet := {}
	Local nAt
	Local cWABUser := cUserName
	
	cMail := RetProfDef(__cUserID,"SFXX02X","GROUP",cGroup)
	If Empty(cMail)
		cMail := RetProfDef(cWABUser,"SFXX02X","GROUP",cGroup)
	EndIf
	While !Empty(cMail)
		nAt := At(";",cMail)
		If nAt > 0
			Aadd(aRet,Subs(cMail,1,nAt-1))
			cMail := Subs(cMail,nAt+1)
		Else
			Aadd(aRet,Trim(cMail))
			cMail := ""
		EndIf
	End

Return aRet

Static Function SeekGroup(cName,aGroup,nGroup)

	Local nRet
	
	DEFAULT cName := ""
	DEFAULT aGroup := {}
	DEFAULT nGroup := 1
	
	cName := Trim(cName)
	nRet := Ascan(aGroup,{|x| Trim(Upper(SubStr(x,1,Len(cName)))) == Upper(cName)})
	nRet := If(nRet == 0,nGroup,nRet)

Return nRet

Static Function DelEntry(cGroup)

	Local lRet := .F.
	
	DEFAULT cGroup := ""
	
	If MsgYesNo(/*STR0015*/"Tem certeza de que pretende remover permanentemente o grupo selecionado deste livro de endereços?",/*STR0013*/"Atenção") //"Are you sure that you want to permanently remove the selected group from this Address Book?" # "Attention"
		EraseProfDef(__cUserID,"SFXX02X","GROUP",cGroup)
		lRet := .T.
	EndIf

Return lRet

Static Function RetEmailGroup()

	DEFAULT __cGroups := ""

Return __cGroups+Space(600-Len(__cGroups))

Static Function WABRead(cGroup)

	Local cSend := ""                    
	Local cRet := ""
	
	DEFAULT cGroup := ""
	
	cRet := Alltrim(RetProfDef(__cUserID,"SFXX02X","GROUP",cGroup))
	If Empty(cRet)
		cRet := Alltrim(RetProfDef(cUserName,"SFXX02X","GROUP",cGroup))
	EndIf	
	cSend += Alltrim(cRet)

Return cSend

Static Function WAB2SXB()

	Local cSvAlias := Alias()
	
	If Select("SXB") > 0
		DbSelectARea("SXB")
		DbSeek(Padr("_EM",Len(SXB->XB_ALIAS))+"101RE")
		If Eof()
			RecLock("SXB",.T.)
			SXB->XB_ALIAS := "_EM"
			SXB->XB_TIPO := "1"
			SXB->XB_SEQ := "01"
			SXB->XB_COLUNA := "RE"
			SXB->XB_DESCRI := "Address Book"
			SXB->XB_DESCSPA := "Address Book"
			SXB->XB_DESCENG := "Address Book"
			SXB->XB_CONTEM := "SX5"
			MsUnlock()
			RecLock("SXB",.T.)
			SXB->XB_ALIAS := "_EM"
			SXB->XB_TIPO := "2"
			SXB->XB_SEQ := "01"
			SXB->XB_COLUNA := "01"
			SXB->XB_CONTEM := "SFXX02X()"
			MsUnlock()
			RecLock("SXB",.T.)
			SXB->XB_ALIAS := "_EM"
			SXB->XB_TIPO := "5"
			SXB->XB_SEQ := "01"
			SXB->XB_COLUNA := "01"
			SXB->XB_CONTEM := "RETEMAILGROUP()"
			MsUnlock()
			DbCommit()
		EndIf
	EndIf
	
	If !Empty(cSvAlias)
		DbSelectArea(cSvAlias)
	EndIf

Return

// -------------------------------

Static Function AddBkSetFocus(nOption,oBut1,oBut2,aBut3)

	If nOption == 1
		oBut1:Enable()
		oBut2:Enable()
		aBut3:Enable()
	Else
		oBut1:Disable()
		oBut2:Disable()
		aBut3:Disable()
	EndIf

Return

// -------------------------------

Static Function AddGroup(nOption,aAlias,__cGroups,oGroups,aGroup,nGroup)

	If nOption == 1
		If (nGroup <> 0)
			If !(Trim(aGroup[nGroup])+";"$__cGroups)
				__cGroups += Trim(aGroup[nGroup])+";"
				oGroups:Refresh()
			EndIf
		EndIf
	Else
		If aAlias[nOption] == "SA1"
			If SA1->(!Eof())
				If Empty(SA1->A1_EMAIL) 
					MsgAlert(/*STR0021*/"Não ha e-mail cadastrado para este cliente!",/*STR0020*/"Atenção")
				Else
					If !(Trim(Trim(SA1->A1_EMAIL))+";"$__cGroups)
						__cGroups += Trim(SA1->A1_EMAIL)+";"
						oGroups:Refresh()
					EndIf
				EndIf
			EndIf
		ElseIf aAlias[nOption] == "SA2"
			If SA2->(!Eof())
				If Empty(SA2->A2_EMAIL) 
					MsgAlert(/*STR0021*/"Não ha e-mail cadastrado para este fornecedor!",/*STR0020*/"Atenção")
				Else
					If !(Trim(Trim(SA2->A2_EMAIL))+";"$__cGroups)
						__cGroups += Trim(SA2->A2_EMAIL)+";"
						oGroups:Refresh()
					EndIf
				EndIf
			EndIf
		ElseIf aAlias[nOption] == "SA3"
			If SA3->(!Eof())
				If Empty(SA3->A3_EMAIL) 
					MsgAlert(/*STR0021*/"Não ha e-mail cadastrado para este vendedor!",/*STR0020*/"Atenção")
				Else
					If !(Trim(Trim(SA3->A3_EMAIL))+";"$__cGroups)
						__cGroups += Trim(SA3->A3_EMAIL)+";"
						oGroups:Refresh()
					EndIf
				EndIf
			EndIf
		ElseIf aAlias[nOption] == "SU5"
			If SU5->(!Eof())
				If Empty(SU5->U5_EMAIL) 
					MsgAlert(/*STR0021*/"",/*STR0020*/"")
				Else
					If !(Trim(Trim(SU5->U5_EMAIL))+";"$__cGroups)
						__cGroups += Trim(SU5->U5_EMAIL)+";"
						oGroups:Refresh()
					EndIf
				EndIf
			EndIf
		EndIf
	EndIf

Return