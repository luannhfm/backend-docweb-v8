#INCLUDE "TOTVS.CH"

/*/{Protheus.doc} SF05R04X
    @description Relatório de uso do parque
    @type user Function
    @author Rafael Karczevski
    @since 09/09/2019
    @version 1.0
    /*/
User Function SF05R04X

    Local oReport
    Local cPerg := "SF05R04X"
    Private cAlTmp := GetNextAlias()

    CriaSX1(cPerg)

    If Pergunte(cPerg)
        oReport := ReportDef(oReport)
        oReport:PrintDialog()
    EndIf

    Iif(Select(cAlTmp) > 0, (cAlTmp)->(dbCloseArea()), Nil)

Return

/*/{Protheus.doc} ReportDef
    @description Criação da estrutura do relatório
    @type Static Function
    @author Rafael Karczevski
    @since 09/09/2019
    @version 1.0
    /*/
Static Function ReportDef(p_cPerg)

    Local cTitle    := "Relatório de uso do Parque"
    Local cHelp     := "Permite gerar relatório para analises"
    Local oReport
    Local oSection1

    oReport := TReport():New("SF05R04X", cTitle, p_cPerg, {|oReport| ReportPrint(oReport)}, cHelp)
    oReport:SetPortrait()

    oSection1 := TRSection():New(oReport, "SF05R04X", {"ZH0"})

    TRCell():New(oSection1, "ZH0_DATA"  , cAlTmp, "Data"    ,, TamSx3("ZH0_DATA" )[01] + 2)
    TRCell():New(oSection1, "ZH0_DATUS" , cAlTmp, "Data Uso",, TamSx3("ZH0_DATUS" )[01] + 2)
    TRCell():New(oSection1, "Z00_CPF"   , cAlTmp, "CPF"     ,, TamSx3("Z00_CPF" )[01])
    TRCell():New(oSection1, "Z00_NOME"  , cAlTmp, "Nome"    ,, 40)
    TRCell():New(oSection1, "Z00_DTNASC", cAlTmp, "Dt Nascim",, TamSx3("Z00_DTNASC" )[01] + 2)
    TRCell():New(oSection1, "NOME"      , cAlTmp, "Nome Ind",, 40)
    TRCell():New(oSection1, "A1_CGC"    , cAlTmp, "CNPJ"    ,, TamSx3("A1_CGC" )[01])

Return oReport

/*/{Protheus.doc} ReportPrint
@description Rotina para montagem dos dados do relatÃ³rio.

@author Rafael Karczevski
@since 09/05/2018
@version 1.0
/*/
Static Function ReportPrint(oReport)
              
	Local oSecao1 := oReport:Section(1)
    oSecao1:Init()

	BeginSQL Alias cAlTmp
        SELECT
            ZH0_DATA, ZH0_DATUS, Z00_CPF, Z00_NOME, Z00_DTNASC, TRIM(A1_NOME) AS NOME, A1_CGC
        FROM %Table:ZH0% ZH0
        INNER JOIN %Table:Z00% Z00 ON
            ZH0_CPFCLI = Z00_CPF
            AND Z00.%NotDel%
        LEFT JOIN %Table:SA1% SA1 ON
            SA1.A1_COD = Z00_IND
            AND SA1.A1_LOJA = Z00_LOJA
            AND SA1.%NotDel%
        WHERE
            ZH0.%NotDel%
            AND ZH0_DATUS BETWEEN %Exp:dToS(mv_par01)% AND %Exp:dToS(mv_par02)%
            AND Z00_DTNASC BETWEEN %Exp:dToS(mv_par03)% AND %Exp:dToS(mv_par04)%
	EndSQL
	MemoWrite('C:\TEMP\SF05R04X.sql', GetLastQuery()[2])
    (cAlTmp)->(dbGoTop())

    While !(cAlTmp)->(eof())
        
        oSecao1:Cell("ZH0_DATA"  ):SetValue( dToC(sToD((cAlTmp)->(ZH0_DATA)))    )
        oSecao1:Cell("ZH0_DATUS" ):SetValue( dToC(sToD((cAlTmp)->(ZH0_DATUS)))   )
        oSecao1:Cell("Z00_DTNASC"):SetValue( dToC(sToD((cAlTmp)->(Z00_DTNASC)))  )
        oSecao1:Cell("Z00_CPF"   ):SetValue( (cAlTmp)->(Z00_CPF) )
        oSecao1:Cell("Z00_NOME"  ):SetValue( (cAlTmp)->(Z00_NOME))
        oSecao1:Cell("NOME"      ):SetValue( (cAlTmp)->(NOME)    )
        oSecao1:Cell("A1_CGC"    ):SetValue( (cAlTmp)->(A1_CGC)  )
        
        oSecao1:PrintLine()
        (cAlTmp)->(dbSkip())
    End
    
    (cAlTmp)->(DbCloseArea())
	oSecao1:Finish()

Return
   
/*/{Protheus.doc} CriaSX1
    @description Cria perguntas caso não exista
    @type Static Function
    @author Rafael Karczevski
    @since 09/09/2019
    @version 1.0
    /*/
Static Function CriaSX1(p_cPerg)

    Local aHelp	:= {}
	
	AAdd(aHelp, {{"Data de uso inicial."            }, {""}, {""}})
	AAdd(aHelp, {{"Data de uso final."              }, {""}, {""}})
	AAdd(aHelp, {{"Data de nascimento de."  	    }, {""}, {""}})
	AAdd(aHelp, {{"Data de nascimento ate."      	}, {""}, {""}})
	
	u_SFPutSx1( p_cPerg, "01","Data Uso De?    		"    , "", "", "mv_ch1", "D",8, 0, 0, "G","", ""	    , "", "", "mv_par01", "", "", "", "", "", "", "", "","","","","","","","",""                       , aHelp[1,1], aHelp[1,2], aHelp[1,3], "")
	u_SFPutSx1( p_cPerg, "02","Data Uso Ate?		"    , "", "", "mv_ch2", "D",8, 0, 0, "G","", ""		, "", "", "mv_par02", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", ""                      , aHelp[2,1], aHelp[2,2], aHelp[2,3], "")
	u_SFPutSx1( p_cPerg, "03","Data Nasc De?		"    , "", "", "mv_ch3", "D",8, 0, 0, "G","", ""		, "", "", "mv_par03", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", ""                      , aHelp[3,1], aHelp[3,2], aHelp[3,3], "")
	u_SFPutSx1( p_cPerg, "04","Data Nasc Ate?		"    , "", "", "mv_ch4", "D",8, 0, 0, "G","", ""	    , "", "", "mv_par04", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", ""                      , aHelp[4,1], aHelp[4,2], aHelp[4,3], "")
	
Return 