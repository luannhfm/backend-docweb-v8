#Include "Protheus.Ch"
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³MT110LEG  ºAutor  ³Microsiga           º Data ³  20/04/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ PE para manipulacao das cores da legenda                   º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±

Alteração..........: (09/07/2014) - Recriar tabela tipo SX5 chamada ZE para
                     que o relatório de processos por legenda seja exibido 
                     com status iguais aos das legendas do protheus.
                                          
±±ºUso       ³ P11 - SISTEMA INDUSTRIA                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

User Function MT110LEG()

Local _aRet    := aClone(ParamIXB[1])
Local _aRetNew := {}
Local _nX      := 0
Local _nY      := 0
Local _aArea   := GetArea()
Local _aAreaX5 := SX5->(GetArea())

Aadd(_aRet,{"PMSTASK3"  ,"Bloqueado por Orçamento" })
Aadd(_aRet,{"PMSEDT3"   ,"Aguardando Contigência" })
Aadd(_aRet,{"BR_CANCEL" ,"Orçamento Reprovado" })

//Adic. por Eduardo(DOIT) em 03/03/14
Aadd(_aRet,{"BPMSTSK1A" ,"Solicitação Pendente de Envio" }) 

//Adic. por Newton(TR) em 02/12/2016, incluido legenda, PE repassado posteriormente
aAdd(_aRet,{"BR_AZUL_CLARO"	, "TR em Analise" }) //Quando TR incluida/Anexa no Sistema
aAdd(_aRet,{"PMSTASK1"		, "TR em Correcao" }) //Quando TR recusada 

dbSelectArea("SX5")
SX5->(dbSetOrder(1))

For _nX := 1 to Len(_aMT110Cor)
	
   _nY := aScan(_aRet, {|x| Alltrim(Upper(x[1])) == Alltrim(Upper(_aMT110Cor[_nX][2]))})   
   aAdd(_aRetNew, {_aMT110Cor[_nX][2], StrZero(_nX,2) + " - " + _aRet[_nY][2]})
   
   If SX5->(!dbSeek(xFilial("SX5")+"ZE"+StrZero(_nX,2)))
   
      Reclock("SX5",.T.)
      Replace X5_FILIAL  With xFilial("SX5")
      Replace X5_TABELA  With "ZE"
      Replace X5_CHAVE   With StrZero(_nX,2)
      Replace X5_DESCRI  With _aRet[_nY][2]
      Replace X5_DESCSPA With _aRet[_nY][2]
      Replace X5_DESCENG With _aRet[_nY][2]
      SX5->(MsUnlock())
      
   Else
   
      If Alltrim(SX5->X5_DESCRI) <> Alltrim(_aRet[_nY][2])
      
         Reclock("SX5",.F.)
         Replace X5_DESCRI  With _aRet[_nY][2]
         Replace X5_DESCSPA With _aRet[_nY][2]
         Replace X5_DESCENG With _aRet[_nY][2]
         SX5->(MsUnlock())
         
      Endif
      
   Endif

Next _nX 

RestArea(_aAreaX5)
RestArea(_aArea)

Return (_aRetNew)//_aRet
