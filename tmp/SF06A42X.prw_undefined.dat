#INCLUDE 'PROTHEUS.CH'
#INCLUDE 'RWMAKE.CH'
#INCLUDE 'FONT.CH'
#INCLUDE 'COLORS.CH' 
#INCLUDE 'TOPCONN.CH'

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ SF06A42X ³ Autor ³ Leonardo P. de Castro ³ Data ³ 07/11/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Locacao   ³                  ³Contato ³                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³Cadastro de Motivos de cobrança.                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Aplicacao ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Analista Resp.³  Data  ³                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³              ³  /  /  ³                                               ³±±
±±³              ³  /  /  ³                                               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/

User Function SF06A42X

Local _lAcesso := .F.
Private cCadastro := "Cadastro de Motivos de Cobrança"
Private aRotina := MenuDef()
Private cDelFunc := ".T." // Validacao para a exclusao. Pode-se utilizar ExecBlock
Private cString := "ZAH" 
Private _aCores := {	{"ZAH_ATIVO = 'S'", "BR_VERDE"},;
						{"ZAH_ATIVO = 'N'", "BR_VERMELHO"}}
Private _aPrvFase  := {} 
Private _lAplcia   := .F.
Private _nPrazo    := 0
Private _cInicial  := "00:00"
Private _cFinal    := "00:00"

_lAcesso := U_TcSupOk()

If !_lAcesso
	Alert("Esta operação somente poderá ser realizada por um Supervisor!")
	Return()         
EndIF

dbSelectArea("ZAH") 
dbSetOrder(1)

mBrowse(6,1,22,75,cString,,,,,,_aCores,,,,,.F.,,)

Return()

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³MenuDef() ºAutor  ³Leonardo P de Castroº Data ³  07/11/2009 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Funcao que define o array contendo as funcoes disponeis no  º±±
±±º          ³menu principal da rotina.                                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ P10                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function MenuDef()

Local _aRet := {   	{"Pesquisar" ,"AxPesqui"  ,0,1,  ,.F.},;    		
					{"Visualizar","U_A003XVis",0,2,  ,.T.},;
					{"Incluir"   ,"U_A003XInc",0,3,  ,.T.},;
					{"Alterar"   ,"U_A003XAlt",0,4,82,.T.},;
					{"Fases Cob.","U_A003XFas",0,4,3 ,.T.},;
					{"Legenda"   ,"U_A003XLeg",0,2,  ,.T.}} 
   
Return( _aRet ) 

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ A003XInc  ³Autor  ³Leonardo P de Castro  ³ Data ³ 07/11/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Programa de Inclusao de Motivos                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ P10                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

User Function A003XInc(cAlias, nReg, nOpc)

Local _aBotao	:= {}
Local _aParam   := {}

Aadd(_aParam, {|| .T.} )
Aadd(_aParam, {|| .T.} )
Aadd(_aParam, {|| U_A003XTrans()})		// Executa dentro da transacao
Aadd(_aParam, {|| .T.} )

AAdd(_aBotao,{"CLIPS",{|| U_A003XFas(cAlias, nReg, nOpc)},"Fases de Cobrança Autorizadas","Fases"} )

AxInclui(cAlias,nReg,nOpc,NIL,NIL,NIL,,NIL,NIL,_aBotao,_aParam)

Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ A003XAlt  ³ Autor ³Leonardo P de Castro  ³ Data ³ 07/11/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Programa de Alteracao de Motivos                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ P10                                                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

User Function A003XAlt(cAlias, nReg, nOpc)

Local _aBotao  := {}
Local _aParam  := {}

Aadd(_aParam, {|| .T. } )
Aadd(_aParam, {|| .T. } )							 			
Aadd(_aParam, {|| U_A003XTrans()} )		// Executa dentro da transacao
Aadd(_aParam, {|| .T. } )

AAdd(_aBotao,{"CLIPS",{|| U_A003XFas(cAlias, nReg, nOpc)},"Fases de Cobrança Autorizadas","Fases"} )

AxAltera(cAlias,nReg,nOpc,NIL,NIL,,NIL,,NIL,NIL,_aBotao,_aParam)

Return(.T.)               

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ A003XVis  ³ Autor ³Leonardo P de Castro  ³ Data ³ 07/11/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Programa de Visuzalizacao de Motivos                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ P10                                                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

User Function A003XVis(cAlias, nReg, nOpc)

Local _aBotao  := {}

AAdd(_aBotao,{"CLIPS",{|| U_A003XFas(cAlias, nReg, nOpc)},"Fases de Cobrança Autorizadas","Fases"} )

AxVisual(cAlias, nReg, nOpc,,,,,_aBotao)

Return(.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍ»±±
±±³Fun‡„o    ³ A003XLeg  ³ Autor ³Leonardo P de Castro  ³ Data ³ 07/11/09 ³±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Legendas do cadastro                                       º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ P10                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

User Function A003XLeg()

BrwLegenda(cCadastro,"Legenda",	{	{"BR_VERDE"		,"Motivo Ativo"},;   
									{"BR_VERMELHO"	,"Motivo Não Ativo"}})

Return(.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³ A003XFas ºAutor  ³Leonardo P de Castroº Data ³  07/11/09   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Janela amarracao de Fases e Motivos                        º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ P10                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

User Function A003XFas(cAlias, nReg, nOpc)

Local _oDlg         
Local _oFase    
Local oOk  := LoadBitmap( GetResources(), "LBOK" )
Local oNo  := LoadBitmap( GetResources(), "LBNO" )
Local _aFase := {}
Local _lTrans := Iif(nOpc=5,.F.,.T.)

//Zera variavel Prazo, Hora inicial e Final
_lAplica  := .F.
_nPrazo   := 0
_cInicial := ""
_cFinal   := ""

DEFINE MSDIALOG _oDlg TITLE "Fases de Cobrança Autorizadas" FROM 00,00 TO 250,717 PIXEL OF oMainWnd

	@02,02 TO 120,325 LABEL "" PIXEL OF _oDlg
	@05,04 LISTBOX _oFase FIELDS ;
				HEADER ;  	
				" ","Código","Descrição","Dias Ven de","Dias Ven ate","Dias Max Age","Hora Inicio","Hora Final";  
				ColSizes 5,20,80,40,40,40,40,40;   
				ON DbLCLICK (_aFase[_oFase:nAt,1]:=!(_aFase[_oFase:nAt,1]),;
							 U_A003XInf(_aFase[_oFase:nAt,1],_oFase:nAt,@_aFase,@_nPrazo,@_cInicial,@_cFinal),;
							 _oFase:Refresh()); 
	SIZE 320,115 OF _oDlg PIXEL NOSCROLL
	_aFase := fFasArray(_lTrans)
	_oFase:SetArray(_aFase)
	_oFase:bLine := {|| {If(_aFase[_oFase:nAT,1],oOk,oNo),_aFase[_oFase:nAt,2],;
	                     _aFase[_oFase:nAt,3],_aFase[_oFase:nAt,4],;
	                     _aFase[_oFase:nAt,5],_aFase[_oFase:nAt,6],;
	                     _aFase[_oFase:nAt,7],_aFase[_oFase:nAt,8]}}	
	_oFase:Refresh()
	
	If _lTrans
		DEFINE SBUTTON FROM 05,328 TYPE 1 ENABLE OF _oDlg ACTION ( _aPrvFase := _aFase,_oDlg:End() )
    Else
		DEFINE SBUTTON FROM 05,328 TYPE 1 ENABLE OF _oDlg ACTION ( U_A003XGrv(ZAH->ZAH_COD, _aFase),_oDlg:End() )
	EndIf
	DEFINE SBUTTON FROM 18,328 TYPE 2 ENABLE OF _oDlg ACTION _oDlg:End()

ACTIVATE MSDIALOG _oDlg CENTERED

Return(.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³fFasArray ºAutor  ³Leonardo P de Castroº Data ³  07/11/09   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Funcao para carregar o Array de fases X Motivo             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ P10                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function fFasArray(_lTrans)                                    

Local _aRet := {}
Local _cSql := ""
Local _nReg := 0
Local _nPos := 0   
Local _cCodZAH := Iif(_lTrans,M->ZAH_COD,ZAH->ZAH_COD)
               
_cSql := ""
_cSql += " SELECT COUNT(*) AS QTDE "
_cSql += " FROM " + RetSqlName("ZAE")
_cSql += " WHERE D_E_L_E_T_ <> '*' AND ZAE_MSBLQL = 'N'"
_cSql := ChangeQuery(_cSql)  

	If Select("tFAS") > 0
		DbSelectArea("tFAS")
		tFAS->(DbCloseArea())
	EndIF

TcQuery _cSql New Alias tFAS

_nReg := tFAS->QTDE

If _nReg > 0

	_cSql := ""
	_cSql += " SELECT * "
	_cSql += " FROM " + RetSqlName("ZAE")
	_cSql += " WHERE D_E_L_E_T_ <> '*' AND ZAE_MSBLQL = 'N'"
	_cSql += " ORDER BY ZAE_COD "
	_cSql := ChangeQuery(_cSql)  
	
		If Select("tFAS") > 0
			DbSelectArea("tFAS")
			tFAS->(DbCloseArea())
		EndIF
	
	TcQuery _cSql New Alias tFAS 
	
	//Carrego o Array com as fases ativas no momento.	
	While tFAS->(!Eof())		
		aAdd(_aRet,{.F.,;
					tFAS->ZAE_COD,;
					tFAS->ZAE_DESCRI,;
					tFAS->ZAE_PRZDE,;
					tFAS->ZAE_PRZATE,;
					0,;
					"00:00",;
					"00:00"})	
		tFAS->(DbSkip())
	EndDo
	
	//Verifico as fases amarradas atualmente
	DbSelectArea("ZAI") 
	ZAI->(DbSetOrder(2))
	ZAI->(DbSeek(xFilial("ZAI") + _cCodZAH))
	
		While ZAI->(!Eof()) .And. ZAI->ZAI_CODMOT = _cCodZAH
		
			_nPos := aScan(_aRet, { |xx| xx[2] == ZAI->ZAI_CODFAS})
			
			If _nPos > 0
				_aRet[_nPos,1] := .T.
				_aRet[_nPos,6] := ZAI->ZAI_PRZAGE
				_aRet[_nPos,7] := ZAI->ZAI_HORADE
				_aRet[_nPos,8] := ZAI->ZAI_HORATE
			EndIF			
			ZAI->(DbSkip())
		EndDo
	
Else 
	aAdd(_aRet,{.F.,"","Nào existem fases ativas! Verifique.",0,0,0,"00:00","00:00"})
EndIF

Return(_aRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³ A003XGrv ºAutor  ³Leonardo P de Castroº Data ³  07/11/09   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Funcao para gravar o Array de fases X Motivo               º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ P10                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

User Function A003XGrv(_cCodZAH, _aFasMot)                                    

DbSelectArea("ZAI")
ZAI->(DbSetOrder(2))
ZAI->(DbSeek(xFilial("ZAI") + _cCodZAH))
    
//Deleto todos os registros gravados anteriormente.
While ZAI->(!Eof()) .And. ZAI->ZAI_CODMOT = _cCodZAH
	If RecLock("ZAI",.F.)
			ZAI->(dbDelete())
		ZAI->(MsUnlock())
	EndIF	
	ZAI->(DbSkip())
EndDo
	
For _nX := 1 To Len(_aFasMot)	
	//Gravo todos os registros gravados anteriormente.
	If _aFasMot[_nX,1]
		If RecLock("ZAI",.T.)
				Replace ZAI->ZAI_FILIAL  With xFilial("ZAI")
				Replace ZAI->ZAI_CODFAS With _aFasMot[_nX,2]
				Replace ZAI->ZAI_CODMOT  With _cCodZAH
				Replace ZAI->ZAI_PRZAGE  With _aFasMot[_nX,6]
				Replace ZAI->ZAI_HORADE  With _aFasMot[_nX,7]
				Replace ZAI->ZAI_HORATE  With _aFasMot[_nX,8]
			ZAI->(MsunLock())
		EndIF
	EndIf 
Next _nX

Return()

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A003XTrans ³ Autor ³Leonardo P de Castro  ³ Data ³ 06/11/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Rotina executada dentro da transacao                        ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ P10                                                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

User Function A003XTrans()

Local _aArea  := GetArea()
Local _lRet   := .T.
Local _lOk    := .T. 
Local _lCheck := .F.

//Nenhum dado foi carregado
If Empty(_aPrvFase)
	_lOk := .F.
Else
	//Verifico se depois de carregado, o usuario marcou algum registro
	For _nX := 1 To Len(_aPrvFase)
		If _aPrvFase[_nX,1]
			_lCheck := .T.
		EndIF	
	Next _nX
	
	If !_lCheck
		_lOk := .F.
	EndIf
EndIF

If _lOk
	U_A003XGrv(M->ZAH_COD, _aPrvFase)     
	_aPrvFase := {}
Else
	MsgInfo("Nenhuma fase foi selcionada!") 
EndIF

Return(_lRet)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ A003XInf  ³ Autor ³Leonardo P de Castro  ³ Data ³ 07/11/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Exibe janela para informar a data de validade da fase       ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ P10                                                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

User Function A003XInf(_lMarca,_nAt,_aFase,_nPrazo,_cInicial,_cFinal)

// Variaveis Locais da Funcao
Local _cFin	  := "00:00"
Local _cIni	  := "00:00"
Local _lCheck := .T.
Local _nPrz	  := 0
Local _oCheck
Local _oFin
Local _oIni
Local _oPrz
Local _lClose := .F.

//So sera executado quando o usuario marcar o registro
If !_lMarca
	_aFase[_nAt,6] := 0
	_aFase[_nAt,7] := "00:00"
	_aFase[_nAt,8] := "00:00"
	Return()
EndIf
     
//Se for maracao e a variavel nao estiver vazia, ja carrego a informacai armazenada
If _lAplica
	_aFase[_nAt,6] := _nPrazo
	_aFase[_nAt,7] := _cInicial
	_aFase[_nAt,8] := _cFinal
	Return()
EndIf 

DEFINE MSDIALOG _oDlg TITLE "Fases de Cobrança" FROM 284,328 TO 429,631 PIXEL

	// Cria as Groups do Sistema
	@ 003,004 TO 055,145 LABEL "" PIXEL OF _oDlg

	// Cria Componentes Padroes do Sistema
	@ 011,012 Say "Prazo dias" Size 026,008 COLOR CLR_BLACK PIXEL OF _oDlg
	@ 011,059 Say "Hora Início" Size 028,008 COLOR CLR_BLACK PIXEL OF _oDlg
	@ 011,101 Say "Hora Final" Size 025,008 COLOR CLR_BLACK PIXEL OF _oDlg
	@ 020,012 MsGet _oPrz Var _nPrz Size 035,009 COLOR CLR_BLACK Picture "@E 999" PIXEL OF _oDlg
	@ 020,059 MsGet _oIni Var _cIni Size 031,009 COLOR CLR_BLACK Picture "@E 99:99" PIXEL OF _oDlg Valid U_TcHoraOk(_cIni, "00:00", "23:59", .T.)
	@ 020,101 MsGet _oFin Var _cFin Size 031,009 COLOR CLR_BLACK Picture "@E 99:99" PIXEL OF _oDlg Valid U_TcHoraOk(_cFin, "00:00", "23:59", .T.)
	@ 036,013 CheckBox _oCheck Var _lCheck Prompt "Aplicar campos para todas as opções" Size 101,008 PIXEL OF _oDlg
	@ 059,113 Button "Ok" Size 030,009 PIXEL OF _oDlg Action(_lClose := .T., _oDlg:End())  

ACTIVATE MSDIALOG _oDlg CENTERED Valid _lClose

_aFase[_nAt,6] := _nPrz
_aFase[_nAt,7] := _cIni
_aFase[_nAt,8] := _cFin

If _lCheck 
	_lAplica  := _lCheck
	_nPrazo   := _nPrz
	_cInicial := _cIni
	_cFinal   := _cFin
EndIF

Return(.T.)