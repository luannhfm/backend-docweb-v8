#Include 'Protheus.ch'

/*/{Protheus.doc} FA050FIN
O ponto de entrada chamado no programa de inclusÃ£o de contas a pagar apos o fim do Begin Transaction.

Solicitante: Jorge - UNETEC
Solicitacao: Apos a inclusao do Contas a Pagar, ajustar a contabilizacao de determinados titulos

@author 	Jose Leite de Barros Neto
@since 	22/10/2014
@version 	1.0

@return Nil, Nulo

@see http://tdn.totvs.com/display/public/mp/FA050FIN+-+Tratamento+dos+dados+no+Contas+a+Pagar+--+11854
/*/
User Function FA050FIN()
	
	Local _aArea 	:= GetArea()
	Local _cFilial 	:= SE2->E2_FILIAL
	Local _cPrefixo	:= SE2->E2_PREFIXO
	Local _cNumero	:= SE2->E2_NUM
	Local _cTipo		:= SE2->E2_TIPO
	Local _cDesdobr	:= SE2->E2_DESDOBR
	Local _cFornece	:= SE2->E2_FORNECE
	Local _cLoja		:= SE2->E2_LOJA
	
	If Alltrim(_cPrefixo) == "CTR" .AND. Alltrim(_cTipo) == "CTR" .AND. _cDesdobr == "S"
		
		DbSelectArea("SE2")
		SE2->( DbSetOrder(1) )
		If SE2->( DbSeek(_cFilial + _cPrefixo + _cNumero) )
			
			While .Not. SE2->(Eof()) .And. SE2->(E2_FILIAL+E2_PREFIXO+E2_NUM+E2_TIPO+E2_FORNECE+E2_LOJA) == _cFilial+_cPrefixo+_cNumero+_cTipo+_cFornece+_cLoja
				
				If Reclock("SE2",.F.)
					
					If .Not. Empty(SE2->E2_BAIXA)
						SE2->E2_LA := ' '
					Else
						SE2->E2_LA := 'S'
					EndIf
					
					SE2->( MsUnlock() )
				EndIf
				
				SE2->(dbSkip())
			End
			
		EndIf
		
	EndIf
	
	RestArea(_aArea)
	
Return( Nil )