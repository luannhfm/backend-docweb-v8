#INCLUDE "PROTHEUS.CH"

#define COL_SELECAO 01
#define COL_TITULO 02
#define COL_TIPO 03
#define COL_PREFIXO 04
#define COL_PARCELA 05
#define COL_VENCIMENTO 06
#define COL_VALOR 07
#define COL_DECRECIMO 08
#define COL_VALOR_ATUAL 09
#define COL_JUROS 10
#define COL_MULTA 11
#define COL_JUROS_E_MULTA 12
#define COL_DIAS_DE_ATRASO 13
#define COL_PERCENT_DE_DESCONTO 14
#define COL_VALOR_DESCONTO_SOBRE_JUROS 15
#define COL_VALOR_A_RECEBER 16
#define COL_PERCE_DESCONTO_SOBRE_TITULO 17
#define COL_VALOR_DESCONTO_SOBRE_TITULO 18
#define COL_CHAVE_BUSCA_FINANCEIRO 19
#define COL_CHAVE_BUSCA_CLIENTE 20
#define COL_PERCENTUAL_DE_JUROS 21
#define COL_RECNO 22
#define COL_DATA_BAIXA_PARCIAL 23
#define COL_DATA_VENCIMENTO 24
#define COL_VALOR_DECRECIMO 25
#define COL_SALDO_DECRESCIMO 26

/*/{Protheus.doc} DTFINR02
Relatório Renegociação
@type function
@author Franklin de Brito de Oliveira
@since 20/09/2021
@param aDados, array, dados para impressão
/*/
User Function DTFINR02(aDados)

	Local oReport
	Local lTRepInUse := .T.
	
	If FindFunction("TRepInUse") .And. lTRepInUse
		oReport:= ReportDef(aDados)
		oReport:PrintDialog()
	EndIf
	
Return Nil                                                           	

/*/{Protheus.doc} ReportDef
Definições do relatório
@type function
@author j2a.luizjunior
@since 01/11/2016
@param aDados, array, Dados para impressão
@return object, Objeto do tipo TReport
/*/
Static Function ReportDef(aDados)
	
	Local cTitle     := "Projeção de Negociação"
	Local oReport
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Criacao do componente de impressao                                      ³
	//³                                                                        ³
	//³TReport():New                                                           ³
	//³ExpC1 : Nome do relatorio                                               ³
	//³ExpC2 : Titulo                                                          ³
	//³ExpC3 : Pergunte                                                        ³
	//³ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  ³
	//³ExpC5 : Descricao                                                       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oReport:= TReport():New("DTFINR02",cTitle,Nil/*"DTFINR02"*/, {|oReport| ReportPrint(oReport,aDados)},)
	
	oReport:SetLandScape(.T.)
	//oReport:SetPortrait()
	oReport:HideParamPage()
	//oReport:HideHeader()
	oReport:HideFooter()
	oReport:SetTotalInLine(.F.)
	oReport:ldisableorientation:=.T.

Return oReport

/*/{Protheus.doc} ReportPrint
Fluxo de impressão do relatório
@type function
@author j2a.luizjunior
@since 01/11/2016
@param oReport, object, objeto de impressão
@param aDados, array, dados para impressão
/*/
Static Function ReportPrint(oReport,aDados)

	Local nLin := 240
	Local nLinFinal := 2300
	Local nI := 0
	Local nColVal := 0700
	Local nCDesTit := nColVal+200
	Local nColDec := nCDesTit+200
	Local nColSal := nColDec+200
	Local nColMul := nColSal+230
	Local nColJur := nColMul+220
	Local nTotal := 0
	Local nTotVal := 0
	Local nTotMul := 0
	Local nTotJur := 0
	Local nTotJuM := 0
	Local nTotDes := 0
	Local nTotVDe := 0
	Local nTotDec := 0
	Local nTotSal := 0
	Local nTotDT := 0 
	Local nTotPerX := 0
	Local nTotVDT := 0
	
	oFont1  := TFont():New( 'Arial', 08, 08, .T., .T., 5, .T., 5, .F., .F. )
	oFont2  := TFont():New( 'Arial', 08, 08, .T., .F., 5, .T., 5, .F., .F. )
	oFont3  := TFont():New( 'Arial', 09, 09, .T., .F., 5, .T., 5, .F., .F. )
	oFont3N := TFont():New( 'Arial', 09, 09, .T., .T., 5, .T., 5, .F., .F. )
	oFont4  := TFont():New( 'Arial', 09, 09, .T., .F., 5, .T., 5, .F., .F. )
	oFont5  := TFont():New( 'Arial', 10, 10, .T., .T., 5, .T., 5, .F., .F. )
	oFont6  := TFont():New( 'Arial', 10, 10, .T., .F., 5, .T., 5, .F., .F. )
	oFont7  := TFont():New( 'Arial', 12, 12, .T., .F., 5, .T., 5, .F., .F. )
	oFont7N := TFont():New( 'Arial', 12, 12, .T., .T., 5, .T., 5, .F., .F. )
	oFont8  := TFont():New( 'Arial', 12, 12, .T., .F., 5, .T., 5, .F., .F. )
	oFont9  := TFont():New( 'Arial', 16, 16, .T., .F., 5, .T., 5, .F., .F. )
	oFont9N := TFont():New( 'Arial', 16, 16, .T., .T., 5, .T., 5, .F., .F. )
	oFont10 := TFont():New( 'Arial', 20, 20, .T., .T., 5, .T., 5, .F., .F. )
	
	nLin := 240
	fCabec(@nLin, oReport)
	
	For nI := 1 To Len(aDados)
	
		If oReport:Cancel()
			Exit
		EndIf
	
		If nLin >= nLinFinal
			oReport:EndPage()
			nLin := 240
			fCabec(@nLin, oReport)
		EndIf
	
		If aDados[nI, COL_SELECAO]
		
			oReport:Say((nLin), 0070, aDados[nI, COL_TITULO]                                                , oFont3N,,,, 0 ) // Titulo
			oReport:Box(nLin-40, 0050, (nLin+50), 0250 )							                                          // Box Titulo
			
			oReport:Say((nLin), 0290, aDados[nI, COL_PARCELA]                                               , oFont3N,,,, 0 ) // Parcela
			oReport:Box(nLin-40, 0250, (nLin+50), 0420 )							                                          // Box Parcela
			
			oReport:Say((nLin), 0490, DtoC(aDados[nI, COL_VENCIMENTO])                                      , oFont3N,,,, 0 ) // Vencimento
			oReport:Box(nLin-40, 0420, (nLin+50), 0700 )							                                          // Box Vencimento
			
			oReport:Say((nLin),  nColVal, Transform(aDados[nI, COL_VALOR], "@E 999,999,999.99")         	, oFont3N,,,, 0 ) // Valor
			oReport:Box(nLin-40, nColVal, (nLin+50), 0900 )												    	              // Box Valor

			oReport:Say((nLin),  nCDesTit, Transform(aDados[nI, COL_DECRECIMO],"@E 999,999,999.99")    		, oFont3N,,,, 0 ) // Decrescimo
			oReport:Box(nLin-40, nCDesTit, (nLin+50), 1120 )											    		          // Box Decrescimo
			
			oReport:Say((nLin),  nColDec, Transform(aDados[nI, COL_VALOR_ATUAL],"@E 999,999,999.99")   		, oFont3N,,,, 0 ) // Saldo
			oReport:Box(nLin-40, 1120, (nLin+50), 1360 )											        	              // Box Saldo

			oReport:Say((nLin), nColSal + 50 , Transform((aDados[nI, COL_PERCE_DESCONTO_SOBRE_TITULO]), "@E 999,999,999.99"), oFont3N,,,, 0)  // %Desc/Tit
			oReport:Box(nLin-40, 1360, (nLin+50), 1550 )                                                                      // Box %Desc/Tit

			oReport:Say((nLin), nColMul + 15, Transform(aDados[nI, COL_VALOR_DESCONTO_SOBRE_TITULO], "@E 999,999,999.99"), oFont3N,,,, 0 ) // Vl Desc
			oReport:Box(nLin-40, 1550, (nLin+50), 1750 )										     				          // Box Vl Desc
			
			oReport:Say((nLin), nColJur + 15, Transform(aDados[nI, COL_MULTA],"@E 999,999,999.99")	        , oFont3N,,,, 0 ) // 9 
			oReport:Box(nLin-40, 1750, (nLin+50), 1950 )												        	          // 
			
			oReport:Say((nLin),  1940, Transform(aDados[nI, COL_JUROS],"@E 999,999,999.99")	                , oFont3N,,,, 0 ) // Multa
			oReport:Box(nLin-40, 1950, (nLin+50), 2150 )													                  // Box Multa

			oReport:Say((nLin),  2150, Transform(aDados[nI, COL_PERCENT_DE_DESCONTO],"@E 999,999,999.99"), oFont3N,,,, 0 ) // Juros
			oReport:Box(nLin-40, 2150, (nLin+50), 2350 )													                  // Box Juros

			oReport:Say((nLin),  2360, Transform(aDados[nI, COL_VALOR_DESCONTO_SOBRE_JUROS],"@E 999,999,999.99"), oFont3N,,,,0) // Juros
			oReport:Box(nLin-40, 2350, (nLin+50), 2550 )													                    // Box Juros
			
			oReport:Say((nLin),  2565, Transform(aDados[nI, COL_VALOR_A_RECEBER], "@E 999,999,999.99")  	, oFont3N,,,, 0 ) // Vl Receber
			oReport:Box(nLin-40, 2550, (nLin+50), 2750) 												                  	  // Box Vl Receber
			
			nTotVal	 += aDados[nI, COL_VALOR]
			nTotJur	 += aDados[nI, COL_JUROS]
			nTotMul	 += aDados[nI, COL_MULTA]
			nTotJuM	 += aDados[nI, COL_JUROS_E_MULTA]
			nTotDes	 += aDados[nI, COL_PERCENT_DE_DESCONTO]	
			nTotVDe	 += aDados[nI, COL_VALOR_DESCONTO_SOBRE_JUROS]
			nTotDec  += aDados[nI, COL_DECRECIMO]
			nTotSal  += aDados[nI, COL_VALOR_ATUAL]
			nTotDT   += 0
			nTotPerX += aDados[nI, COL_VALOR_DESCONTO_SOBRE_JUROS]
			nTotVDT  += aDados[nI, COL_VALOR_DESCONTO_SOBRE_TITULO]
			nTotal 	 += aDados[nI, COL_VALOR_A_RECEBER]
			nLin := nLin + 87
			
		EndIf
	
	Next nI
	
	oReport:Say((nLin), 0100, "TOTAL"		                                             , oFont3N,,,, 0 ) 	// Total
	oReport:Box(nLin-35, 0050, (nLin+50), nColVal )													       	// Box Total
	
	oReport:Say((nLin), nColVal, Transform(nTotVal, "@E 999,999,999.99")		         , oFont3N,,,, 0 ) 	// Soma Valor
	oReport:Box(nLin-35, nColVal, (nLin+50), 900 )												       		// Box Soma Valor
	
	oReport:Say((nLin), nCDesTit, Transform(nTotDec,"@E 999,999,999.99")		         , oFont3N,,,, 0 ) // Soma Decrecimo
	oReport:Box(nLin-35, nCDesTit, (nLin+50), 1120   )												       // Box Soma Decrecimo

	oReport:Say((nLin), nColDec, Transform(nTotSal,"@E 999,999,999.99")		             , oFont3N,,,, 0 ) // Soma Saldo
	oReport:Box(nLin-35, 1120, (nLin+50), 1360 )												           // Box Soma Saldo

	//oReport:Say( (nLin), nColSal + 50, Transform(nTotDT, "@E 999,999,999.99")          , oFont3N,,,, 0)  // %Desc/Tit
	oReport:Box(nLin-35, 1360, (nLin+50), 1550 )                                                           // Box %Desc/Tit

	oReport:Say((nLin), nColMul + 15, Transform(nTotVDT, "@E 999,999,999.99")	         , oFont3N,,,, 0 ) // Soma Valor Desconto Titulo
	oReport:Box(nLin-35, 1550, (nLin+50), 1750 )												           // Box Soma Valor Desconto Titulo

	oReport:Say((nLin), nColJur + 15, Transform(nTotMul, "@E 999,999,999.99")	         , oFont3N,,,, 0 ) // Soma Multa
	oReport:Box(nLin-35, 1750, (nLin+50), 1950 )												           // Box Soma Multa

	oReport:Say((nLin),  1940, Transform(nTotJur, "@E 999,999,999.99")		             , oFont3N,,,, 0 ) // Soma Juros
	oReport:Box(nLin-35, 1950, (nLin+50), 2150 )												           // Box Soma Juros

	//oReport:Say((nLin),  2150, Transform(, "@E 999,999,999.99")	  	                 , oFont3N,,,, 0 ) // %Desc/JM
	oReport:Box(nLin-35, 2150, (nLin+50), 2350 )												           // Box %Desc/JM

	oReport:Say((nLin),  2360, Transform((nTotPerX), "@E 999,999,999.99")		         , oFont3N,,,, 0 ) // Soma Valor Desconto Juros e Multa
	oReport:Box(nLin-35, 2350, (nLin+50), 2550 )							             				   // Box Soma Valor Desconto Juros e Multa
	
	oReport:Say((nLin),  2565, Transform(nTotal, "@E 999,999,999.99")		             , oFont3N,,,, 0 ) // Soma Valor a Receber
	oReport:Box(nLin-35, 2550, (nLin+50), 2750 )							             				   // Box Soma Valor a Receber

Return Nil

/*/{Protheus.doc} fCabec
Impressão do cabeçalho
@type function
@author j2a.luizjunior
@since 01/11/2016
/*/
Static Function fCabec(nLin, oReport)

	nLin := 240
	oReport:Say( (nLin)	, 0050, "Unidade: "	+ SM0->M0_FILIAL															, oFont7N,,,, 0 )
	nLin := nLin + 50
	oReport:Say( (nLin)	, 0050, "Unidade Organizacional: " + SM0->M0_FILIAL												, oFont7N,,,, 0 )
	nLin := nLin + 50
	oReport:Say( (nLin)	, 0050, "Data Base: " + DtoC(dDataBase)															, oFont7N,,,, 0 )
	nLin := nLin + 50
	oReport:Say( (nLin)	, 0050, "Cliente: "	+ Posicione("SA1",1,xFilial("SA1")+SE1->E1_CLIENTE+SE1->E1_LOJA,"A1_NOME")	, oFont7N,,,, 0 )
	
	nLin := nLin + 150
	
	oReport:Say(  (nLin), 0100, "TITULO"		  , oFont3N,,,, 0 )
	oReport:Box(  nLin-40, 0050, (nLin+50), 0250 )	// Box Titulo
	
	oReport:Say(  (nLin), 0290, "PARC"			  , oFont3N,,,, 0 )
	oReport:Box(  nLin-40, 0250, (nLin+50), 0420 )	// Box Parcela
	
	oReport:Say(  (nLin), 0480, "VENCIMENTO"	  , oFont3N,,,, 0 )
	oReport:Box(  nLin-40, 0420, (nLin+50), 0700 )	// Box Vencimento
	
	oReport:Say(  (nLin), 0775, "VALOR"			  , oFont3N,,,, 0 )
	oReport:Box(  nLin-40, 0700, (nLin+50), 0900 )	// Box Valor
	
	oReport:Say(  (nLin), 0940, "DECRESCIMO"      , oFont3N,,,, 0)
	oReport:Box(  nLin-40,0900, (nLin+50), 1120  )  //Box Saldo
	//----------------------------------------------------------------------------
	oReport:Say(  (nLin), 1170, "SALDO"           , oFont3N,,,, 0)  
	oReport:Box(  nLin-40,1120, (nLin+50), 1360  )  //Box Decrescimo
	
	oReport:Say( (nLin), 1370, "% DESC/TIT"       , oFont3N,,,, 0)
	oReport:Box( nLin-40,1360, (nLin+50), 1550   ) //Box %Desc/Tit
	
	oReport:Say(  (nLin), 1560, "VLR.DESC.TIT"    , oFont3N,,,, 0 )             
	oReport:Box(  nLin-40,1550, (nLin+50), 1750 )	// Box Vl. Desc

	/*	
	oReport:Say(  (nLin),  1770, "% DESC/JM"	  , oFont3N,,,, 0 )            
	oReport:Box(  nLin-40, 1750, (nLin+50), 1950 )	// Box % Desc/Jm
	*/
	
	oReport:Say(  (nLin),  1790, "MULTA"	      , oFont3N,,,, 0 )            
	oReport:Box(  nLin-40, 1750, (nLin+50), 1950 )	// Box % Desc/Jm

	oReport:Say(  (nLin),  2000, "JUROS"	      , oFont3N,,,, 0 )            
	oReport:Box(  nLin-40, 1950, (nLin+50), 2150 )	// Box Multa
	
	oReport:Say(  (nLin),  2180, "% DESC/JM"	      , oFont3N,,,, 0 )        
	oReport:Box(  nLin-40, 2150, (nLin+50), 2350 )	// Box Juros

	oReport:Say(  (nLin),  2365, "VLR.DESC.JM"	  , oFont3N,,,, 0 )          
	oReport:Box(  nLin-40, 2350, (nLin+50), 2550 )	// Box Vl Receber
	
	oReport:Say(  (nLin),  2565, "VL RECEBER"	  , oFont3N,,,, 0 )          
	oReport:Box(  nLin-40, 2550, (nLin+50), 2750 )	// Box Vl Receber
	
	nLin := nLin + 100

Return(Nil)
