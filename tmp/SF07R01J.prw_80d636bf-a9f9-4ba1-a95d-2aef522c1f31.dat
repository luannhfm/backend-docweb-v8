#Include 'Protheus.ch'
#Include 'Topconn.ch'

/*/{Protheus.doc} SF07R01J
Relatorio de Dados Finais da Folha
Solicitante: Jose Cardoso

@author 	Jose Leite de Barros Neto
@since 	03/07/2014
@version 	1.0
@return 	Nil, Nulo

/*/
User Function SF07R01J()
	
	Local 	oReport
	Local _cCodigo	:= GetMv("MV_XGERCCR")
	
	Private cPerg	:= 'SF07R01J'

	If TRepInUse()

		AjustaSx1(cPerg)
		If !Pergunte(cPerg,.T.)
			Return( Nil )
		EndIf
		
		If mv_par03 <> _cCodigo
			Alert("Usuário não autorizado!")
			Return( Nil )
		EndIf
		
		oReport := ReportDef(cPerg)
		oReport:PrintDialog()
		
	EndIf
			
Return( Nil )

/** {Protheus.doc} ReportDef
Funcao que define as colunas

@author: 	Jose Leite de Barros Neto
@since: 	03/07/2014
@Uso: 		SFIEMT
*/
Static Function ReportDef(cPerg)
	
	Local oReport
	Local oSection
	
	oReport := TReport():New(cPerg,"Dados Finais Folha",cPerg,{|oReport| PrintReport(oReport)},"DadosFinais")
	oReport:SetLandScape()
	
	oSection:= TRSection():New(oReport,OemToAnsi(""),{"SRD"})
		
	oCCusto:= TRCell():New(oSection, "_cCentroC","TRA", "CENTRO DE CUSTO")
	oCCusto:SetSize(20)
	
	oCargo:= TRCell():New(oSection, "_cCargo","TRA", "CARGO")
	oCargo:SetSize(50)
	
	oPonder:= TRCell():New(oSection, "_cPonderador","TRA", "PONDERADOR")
	oPonder:SetSize(1)
	
	oQuant:= TRCell():New(oSection, "_cQuantidade","TRA", "QUANTIDADE")
	oQuant:SetSize(1)
	
	oNome:= TRCell():New(oSection, "_cNome","TRA", "NOME")
	oNome:SetSize(50)
	
Return( oReport )	
	
/** {Protheus.doc} ReportDef
Funcao que gera o relatorio

@author: 	Jose Leite de Barros Neto
@since: 	03/07/2014
@Uso: 		SFIEMT
*/
Static Function PrintReport(oReport)	
	
	Local _cCentroC 	:= ""
	Local _cCodFunc		:= ""
	Local _cCargo		:= ""
	Local _cPonderador	:= ""
	Local _cQuantidade	:= "1"
	Local _cNome			:= ""
	Local _nCont			:= 0
	Local oSection 		:= oReport:Section(1)
	
	_cQuery := ""
	
	If mv_par02 == 1
	
		_cQuery += " SELECT RC_FILIAL FILIAL, RC_CC CC, RC_MAT MAT, RA_NOMECMP NOME, RA_CODFUNC CODFUNC, RC_DATA DATA, SUM(RC_VALOR) VALOR	"  
		_cQuery += "		FROM "+ RETSQLNAME("SRC") + " RC "
		_cQuery += "		INNER JOIN "+ RETSQLNAME("SRA") + " RA ON RA_FILIAL = RC.RC_FILIAL AND RA.RA_MAT = RC.RC_MAT	" 
		_cQuery += "	WHERE 	RC_FILIAL <> '05MT0001'											"
		_cQuery += "		AND (RC_PD = '100' OR RC_PD = '103' OR RC_PD = '111' OR RC_PD = '113' OR RC_PD = '122' OR RC_PD = '190') "	
		_cQuery += "		AND SUBSTR(RC_DATA,1,6) =  '"+mv_par01+"'										"
		_cQuery += "		AND RA.D_E_L_E_T_ <> '*'														"
		_cQuery += "		AND RC.D_E_L_E_T_ <> '*'														"
		_cQuery += "	GROUP BY RC_FILIAL, RC_CC, RC_MAT, RA_NOMECMP, RA_CODFUNC, RC_DATA	"	
		_cQuery += "	ORDER BY RA_NOMECMP																"
	
	Else
		
		_cQuery += "	SELECT  RD_FILIAL FILIAL, RD_CC CC, RD_MAT MAT, RA_NOMECMP NOME, RA_CODFUNC CODFUNC, RD_DATARQ DATA, SUM(RD_VALOR) VALOR "
		_cQuery += "		FROM "+ RETSQLNAME("SRD") + " RD "
		_cQuery += "		INNER JOIN "+ RETSQLNAME("SRA") + " RA ON RA_FILIAL = RD.RD_FILIAL AND RA.RA_MAT = RD.RD_MAT "
		_cQuery += "	WHERE 	RD_FILIAL <> '05MT0001'										" 
		_cQuery += "	  		AND (RD_PD = '100' OR RD_PD = '103' OR RD_PD = '111' OR RD_PD = '113' OR RD_PD = '122' OR RD_PD = '190')	"
		_cQuery += "	  		AND RD_DATARQ LIKE '"+mv_par01+"%'										"
		_cQuery += "	  		AND RA.D_E_L_E_T_ <> '*'													"
		_cQuery += "	  		AND RD.D_E_L_E_T_ <> '*'													"
		_cQuery += "	GROUP BY RD_FILIAL,RD_CC,RD_MAT,RA_NOMECMP, RA_CODFUNC, RD_DATARQ		"
		_cQuery += "	ORDER BY RA_NOMECMP																"
	
	EndIf
	
	_cQuery := ChangeQuery( _cQuery )
	
	TCQUERY _cQuery NEW ALIAS "TRA"
	
	DbSelectArea("TRA")
	DbGoTop()
	
	oReport:SetMeter(RecCount())
	oSection:Init()
	
	While .Not. TRA->( EOF() )
	
		If oReport:Cancel()
			Exit
		EndIf
		
		_cCentroC 		:= AllTrim(TRA->CC)
		
		//Centro de Custo
		If SubStr(_cCentroC,1,6) == "130104"
			_cCentroC := "7"+_cCentroC
		Else
			If SubStr(TRA->FILIAL,1,2) == "01"
				_cCentroC := "5"+_cCentroC
			ElseIf SubStr(TRA->FILIAL,1,2) == "02"
				_cCentroC := "2"+_cCentroC
			ElseIf SubStr(TRA->FILIAL,1,2) == "03"
				_cCentroC := "3"+_cCentroC
			ElseIf SubStr(TRA->FILIAL,1,2) == "04"
				_cCentroC := "4"+_cCentroC
			EndIf
		EndIf
			
		//Cargo
		_cCodFunc		:= AllTrim(TRA->CODFUNC)
		_cCargo		:= AllTrim(Posicione("SRJ",1,xFilial("SRJ")+_cCodFunc,"RJ_DESC"))
		
		//Ponderador
		Do Case
		
			Case TRA->VALOR <= 2000
				_cPonderador 	:= "1"
			Case TRA->VALOR > 2000 .AND. TRA->VALOR <= 3000 
				_cPonderador 	:= "2"
			Case TRA->VALOR > 3000 .AND. TRA->VALOR <= 4000
				_cPonderador 	:= "3"
			Case TRA->VALOR > 4000 .AND. TRA->VALOR <= 7000
				_cPonderador 	:= "4"
			Case TRA->VALOR > 7000
				_cPonderador 	:= "5"
		
		EndCase
		
		//Quantidade
		_cQuantidade	:= "1"
		
		//Nome
		_cNome := AllTrim(TRA->NOME)
		
		//Dados
		oSection:Cell("_cCentroC"):SetValue(_cCentroC)
		oSection:Cell("_cCargo"):SetValue(_cCargo)
		oSection:Cell("_cPonderador"):SetValue(_cPonderador)
		oSection:Cell("_cQuantidade"):SetValue(_cQuantidade)
		oSection:Cell("_cNome"):SetValue(_cNome)
		
		oSection:PrintLine()
		oReport:IncMeter()
		
		_nCont++
		TRA->( DbSkip() )
	End
	
	TRA->(DbCloseArea())
	oSection:Finish()

Return( Nil )

/** {Protheus.doc} AjustaSx1
Pergunta do Relatorio

@author: 	Jose Leite de Barros Neto
@since: 	03/07/2014
@Uso: 		SFIEMT
*/
Static Function AjustaSx1(cPerg)
	
	Local aHelp := {}

	AAdd(aHelp, {{"Informe o Ano Mes Folha. Ex.: 201406"		}, {""}, {""}})
	AAdd(aHelp, {{"Informe se Folha Aberta ou Fechada"		}, {""}, {""}})
	AAdd(aHelp, {{"Informe o Codigo CCR"							}, {""}, {""}})
	
	u_SFPUTSX1(cPerg,"01","Ano Mes:	","","","mv_ch1","C",06,00,00,"G","",""	,"","","mv_par01",""			,"","","",""				,"","","","","","","","","","","",aHelp[1,1],aHelp[1,2],aHelp[1,3],"")
	u_SFPUTSX1(cPerg,"02","Folha:	","","","mv_ch2","C",01,00,00,"C","",""	,"","","mv_par02","Aberta"	,"","","","Fechada"	,"","","","","","","","","","","",aHelp[2,1],aHelp[2,2],aHelp[2,3],"")
	u_SFPUTSX1(cPerg,"03","Código:	","","","mv_ch3","C",06,00,00,"C","",""	,"","","mv_par03",""			,"","","",""				,"","","","","","","","","","","",aHelp[3,1],aHelp[3,2],aHelp[3,3],"")
	
Return( Nil )