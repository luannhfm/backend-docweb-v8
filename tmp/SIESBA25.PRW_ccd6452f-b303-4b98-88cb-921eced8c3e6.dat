#INCLUDE "PROTHEUS.CH"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³SIESBA25   ºAutor  ³Microsiga          º Data ³  13/04/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Receptor de EAI - Estorno de Baixas a Receber              º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MP 11.5 - Sistema Industria                                º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

User Function SIESBA25(_cXML)
Local _cError    := ""
Local _cWarning  := ""
Local _cDelimit  := "_"
Local aStru      := {}
Local _lInput    := GetRemoteType() == -1 //-1 = sem remote/ 0 = delphi/ 1 = QT windows/ 2 = QT Linux
Default _cXML    := ""

IF _lInput // Chamada via ESB/EAI
	
	//Gera o Objeto XML com a estrutura recebida
	oXml := XmlParser(_cXML, _cDelimit, @_cError, @_cWarning)
	
	//Verifica se a estrutura foi criada
   //	IF !(Empty(_cError) .and. Empty(_cWarning))
//		Return()
//	ENDIF
	
	_nOperac := Val(oXml:_FINA070:_OPERATION:TEXT)
	_cIDESB  := IIF(Type("oXml:_FINA070:_SE1MASTER:_E1_XIDESB:REALNAME") <> "U",&("oXml:_FINA070:_SE1MASTER:_E1_XIDESB:_VALUE:TEXT"),"")
	
	// Verifica se mensagem trata-se de Produto x Fornecedor
	IF Type('oXml:_FINA070:_SE1MASTER') == "U"
		_cMotivo := "Mensagem invalida para a rotina FINA070 - Estorno de Baixa a Receber"
		// Gera XML de retorno
		U_SIXMLMSG("FINA070","Estorno de Baixa a Receber",Alltrim(Str(_nOperac)),_cIDESB,"1",_cMotivo)
		Return()
	ENDIF
	
	_cPrefixo := IIF(Type("oXml:_FINA070:_SE1MASTER:_E1_PREFIXO") == "U","",oXml:_FINA070:_SE1MASTER:_E1_PREFIXO:_VALUE:TEXT)
	_cNum     := IIF(Type("oXml:_FINA070:_SE1MASTER:_E1_NUM") == "U","",oXml:_FINA070:_SE1MASTER:_E1_NUM:_VALUE:TEXT)
	_cParcela := IIF(Type("oXml:_FINA070:_SE1MASTER:_E1_PARCELA") == "U","",oXml:_FINA070:_SE1MASTER:_E1_PARCELA:_VALUE:TEXT)
	_cTipo    := IIF(Type("oXml:_FINA070:_SE1MASTER:_E1_TIPO") == "U","",oXml:_FINA070:_SE1MASTER:_E1_TIPO:_VALUE:TEXT)
	_cCliente := IIF(Type("oXml:_FINA070:_SE1MASTER:_E1_CLIENTE") == "U","",oXml:_FINA070:_SE1MASTER:_E1_CLIENTE:_VALUE:TEXT)
	_cLoja    := IIF(Type("oXml:_FINA070:_SE1MASTER:_E1_LOJA") == "U","",oXml:_FINA070:_SE1MASTER:_E1_LOJA:_VALUE:TEXT)
	_cBanco   := IIF(Type("oXml:_FINA070:_SE1MASTER:_CBANCO") == "U","",oXml:_FINA070:_SE1MASTER:_CBANCO:_VALUE:TEXT)
	_cAgencia := IIF(Type("oXml:_FINA070:_SE1MASTER:_CAGENCIA") == "U","",oXml:_FINA070:_SE1MASTER:_CAGENCIA:_VALUE:TEXT)
	_cConta   := IIF(Type("oXml:_FINA070:_SE1MASTER:_CCONTA") == "U","",oXml:_FINA070:_SE1MASTER:_CCONTA:_VALUE:TEXT)
	
	// Verifica faltam campos do cabecalho
	//IF Empty(_cNum) .or. Empty(_cTipo) .or. Empty(_cCliente) .or. Empty(_cLoja) .or. Empty(_cBanco) .or. Empty(_cAgencia) .or. Empty(_cConta)   
		If Empty(_cNum) .or. Empty(_cTipo) .or. Empty(_cCliente) .or. Empty(_cLoja)   
		_cMotivo := "Alguns campos do cabecalho nao foram informados. Verifique!"
		// Gera XML de retorno
		U_SIXMLMSG("FINA070","Estorno de Baixa a Receber",Alltrim(Str(_nOperac)),_cIDESB,"1",_cMotivo)
		Return()
	ELSE
		
		_cCliente := Padr(_cCliente,TamSX3("E1_CLIENTE")[1])
		_cLoja	  := Padr(_cLoja,TamSX3("E1_LOJA")[1])
		_cPrefixo := Padr(_cPrefixo,TamSX3("E1_PREFIXO")[1])
		_cNum	  := Padr(_cNum,TamSX3("E1_NUM")[1])
		_cParcela := Padr(_cParcela,TamSX3("E1_PARCELA")[1])
		_cTipo	  := Padr(_cTipo,TamSX3("E1_TIPO")[1])
		
		SE1->(dbSetOrder(2))
		IF !SE1->(dbSeek(XFilial("SE1")+_cCliente+_cLoja+_cPrefixo+_cNum+_cParcela+_cTipo))
			_cMotivo := "O titulo informado nao foi localizado. Verifique!"
			// Gera XML de retorno
			U_SIXMLMSG("FINA070","Estorno de Baixa a Receber",Alltrim(Str(_nOperac)),_cIDESB,"1",_cMotivo)
			Return()
		ENDIF
		
		aStru := {}
		AADD( aStru, { 'E1_PREFIXO'  , SE1->E1_PREFIXO	, Nil } )	// Prefixo
		AADD( aStru, { 'E1_NUM'  	 , SE1->E1_NUM		, Nil } )	// Numero do titulo
		AADD( aStru, { 'E1_PARCELA'  , SE1->E1_PARCELA	, Nil } )	// Parcela
		AADD( aStru, { 'E1_TIPO'  	 , SE1->E1_TIPO		, Nil } )	// Tipo
		AADD( aStru, { 'E1_CLIENTE'  , SE1->E1_CLIENTE	, Nil } )	// Cliente
		AADD( aStru, { 'E1_LOJA'  	 , SE1->E1_LOJA		, Nil } )	// Loja
		AADD( aStru, { 'CBANCO' 	 , _cBanco			, Nil } )	// Banco
		AADD( aStru, { 'CAGENCIA'	 , _cAgencia		, Nil } )	// Agencia
		AADD( aStru, { 'CCONTA'    	 , _cConta			, Nil } )	// Conta
		
	ENDIF
	
	INCLUI         := .T.
	lMsErroAuto    := .F.
	lMsHelpAuto    := .T.
	lAutoErrNoFile := .T.
	
	Begin Transaction
	MSExecAuto( { | x, y | FINA070( x, y ) }, aStru, _nOperac )
	
	If lMsErroAuto
		If (__lSX8)
			RollBackSX8()
		EndIf
		
		DisarmTransaction()
		
		// Tratamento da Mensagem de erro do MSExecAuto
		aLogErr  := GetAutoGRLog()
		aLogErr2 := U_SIESBID(aLogErr)
		_cMotivo := ""
		
		For i := 1 to Len(aLogErr2)
			_cMotivo += aLogErr2[i]
		Next
		
		// Gera XML de retorno
		U_SIXMLMSG("FINA070","Estorno de Baixa a Receber",Alltrim(Str(_nOperac)),_cIDESB,"1",_cMotivo)
		
		Break
	Else
		If (__lSX8)
			ConfirmSX8()
		EndIf
		
		// Gera XML de retorno
		U_SIXMLMSG("FINA070","Estorno de Baixa a Receber",Alltrim(Str(_nOperac)),_cIDESB,"0","")
		
	EndIf
	End Transaction
	
ELSE
	
	_cData     := Dtos(Date())
	_cTime     := Time()
	_cDateTime := Transform(_cData,"@R 9999-99-99")+"T"+_cTime+"Z"
	_cFile     := "FINA070"+"5"+_cData+StrTran(_cTime,":","")+".XML"
	
	// Montagem das tags do XML
	_cXML += '<TOTVSIntegrator>'
	_cXML += '<GlobalProduct>TOTVS|ESB</GlobalProduct>'
	_cXML += '<GlobalFunctionCode>EAI</GlobalFunctionCode>'
	_cXML += '<GlobalDocumentFunctionCode>FINA070</GlobalDocumentFunctionCode>'
	_cXML += '<GlobalDocumentFunctionDescription>Estorno de Baixas a Receber</GlobalDocumentFunctionDescription>'
	_cXML += '<DocVersion>1.0</DocVersion>'
	_cXML += '<DocDateTime>'+_cDateTime+'</DocDateTime>'
	_cXML += '<DocIdentifier></DocIdentifier>'
	_cXML += '<DocCompany>'+cEmpAnt+'</DocCompany>'
	_cXML += '<DocBranch>'+cFilAnt+'</DocBranch>'
	_cXML += '<DocName></DocName>'
	_cXML += '<DocFederalID></DocFederalID>'
	_cXML += '<DocType>2</DocType>'
	_cXML += '<Message>'
	_cXML += '<Layouts>'
	_cXML += '<Identifier>FINA070</Identifier>'
	_cXML += '<Version>1.0</Version>'
	_cXML += '<FunctionCode>U_SIESBA25</FunctionCode>'
	_cXML += '<Content>'
	_cXML += '<FINA070 Operation="3" version="1.01">'
	_cXML += '<SE1MASTER modeltype="FIELDS">'
	_cXML += '<E1_FILIAL order="1">'
	_cXML += '<value>'+SE1->E1_FILIAL+'</value>'
	_cXML += '</E1_FILIAL>'
	_cXML += '<E1_PREFIXO order="2">'
	_cXML += '<value>'+SE1->E1_PREFIXO+'</value>'
	_cXML += '</E1_PREFIXO>'
	_cXML += '<E1_NUM order="3">'
	_cXML += '<value>'+SE1->E1_NUM+'</value>'
	_cXML += '</E1_NUM>'
	_cXML += '<E1_PARCELA order="4">'
	_cXML += '<value>'+SE1->E1_PARCELA+'</value>'
	_cXML += '</E1_PARCELA>'
	_cXML += '<E1_TIPO order="5">'
	_cXML += '<value>'+SE1->E1_TIPO+'</value>'
	_cXML += '</E1_TIPO>'
	_cXML += '<E1_CLIENTE order="6">'
	_cXML += '<value>'+SE1->E1_CLIENTE+'</value>'
	_cXML += '</E1_CLIENTE>'
	_cXML += '<E1_LOJA order="7">'
	_cXML += '<value>'+SE1->E1_LOJA+'</value>'
	_cXML += '</E1_LOJA>'
	_cXML += '<AUTBANCO order="9">'
	_cXML += '<value>'+CBANCO+'</value>'
	_cXML += '</AUTBANCO>'
	_cXML += '<AUTAGENCIA order="10">'
	_cXML += '<value>'+CAGENCIA+'</value>'
	_cXML += '</AUTAGENCIA>'
	_cXML += '<AUTCONTA order="11">'
	_cXML += '<value>'+CCONTA+'</value>'
	_cXML += '</AUTCONTA>'
	_cXML += '<AUTVALREC order="15">'
	_cXML += '<value>'+Alltrim(Transform(nValRec,"@E 99,999,999,999.99"))+'</value>'
	_cXML += '</AUTVALREC>'
	_cXML += '<AUTDESCONT order="16">'
	_cXML += '<value>'+Alltrim(Transform(VALOR3,"@E 99,999,999,999.99"))+'</value>'
	_cXML += '</AUTDESCONT>'
	_cXML += '<AUTMULTA order="17">'
	_cXML += '<value>'+Alltrim(Transform(VALOR,"@E 99,999,999,999.99"))+'</value>'
	_cXML += '</AUTMULTA>'
	_cXML += '<AUTJUROS order="18">'
	_cXML += '<value>'+Alltrim(Transform(VALOR2,"@E 99,999,999,999.99"))+'</value>'
	_cXML += '</AUTJUROS>'
	_cXML += '<AUTDECRESC order="19">'
	_cXML += '<value>'+Alltrim(Transform(nDecresc,"@E 99,999,999,999.99"))+'</value>'
	_cXML += '</AUTDECRESC>'
	_cXML += '<AUTACRESC order="20">'
	_cXML += '<value>'+Alltrim(Transform(nAcresc,"@E 99,999,999,999.99"))+'</value>'
	_cXML += '</AUTACRESC>'
	_cXML += '</SE1MASTER>'
	_cXML += '</FINA070>'
	_cXML += '</Content>'
	_cXML += '</Layouts>'
	_cXML += '</Message>'
	_cXML += '</TOTVSIntegrator>'
	

	// Geração do arquivo
	U_SIXMLDATA(_cXML,_cFile,"FINA070","Estorno de Baixas a Receber")
ENDIF

Return()
