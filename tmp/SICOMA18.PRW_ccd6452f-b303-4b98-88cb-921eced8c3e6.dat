#Include "Protheus.Ch"
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ SIcoma18 ³ Autor ³ CADUBITSKI            ³ Data ³ Jan/2012 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Cadastro de Finalidade de Compra x Conta Contabil.         ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

User Function SIcoma18()

Local cVldExc := "U_coma18Exc()"// Validacao para permitir a exclusao. Pode-se utilizar ExecBlock.
Local cVldAlt := ".T." 			// Validacao para permitir a alteracao. Pode-se utilizar ExecBlock.

AxCadastro("SZO","Cadastro de Finalidade de Compra x Conta Contabil.",cVldExc,cVldAlt)

Return()

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Descricao ³ Verifica se o registro pode ser excluido                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
User Function coma18Exc()

Local lRet  		:= .T.
Local cCont 		:= SZO->ZO_FINCOM //Conteudo do registro posicionado
Local cARQPROD	:= Upper(Alltrim(GetMv("MV_ARQPROD"))) //Configura se os dados de indicadores de produto serao considerados pela tabela "SB1" ou "SBZ"

cAli := GetNextAlias()//Carrega o proximo alias disponivel

//Verifica se a finalidade da compra esta em uso no SB1 ou SBZ
If cARQPROD == "SB1"
	BeginSQL Alias cAli
		SELECT B1_FINCOM AS FINCOM 
		FROM %table:SB1%
		WHERE B1_FILIAL = %xFilial:SB1%
		AND %NotDel%
		AND B1_FINCOM = %Exp:cCont%
	EndSQL
Else
	BeginSQL Alias cAli
		SELECT BZ_FINCOM AS FINCOM 
		FROM %table:SBZ%
		WHERE BZ_FILIAL = %xFilial:SBZ%
		AND %NotDel%
		AND BZ_FINCOM = %Exp:cCont%
	EndSQL
EndIf

DbSelectArea((cAli))

If Select((cAli)) > 0
	If !Empty((cAli)->FINCOM)
		lRet := .F.
	EndIf
EndIf

DbCloseArea((cAli))

cAli := GetNextAlias()//Carrega o proximo alias disponivel

//Verifica se a finalidade da compra esta em uso no SC1
BeginSQL Alias cAli
	SELECT C1_FINCOM AS FINCOM 
	FROM %table:SC1%
	WHERE C1_FILIAL = %xFilial:SC1%
	AND %NotDel%
	AND C1_FINCOM = %Exp:cCont%
EndSQL

DbSelectArea((cAli))

If Select((cAli)) > 0
	If !Empty((cAli)->FINCOM)
		lRet := .F.
	EndIf
EndIf

DbCloseArea((cAli))

If !lRet
	Aviso("Atencao !","Não é possível excluir o registro, pois ele está em uso.",{"Ok"})
EndIf

Return(lRet)