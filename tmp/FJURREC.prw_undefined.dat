#INCLUDE 'PROTHEUS.ch'

/*/{Protheus.doc} nomeFunction
@description Calculo de Juros e Multas considerando o (SALDO - DECRESCIMO)
@obs Chamado de baixa manual FINA070 e baixa automática FINA110
@type User Function
@author Edmar Tinti - Doit Sistemas
@since 13/03/2014
@version 11.0
@param dBaixa,	date, Data da baixa.
@param dVencto, date, Data do vencimento.
@param nAtraso, numeric, Atraso.
@param nValJur, numeric, Valor do juros.
@param nPerJur, numeric, Percentual de juros.
@param nJuros, numeric, Valor do juros.
@return nJuros, numeric, o valor do juros recalculado. 
@history 12/03/2019, Alan Teles de Oliveira, adicionado a função de recebimento com cartão a regra de calculo do juros e multa.
/*/
User Function FJURREC()

	Local dBaixa 	:= PARAMIXB[1]
	Local dVencto 	:= PARAMIXB[2]	
	Local nAtraso 	:= PARAMIXB[3]
	Local nValJur 	:= PARAMIXB[4]
	Local nPerJur 	:= PARAMIXB[5]
	Local nJuros  	:= PARAMIXB[6]  
	Local nPerMul	:= GETMV("MV_LJMULTA")
	Local nValor    := 0     
	
	If IsInCallStack("FINA070") .Or. IsInCallStack("FINA110") .or. IsInCallStack("U_SFFN462A")

		nValor  := SE1->E1_SALDO - SE1->E1_DECRESC
		nPerJur := nPerJur * nAtraso

		If nAtraso > 0 .And. SE1->E1_VALOR == SE1->E1_SALDO

			nJuros := Round(nValor * (nPerJur / 100), 2)

			If Type("nMulta") == "N" 

				nMulta := Round(nValor * (nPerMul / 100), 2)

			Endif	

		Endif    

		If Type("nDescont") == "N"  

			If dBaixa > dVencto

				nDescont := 0

			Endif	

		Endif	

	Endif	

Return nJuros
