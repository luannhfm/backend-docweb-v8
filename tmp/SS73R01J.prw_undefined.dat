#Include 'Protheus.ch'
#Include 'TopConn.ch'

/*/{Protheus.doc} SS73R01J
Relatorio Geral de Propostas (CRM)

@type 		function
@author 	Jose Leite de Barros Neto
@since 		18/03/2016
@version 	1.0
@return 	Nil, Nulo
/*/
User Function SS73R01J()
	
	Local _oReport		:= Nil
	Local _cPerg 		:= FunName()
	
	If TRepInUse()
		fCriaSx1( _cPerg )
		If Pergunte( _cPerg )
			_oReport := ReportDef( _cPerg )
			_oReport:PrintDialog()
		EndIf
	EndIf
	
Return( Nil )


/** {Protheus.doc} ReportDef
Funcao que define as colunas

@author: 	Jose Leite de Barros Neto
@since: 	18/03/2016
@Uso: 		SFIEMT
*/
Static Function ReportDef(p_cPerg)
	
	Local oReport	:= Nil
	Local oSection	:= Nil
	Local oBreak	:= Nil
	Local cTitle 	:= "Relatorio Geral de Oportunidades x Propostas"  
	
	oReport := TReport():New(p_cPerg,cTitle,p_cPerg,{|oReport| PrintReport(oReport)},cTitle)
	oReport:SetLandScape()
	
	oSection:= TRSection():New(oReport,OemToAnsi(""),{"AD1","ADY","ADZ","SA1"})
	
	oFilial:= TRCell():New(oSection, "_cFilial","TRA", "Filial")
	oFilial:SetSize(13)
		
	oOport:= TRCell():New(oSection, "_cOpor","TRA", "Oportunidade")
	oOport:SetSize(11)
	
	oProp:= TRCell():New(oSection, "_cProp","TRA", "Proposta")
	oProp:SetSize(10)
	
	oEmissao:= TRCell():New(oSection, "_cEmissao","TRA", "Emissão Prop.")
	oEmissao:SetSize(16)
	
	oVencto:= TRCell():New(oSection, "_cVncto","TRA", "Vencto Prop.")
	oVencto:SetSize(16)
	
	oClient:= TRCell():New(oSection, "_cNomCli","TRA", "Cliente")
	oClient:SetSize(50)
	
	oCGC:= TRCell():New(oSection, "_cCGC","TRA", "CNPJ")
	oCGC:SetSize(30)
	
	oMunic:= TRCell():New(oSection, "_cMunCli","TRA", "Município")
	oMunic:SetSize(30)
	
	oTelCto:= TRCell():New(oSection, "_cTelCto","TRA", "Tel. Contato")
	oTelCto:SetSize(20)
	
	oProdut:= TRCell():New(oSection, "_cProdut","TRA", "Serviço(s)")
	oProdut:SetSize(50)
	
	oUndExec:= TRCell():New(oSection, "_cUndExe","TRA", "Unid. Exec.")
	oUndExec:SetSize(15)
	
	oConsult:= TRCell():New(oSection, "_cNomVend","TRA", "Consultor(a)")
	oConsult:SetSize(35)
	
	oSitOpt:= TRCell():New(oSection, "_cSitOpor","TRA", "Situação Opt.")
	oSitOpt:SetSize(16)
	
	oSitProp:= TRCell():New(oSection, "_cSitProp","TRA", "Situação Prop.")
	oSitProp:SetSize(16)
		
	oSitProp:= TRCell():New(oSection, "_cB1Tip","TRA", "Tipo Prd")
	oSitProp:SetSize(16) 
	
	oSitProp:= TRCell():New(oSection, "_cB1Grp","TRA", "Grupo Prd")
	oSitProp:SetSize(16)
		
	oBreak := TRBreak():New(oSection, oSection:Cell("_cUndExe"), "Unidade:",.F.)
	
Return( oReport )


/** {Protheus.doc} ReportDef
Funcao que gera o relatorio

@author: 	Jose Leite de Barros Neto
@since: 	15/03/2016
@Uso: 		SFIEMT
*/
Static Function PrintReport(oReport)
	
	Local oSection 	:= oReport:Section(1)
	Local _cQuery 	:= ""
	Local _cFilial	:= ""
	Local _cOpor	:= ""
	Local _cProp	:= ""
	Local _cEmissao	:= ""
	Local _cVncto	:= ""
	Local _cNomCli	:= ""
	Local _cCGC		:= ""
	Local _cMunCli	:= ""
	Local _cTelCto	:= ""
	Local _cProdut	:= ""
	Local _cUndExe	:= ""
	Local _cNomVend	:= ""
	Local _cSitOpor	:= ""
	Local _cSitProp	:= ""
	//Walmir Junior em 04/11/2016
	Local _cB1Tip	:= ""
	Local _cB1Grp	:= ""

	If Select("TRA") > 0
		DbSelectarea("TRA")
		DbCloseArea()
	Endif
	
	_cQuery := "SELECT								"
	_cQuery += CRLF + "AD1_FILIAL,					"	
	_cQuery += CRLF + "AD1_NROPOR,					"
  	_cQuery += CRLF + "ADY_PROPOS,					"
  	_cQuery += CRLF + "ADY_DATA,					"
  	_cQuery += CRLF + "A1_NOME,						"
  	_cQuery += CRLF + "A1_CGC,						"
  	_cQuery += CRLF + "A1_MUN,						"
  	_cQuery += CRLF + "AD1_CNTPRO,					"
  	_cQuery += CRLF + "U5_DDD,						"
  	_cQuery += CRLF + "U5_FCOM1,					"
  	_cQuery += CRLF + "ADZ_PRODUT,					"
  	_cQuery += CRLF + "ADZ_XUNEXE,					"
  	_cQuery += CRLF + "AD1_VEND,					"
  	_cQuery += CRLF + "CASE AD1_STATUS				"
   _cQuery += CRLF + " WHEN '1' THEN 'Em Aberto'			"
   _cQuery += CRLF + " WHEN '2' THEN 'Perdido'				"
   _cQuery += CRLF + " WHEN 'E' THEN 'Ganha sem Pedido'		"
   _cQuery += CRLF + " WHEN 'F' THEN 'Ganha com Pedido'		"
   _cQuery += CRLF + " WHEN 'D' THEN 'Canc. Apos Aceite'	"
   _cQuery += CRLF + " ELSE ' '								"
  	_cQuery += CRLF + "END AD1_STATUS,						"
  	_cQuery += CRLF + "CASE ADY_STATUS						"
   _cQuery += CRLF + " WHEN 'A' THEN 'Aberta'				"
   _cQuery += CRLF + " WHEN 'B' THEN 'Fechada'				"
   _cQuery += CRLF + " WHEN 'C' THEN 'Cancelada'			"
   _cQuery += CRLF + " WHEN 'D' THEN 'Nao Aprovada'			"
   _cQuery += CRLF + " WHEN 'E' THEN 'Aprovada'				"
   _cQuery += CRLF + " WHEN 'F' THEN 'Bloqueada'			"
   _cQuery += CRLF + " WHEN 'G' THEN 'Upload pendente'		"
   _cQuery += CRLF + " WHEN 'H' THEN 'Aguardando Analise'	"
   _cQuery += CRLF + " WHEN 'I' THEN 'Contrato Gerado'		"
   _cQuery += CRLF + " WHEN 'J' THEN 'Pedido de Venda Gerado'"
   _cQuery += CRLF + " WHEN 'K' THEN 'Nota Fiscal Gerada'	"
   _cQuery += CRLF + " WHEN 'Y' THEN 'Cancelada Apos Aprovacao'	"
   _cQuery += CRLF + " WHEN 'Z' THEN 'Distratada'	"
   _cQuery += CRLF + " ELSE ' '								"
	_cQuery += CRLF + "END ADY_STATUS,						"
  	_cQuery += CRLF + "SB1.B1_TIPO,							"
  	_cQuery += CRLF + "SB1.B1_GRUPO							"
	_cQuery += CRLF + "FROM " + RetSqlName("AD1") + " AD1 	"
	_cQuery += CRLF + "LEFT JOIN " + RetSqlName("ADY") + " ADY ON 	ADY.ADY_FILIAL = AD1.AD1_FILIAL			" 
  	_cQuery += CRLF + "															AND ADY.ADY_OPORTU = AD1.AD1_NROPOR 	"
  	_cQuery += CRLF + "															AND ADY.D_E_L_E_T_ <> '*'				"
  	_cQuery += CRLF + "LEFT JOIN " + RetSqlName("ADZ") + " ADZ ON 	ADZ.ADZ_FILIAL = ADY.ADY_FILIAL 		"
  	_cQuery += CRLF + "															AND ADZ.ADZ_PROPOS = ADY.ADY_PROPOS 	"
  	_cQuery += CRLF + "															AND ADZ.ADZ_REVISA = ADY.ADY_PREVIS 	"
  	_cQuery += CRLF + "															AND ADZ.D_E_L_E_T_ <> '*'				"
  	
  	/**INICIO - Adição de joins para trazer tipo e grupo do produto. Walmir Junior em 01/11/2016*/
  	_cQuery += CRLF + "INNER JOIN " + RetSqlName("SB1") + " SB1 ON 	SB1.B1_FILIAL = '"+xFilial("SB1")+"' 				"
	_cQuery += CRLF + "															AND SB1.B1_COD = ADZ.ADZ_PRODUT 		"
	/*
  	_cQuery += CRLF + "INNER JOIN " + RetSqlName("SX5") + " SX5 ON 	SX5.X5_FILIAL = '"+xFilial("SX5")+"' 				"
	_cQuery += CRLF + "															AND SX5.X5_TABELA = '02'				"
	_cQuery += CRLF + "															AND SX5.X5_CHAVE  = SB1.B1_TIPO			"

  	_cQuery += CRLF + "INNER JOIN " + RetSqlName("SBM") + " SBM ON 	SBM.BM_FILIAL = '"+xFilial("SBM")+"' 				"
	_cQuery += CRLF + "															AND SBM.BM_GRUPO = SB1.B1_GRUPO 		"*/
	/**FINAL - Adição de joins para trazer tipo e grupo do produto. Walmir Junior em 01/11/2016*/
  	
  	_cQuery += CRLF + "INNER JOIN " + RetSqlName("SA1") + " SA1 ON SA1.A1_FILIAL = '"+xFilial("SA1")+"' 	"
	_cQuery += CRLF + "															AND SA1.A1_COD = AD1.AD1_CODCLI			"
	_cQuery += CRLF + "															AND SA1.A1_LOJA = AD1.AD1_LOJCLI		"	
	_cQuery += CRLF + "															AND SA1.D_E_L_E_T_ <> '*'				"
	_cQuery += CRLF + "LEFT JOIN " + RetSqlName("SU5") + " SU5 ON SU5.U5_FILIAL = '"+xFilial("SU5")+"' 	"
	_cQuery += CRLF + "															AND SU5.U5_CODCONT = AD1.AD1_CNTPRO	"
	_cQuery += CRLF + "															AND SU5.D_E_L_E_T_ <> '*'				"
	_cQuery += CRLF + "WHERE																										"
	
	If cFilAnt $ '02MT0001/03MT0001'
		_cQuery += CRLF + "	SubStr(AD1_FILIAL,1,4) = '"+ SubStr(cFilAnt,1,4) +"'
	Else
		_cQuery += CRLF + "	AD1_FILIAL = '"+ xFilial("AD1") +"'
	EndIf
	
  	_cQuery += CRLF + "	AND AD1_DATA BETWEEN '" + DtoS(MV_PAR01) + "' AND '" + DtoS(MV_PAR02 + 20) + "'		"
  	
  	If SubStr(cFilAnt,1,4) == '02MT'
  		_cQuery += CRLF + "	AND ADZ_XUNEXE BETWEEN '"+ MV_PAR03 +"' AND '"+ MV_PAR04 +"'
  	EndIf
  	
  	_cQuery += CRLF + "	AND AD1_VEND BETWEEN '"+ MV_PAR05 +"' AND '"+ MV_PAR06 +"'
  	
  	If .Not. Empty(Alltrim(MV_PAR07))
		_cQuery += CRLF + " AND AD1.AD1_STATUS IN "+ FormatIn(MV_PAR07,";")
	Endif
  	
  	If .Not. Empty(Alltrim(MV_PAR08)) .And. SubStr(cFilAnt,1,4) == '02MT' 
		_cQuery += CRLF + " AND ADY.ADY_STATUS IN "+ FormatIn(MV_PAR08,";")
	Endif
	
  	_cQuery += CRLF + " AND AD1.D_E_L_E_T_ <> '*'	"
  	_cQuery += CRLF + " ORDER BY ADZ_XUNEXE, AD1_NROPOR, ADY_PROPOS, ADY_DATA, AD1_VEND	"
		
	TCQUERY _cQuery NEW ALIAS "TRA"
		
	MemoWrite("C:\temp\"+FunName()+"_"+ProcName()+".TXT",_cQuery)
	
	DbSelectArea("TRA")
	TRA->(DbGoTop())
		
	oReport:SetMeter(RecCount())
	oSection:Init()
		
	While .Not. TRA->( EOF() )
		
		If oReport:Cancel()
			Exit
		EndIf
		
		_cFilial	:= AllTrim(TRA->AD1_FILIAL)
		_cOpor		:= AllTrim(TRA->AD1_NROPOR)
		_cProp		:= AllTrim(TRA->ADY_PROPOS)
		_cEmissao	:= DtoC(StoD(ADY_DATA))
		_cVncto		:= DtoC(StoD(ADY_DATA) + 20)
		_cNomCli	:= AllTrim(TRA->A1_NOME)
		_cCGC		:= AllTrim(Transform(TRA->A1_CGC,Iif(Len(AllTrim(TRA->A1_CGC))==11,"@R 999.999.999-99","@R 99.999.999/9999-99")))
		_cMunCli	:= AllTrim(TRA->A1_MUN)
		_cTelCto	:= "(" + AllTrim(TRA->U5_DDD) + ") " + Transform(TRA->U5_FCOM1,"@R 9999-9999")
		_cProdut	:= AllTrim(Posicione("SB1",1,xFilial("SB1")+TRA->ADZ_PRODUT,"B1_DESC"))
		_cUndExe	:= AllTrim(TRA->ADZ_XUNEXE)
		_cNomVend	:= AllTrim(Posicione("SA3",1,xFilial("SA3")+TRA->AD1_VEND,"A3_NOME"))
		_cSitOpor	:= TRA->AD1_STATUS
		_cSitProp	:= TRA->ADY_STATUS
		_cB1Tip		:= TRA->B1_TIPO
		_cB1Grp		:= TRA->B1_GRUPO
		
		oSection:Cell("_cFilial"):SetValue(_cFilial)
		oSection:Cell("_cOpor"):SetValue(_cOpor)
		oSection:Cell("_cProp"):SetValue(_cProp)
		oSection:Cell("_cEmissao"):SetValue(_cEmissao)
		oSection:Cell("_cVncto"):SetValue(_cVncto)
		oSection:Cell("_cNomCli"):SetValue(_cNomCli)
		oSection:Cell("_cCGC"):SetValue(_cCGC)
		oSection:Cell("_cMunCli"):SetValue(_cMunCli)
		oSection:Cell("_cTelCto"):SetValue(_cTelCto)
		oSection:Cell("_cProdut"):SetValue(_cProdut)
		oSection:Cell("_cUndExe"):SetValue(_cUndExe)
		oSection:Cell("_cNomVend"):SetValue(_cNomVend)
		oSection:Cell("_cSitOpor"):SetValue(_cSitOpor)
		oSection:Cell("_cSitProp"):SetValue(_cSitProp)
		//Walmir Junior em 04/11/2016
		oSection:Cell("_cB1Tip"):SetValue(_cB1Tip)
		oSection:Cell("_cB1Grp"):SetValue(_cB1Grp)
			
		oSection:PrintLine()
		oReport:IncMeter()
			
		TRA->( DbSkip() )
	End
		
	TRA->(DbCloseArea())
	oSection:Finish()
	
Return( Nil )


/** {Protheus.doc} fCriaSx1
Funcao para criacao das perguntas

@param: 	<Nil>
@author 	Jose Leite de Barros Neto
@since 	15/03/2016
@Uso: 		SFIEMT
*/
Static Function fCriaSx1( p_cPerg )
	
	Local aHelp := {}

	AAdd(aHelp, {{"Informe a Data de Emissao Inicial"		}, {""}, {""}})
	AAdd(aHelp, {{"Informe a Data de Emissao Final"			}, {""}, {""}})
	AAdd(aHelp, {{"Informe a Unidade Executora Inicial"		}, {""}, {""}})
	AAdd(aHelp, {{"Informe a Unidade Executora Final"		}, {""}, {""}})
	AAdd(aHelp, {{"Informe o Consultor Inicial"				}, {""}, {""}})
	AAdd(aHelp, {{"Informe o Consultor Inicial"				}, {""}, {""}})
	AAdd(aHelp, {{"Informe a Situação da Oportunidade"		}, {""}, {""}})
	AAdd(aHelp, {{"Informe o Status da Proposta"			}, {""}, {""}})
	
	u_SFPUTSX1( p_cPerg, "01","Data Vigencia Inicial?	"  ,"","","mv_ch1","D",8						,0,0,"G",""						,""			,"","","mv_par01","","","","","","","","","","","","","","","","",aHelp[1,1],aHelp[1,2],aHelp[1,3],"")
	u_SFPUTSX1( p_cPerg, "02","Data Vigencia Final?		"  ,"","","mv_ch2","D",8						,0,0,"G",""						,""			,"","","mv_par02","","","","","","","","","","","","","","","","",aHelp[2,1],aHelp[2,2],aHelp[2,3],"")
	u_SFPUTSX1( p_cPerg, "03","Unidade Executora de?	"  ,"","","mv_ch3","C",TamSX3("ADZ_XUNEXE")[1]	,0,0,"G",""						,"SM0"		,"","","mv_par03","","","","","","","","","","","","","","","","",aHelp[3,1],aHelp[3,2],aHelp[3,3],"")
	u_SFPUTSX1( p_cPerg, "04","Unidade Executora Ate?	"  ,"","","mv_ch4","C",TamSX3("ADZ_XUNEXE")[1]	,0,0,"G",""						,"SM0"		,"","","mv_par04","","","","","","","","","","","","","","","","",aHelp[4,1],aHelp[4,2],aHelp[4,3],"")	
	u_SFPUTSX1( p_cPerg, "05","Consultor de?			"  ,"","","mv_ch5","C",TamSX3("ADY_VEND")[1]	,0,0,"G",""						,"SA3"		,"","","mv_par05","","","","","","","","","","","","","","","","",aHelp[5,1],aHelp[5,2],aHelp[5,3],"")
	u_SFPUTSX1( p_cPerg, "06","Consultor Ate?			"  ,"","","mv_ch6","C",TamSX3("ADY_VEND")[1]	,0,0,"G",""						,"SA3"		,"","","mv_par06","","","","","","","","","","","","","","","","",aHelp[6,1],aHelp[6,2],aHelp[6,3],"")
	u_SFPUTSX1( p_cPerg, "07","Situação Oportunidade ?	"  ,"","","mv_ch7","C",99						,0,0,"G","u_MrkAD1()"			,""			,"","","mv_par07","","","","","","","","","","","","","","","","",aHelp[7,1],aHelp[7,2],aHelp[7,3],"")
	u_SFPUTSX1( p_cPerg, "08","Status Proposta ?		"  ,"","","mv_ch8","C",99						,0,0,"G","u_MrkADY()"			,""			,"","","mv_par08","","","","","","","","","","","","","","","","",aHelp[8,1],aHelp[8,2],aHelp[8,3],"")
	
Return( Nil )