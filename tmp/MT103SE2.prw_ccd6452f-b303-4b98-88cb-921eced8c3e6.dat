#include 'protheus.ch'

//Ponto de Entrada MATA103 
//Permitir editar o Codigo de Barras na aba Duplicatas e alerta ao usuario
//Edmar Tinti - Doit Sistemas 06/03/2014 - Alteração 24/03/14 Botão Visual Fornec.
//Obs: Corrigir Gatilho E2_CODBAR = iif("FIN"$(FunName()), U_CONVLD(), M->E2_CODBAR)
                        
User Function MT103SE2()
Local aHeader := PARAMIXB[1]
Local lVisual := PARAMIXB[2]
Local aReturn := {}        
If Type("oFolder") == "O"
	If Type("lVisual") == "L" .And. ! lVisual
		@ 52, 10 SAY "Atenção... Em caso de Boleto, informar o código de barras na aba Duplicatas" OF oFolder:aDialogs[1] SIZE 200,9 COLORS 255, 16777215 PIXEL
	Endif                                                                    
	If Type("cTipo") == "C" .And. ! cTipo $("BD") .And. ! Empty(SA2->A2_COD) 
		@ 5, 245 BUTTON "Visual" SIZE 30,11 ACTION AxVisual("SA2", SA2->(RECNO()), 2) OF oFolder:aDialogs[2] PIXEL
	Endif	
	//Os dados do acols neste array serão gravados no PE MT100GE2.PRW
	If MsSeek("E2_CODBAR")
		aAdd(aReturn, { TRIM(X3Titulo()),;
		SX3->X3_CAMPO,;
		SX3->X3_PICTURE,;
		SX3->X3_TAMANHO,;
		SX3->X3_DECIMAL,;
		".T.",;
		SX3->X3_USADO,;
		SX3->X3_TIPO,;
		SX3->X3_F3,;
		SX3->X3_CONTEXT,;
		SX3->X3_CBOX,;
		SX3->X3_RELACAO,;
		".T." })
	EndIf
Endif
Return(aReturn)
    