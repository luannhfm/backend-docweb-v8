#INCLUDE "PROTHEUS.CH" 

/*/{Protheus.doc} SF05R02X
Gera recibo de carteira
@author j2a.luizjunior
@since 17/08/2017
@version 1.0
@example
(examples)
@see (links_or_references)
/*/

User Function SF05R02X

	Local cEstat := U_SFGN001A(ProcName(0), "SF05R02X")
	Local   oReport
	Private cPerg   := "SF05R02X"
	Private cAlFor  := GetNextAlias()
	
	CriaSX1(cPerg)
	If !Pergunte(cPerg,.T.)
		Return(Nil)
	EndIf

	oReport := ReportDef()
	oReport:PrintDialog()	

Return

/*/{Protheus.doc} ReportDef
(long_description)
@author j2a.luizjunior
@since 17/08/2017
@version 1.0
@example
(examples)
@see (links_or_references)
/*/

Static Function ReportDef(cPerg)

	Local cEstat := U_SFGN001A(ProcName(0), "SF05R02X")
	Local oReport
	Local cIDCelula	:= ""	
	Local cNomeRel	:= "SF05R02X"
	
	Local cTitRel	:= "Relatorio de Socios Ativos"
	Local cDescRel	:= "Relatorio de Socios Ativos"

	oReport         := TReport():New( cNomeRel, cTitRel, cPerg, { |oReport| ReportPrint(oReport) }, cDescRel )
	oReport:lHeaderVisible := .T.
	oReport:SetLandScape() 
	//oReport:SetTotalInLine(.F.) 
	oReport:Page()
	
	oSocio  := TRSection():New(oReport, "Relatorio de Socios Ativos" ,{"ZH1"})	
	//oSocio:lHeaderVisible := .T.
	oSocio:SetHeaderBreak(.T.)
		
	oCod    := TRCell():New(oSocio,"ZH1_COD"   ,"ZH1"                        )	
	oCod:SetSize(25)
	
	oMatric := TRCell():New(oSocio,"ZH1_MATRIC","ZH1"                        )
	oMatric:SetSize(20)
	
	oNumero := TRCell():New(oSocio,"ZH1_NUMERO","ZH1"                        )
	oNumero:SetSize(55)
	
	oRevisa := TRCell():New(oSocio,"ZH1_REVISA","ZH1"                        )
	oRevisa:SetSize(10)
	
	oNome   := TRCell():New(oSocio,"ZH1_NOME"  ,"ZH1"                        )
	oNome:SetSize(45)
	
	oRG     := TRCell():New(oSocio,"ZH1_RG"    ,"ZH1"                        )
	oRG:SetSize(25)
	
	oTipo   := TRCell():New(oSocio,"ZH1_TIPO"  ,"ZH1"                        )
	oTipo:SetSize(45)
	
	oCGC    := TRCell():New(oSocio,"ZH1_CGC"   ,"ZH1"                        )
	oCGC:SetSize(55)
	
	oValid  := TRCell():New(oSocio,"ZH1_VALID" ,"ZH1"                        )
	
	oValor  := TRCell():New(oSocio,"ZH1_VALOR" ,"ZH1"                        )
	oValor:SetSize(20)
	
	oVlNeg  := TRCell():New(oSocio,"ZH1_VLNEG" ,"ZH1"                        )
	oVlNeg:SetSize(20)
	
	oDtNas  := TRCell():New(oSocio,"ZH1_DTNASC","ZH1"                        )
	oDtNas:SetSize(20)

	oEnd    := TRCell():New(oSocio,"ZH1_END"   ,"ZH1"                        )
	oEnd:SetSize(45) 
	
	oNumRes := TRCell():New(oSocio,"ZH1_NUMRES","ZH1"                        )
	oNumRes:SetSize(15)
	
	oBairro := TRCell():New(oSocio,"ZH1_BAIRRO","ZH1"                        )
	oBairro:SetSize(45)
		
	oEst    := TRCell():New(oSocio,"ZH1_EST"   ,"ZH1"                        )
	oEst:SetSize(15)
	
	oMun    := TRCell():New(oSocio,"ZH1_MUN"   ,"ZH1"                        )
	oMun:SetSize(45)
	
	oSitua  := TRCell():New(oSocio,"ZH1_SITUAC","ZH1"                        )
	oSitua:SetSize(35)
	
	oCateg  := TRCell():New(oSocio,"ZH1_CATEGO","ZH1"                        )
	oCateg:SetSize(35)
	
	oBrFor  := TRBreak():New(oSocio,oSocio:Cell("ZH1_COD"),Nil,.T.)
	
	oDepen  := TRSection():New(oReport, "Dependentes", {"ZH2"})
	oDepen:SetHeaderBreak(.F.)
	oDepen:SetHeaderPage(.T.)
	
	oCod    := TRCell():New(oDepen,"ZH2_COD","ZH2"                           )		
	oCodDep := TRCell():New(oDepen,"ZH2_CODDEP","ZH2"                        )
	oItem   := TRCell():New(oDepen,"ZH2_ITEM"  ,"ZH2"                        )
	oNome   := TRCell():New(oDepen,"ZH2_NOME"  ,"ZH2"                        )
	oNome:SetSize(60)
	oDtNZh2 := TRCell():New(oDepen,"ZH2_DTNASC","ZH2"                        )
	oRGZH2  := TRCell():New(oDepen,"ZH2_RG"    ,"ZH2"                        )
	oCGCZH2 := TRCell():New(oDepen,"ZH2_CGC"   ,"ZH2"                        )
	oTpZH2  := TRCell():New(oDepen,"ZH2_TIPO"  ,"ZH2"                        )
	
	oBrFor  := TRBreak():New(oDepen,oDepen:Cell("ZH2_COD"),Nil,.T.)

	oReport:Section(1):SetHeaderSection(.T.)
	oReport:Section(2):SetHeaderSection(.T.)

Return(oReport)

/*/{Protheus.doc} ReportPrint
(long_description)
@author j2a.luizjunior
@since 17/08/2017
@version 1.0
@example
(examples)
@see (links_or_references)
/*/

Static Function ReportPrint(oReport)

	Local cEstat := U_SFGN001A(ProcName(0), "SF05R02X")
	Local cAlZH1 := GetNextAlias()  
	Local cAlZH2 := GetNextAlias()
	Local cSQL   := ""	 
	Local oSec1  := oReport:Section(1)
	Local oSec2  := oReport:Section(2)
	Local lPrint := .F.
	
	If !Empty(MV_PAR01)
		cSQL += " AND ZH1_NUMERO = '" + MV_PAR01 + "' "
		cSQL += " AND ZH1_REVISA = '" + MV_PAR02 + "' "
	EndIf	

	Do Case
		Case MV_PAR03 == 1
			cSQL += " AND ZH1_SITUAC = 'A' "
		Case MV_PAR03 == 2
			cSQL += " AND ZH1_SITUAC = 'B' "
		Case MV_PAR03 == 3
			cSQL += " AND ZH1_SITUAC = 'D' "
	EndCase

	If !Empty(MV_PAR05)
		cSQL += " AND ZH1_COD    = '" + MV_PAR05 + "' "
	EndIf	
	
	If !Empty(MV_PAR07)
		cSQL += " AND ZH1_VALID  = '" + DToS(MV_PAR07) + "' "
	EndIf	
	
	cSql := "%"+cSql+"%"
	
	BeginSql Alias cAlZH1

		SELECT ZH1_COD,
			   ZH1_MATRIC,
			   ZH1_NUMERO,
			   ZH1_REVISA,
			   ZH1_NOME  ,
			   ZH1_RG    ,
			   ZH1_TIPO  ,
			   ZH1_CGC   ,
			   ZH1_VALID ,
			   ZH1_VALOR ,
			   ZH1_VLNEG ,
			   ZH1_DTNASC,
			   ZH1_SEXO  ,
			   ZH1_ESTCIV,
			   ZH1_END   ,
			   ZH1_NUMRES,
			   ZH1_BAIRRO,
			   ZH1_EST   ,
			   ZH1_MUN   ,
			   ZH1_SITUAC,
			   ZH1_CATEGO
		FROM   %Table:ZH1%
		WHERE  %NotDel%
		%Exp:cSql%
	
	EndSql	
	
	While !(cAlZH1)->(Eof())
		
		oReport:IncMeter()
		
		oSec1:Init()
		
		oSec1:Cell("ZH1_COD")   :SetValue((cAlZH1)->ZH1_COD)
		oSec1:Cell("ZH1_MATRIC"):SetValue((cAlZH1)->ZH1_MATRIC)
		oSec1:Cell("ZH1_NUMERO"):SetValue((cAlZH1)->ZH1_NUMERO)
		oSec1:Cell("ZH1_REVISA"):SetValue((cAlZH1)->ZH1_REVISA)
		oSec1:Cell("ZH1_NOME")  :SetValue((cAlZH1)->ZH1_NOME)
		oSec1:Cell("ZH1_RG")    :SetValue((cAlZH1)->ZH1_RG)
		
		Do Case
			Case (cAlZH1)->ZH1_TIPO == "F"
				cTipo := "Fisica"
			Case (cAlZH1)->ZH1_TIPO == "J"
				cTipo := "Juridica"
			Case (cAlZH1)->ZH1_TIPO == "S"
				cTipo := "Funcionario"
			Case (cAlZH1)->ZH1_TIPO == "O"
				cTipo := "Outros" 
		EndCase
		
		oSec1:Cell("ZH1_TIPO")  :SetValue(cTipo)
		oSec1:Cell("ZH1_CGC")   :SetValue((cAlZH1)->ZH1_CGC)
		oSec1:Cell("ZH1_VALID") :SetValue(DToC(SToD((cAlZH1)->ZH1_VALID)))
		oSec1:Cell("ZH1_VALOR") :SetValue((cAlZH1)->ZH1_VALOR)
		oSec1:Cell("ZH1_VLNEG") :SetValue((cAlZH1)->ZH1_VLNEG)
		oSec1:Cell("ZH1_DTNASC"):SetValue(DToC(SToD((cAlZH1)->ZH1_DTNASC)))
		oSec1:Cell("ZH1_END")   :SetValue((cAlZH1)->ZH1_END)
		oSec1:Cell("ZH1_NUMRES"):SetValue((cAlZH1)->ZH1_NUMRES)
		oSec1:Cell("ZH1_BAIRRO"):SetValue((cAlZH1)->ZH1_BAIRRO)
		oSec1:Cell("ZH1_EST")   :SetValue((cAlZH1)->ZH1_EST)
		oSec1:Cell("ZH1_MUN")   :SetValue((cAlZH1)->ZH1_MUN)
		
		Do Case		
			Case (cAlZH1)->ZH1_CATEGO == "I"
			 	cCategoria := "Industria"
			Case (cAlZH1)->ZH1_CATEGO == "C"
				cCategoria := "Comunidade"
			Case (cAlZH1)->ZH1_CATEGO == "S"
				cCategoria := "Sistema Fiemt"
		EndCase
		
		oSec1:Cell("ZH1_CATEGO"):SetValue(cCategoria)
		
		Do Case		
			Case (cAlZH1)->ZH1_SITUAC == "A"
			 	cSitua := "Ativo"
			Case (cAlZH1)->ZH1_SITUAC == "B"
				cSitua := "Bloqueado"
			Case (cAlZH1)->ZH1_SITUAC == "D"
				cSitua := "Desativado"
		EndCase
		
		oSec1:Cell("ZH1_SITUAC"):SetValue(cSitua)
		
		oSec1:Printline()
		
		If oReport:Cancel()
			Return(Nil)
		EndIf		
	
		BeginSql Alias cAlZH2 

			SELECT ZH2_CODDEP,
				   ZH2_ITEM,
				   ZH2_NOME,
				   ZH2_SEXO,
				   ZH2_DTNASC,
				   ZH2_RG,
				   ZH2_CGC,
				   ZH2_TIPO,
				   ZH2_COD
			FROM   %Table:ZH2%
			WHERE  %NotDel%
			AND    ZH2_COD = %Exp:(cAlZH1)->ZH1_COD%
		
		EndSql
		
			While !(cAlZH2)->(Eof())

				oSec2:Init()
				oSec2:Cell("ZH2_COD")   :SetValue((cAlZH2)->ZH2_COD   )
				oSec2:Cell("ZH2_CODDEP"):SetValue((cAlZH2)->ZH2_CODDEP)
				oSec2:Cell("ZH2_ITEM")  :SetValue((cAlZH2)->ZH2_ITEM  )
				oSec2:Cell("ZH2_NOME")  :SetValue((cAlZH2)->ZH2_NOME  )				
				oSec2:Cell("ZH2_DTNASC"):SetValue((cAlZH2)->ZH2_DTNASC)
				oSec2:Cell("ZH2_RG")    :SetValue((cAlZH2)->ZH2_RG    )
				oSec2:Cell("ZH2_CGC")   :SetValue((cAlZH2)->ZH2_CGC   )
				oSec2:Cell("ZH2_TIPO")  :SetValue((cAlZH2)->ZH2_TIPO  )
				
				oSec2:Printline()
				
				If oReport:Cancel()
					Return(Nil)
				EndIf				
				
				(cAlZH2)->(DbSkip())
			EndDo		
			
			(cAlZH2)->(DbCloseArea())
					
		(cAlZH1)->(DbSkip())
	EndDo
	
	oSec1:Finish()
	oSec2:Finish()
	
	(cAlZH1)->(DbCloseArea())
	
Return

/*/{Protheus.doc} CriaSX1
(long_description)
@author j2a.luizjunior
@since 17/08/2017
@version 1.0
@example
(examples)
@see (links_or_references)
/*/

Static Function CriaSX1

	Local cEstat := U_SFGN001A(ProcName(0), "SF05R02X")
	Local aHelp := {}
	
	//-> Texto do help em português,inglês, espanhol
	AAdd(aHelp, {{"Contrato"                   },{""},{""}})
	AAdd(aHelp, {{"Revisao"                    },{""},{""}})
	AAdd(aHelp, {{"Situacao"                   },{""},{""}})
	AAdd(aHelp, {{"Tipo"                       },{""},{""}})
	AAdd(aHelp, {{"N Car Tit"                  },{""},{""}})
	AAdd(aHelp, {{"N Car Dep"                  },{""},{""}})
	AAdd(aHelp, {{"Validade"                   },{""},{""}})

	u_SFPUTSX1(cPerg,"01","Contrato"  ,"","","mv_ch01","C",15,00,00,"G","","CN9VEN","","","mv_par01",""       ,"","","",""          ,"","",""          ,"","","","","","","","",aHelp[1,1],aHelp[1,2],aHelp[1,3],"")
	u_SFPUTSX1(cPerg,"02","Revisão"   ,"","","mv_ch02","C",03,00,00,"G","",""      ,"","","mv_par02",""       ,"","","",""          ,"","",""          ,"","","","","","","","",aHelp[2,1],aHelp[2,2],aHelp[2,3],"")
	u_SFPUTSX1(cPerg,"03","Situacao"  ,"","","mv_ch03","N",01,00,00,"C","",""      ,"","","mv_par03","Ativo"  ,"","","","Bloqueado" ,"","","Desativado","","","","","","","","",aHelp[3,1],aHelp[3,2],aHelp[3,3],"")
	u_SFPUTSX1(cPerg,"04","Tipo"      ,"","","mv_ch04","N",01,00,00,"C","",""      ,"","","mv_par04","Titular","","","","Dependente","","","Ambos"     ,"","","","","","","","",aHelp[4,1],aHelp[4,2],aHelp[4,3],"")
	u_SFPUTSX1(cPerg,"05","Titular"   ,"","","mv_ch05","C",09,00,00,"C","",""      ,"","","mv_par05",""       ,"","","",""          ,"","",""          ,"","","","","","","","",aHelp[5,1],aHelp[5,2],aHelp[5,3],"")
	u_SFPUTSX1(cPerg,"06","Dependente","","","mv_ch06","C",09,00,00,"C","",""      ,"","","mv_par06",""       ,"","","",""          ,"","",""          ,"","","","","","","","",aHelp[6,1],aHelp[6,2],aHelp[6,3],"")
	u_SFPUTSX1(cPerg,"07","Validade"  ,"","","mv_ch07","D",08,00,00,"G","",""      ,"","","mv_par07",""       ,"","","",""          ,"","",""          ,"","","","","","","","",aHelp[7,1],aHelp[7,2],aHelp[7,3],"")

Return