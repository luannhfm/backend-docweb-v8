#include "Rwmake.ch"
#include "Protheus.ch"


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  |SF06R15X  ºAutor  ³Caio Renan          º Data ³ 18/07/2017  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Relatorio de compensacao de PA contas a pagar              º±±
±±º          ³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³                                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

User Function SF06R15X()
Local oReport

Private _cPerg := "SF06R15X"
Private _cALias := GetNextAlias()
Private _oTitulo , _oFornece , _oTotal // variaveis dos objetos de secao
Private _nTotal := 0, _cTotal := ""
Private _cPicture := TM(999999999.99,14,2) // "@E 999,999,999.99"

If FindFunction("TRepInUse") // .And. TRepInUse()
	//-- Interface de impressao
	oReport := ReportDef()
	oReport:PrintDialog()
EndIf

Return
//ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
// funcao Definicoes dos objetos treport
//ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
Static Function ReportDef()
Local oReport
Local _cDescri := "Relatório adiantamento de despesas - Compensações"

AjustaSX1(_cPerg)

Pergunte(_cPerg, .F.)

oReport := TReport():New(_cPerg,_cDescri,_cPerg,{|oReport| PrintReport(oReport)},_cDescri)
oReport:SetLeftMargin(2)
oReport:SetPortrait() // Retrato
oReport:oPage:SetPaperSize(9) // papel A4
oReport:SetEnvironment(2) // local
oReport:SetTotalInLine(.F.)

_oFornece := TRSection():New( oReport,"Fornecedor",{_cALias, "SA2"} )
//_oFornece:SetLineStyle(.T.)
TRCell():New(_oFornece,"A2_COD"  , "SA2" )
TRCell():New(_oFornece,"A2_LOJA" , "SA2" )
TRCell():New(_oFornece,"A2_NOME" , "SA2" )

_oTitulo := TRSection():New( oReport,"Titulo",{_cALias, "SE2", "SA2"} )

TRCell():New(_oTitulo,"DATA_MOV"     ,_cALias,"Data",,10)
TRCell():New(_oTitulo,"E5_DOCUMEN"   ,_cALias,,,,, {|| fGetDocumen() } )
TRCell():New(_oTitulo,"ENTRADA"      ,_cALias,"Entrada",_cPicture,14,, {|| fGetValor(1) } )
TRCell():New(_oTitulo,"SAIDA"        ,_cALias,"Saida"  ,_cPicture,14,, {|| fGetValor(2) } )
TRCell():New(_oTitulo,"TOTAL"        ,_cALias,"Total"  ,_cPicture,14,, {|| fGetValor(3) } )

_oTotal := TRSection():New( oReport,"Total" )

TRCell():New(_oTotal,"DATA_MOV"     ,_cALias,"Data",,10,, {|| ""} )
TRCell():New(_oTotal,"ENTRADA"      ,_cALias,"Entrada",,14,, {|| "" } )
TRCell():New(_oTotal,"SAIDA"        ,_cALias,"Saida"  ,,14,, {|| "" } )
TRCell():New(_oTotal,"E5_DOCUMEN"   ,_cALias,,,,, {|| _cTotal } )
TRCell():New(_oTotal,"TOTAL"        ,_cALias,"Total"  ,_cPicture,14,, {|| _nTotal } )
_oTotal:Cell("E5_DOCUMEN"):SetAlign("RIGHT")
_oTotal:lHeaderSection := .F.
_oTotal:SetLinesBefore(0)
_oTotal:SetCellBorder("TOP",1,,.F.)

_oTitulo:SetTotalInLine(.F.)

// posicionamento das tabelas auxiliares
TRPosition():New(_oFornece,"SA2",1,{|| xFilial("SA2", (_cAlias)->E2_FILIAL)+ (_cAlias)->E2_FORNECE + (_cAlias)->E2_LOJA})
TRPosition():New(_oTitulo,"SA2",1,{|| xFilial("SA2", (_cAlias)->E2_FILIAL)+ (_cAlias)->E2_FORNECE + (_cAlias)->E2_LOJA})
TRPosition():New(_oTitulo,"SE2",1,{|| SE2->(dbGoTo((_cAlias)->RECSE2)) }, .F. )

Return(oReport)

/*/{Protheus.doc} fGetDocumen
Retorna o numero do documento
@author j2a.caiolima
@since 19/07/2017
@return Caractere, Numero do docuemnto
/*/
Static Function fGetDocumen()
	Local _cRet := ""
	
	If (_cAlias)->E5_TIPODOC = "BA"
		_cRet := (_cAlias)->E5_DOCUMEN
	Else
		_cRet := (_cAlias)->E2_PREFIXO + (_cAlias)->E2_NUM + (_cAlias)->E2_PARCELA + (_cAlias)->E2_TIPO + (_cAlias)->E2_FORNECE + (_cAlias)->E2_LOJA
	EndIf
	
Return _cRet

/*/{Protheus.doc} fGetValor
Funcao que retorna o valor do alias de acordo como parametro
@author j2a.caiolima
@since 19/07/2017
@version 1.0
@param _nTipo, Numerico, tipo de retorno entrada, saida ou total
@return Numerico, o valor de acordo com o parametro
/*/
Static Function fGetValor(_nTipo)
	Local _nRet := 0
	
	Do Case
		Case _nTipo == 1
			If (_cAlias)->E5_TIPODOC = "BA"
				_nRet := (_cAlias)->VAL_MOV
			ElseIf (_cAlias)->E5_TIPODOC <> "PA"
				_nRet := (_cAlias)->VAL_MOV	
			Else
				_nRet := 0
			EndIf
			
		Case _nTipo == 2
			If (_cAlias)->E5_TIPODOC = "PA"
				_nRet := (_cAlias)->VAL_MOV
			Else
				_nRet := 0
			EndIf
			
		Case _nTipo == 3
			If (_cAlias)->E5_TIPODOC = "PA"
				_nRet := (_cAlias)->VAL_MOV * (-1)
			Else
				_nRet := (_cAlias)->VAL_MOV
			EndIf
	EndCase
Return(_nRet)

//ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
//³funcao que realiza o filtro e imprimi os dados na tela³
//ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
Static Function PrintReport(oReport)
Local _cFor := ""
Local _bFor := {|| xFilial("SA2", (_cAlias)->E2_FILIAL)+ (_cAlias)->E2_FORNECE + (_cAlias)->E2_LOJA }
Local _cNFor := ""
Local _bNFor := {|| AllTrim(MemoLine(SA2->A2_NOME,15,1)) }
Local _cPa := ""
Local _bPA := {|| (_cAlias)->E2_FILIAL+ (_cAlias)->E2_PREFIXO + (_cAlias)->E2_NUM + (_cAlias)->E2_PARCELA + (_cAlias)->E2_TIPO + (_cAlias)->E2_FORNECE + (_cAlias)->E2_LOJA }
Local _nTFor := 0
Local _nTGeral := 0
Local _lReg := .F. // variavel apenas para identificar se tem registro

MsgRun( "Selecionando registros...", "Aguarde" ,  {|| GeraQuery() } )

While ( (_cALias)->(!Eof()) )
	_lReg := .T.
	
	If _cPa <> Eval(_bPA)
		_cPa := Eval(_bPA)
		_oTitulo:Init()
	EndIf
	
	If _cFor <> Eval(_bFor)
		_cFor := Eval(_bFor)
		
		_oFornece:Init()
		_oFornece:PrintLine()
		_oFornece:Finish()
		
		_cNFor := Eval(_bNFor)
	EndIf
	
	_nTotal += fGetValor(3)
	_nTFor += fGetValor(3)
	_nTGeral += fGetValor(3)
	_oTitulo:PrintLine()
	
	(_cALias)->(dbSkip())
	If _cPa <> Eval(_bPA)
		_oTitulo:Finish()
		_cTotal := "Saldo PA " + AllTrim(_cPa)
		_oTotal:Init()
		_oTotal:PrintLine()
		_oTotal:Finish()
		oReport:Skipline()
		
		_nTotal := 0
	EndIf
	
	If _cFor <> Eval(_bFor)
		_cTotal := "Total " + AllTrim(_cNFor)
		_nTotal := _nTFor
		_oTotal:Init()
		_oTotal:PrintLine()
		_oTotal:Finish()
		oReport:Skipline()
		
		_nTotal := 0
		_nTFor := 0
	EndIf
End

If _lReg
	_cTotal := "TOTAL GERAL"// + AllTrim(_cNFor)
	_nTotal := _nTGeral
	_oTotal:Init()
	_oTotal:PrintLine()
	_oTotal:Finish()
	//oReport:Skipline()
EndIf

(_cALias)->(dbClosearea())

Return
//ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
// funcao que realiza o sql do relatorio
//ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
Static Function GeraQuery()
Local _cSql := ""

_cSql+=" SELECT E5_VALOR AS VAL_MOV, E2_VALOR, E2_SALDO " +CRLF
_cSql+=" , E2_FILIAL, E2_PREFIXO, E2_NUM, E2_PARCELA, E2_TIPO, E2_FORNECE, E2_LOJA " +CRLF
_cSql+=" , E2_EMISSAO, E2_VENCTO, E5_DATA AS DATA_MOV " +CRLF
_cSql+=" , SE2.R_E_C_N_O_ AS RECSE2, E5_DOCUMEN " +CRLF
_cSql+=" , E5_TIPODOC" +CRLF
_cSql+=" FROM "+ RetSqlName("SE2") +" SE2 " +CRLF
_cSql+=" INNER JOIN "+ RetSqlName("SE5") +" SE5  " +CRLF
_cSql+=" 	ON  E5_FILIAL  = E2_FILIAL AND  E5_PREFIXO = E2_PREFIXO AND  E5_NUMERO  = E2_NUM " +CRLF
_cSql+=" 	AND  E5_PARCELA = E2_PARCELA AND  E5_TIPO    = E2_TIPO AND  E5_CLIFOR  = E2_FORNECE " +CRLF
_cSql+=" 	AND  E5_LOJA    = E2_LOJA AND  SE5.D_E_L_E_T_ <> '*' AND E5_TIPO='PA ' " +CRLF
_cSql+=" 	AND  SE5.E5_SITUACA NOT IN ('C','E','X') AND E5_TIPODOC NOT IN ('RA','ES','TR','TE','CH','EC') "+CRLF
_cSql+=" 	AND SE5.E5_MOTBX NOT IN ('DSD','FAT','LIQ') "+CRLF
_cSql+=" WHERE SE2.E2_FILIAL BETWEEN '"+ MV_PAR01 +"' AND '"+ MV_PAR02 +"'"
_cSql+=" 	AND SE2.E2_FORNECE BETWEEN '"+ MV_PAR05 +"' AND '"+ MV_PAR06 +"'"
_cSql+=" 	AND SE2.E2_PREFIXO BETWEEN '"+ MV_PAR07 +"' AND '"+ MV_PAR08 +"'"
_cSql+=" 	AND SE2.D_E_L_E_T_ <> '*' AND E2_TIPO='PA '  " +CRLF
_cSql+=" 	AND (SE2.E2_EMISSAO BETWEEN '"+Dtos(MV_PAR03)+"' AND '"+Dtos(MV_PAR04)+"' )  " +CRLF
// Busca movimentos apenas que não existem extorno do mesmo
_cSql += " AND NOT EXISTS (SELECT * FROM "+RetSQLName("SE5")+" E5X WHERE E5X.E5_FILIAL=SE5.E5_FILIAL AND E5X.E5_PREFIXO=SE5.E5_PREFIXO " + CRLF
_cSql += " AND E5X.E5_NUMERO=SE5.E5_NUMERO AND E5X.E5_PARCELA=SE5.E5_PARCELA AND E5X.E5_TIPO=SE5.E5_TIPO " + CRLF
_cSql += " AND E5X.E5_CLIFOR=SE5.E5_CLIFOR AND E5X.E5_LOJA=SE5.E5_LOJA AND E5X.E5_VALOR=SE5.E5_VALOR " + CRLF
_cSql += " AND E5X.E5_RECPAG='R' AND E5X.E5_SEQ=SE5.E5_SEQ AND E5X.E5_TIPODOC='ES' AND E5X.E5_DOCUMEN=SE5.E5_DOCUMEN) " + CRLF

_cSql+=" ORDER BY E2_FILIAL, E2_FORNECE, E2_LOJA, E2_EMISSAO, E2_PREFIXO, E2_NUM, E2_PARCELA, E2_TIPO, SE5.R_E_C_N_O_ " +CRLF

MemoWrite("D:\temp\"+FunName() + "-" + ProcName()+".txt" , _cSql)
If Select(_cALias) > 0
	(_cALias)->(dbClosearea())
Endif

DbUseArea(.T., "TOPCONN", TCGenQry(,,ChangeQuery(_cSql)), _cALias, .F., .F.)
TCSetField(_cALias,"DATA_MOV","D")
TCSetField(_cALias,"E2_VENCTO","D")
TCSetField(_cALias,"E2_EMISSAO","D")

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³sx1inv    ºAutor  ³caio renan          º Data ³  05/23/09   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³AJUSTA AS PERGUNTAS                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function AjustaSX1(cPerg)

Local aHelp01 := {}
Local aHelp03 := {}
Local aHelp04 := {}
Local aHelp05 := {}
Local aHelp06 := {}
Local _nFil := TamSX3("A2_FILIAL")[1]
Local _nFor := TamSX3("A2_COD")[1]
Local _nPre := TamSX3("E2_PREFIXO")[1]

u_SFPUTSX1(cPerg, "01", "Filial de?"     	,"","","mv_ch1","C",_nFil,00,00,"G","","XM0" ,"","","mv_par01","","","","","","","","","","","","","","","","",,"","","") 
u_SFPUTSX1(cPerg, "02", "Filial ate?" 		,"","","mv_ch2","C",_nFil,00,00,"G","","XM0" ,"","","mv_par02","","","","","","","","","","","","","","","","",,"","","") 
u_SFPUTSX1(cPerg, "03", "Emissao de?"     	,"","","mv_ch3","D",08   ,00,00,"G","",""	 ,"","","mv_par03","","","","","","","","","","","","","","","","",,"","","") 
u_SFPUTSX1(cPerg, "04", "Emissao ate?" 		,"","","mv_ch4","D",08   ,00,00,"G","",""	 ,"","","mv_par04","","","","","","","","","","","","","","","","",,"","","")
u_SFPUTSX1(cPerg, "05", "Fornecedor de?"	,"","","mv_ch5","C",_nFor,00,00,"G","","SA2" ,"","","mv_par05","","","","","","","","","","","","","","","","",,"","","") 
u_SFPUTSX1(cPerg, "06", "Fornecedor até?" 	,"","","mv_ch6","C",_nFor,00,00,"G","","SA2" ,"","","mv_par06","","","","","","","","","","","","","","","","",,"","","") 
u_SFPUTSX1(cPerg, "07", "Prefixo de?"		,"","","mv_ch7","C",_nPre,00,00,"G","",""	 ,"","","mv_par07","","","","","","","","","","","","","","","","",,"","","") 
u_SFPUTSX1(cPerg, "08", "Prefixo até?" 		,"","","mv_ch8","C",_nPre,00,00,"G","",""	 ,"","","mv_par08","","","","","","","","","","","","","","","","",,"","","") 

Return