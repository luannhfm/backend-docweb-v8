#INCLUDE 'RWMAKE.CH'
#INCLUDE 'PROTHEUS.CH'

#DEFINE	 CR		Chr(13)+Chr(10)


/*/{Protheus.doc} LJ7031
@description Ponto de entrada chamado no momento em que a tela de
			 Definição de Pagamentos é carregada. Foi criado com o
			 objetivo de enviar uma função para zerar as formas de
			 pagamento.

@author Helitom Silva
@since  03/09/2019

@see http://tdn.totvs.com/pages/releaseview.action?pageId=6790900

/*/
User Function LJ7031

	ConteudPE(ParamIXB)

Return


/*/{Protheus.doc} ConteudPE
@description Conteudo do PE tanto para loja como para venda direta.

@author Helitom Silva
@since  03/09/2019

/*/
Static Function ConteudPE(aParamIXB)

	Local lCria      := aParamIXB[1]	
	Local lPanVa3    := aParamIXB[2]
	Local oTmpDlg    := GetWndDefault()
	Local oTmpPanPag
	Local oTmpBotPag
	Local nX    	 := 0
	Local bActionBol := {||}
	Local bNewActBol := {||}
	Local cMVCliPad  := SuperGetMV("MV_CLIPAD")  // Cliente padrao
	Local cMVLojPad  := SuperGetMV("MV_LOJAPAD") // Loja Padrao
	Local cFormGT  	 := GetNewPar('MV_XFORMGT', 'GT') // Forma de Pagamento Gratuito.
		
	If !lCria // A tela de Definicao de Pagamentos já foi criada.
	
		// Atribui ao botão de cheque a rotina customizada de leitura de cheque
		For nX := 1 to Len(oTmpDlg:aControls)
		
			If ValType(oTmpDlg:aControls[nX]) = 'O' .and. 'BOLETO' $ Upper(AllTrim(Substr(oTmpDlg:aControls[nX]:cCaption, 2)))
				
				bActionBol := oTmpDlg:aControls[nX]:bAction
				bNewActBol := {|| Iif(AllTrim(M->LQ_CLIENTE) = AllTrim(cMVCliPad) .and. AllTrim(M->LQ_LOJA) = cMVLojPad, MsgInfo('Não é permitido pagamento via Boleto para Cliente Padrão!', 'IEL'), Eval(bActionBol))}
				oTmpDlg:aControls[nX]:bAction := bNewActBol	
							
			EndIf 	
					
		Next	
				
		If !lPanVA3 // A chamada foi realizada a partir da grade de itens - aCols. 
		
			If !lRecebe .and. (INCLUI .or. ALTERA)
				
				If .not. ( nNCCUsada > 0 )
					Lj7ZeraPgtos()	
				EndIf
				
			    // Sesi Park (Venda de Day Use)
			    If ( cFilAnt = '02MT0012' )				
					U_SF12A04X() // Lança forma de Pagamento Gratuita.
				EndIf
						
			Else
			
				If Len(aPgtos) = 1 .and. aScan(aPgtos,  {|X| (AllTrim(X[3]) $ 'XX')}) > 0 .and. (INCLUI .or. ALTERA)
					Lj7ZeraPgtos()
				Endif
							
			EndIf
					
		Else // A chamada foi realizada a partir da Tela de Definicao de Pagamentos
		
			If Len(aPgtos) = 1 .and. aScan(aPgtos,  {|X| (AllTrim(X[3]) $ 'XX')}) > 0 .and. (INCLUI .or. ALTERA)
				Lj7ZeraPgtos()
			Endif
			 				
		EndIf	

	Else
		
		/* Redimensiona Panel de Pagamentos e Grid de Pagamentos */
													
	EndIf

Return