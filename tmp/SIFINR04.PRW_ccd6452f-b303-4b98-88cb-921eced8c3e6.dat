#Include 'Protheus.ch'
#Include 'rwmake.ch'
#Include 'Topconn.ch'

#Define CRLF Chr(13) + Chr(10)
#Define MAX_TAMANHO 130	//numero maximo de carateres por linha
#Define MAX_LINHA	   74	// numero maximo de linhas por pagina

#Define ESQUERDA 0		//Alinhamento utilizado pelo TMSPrinter:Say()
#Define DIREITA 1			//Alinhamento utilizado pelo TMSPrinter:Say()
#Define CENTRALIZADO 2	//Alinhamento utilizado pelo TMSPrinter:Say()
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³SIFINR04  ºAutor  ³renato.neves           º Data ³02/09/2011º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Relatorio Ordem de Recebimento                              º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º Data     ³ Autor - descricao                                          º±±
±±º----------³------------------------------------------------------------º±±
±±º 01/03/19 ³ Caio J2A consultoria - #001                                º±±
±±º          ³ Alteração para puxar todas as baixas do titulo             º±±
±±º          ³                                                            º±±
±±º 01/03/19 ³ Caio J2A consultoria - #002                                º±±
±±º          ³                                                            º±±
±±º          ³                                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

User Function SIFINR04()

	Local _cTexto  	:= ""
	Local _nVias		:= 1
	Local _nOpc		:= 0
	Local _cTitulo	:= "Dados Complementares"
	Local _oBrw		:= Nil
	Local _oMemo		:= Nil
	Local _oGet		:= Nil
	Local _lSe5		:= .F.

	Private _aDados	 	:= {}
	Private _nValTot 	:= 0
	Private _cPerg		:= 'SIFINR04'

/*	Jose Leite - 10-12-14
	Ajuste para filtrar somente titulos que foram recebidos
	na data informada
*/
/* #001
//Inicio
fCriaSx1()
If .Not. Pergunte(_cPerg,.T.)
	Return( Nil )
EndIf
//FIM
*/

	If .Not. Empty(SE1->E1_BAIXA)

		Define Msdialog _oBrw Title _cTitulo FROM 05,0 TO 22,50
	
		@ 007,007 Get _cTexto  When .T.  Size 185,100  MEMO Object _oMemo
		@ 115,010 Say "Qtde Vias:"
		@ 115,040 Get _nVias  Size 020,030   Picture "99" Object _oGet
	
		@ 115,140  BMPBUTTON TYPE 6 ACTION (_nOpc := 1, _oBrw:End())
		@ 115,170  BMPBUTTON TYPE 2 ACTION (_nOpc := 0, _oBrw:End())
	
		Activate Msdialog _oBrw Centered
	
	
		If _nOpc = 1 .and. _nVias > 0
		
			_lSe5 := GetSE5()
		
			If _lSe5
				Processa({|| CursorWait(),fPrint(_cTexto,_nVias), CursorArrow() })
			Else
				Aviso("Atenção","Não existe recebimento para data informada.",{"Ok"})
			EndIf
		
		EndIf
	
	Else
		Aviso("Atenção","Funcionalidade disponivel somente para titulos liquidados.",{"Ok"})
	Endif


Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fPrint    ºAutor  ³renato.neves        º Data ³02/09/2011   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Imprime o Relatorio                                        º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function fPrint(_cTexto,_nVias)

	Local _aArea			:= GetArea()
	Local _nI 			:= 0
	Local _nX				:= 0
	Local _nPag			:= 0
	Local _nTotPag		:= 0
	Local _lPrimeiro	:= .T.
	Local _nLin			:= 0
	Local _oPrint	 	:= TMSPrinter():New("Recibo de Ordem de Recebimento")
	Local _nLinMemo		:= 0
	Local _l1PorPag		:= .F.
	Local _cValor		:= ""
	Local _cTxtDefault	:= ""
	Local _nLinha		:= 0
	
	Local _lImpr			:= .F.
	
	Private _oFont08N	:= TFont():New( "Arial",, 08,,.t.,,,,,.f. )
	Private _oFont08	:= TFont():New( "Arial",, 08,,.f.,,,,,.f. )
	Private _oFont10N	:= TFont():New( "Arial",, 10,,.t.,,,,,.f. )
	Private _oFont10	:= TFont():New( "Arial",, 10,,.f.,,,,,.f. )
	Private _oFont14N	:= TFont():New( "Arial",, 14,,.t.,,,,,.f. )
	Private _oFont14 	:= TFont():New( "Arial",, 14,,.f.,,,,,.f. )
	Private _oFont18N	:= TFont():New( "Arial",, 18,,.t.,,,,,.f. )
	Private _oFont12N	:= TFont():New( "Arial",, 12,,.t.,,,,,.f. )
	Private _oFont12	:= TFont():New( "Arial",, 12,,.f.,,,,,.f. )

	If Len(_aDados) > 0
		_lImpr := .T.
	EndIf

	ProcRegua(_nVias)
	
	//Valor a ser impresso
	If _nValTot > 0
	
		_cValor := "Valor R$ "
		_cValor += AllTrim(Transform(_nValTot, '@E 9,999,999,999,999.99' ))
		
		_cNome := AllTrim(Posicione("SA1",1,xFilial("SA1")+SE1->E1_CLIENTE+SE1->E1_LOJA,"A1_NOME"))
		_cCGC	 := AllTrim(Posicione("SA1",1,xFilial("SA1")+SE1->E1_CLIENTE+SE1->E1_LOJA,"A1_CGC"))
		
		//Texto do campo MEMO a ser impresso
		_cTxtDefault := "Recebi de "+_cNome+", CPF/CNPJ "+_cCGC+", a importância de R$ "+ AllTrim(Transform(_nValTot, '@E 9,999,999,999,999.99' )) + " (" + AllTrim(Lower(Extenso(_nValTot))) +"), conforme abaixo:" + CRLF
		
		If Empty(_cTexto)
			_cTexto := _cTxtDefault
		Else
			_cTexto := _cTxtDefault+rTrim(_cTexto)
		EndIf
		
		//Quantidades de linhas necessaria para imprimir o campo memo
		_nLinMemo	:= MLCount(_cTexto,MAX_TAMANHO)
		
		//indica que deve ser impresso 1 recibo por pagina
		_l1PorPag	:= ( _nLinMemo > (Int(MAX_LINHA/2)-15))
		
		//Calcula o total de paginas impressas
		_nTotPag := Int(_nLinMemo/MAX_LINHA)
		If Mod(_nLinMemo,MAX_LINHA) <> 0
			_nTotPag++
		EndIF
		
		//Imprimindo as vias do relatorio
		For _nI := 1 to _nVias
			
			_nPag := 1
			_nLinha := 0
			
			//Imprime informacoes do cabecalho
			Cabecalho(@_oPrint,_lPrimeiro,_l1PorPag,_nPag,_nTotPag)
			_nLin := iif(_lPrimeiro, 330, 1930)
			
			_oPrint:Say(_nLin,85,"Nome:",_oFont12N)
			_oPrint:Say(_nLin,205,AllTrim(Upper(SM0->M0_NOMECOM)) + "-"+ SM0->M0_CGC,_oFont12)
			
			_nLin += 60
			
			_oPrint:Say(_nLin,2300, _cValor , _oFont12N,,,,DIREITA)
			
			_nLin += 55
			
			//Imprime as informacoes do campo MEMO
			For _nX := 1 to _nLinMemo
				_nLinha++
				//Quebra de Pagina
				If _nLinha > MAX_LINHA//_nLin > 3200
					_nLinha := 1
					_oPrint:EndPage()
					_nPag++
					Cabecalho(@_oPrint,_lPrimeiro,_l1PorPag,_nPag,_nTotPag)
					_nLin := 400
				EndIf
				
				_oPrint:Say(_nLin,95,MemoLine(_cTexto,MAX_TAMANHO,_nX),_oFont10)
				_nLin+=35
			Next _nX
			
			/* Jose Leite - 04-12-14
				Imprime parcelas de acordo com os motivos de baixa
			*/
			//INICIO
			If _lImpr
				
				_nLin += 60
				_nCol := 085
				
				_oPrint:Say(_nLin,_nCol,"Parcela"			,_oFont08N)
					
				_nCol += 150
				_oPrint:Say(_nLin,_nCol,"Forma"				,_oFont08N)
				
				_nCol += 150
				_oPrint:Say(_nLin,_nCol,"Vencto"			,_oFont08N)
				
				_nCol += 150
				_oPrint:Say(_nLin,_nCol,"Pagto"				,_oFont08N)
				
				_nCol += 200
				_oPrint:Say(_nLin,_nCol,"Vl. Recebido"	,_oFont08N)
				
				_nCol += 250
				_oPrint:Say(_nLin,_nCol,"Observações"		,_oFont08N)
				
				_nLin += 60
				
				For i := 1 To Len(_aDados)
					
					_nCol := 085
					_oPrint:Say(_nLin,_nCol,cValToChar(_aDados[i][1])						,_oFont08) //Parcela
					
					_nCol += 150
					_oPrint:Say(_nLin,_nCol,AllTrim(_aDados[i][2])							,_oFont08) //Forma
					
					_nCol += 150
					_oPrint:Say(_nLin,_nCol,_aDados[i][3]										,_oFont08) //Vencimento
					
					_nCol += 150
					_oPrint:Say(_nLin,_nCol,_aDados[i][4]										,_oFont08) //Pagamento
					
					_nCol += 200
					_oPrint:Say(_nLin,_nCol,AllTrim(Transform(_aDados[i][5],"@E 999,999.99")),_oFont08) //Valor Recebido
					
					_nCol += 250
					_oPrint:Say(_nLin,_nCol,AllTrim(_aDados[i][6])							,_oFont08) //Observacoes
					
					_nLin += 55
				Next
				
			EndIf
			
			_nLin += 320
			_oPrint:Say(_nLin,085,"OBS: Em caso de recebimento em cheque somente terá validade após a compensação do mesmo.",_oFont08) //Observacao
			_nLin += 60
			//FIM
			
			//Imprime informacoes do rodape
			RodaPe(@_oPrint,_lPrimeiro,_l1PorPag)
			
			Do Case
			Case _lPrimeiro .and. !(_l1PorPag)
				_lPrimeiro := .F.
			Case !(_lPrimeiro) .and. !(_l1PorPag)
				_lPrimeiro := .T.
				_oPrint:EndPage()
			Case _l1PorPag
				_oPrint:EndPage()
			EndCase
			
		Next _nI
		
	Else
		_oPrint:Say(100,85,"Não há dados a serem impressos!",_oFont08)
	EndIf
	
	_oPrint:Preview()

	RestArea(_aArea)

Return
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Cabecalho ºAutor  ³renato.neves        º Data ³02/09/2011   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Imprime o cabecalho do relatorio                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function Cabecalho(_oPrint,_lPrimeiro,_l1PorPag,_nPag,_nTotPag)

	Local _nLin 	:= IIf(_lPrimeiro,0,1600)
	Local _cLogo	:= GetSrvProfString("Startpath","") + "REC" + cEmpAnt + cFilAnt + ".BMP"
	Local _cNumDoc:= "Número: "+AllTrim(SE1->E1_NUM)+"/"+SE1->E1_PARCELA
	Local _cPagina:= "Página "+AllTrim(Str(_nPag))+" de "+AllTrim(Str(_nTotPag))

	If !File(_cLogo)
		_cLogo	:= GetSrvProfString("Startpath","") + "LG300X90.BMP"
	EndIf

	_oPrint:StartPage()

	_oPrint:Box(30+_nLin,45,250+_nLin,2400)
	_oPrint:SayBitmap(35+_nLin,50,_cLogo,095,096)
	_oPrint:Say(70+_nLin,1200, _oPrint:cDocument, _oFont18N,,,,CENTRALIZADO)
	_oPrint:Say(130+_nLin,1200,_cNumDoc ,_oFont12N,,,,CENTRALIZADO)
	_oPrint:Say(60+_nLin,2380,_cPagina ,_oFont10,,,,DIREITA)
	_oPrint:Say(105+_nLin,2380,dToC(dDataBase) ,_oFont10,,,,DIREITA)
	_oPrint:Say(150+_nLin,2380,Time(),_oFont10,,,,DIREITA)

//Imprime o picote da folha
	If _lPrimeiro .and. !_l1PorPag
		QuebraPag(@_oPrint)
	EndIf

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³NOVO2     ºAutor  ³renato.neves        º Data ³02/09/2011   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Imprime o roda pe do relatorio                             º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function RodaPe(_oPrint,_lPrimeiro,_l1PorPag)

	Local _nLin := 0


	If  _lPrimeiro .and. !(_l1PorPag)
		_nLin := 1400
	Else
		_nLin := 3100
	EndIf

	_oPrint:Say(_nLin,2300, "_______________________________"	,_oFont10,,,,DIREITA)
	_nLin+= 65
	_oPrint:Say(_nLin,2200, "Assinatura/Carimbo"					,_oFont10,,,,DIREITA)

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³QuebraPag ºAutor  ³renato.neves        º Data ³02/09/2011   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Impressao do picote de quebra de pagina                     º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function QuebraPag(_oPrint)

	Local _nI := 0

	While _nI < 2450
		_oPrint:Line(1600,_nI,1600,_nI+15)
		_nI+= 30
	EndDo

Return

/** {Protheus.doc} GetSE5
Funcao para verificar a movimentacao do titulo

@param: 	<Nil>
@author: 	Jose Leite de Barros Neto
@since: 	04/12/2014
@Uso: 		SFIEMT
*/
Static Function GetSE5()
	
	Local _cQuery	:= ""
	Local _cQuery2	:= ""
	Local _nCont	:= 0
	Local _cForma	:= ""
	Local _cObserv	:= ""
	Local _cDscBc	:= ""
	Local _cAgeBc	:= ""
	Local _cCtaBc	:= ""
	Local _dVcto	:= ""
	Local _dPgto	:= ""
	Local _lRet		:= .F.
	Local _aArea	:= GetArea()
	Local _nx 		:= 0
	
	If Select('TRB') > 0
		dbSelectArea('TRB')
		TRB->( DbCloseArea() )
	EndIf
	
	_cQuery := "	SELECT  *	"
	_cQuery += "		FROM "+RetsqlName("SE5")
	_cQuery += "	WHERE	"
	//_cQuery += "   E5_DATA		   = '"+ DtoS(mv_par01) +"'	" // #001
	_cQuery += "   E5_FILIAL   = '"+SE1->E1_FILIAL+"'	"
	_cQuery += "   AND E5_PREFIXO  = '"+SE1->E1_PREFIXO+"'	"
	_cQuery += "   AND E5_NUMERO   = '"+SE1->E1_NUM+"'		"
	_cQuery += "   AND E5_PARCELA  = '"+SE1->E1_PARCELA+"'	"
	_cQuery += "   AND E5_TIPO     = '"+SE1->E1_TIPO+"'		"
	_cQuery += "   AND E5_CLIFOR   = '"+SE1->E1_CLIENTE+"'	"
	_cQuery += "   AND E5_LOJA     = '"+SE1->E1_LOJA+"'		"
	_cQuery += "   AND E5_TIPODOC  IN ('BA','VL')				"
	_cQuery += "   AND E5_SITUACA  <> 'C'						"
	_cQuery += "   AND D_E_L_E_T_  <> '*'						"
	_cQuery += "ORDER BY E5_FILIAL, E5_PREFIXO, E5_NUMERO, E5_PARCELA, E5_MOTBX	"
   
	_cQuery := ChangeQuery(_cQuery)
	
	TcQuery _cQuery New Alias 'TRB'
	
	_aMotBx := ReadMotBx() // #001
	//Aviso("VarInfo", VarInfo("_aMotBx",_aMotBx,,.F.) , {"ok"},3 )
	
	While .Not. TRB->(Eof())
		
		_dVcto := DtoC(SE1->E1_VENCREA) //Posicione("SE1",1,TRB->(E5_FILIAL+E5_PREFIXO+E5_NUMERO+E5_PARCELA+E5_TIPO),"E1_VENCREA")
		_dPgto := DtoC(StoD(TRB->(E5_DATA)))

		If Select("TRC") > 0
			DbSelectArea("TRC")
			DbCloseArea()
		EndIf
	
		_cQuery2 := "	SELECT  	E5_FILIAL, E5_PREFIXO, E5_NUMERO, E5_PARCELA,
		_cQuery2 += "				E5_MOTBX, E5_TIPODOC, E5_RECPAG	, E5_BANCO, E5_AGENCIA, E5_CONTA, E5_DATA	"
		_cQuery2 += "		FROM "+RetsqlName("SE5")
		_cQuery2 += "	WHERE	"
		//_cQuery2 += "   E5_DATA		    = '"+ DtoS(mv_par01) +"'" // #001
		_cQuery2 += "   E5_FILIAL   = '"+TRB->E5_FILIAL+"'	"
		_cQuery2 += "   AND E5_PREFIXO  = '"+TRB->E5_PREFIXO+"'	"
		_cQuery2 += "   AND E5_NUMERO   = '"+TRB->E5_NUMERO+"'	"
		_cQuery2 += "   AND E5_PARCELA  = '"+TRB->E5_PARCELA+"'	"
		_cQuery2 += "   AND E5_TIPO     = '"+TRB->E5_TIPO+"'		"
		_cQuery2 += "   AND E5_CLIFOR   = '"+TRB->E5_CLIFOR+"'	"
		_cQuery2 += "   AND E5_LOJA     = '"+TRB->E5_LOJA+"'		"
		_cQuery2 += "   AND E5_TIPODOC  = 'ES'						"
		_cQuery2 += "   AND E5_RECPAG   = 'P'							"
		_cQuery2 += "   AND E5_BANCO    = '"+TRB->E5_BANCO+"'	"
		_cQuery2 += "   AND E5_AGENCIA  = '"+TRB->E5_AGENCIA+"'	"
		_cQuery2 += "   AND E5_CONTA    = '"+TRB->E5_CONTA+"'	"
		_cQuery2 += "   AND E5_SEQ		 = '"+TRB->E5_SEQ+"'		"
		_cQuery2 += "   AND D_E_L_E_T_  <> '*'						"
		_cQuery2 += "ORDER BY E5_FILIAL, E5_PREFIXO, E5_NUMERO, E5_PARCELA, E5_MOTBX	"
	   
		_cQuery2 := ChangeQuery(_cQuery2)
		
		TcQuery _cQuery2 New Alias 'TRC'
		
		If .Not. Empty(TRC->(E5_RECPAG))
			TRB->( DbSkip() )
			Loop
		EndIf
		
		_lRet := .T.
		
		_nCont++
		
		If _dVcto == "  /  /    "
			_dVcto := DtoC(StoD(TRC->(E5_DATA)))
		EndIf
		
		_cForma := ""
		_cObserv:= ""
		
		If TRB->( E5_MOTBX ) == "CCO" .And. ( TRB->( E5_BANCO ) == "C01" .OR. TRB->( E5_BANCO ) == "CO1" ) .And. TRB->( E5_TIPODOC ) == 'VL'
		
			_cForma := "Dinheiro"
			_cObserv:= "Pagamento efetuado em dinheiro."
		
		ElseIf TRB->( E5_MOTBX ) == "CCO" .And. ( TRB->( E5_BANCO ) <> "C01" .AND. TRB->( E5_BANCO ) <> "CO1" .AND. TRB->( E5_BANCO ) <> "CHP" ) .And. TRB->( E5_TIPODOC ) == 'VL'
			
			_cDscBc := AllTrim(TRB->( E5_BANCO )) + " - " + AllTrim(Posicione("SA6", 1, xFilial("SA6") + TRB->(E5_BANCO) + TRB->(E5_AGENCIA) + TRB->(E5_CONTA), "A6_NOME"))
			_cAgeBc := AllTrim(TRB->(E5_AGENCIA))
			_cCtaBc := AllTrim(TRB->(E5_CONTA))
			
			_cForma := "Banco"
			_cObserv:= "Pagamento efetuado via conta corrente no Banco: " + _cDscBc + ','
			_cObserv+= " Agência: " + _cAgeBc + " e Conta Corrente: " + _cCtaBc
		
		ElseIf TRB->( E5_MOTBX ) == "MAS" .And. TRB->( E5_TIPODOC ) == 'BA'
			
			_cForma := "Master"
			_cObserv:= "Pagamento efetuado por Cartão Master Card."
			
		ElseIf TRB->( E5_MOTBX ) == "VIS" .And. TRB->( E5_TIPODOC ) == 'BA'
		
			_cForma := "VISA"
			_cObserv:= "Pagamento efetuado por Cartão VISA."
		
		ElseIf TRB->( E5_MOTBX ) == "CHE" .And. TRB->( E5_BANCO ) == "CHP" .And. TRB->( E5_RECPAG ) == 'R'
		
			_cForma := "Cheque"
			_cObserv:= "Pagamento efetuado por Cheque."
		
		Else // #001
			If TRB->( E5_MOTBX ) == "REN"
				_cForma := "Renegociação"
				_cObserv:= "Titulo renegociado."
			ElseIf TRB->( E5_MOTBX ) == "CRT"
				_cForma := "Cartão"
				_cObserv:= "Baixa cartão."
			Else
				For _nx:= 1 to Len(_aMotBx)
					_cMot  := Left(_aMotBx[_nx],3) // motivo da baixa
					_cDMot := SubStr(_aMotBx[_nx],7,10) // descricao do motivo
					
					If TRB->( E5_MOTBX ) = _cMot
						_cForma := _cMot
						_cObserv:= Capital(_cDMot)
						Exit
					EndIf
				Next
			EndIf
		EndIf
		
		If .Not. Empty(_cForma)
			aAdd(_aDados, {_nCont, _cForma, _dVcto, _dPgto, TRB->(E5_VALOR), _cObserv})
		EndIf
		
		_nValTot += TRB->(E5_VALOR)
			
		TRB->(DbSkip())
	End
	
	If Select("TRC") > 0
		DbSelectArea("TRC")
		DbCloseArea()
	EndIf
		
	If Select("TRB") > 0
		DbSelectArea("TRB")
		TRB->( DbCloseArea() )
	EndIf
	
	RestArea(_aArea)
	
Return( _lRet )

/** {Protheus.doc} fCriaSx1
Funcao para criacao das perguntas

@param: 	<Nil>
@author 	Jose Leite de Barros Neto
@since 	04/11/2014
@Uso: 		SFIEMT
*/
Static Function fCriaSx1()

	Local aHelp := {}

	AAdd(aHelp, {{"Informe a data do Recebimento"	}, {""}, {""}})
	
	u_SFPUTSX1( _cPerg, "01","Data","","","mv_ch1","D",8,0,0,"C","","","","","mv_par01","","","","","","","","","","","","","","","","",aHelp[1,1],aHelp[1,2],aHelp[1,3])
Return( Nil )