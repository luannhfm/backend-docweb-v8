#INCLUDE 'PROTHEUS.CH'
#INCLUDE 'FWMVCDEF.CH'

/*/{Protheus.doc} SF7301J
Cadastro De Sindicatos

@author 	José Leite de Barros Neto
@since 		09/05/2014
@version 	1.0

@return Nil, Nulo

/*/
User Function SF7301J()
	
	Local oBrowse
	
	oBrowse := FWMBrowse():New()
	oBrowse:SetAlias('ZCA')
	oBrowse:SetDescription ('Cadastro de Sindicatos')
	oBrowse:Activate()

Return NIL

//-------------------------------------------------------------------
Static Function MenuDef()
	
	Local aRotina := {}

	ADD OPTION aRotina TITLE 'Pesquisar'  ACTION 'PesqBrw'          OPERATION 1 ACCESS 0
	ADD OPTION aRotina TITLE 'Visualizar' ACTION 'VIEWDEF.SF7301J'  OPERATION 2 ACCESS 0
	ADD OPTION aRotina TITLE 'Incluir'    ACTION 'VIEWDEF.SF7301J'  OPERATION 3 ACCESS 0
	ADD OPTION aRotina TITLE 'Alterar'    ACTION 'VIEWDEF.SF7301J'  OPERATION 4 ACCESS 0
	ADD OPTION aRotina TITLE 'Excluir'    ACTION 'VIEWDEF.SF7301J'  OPERATION 5 ACCESS 0
	ADD OPTION aRotina TITLE 'Imprimir'   ACTION 'VIEWDEF.SF7301J'  OPERATION 8 ACCESS 0
	ADD OPTION aRotina TITLE 'Copiar'     ACTION 'VIEWDEF.SF7301J'  OPERATION 9 ACCESS 0
	
Return aRotina

//-------------------------------------------------------------------
Static Function ModelDef()
	
	// Cria a estrutura a ser usada no Modelo de Dados
	Local oStruZCA := FWFormStruct(1,'ZCA', /*bAvalCampo*/,/*lViewUsado*/ )
	Local oModel

	// Cria o objeto do Modelo de Dados
	oModel := MPFormModel():New('SF7301M',/*bPreValidacao*/, /*bPosValidacao*/, /*bCommit*/, /*bCancel*/ )

	//Adiciona ao Modelo uma estrutura de formulario de edição por campo
	oModel:AddFields('ZCAMASTER',/*cOwner*/, oStruZCA, /*bPreValidacao*/, /*bPosValidacao*/, /*bCarga*/ )

	//Adiciona a Descrição do Modelo de Dados
	oModel:SetDescription('Cadastro De Sindicatos' )

	oModel:GetModel('ZCAMASTER'):SetDescription('Dados do CADASTRO DE SINDICATOS')
	
	oModel:SetPrimaryKey( { "ZCA_FILIAL", "ZCA_CODIGO" } )

Return oModel

//-------------------------------------------------------------------
Static Function ViewDef()

	//Cria um objeto de Modelo de Dados baseado no ModelDef
	Local oModel := FWLoadModel('SF7301J')

	//Cria a estrutura a ser usada na View
	Local oStruZCA := FWFormStruct(2,'ZCA')
	Local oView
	Local cCampos := {}

	//Cria o Objeto da View
	oView := FWFormView():New()

	//Define qual o Modelo de dados será utilizado
	oView:SetModel( oModel )

	//Adiciona no View um controle do tipo FormField (Antiga Enchoice)
	oView:addField( 'VIEW_ZCA',oStruZCA, 'ZCAMASTER' )

	//Cria um BOX horizontal para receber elementos da View
	oView:CreateHorizontalBox('TELA' , 100 )

	//Relaciona o ID da View com o Box para exibição
	oView:SetOwnerView( 'VIEW_ZCA' , 'TELA' )

Return oView