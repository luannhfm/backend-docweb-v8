#INCLUDE "RWMAKE.CH"
#INCLUDE "PROTHEUS.CH"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³SICOMA46  ºAutor  ³Claudinei Ferreira  º Data ³  04/11/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Rotina para inserir a Cotacao da Viagem(ZAD)         		  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³Especifico CNI (Controle de Viagens)                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

User Function SICOMA46()

Local aPosObj   := {}
Local aObjects  := {}
Local aSize     := {}
Local aPosGet   := {}
Local cCadastro:= "Cotações"
Local cAlias := 'ZAD'
Local nOpcA  := 0
Local nOpcx  := 4 //Altera
Local nOpcI  := 3 //Inclui
Local cQuery  := ''
Local cItemSV := ''
Local aNoFields := {}
Local nPosSts := aScan(aHeader,{|x| AllTrim(x[2]) == "ZA7_STS"}) 

aHeadSV := aClone(aHeader)
aColsSV := aClone(aCols)
cItemSV := aColsSV[n][1]

Private aHeader := {}
Private aCols   := {}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Salva a integridade dos campos de Bancos de Dados    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("ZAD")
dbSetOrder(1)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Criar uma nova instancia de variaveis private       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RegToMemory( "ZAD", .T., .F. )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Calculo automatico de dimensoes de objetos ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aSize := MsAdvSize()
aObjects := {}
AAdd( aObjects, { 100, 100, .T., .T. } )
aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }
aPosObj := MsObjSize( aInfo, aObjects )
aPosGet := MsObjGetPos(aSize[3],315,{{003,033,160,200,240,263}} )

DEFINE MSDIALOG oDlgC TITLE cCadastro From aSize[7],0 to aSize[3]-aSize[4],aSize[5]-aSize[4] of oMainWnd PIXEL STYLE nOR( WS_VISIBLE ,DS_MODALFRAME )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Filtros para montagem do aCols                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cSeek  := xFilial("ZAD")+ZA6->ZA6_NUM+ZA6->ZA6_FORNEC+ZA6->ZA6_LOJA+cItemSV
bWhile := "ZAD->ZAD_FILIAL+ZAD->ZAD_NUM+ZAD->ZAD_FORNEC+ZAD->ZAD_LOJA+ZAD->ZAD_ITEM"

FillGetDados(nOpcx,"ZAD",1,cSeek,{|| &bWhile },,aNoFields,,,,,,,,)
aCols[1][1] := If(Empty(aCols[1][1]),"001",aCols[1][1])

If !aColsSV[n][nPosSts]=='2' //Trecho cancelado nao permite alteracao da cotacao
	oGetCot := MsGetDados():New(aPosObj[1,1],aPosObj[1,2],aPosObj[1,3]/2,aPosObj[1,4]-(aPosObj[1,3]/2),nOpcI,,,"+ZAD_ITEMC",.T.)
Else
	oGetCot := MsGetDados():New(aPosObj[1,1],aPosObj[1,2],aPosObj[1,3]/2,aPosObj[1,4]-(aPosObj[1,3]/2),2,,,"+ZAD_ITEMC",.T.)
Endif

ACTIVATE MSDIALOG oDlgC ON INIT EnchoiceBar(oDlgC,{|| nOpcA := 1,If(oGetCot:TudoOk(),oDlgC:End(),nOpcA := 0)},{||oDlgC:End()}) CENTERED

If nOpcA == 1
 U_A46Inclui(cItemSV)
Endif

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³A46Inclui ºAutor  ³Claudinei Ferreira  º Data ³  05/11/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Grava informacoes a cotacao conforme e Trechos              º±±
±±º          ³Solicitados                                                 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³Especifico CNI (Controle de Viagens)                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

User Function A46Inclui(cItemSV)

Local nPosItem  := aScan(aHeader,{|x| AllTrim(x[2]) == "ZAD_ITEMC"})
Local nPCia  := aScan(aHeader,{|x| AllTrim(x[2]) == "ZAD_CIA"})
Local nPData  := aScan(aHeader,{|x| AllTrim(x[2]) == "ZAD_DATA"})
Local nPHrS   := aScan(aHeader,{|x| AllTrim(x[2]) == "ZAD_HRSAI"})
Local nPHrC    := aScan(aHeader,{|x| AllTrim(x[2]) == "ZAD_HRCHE"})
Local nPVlr     := aScan(aHeader,{|x| AllTrim(x[2]) == "ZAD_VLR"})
Local aItensSV  := {}
Local aItensCot := {}
Local lFindCot := .T.
Local aAprv  := {}
Local cEmail := ''
Local cMensagem := ''
Local cTpUsr := '' 
Local cStatus := '2' //Aguardando Passagens/Hospedagem

aItensCot := aClone(aCols)

Begin Transaction

dbSelectArea("ZAD")
ZAD->(DbSetOrder(1))

For nX:=1 to Len(aItensCot)
 
 If !DbSeek(xFilial("ZAD")+M->ZA6_NUM+M->ZA6_FORNEC+M->ZA6_LOJA+cItemSV+aItensCot[nX][nPosItem])
  RecLock("ZAD",.T.)
 Else
  RecLock("ZAD",.F.)
 Endif
 
 If !aItensCot[nX][Len(aItensCot[n])]
  
  ZAD->ZAD_FILIAL 	:= xFilial("ZAD")
  ZAD->ZAD_NUM    	:= M->ZA6_NUM
  ZAD->ZAD_FORNEC 	:= M->ZA6_FORNEC
  ZAD->ZAD_LOJA		:= M->ZA6_LOJA
  ZAD->ZAD_ITEM 	:= cItemSV
  ZAD->ZAD_ITEMC 	:= aItensCot[nX][nPosItem]
  ZAD->ZAD_CIA    	:= aItensCot[nX][nPCia]
  ZAD->ZAD_DATA 	:= aItensCot[nX][nPData]
  ZAD->ZAD_VLR    	:= aItensCot[nX][nPVlr]
  ZAD->ZAD_HRSAI 	:= aItensCot[nX][nPHrS]
  ZAD->ZAD_HRCHE 	:= aItensCot[nX][nPHrC]
   
 ElseIf aItensCot[nX][Len(aItensCot[n])] .and. lFindCot
  dbDelete()
 Endif
 ZAD->(MsUnlock())
 
Next nX

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ ¿
//³Valida se todos os trechos possuem cotacao³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ Ù

//Itens da Solicitacao
DbSelectArea("ZA7")
ZA7->(DbSetOrder(1))
ZA7->(DbSeek(xFilial("ZA7")+M->ZA6_NUM))
ZA7->(dbEval({|| AADD(aItensSV,{ZA7_NUM,ZA7_ITEM,ZA7_STS})},, {|| ZA7_FILIAL+ZA7_NUM == xFilial('ZA7')+M->ZA6_NUM }))

//Itens da Cotacao
DbSelectArea("ZAD")
ZAD->(DbSetOrder(1)) //ZAD_FILIAL+ZAD_NUM+ZAD_FORNEC+ZAD_LOJA+ZAD_ITEM+ZAD_ITEMC

For nX:=1 to Len(aItensSV)
 If !DbSeek(xFilial("ZAD")+M->ZA6_NUM+M->ZA6_FORNEC+M->ZA6_LOJA+aItensSV[nX][2]) .AND. aItensSV[nX][3] == '1'
  lFindCot:=.F.
  nX:= Len(aItensSV)
 Endif
Next

If lFindCot
 If MsgYesNo("Deseja enviar Cotação para Validação ?","Atenção")
 cStatus:= '3' //Aguardando Validacao
 Endif
Else
 cStatus:= '2' //Aguardando Passagens/Hospedagem
Endif

dbSelectArea("ZA6")
RecLock("ZA6",.F.)
Replace ZA6_STATUS With cStatus
ZA6->(MsUnlock())

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Envia Email para usuarios ADM=1³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ZA6_STATUS == '3'

cTpUsr:= '1' 

 BEGINSQL ALIAS "TRBZA8"
  
  SELECT
  ZA8_USER, ZA8_EMAIL
  
  FROM  %table:ZA8%  ZA8
  
  WHERE  ZA8.%notDel%
  AND ZA8_FILIAL = %exp:xFilial("ZA8")%
  AND ZA8_TIPO   = %exp:cTpUsr%
  
 ENDSQL
 
 TRBZA8->(dbEval({|| AADD(aAprv,{ZA8_USER,ZA8_EMAIL})},, {|| !EOF() }))
 
 For nX:=1 to Len(aAprv)
  cEmail:= IIf(Empty(cEmail),AllTrim(aAprv[nX][2]),cEmail+ ";" + AllTrim(aAprv[nX][2]))
 Next
 
 //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
 //³Fecha o temporario                |
 //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
 TRBZA8->(dbCloseArea())
 
 cMensagem:= U_Geramail(ZA6->ZA6_NUM,ZA6->ZA6_OBJETI)
 FSSendMail( cEmail, "Cotação - Solicitação de Viagem: "+M->ZA6_NUM, cMensagem )
 
Endif

End Transaction

Return