#Include 'Protheus.ch'

/*/{Protheus.doc} SFATFR3J
Relatório Posição Valorizada dos Bens.
Módulo: Ativo Fixo
@type function
@author 	José Leite de Barros Neto
@since 		29/11/2013
@History 29/09/2020, Franklin de Brito de Oliveira, removido ajuste para tratar bens transferidos.
@history 10/02/2021, Franklin de Brito de Oliveira, incluido filtro por status do bem.
@history 14/04/2021, Franklin de Brito de Oliveira, correção na consulta e no parâmetro MV_PAR07 - Quanto as Baixas ?
/*/
User Function SFATFR3J()

	Private cPerg 	:= "XFATFR3"
	Private cCadastro	:= "Impressão Posição Valorizada de Bens"
	Private aSays		:= {}
	Private aButtons	:= {}
	Private nOpca 	:= 0
	Private oPrint
	Private nCont		:= 0
	Private nContLoc	:= 0

	// Funcao para criacao das perguntas.
	fCriaSx1()

	// Forca o usuario a preencher as perguntas.
	If .Not. Pergunte(cPerg,.t.)
		Return
	EndIf

	AADD(aSays,"Este programa irá realizar a impressão do relatório," )
	AADD(aSays,"Posição Valorizada dos Bens." )

	AADD(aButtons, { 5,.T.,{|| Pergunte(cPerg,.T. )}})
	AADD(aButtons, { 1,.T.,{|o| nOpca := 1,FechaBatch()}})
	AADD(aButtons, { 2,.T.,{|o| FechaBatch() }} )

	FormBatch( cCadastro, aSays, aButtons )

	If nOpca == 1

		If ApMsgYesNo("Confirma impressão do Relatório ?","Confirmar")
			Processa({|| fImprime()})
		EndIf
	
	EndIf
	
Return( Nil )

/** {Protheus.doc} fImprime
Funcao para impressao do relatorio
@type function
@author 	Jose Leite de Barros Neto
@since 	29/11/2013
@Uso: 		SFIEMT
*/	
Static Function fImprime()

	Local _lPrimeiro	:= .T.	
	Local _cContaC		:= ""
	Local _cWhere		:= ""
	Local dUltDepr 		:= GetMV("MV_ULTDEPR")
	Local _nQuant 		:= 0
	Local _nAquis 		:= 0                        
	Local _nAmpli 		:= 0
	Local _nAtual 		:= 0
	Local _nDePer 		:= 0
	Local _nDeAcu 		:= 0
	Local _nResid 		:= 0
			
	Private oCour07N 	:= TFont():New("Courier New",07,07,.T.,.T.,5,.T.,5,.T.,.F.)
	Private oCour08N 	:= TFont():New("Courier New",08,08,.T.,.T.,5,.T.,5,.T.,.F.)
	Private oArial14N	:= TFont():New("Arial",13,14,.T.,.T.,5,.T.,5,.T.,.F.)
	Private oArial08N	:= TFont():New("Arial",08,08,.T.,.T.,5,.T.,5,.T.,.F.)
	Private nRowIni 	:= 010
	Private nColIni 	:= 050
	Private nColFim 	:= 3250
	Private nRowFim 	:= 2300
	Private nRowAtu 	:= 0
	Private _cAlias		:= ""
	Private _cFilial	:= ""	
	Private _nTQtde 	:= 0
	Private _nTAqui 	:= 0
	Private _nTAmpl 	:= 0
	Private _nTAtua 	:= 0
	Private _nTDePe 	:= 0
	Private _nTDeAc 	:= 0
	Private _nTResi 	:= 0	
	
	nRowAtu := nRowIni

	If MV_PAR07 == 1	//Quanto as Baixas ?1=Mes Calc.
		_cWhere += " AND (N3_DTBAIXA	= ' '
		_cWhere += " OR SUBSTR(N3_DTBAIXA, 1, 6) = '" + SubStr(DtoS(dUltDepr), 1, 6) + "' )"
	EndIf  
	
	_cWhere += " AND	N3_CCONTAB BETWEEN '" + MV_PAR01 + "' AND '" + MV_PAR02 + "' "
	_cWhere += " AND 	N3_CCUSTO  BETWEEN '" + MV_PAR03 + "' AND '" + MV_PAR04 + "' "
	_cWhere += " AND SN1.N1_STATUS	NOT IN ('0') "	//0=Pendente de classificacao;1=Em Uso;2=Bloqueado por usuario;3=Bloqueado por local;4=Transferencia interna entre filiais
	_cWhere += " AND SN1.N1_LOCAL	BETWEEN '"+MV_PAR05+"' AND '"+MV_PAR06+"' "
	
	If MV_PAR08 == 2	//Considera Aquisicao Pos Calculo?1=Sim;2=Não
		_cWhere += " AND N1_AQUISIC	<= '" + DtoS(dUltDepr) + "' "
	EndIf
	
	_cWhere := "%" + _cWhere + "%"	  
	
	_cAlias := GetNextAlias()

	BeginSql Alias _cAlias
	
		Column QUANTIDADE 	as Numeric(14,2)	
		Column AQUISICAO 		as Numeric(14,2)
		Column AMPLIACAO 		as Numeric(14,2)
		Column ATUALIZADO 	as Numeric(14,2)
		Column DEPPERIODO 	as Numeric(14,2)
		Column DEPACUMULA 	as Numeric(14,2)
		Column RESIDUAL 		as Numeric(14,2)
		
		SELECT  
        		SUBSTR(N3_FILIAL,1,2) N3_FILIAL
				,N3_CCONTAB
				,SUM(N1_QUANTD) QUANTIDADE
				,SUM(case n3_dtbaixa when ' ' then n3_vorig1 else 0 end) AQUISICAO
				,SUM(case n3_dtbaixa when ' ' then N3_AMPLIA1 else 0 end) AMPLIACAO
				,SUM(case n3_dtbaixa when ' ' then n3_vorig1 + n3_amplia1 else 0 end) ATUALIZADO
				,SUM(N3_VRDMES1) DEPPERIODO
				,SUM(case n3_dtbaixa when ' ' then n3_vrdacm1 else 0 end) DEPACUMULA
				,SUM(case n3_dtbaixa when ' ' then (n3_vorig1 + n3_amplia1) - n3_vrdacm1 else  0 end) RESIDUAL
		                
		FROM %Table:SN3% SN3
		  
			INNER JOIN %Table:SN1% SN1 ON N1_FILIAL = N3_FILIAL AND N1_CBASE = N3_CBASE AND N1_ITEM = N3_ITEM AND SN1.%NotDel%
		  
		WHERE SN3.%NotDel%
		%Exp:_cWhere%
		      
		GROUP BY SUBSTR(N3_FILIAL,1,2), N3_CCONTAB
		   
		ORDER BY SUBSTR(N3_FILIAL,1,2), N3_CCONTAB  

	EndSql
	
	MemoWrite("C:/Util/" + FunName() + ".sql",  GetLastQuery()[2])
	
	While .Not. (_cAlias)->(Eof()) 
		
		If _lPrimeiro
		
			oPrint := TMSPrinter():New( "Posição Valorizada de Bens" )
			oPrint:SetLandScape()
			_lPrimeiro := .F.
			
		EndIf
		
		fForm( (_cAlias)->N3_FILIAL )
		
		_cFilial := (_cAlias)->N3_FILIAL
		_cContaC := ""
		
		While .Not. (_cAlias)->( Eof() ) .And. (_cAlias)->N3_FILIAL == _cFilial
		
			//Totalizadores Gerais
			_nTQtde += (_cAlias)->QUANTIDADE
			_nTAqui += (_cAlias)->AQUISICAO
			_nTAmpl += (_cAlias)->AMPLIACAO
			_nTAtua += (_cAlias)->ATUALIZADO
			_nTDePe += (_cAlias)->DEPPERIODO
			_nTDeAc += (_cAlias)->DEPACUMULA
			_nTResi += (_cAlias)->RESIDUAL
			
			//Totalizadores
			_nQuant += (_cAlias)->QUANTIDADE
			_nAquis += (_cAlias)->AQUISICAO
			_nAmpli += (_cAlias)->AMPLIACAO
			_nAtual += (_cAlias)->ATUALIZADO
			_nDePer += (_cAlias)->DEPPERIODO
			_nDeAcu += (_cAlias)->DEPACUMULA
			_nResid += (_cAlias)->RESIDUAL	
			
			If nRowAtu > (nRowFim-0250) 
						
				oPrint:EndPage()
				fForm( (_cAlias)->N3_FILIAL )
				
			EndIf	
			
			If (_cAlias)->N3_CCONTAB <> _cContaC 
					
				oPrint:Say(nRowAtu,nColIni+0020,AllTrim( (_cAlias)->N3_CCONTAB ) +" - "+ AllTrim( Posicione("CT1",1,xFilial("CT1")+(_cAlias)->N3_CCONTAB,"CT1_DESC01") ) ,oCour08N)
				oPrint:Say(nRowAtu,nColIni+0980,Transform( _nQuant	, "@E 999,999,999.99" ),oCour08N)
				oPrint:Say(nRowAtu,nColIni+1320,Transform( _nAquis	, "@E 999,999,999.99" ),oCour08N)
				oPrint:Say(nRowAtu,nColIni+1600,Transform( _nAmpli	, "@E 999,999,999.99" ),oCour08N)
				oPrint:Say(nRowAtu,nColIni+1950,Transform( _nAtual	, "@E 999,999,999.99" ),oCour08N)
				oPrint:Say(nRowAtu,nColIni+2200,Transform( _nDePer	, "@E 999,999,999.99" ),oCour08N)
				oPrint:Say(nRowAtu,nColIni+2550,Transform( _nDeAcu	, "@E 999,999,999.99" ),oCour08N)
				oPrint:Say(nRowAtu,nColIni+2900,Transform( _nResid	, "@E 999,999,999.99" ),oCour08N)
				
				nRowAtu += 0050
				
				_cFilial	:= (_cAlias)->N3_FILIAL
				_cContaC	:= (_cAlias)->N3_CCONTAB
				_nQuant 	:= 0
				_nAquis 	:= 0
				_nAmpli 	:= 0
				_nAtual 	:= 0
				_nDePer 	:= 0
				_nDeAcu 	:= 0
				_nResid 	:= 0	
				
			EndIf
			
			(_cAlias)->( DbSkip() )
			
			If SubStr( (_cAlias)->N3_FILIAL, 1 ,2) <> _cFilial
			
				oPrint:Say(nRowFim-0220,nColIni+0600,"Total por empresa",oArial08N)
				
				oPrint:Say(nRowFim-0220,nColIni+1080,Transform( _nTQtde , "@E 999,999,999.99"),oArial08N)
				_nTQtde := 0
					
				oPrint:Say(nRowFim-0220,nColIni+1350,Transform( _nTAqui , "@E 999,999,999.99" ),oArial08N)
				_nTAqui := 0
						
				oPrint:Say(nRowFim-0220,nColIni+1620,Transform( _nTAmpl , "@E 999,999,999.99" ),oArial08N)
				_nTAmpl := 0
				
				oPrint:Say(nRowFim-0220,nColIni+1980,Transform( _nTAtua , "@E 999,999,999.99" ),oArial08N)
				_nTAtua := 0
					
				oPrint:Say(nRowFim-0220,nColIni+2300,Transform( _nTDePe , "@E 999,999,999.99" ),oArial08N)
				_nTDePe := 0
					
				oPrint:Say(nRowFim-0220,nColIni+2600,Transform( _nTDeAc , "@E 999,999,999.99" ),oArial08N)
				_nTDeAc := 0
					
				oPrint:Say(nRowFim-0220,nColIni+2950,Transform( _nTResi , "@E 999,999,999.99" ),oArial08N)
				_nTResi := 0
			
			EndIf
			
		End
		
		oPrint:EndPage()
		
	End
	
	(_cAlias)->(DbCloseArea())
	
	If .Not. _lPrimeiro
		oPrint:EndPage()
		oPrint:Preview()     // Visualiza antes de imprimir
	EndIf

Return( Nil )

/** {Protheus.doc} fForm
Funcao para montagem do formulario

@type function
@author 	José Leite de Barros Neto
@since 	28/10/2013
@Uso: 		SFIEMT
*/
Static Function fForm(pFilial)
	
	Local _cFilial	:= ""
	Local _cLogo		:= ""
	
	Do Case
		
		Case pFilial == "01"
		
			_cFilial := "FIEMT"
		
		Case pFilial == "02"
			
			_cFilial := "SESI"
			
		Case pFilial == "03"
			
			_cFilial := "SENAI"
			
		Case pFilial == "04"
		
			_cFilial := "IEL"
			
		Case pFilial == "05"
			
			_cFilial := "CONDOMINIO"
			
	EndCase

	oPrint:StartPage()

	oPrint:Box(nRowIni,nColIni,nRowFim-0250,nColFim)
	oPrint:Line(nRowIni+0300,nColIni,nRowIni+0300,nColFim)
	oPrint:Say(nRowIni+0070,nColIni+1200,"POSIÇÃO VALORIZADA DOS BENS",oArial14N)
	
	//Logo da Empresa - Lado esquerdo
	If File('\system\lgrl'+cEmpAnt+cFilAnt+'.bmp')
		_cLogo := 'lgrl'+cEmpAnt+cFilAnt+'.bmp'
	ElseIf File('\system\lgrl'+cEmpAnt+'.bmp')
		_cLogo := 'lgrl'+cEmpAnt+'.bmp'
	Else
		_cLogo := 'msmdilogo.bmp'
	EndIf
    
	oPrint:SayBitmap(nRowIni+010,nColIni+050,_cLogo,nRowIni+0250,nColIni+200)
	
	oPrint:Say(nRowIni+0310,nColIni+0010,"Empresa: "+_cFilial,oCour08N)
	
	oPrint:Line(nRowIni+0400,nColIni,nRowIni+0400,nColFim)
	
	//Colunas
	oPrint:Say(nRowIni+0405,nColIni+0250,"Conta Contabil - Descrição da Conta",oArial08N)
	oPrint:Say(nRowIni+0405,nColIni+1120,"Qtde.",oArial08N)
	oPrint:Say(nRowIni+0405,nColIni+1370,"Vlr. Aquisição",oArial08N)
	oPrint:Say(nRowIni+0405,nColIni+1670,"Vlr. Ampliação",oArial08N)
	oPrint:Say(nRowIni+0405,nColIni+1990,"Vlr. Atualizado",oArial08N)
	oPrint:Say(nRowIni+0405,nColIni+2290,"Deprec. Periodo",oArial08N)
	oPrint:Say(nRowIni+0405,nColIni+2600,"Deprec. Acum.",oArial08N)
	oPrint:Say(nRowIni+0405,nColIni+2930,"Vlr. Residual",oArial08N)
	
	oPrint:Line(nRowIni+0440,nColIni,nRowIni+0440,nColFim)

	//Linhas Verticais
	oPrint:Line(nRowIni+0400,nColIni+1000,nRowFim-0250,nColIni+1000)
	oPrint:Line(nRowIni+0400,nColIni+1300,nRowFim-0250,nColIni+1300)
	oPrint:Line(nRowIni+0400,nColIni+1600,nRowFim-0250,nColIni+1600)
	oPrint:Line(nRowIni+0400,nColIni+1900,nRowFim-0250,nColIni+1900)
	oPrint:Line(nRowIni+0400,nColIni+2250,nRowFim-0250,nColIni+2250)
	oPrint:Line(nRowIni+0400,nColIni+2550,nRowFim-0250,nColIni+2550)
	oPrint:Line(nRowIni+0400,nColIni+2850,nRowFim-0250,nColIni+2850)
	
	nRowAtu := nRowIni+0450

Return( Nil )


/** {Protheus.doc} fCriaSx1
Funcao para criacao das perguntas
@type function
@author 	Jose Leite de Barros Neto
@since 	29/11/2013
@Uso: 		SFIEMT
*/
Static Function fCriaSx1()

	/*
		MV_PAR01  Conta Contabil de		?   
		MV_PAR02  Conta Contabil ate	?  
		MV_PAR03  Centro de Custo de	?  
		MV_PAR04  Centro de Custo ate	? 
		MV_PAR05  Local de				?            
		MV_PAR06  Local ate				?          
		MV_PAR07  Considera Baixados 		?   
		MV_PAR08  Consid.Aquisicao		?
	*/
	
	

	u_SFPUTSX1( cPerg, "01","Conta de  "							     ,"","","mv_ch1","C",TamSX3("CT1_CONTA")[1]	,0,0,"G","","CT1"		,"","","mv_par01","","","","","","","","","","","","","","","","",{},{},{})
	u_SFPUTSX1( cPerg, "02","Conta ate "							     ,"","","mv_ch2","C",TamSX3("CT1_CONTA")[1]	,0,0,"G","","CT1"		,"","","mv_par02","","","","","","","","","","","","","","","","",{},{},{})
	u_SFPUTSX1( cPerg, "03","C.Custo De"						    	  ,"","","mv_ch3","C",TamSX3("CTT_CUSTO")[1]	,0,0,"G","","CTT"		,"","","mv_par03","","","","","","","","","","","","","","","","",{},{},{})
	u_SFPUTSX1( cPerg, "04","C.Custo Ate"							  ,"","","mv_ch4","C",TamSX3("CTT_CUSTO")[1]	,0,0,"G","","CTT"		,"","","mv_par04","","","","","","","","","","","","","","","","",{},{},{})
	u_SFPUTSX1( cPerg, "05","Local de   "							  ,"","","mv_ch5","C",TamSX3("N1_LOCAL")[1]	,0,0,"G","","SN1LOC"	,"","","mv_par05","","","","","","","","","","","","","","","","",{},{},{})
	u_SFPUTSX1( cPerg, "06","Local ate  "							  ,"","","mv_ch6","C",TamSX3("N1_LOCAL")[1]	,0,0,"G","","SN1LOC"	,"","","mv_par06","","","","","","","","","","","","","","","","",{},{},{})
	u_SFPUTSX1( cPerg, "07","Considera Baixados? "				  ,"","","mv_ch7","N",1							   ,0,0,"C","",""		   ,"","","mv_par07","1=Sim","","","","2=Não","","","","","","","","","","","",{},{},{})
	u_SFPUTSX1( cPerg, "08","Considera Aquisicao Pos Calculo ? ","","","mv_ch8","N",1							   ,0,0,"C","",""		   ,"","","mv_par08","1=Sim","","","","2=Não","","","","","","","","","","","",{},{},{})
	
Return( Nil )
