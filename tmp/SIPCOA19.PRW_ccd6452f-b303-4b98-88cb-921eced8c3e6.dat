#INCLUDE "protheus.ch"
#INCLUDE "topconn.ch"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ SIPCOA19 º Autor ³ Leonardo Soncin    º Data ³  30/01/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Saldo Historico - Simulacao                                º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ CNI                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

User Function SIPCOA19()
Local aArea	:= GetArea()
Local oDlg
Local oGet
Local oDtDe
Local oDtAte
Local oCtade
Local oCtaAte
Local oCcDe
Local oCcAte
Local oCLVLDe
Local oCLVLAte
Local oOperDe
Local oOperAte
Local oItemDe
Local oItemAte
Local oTpSaldo
Local oClasseDe
Local oClasseAte
Local oPercent
Local aObjects
PRIVATE aHeaderAK2	:= {}
PRIVATE aColsAK2	:= {}
PRIVATE dDataDe  	:= CriaVar("AKD_DATA")
PRIVATE dDataAte 	:= CriaVar("AKD_DATA")
PRIVATE cCtaDe   	:= Criavar("AKD_CO")
PRIVATE cCtaAte  	:= Replicate("Z",TamSX3("AKD_CO")[1])
PRIVATE cCcDe  		:= Criavar("AKD_CC")
PRIVATE cCcAte 		:= Replicate("Z",TamSX3("AKD_CC")[1])
PRIVATE cItemDe  	:= Criavar("AKD_ITCTB")
PRIVATE cItemAte 	:= Replicate("Z",TamSX3("AKD_ITCTB")[1])
PRIVATE cCLVLDe	    := Criavar("AKD_CLVLR")
PRIVATE cCLVLAte	:= Replicate("Z",TamSX3("AKD_CLVLR")[1])
PRIVATE cOperDe     := Criavar("AKD_OPER")
PRIVATE cOperAte    := Replicate("Z",TamSX3("AKD_OPER")[1])
PRIVATE cTpSaldo	:= Criavar("AKD_TPSALD")
PRIVATE cClasseDe 	:= Criavar("AKD_CLASSE")
PRIVATE cClasseAte 	:= Replicate("Z",TamSX3("AKD_CLASSE")[1])
PRIVATE nPercent	:= 100
Private aPeriodos 	:= PcoRetPer()
Private cPlanOri 	:= AK1->AK1_CODIGO
Private cRevOri 	:= IF(Empty(AK1->AK1_VERREV), AK1->AK1_VERSAO, AK1->AK1_VERREV)

PRIVATE cArquivx := "AK2"


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Montagem do aHeader do AK2                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SX3")
dbSetOrder(1)
dbSeek("AK2")
While !EOF() .And. (x3_arquivo =="AK2")
	IF X3USO(x3_usado) .AND. cNivel >= x3_nivel .And. ;
		( AllTrim(X3_CAMPO) $ "AK2_CO|AK2_DESCCO|AK2_ITCTB|AK2_DESCIT|AK2_CLVLR|AK2_DESCCL|AK2_CC|AK2_DESCCC|AK2_OPER|AK2_CLASSE|AK2_DESCLA")
		AADD(aHeaderAK2,{ 	TRIM(x3titulo()),;
		SX3->X3_CAMPO,;
		SX3->X3_PICTURE,;
		SX3->X3_TAMANHO,;
		SX3->X3_DECIMAL,;
		"PCOChk_Acesso(cArquivx, oGet, oGet:nAt)"+If(!Empty(SX3->X3_VALID),".And.("+SX3->X3_VALID+")",""),;
		SX3->X3_USADO,;
		SX3->X3_TIPO,;
		SX3->X3_F3,;
		SX3->X3_CONTEXT,;
		SX3->X3_CBOX,;
		SX3->X3_RELACAO,;
		"PCOChk_Acesso(cArquivx, oGet, oGet:nAt)"+If(!Empty(SX3->X3_WHEN),".And.("+SX3->X3_WHEN+")","")})
	Endif
	dbSkip()
End

dbSelectArea("SX3")
dbSetOrder(2)
dbSeek("AK2_VALOR")
For nX :=1 To Len(aPeriodos)
	AADD(aHeaderAK2,{ aPeriodos[nX],;
	SX3->X3_CAMPO,;
	SX3->X3_PICTURE,;
	SX3->X3_TAMANHO,;
	SX3->X3_DECIMAL,;
	"PCOChk_Acesso(cArquivx, oGet, oGet:nAt)"+If(!Empty(SX3->X3_VALID),".And.("+SX3->X3_VALID+")",""),;
	SX3->X3_USADO,;
	SX3->X3_TIPO,;
	SX3->X3_F3,;
	SX3->X3_CONTEXT,;
	SX3->X3_CBOX,;
	SX3->X3_RELACAO,;
	"PCOChk_Acesso(cArquivx, oGet, oGet:nAt)"+If(!Empty(SX3->X3_WHEN),".And.("+SX3->X3_WHEN+")","")})
Next

aadd(aColsAK2,Array(Len(aHeaderAK2)+1))
For ny := 1 to Len(aHeaderAK2)
	aColsAK2[1][ny] := CriaVar(aHeaderAK2[ny][2])
Next ny
aColsAK2[1][Len(aHeaderAK2)+1] := .F.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
//³ Montagem da Tela                                             ³
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

dbSelectArea("AKD")

aObjects 	:= {}
aSizeAut	:= MsAdvSize()
Aadd( aObjects, { 100, 100, .T., .T. } )
Aadd( aObjects, { 100, 100, .T., .T. } )
Aadd( aObjects, { 100, 015, .T., .F. } )
aInfo		:= { aSizeAut[ 1 ], aSizeAut[ 2 ], aSizeAut[ 3 ], aSizeAut[ 4 ], 3, 3 }
aPosObj		:= MsObjSize( aInfo, aObjects )
aPosGet		:= MsObjGetPos(aSizeAut[ 3 ]-aSizeAut[ 1 ], 315, {{003, 033, 160, 200, 240, 265}} )

oDlg 	:= MSDIALOG():New(aSizeAut[7],000, aSizeAut[6],aSizeAut[5], "Simulação",,,,,,,,,.T.)

oPanel := TPanel():New(0,0,'',oDlg,, .T., .T.,, ,115,115,.T.,.T. )
oPanel:Align := CONTROL_ALIGN_TOP

@ C(010),C(005) Say "Período de: "  Size C(060),C(010) COLOR CLR_BLACK PIXEL OF oPanel
@ C(010),C(055) MsGet oDtDe  	   Var dDataDe  Size C(039),C(007) COLOR CLR_BLACK Picture PesqPict("AKD","AKD_DATA") F3 CpoRetF3('AKD_DATA') PIXEL OF oPanel
@ C(010),C(110) Say "Período até: "  Size C(060),C(010) COLOR CLR_BLACK PIXEL OF oPanel
@ C(010),C(160) MsGet oDtAte  	  Var dDataAte  Size C(039),C(007) COLOR CLR_BLACK Picture PesqPict("AKD","AKD_DATA") F3 CpoRetF3('AKD_DATA')  PIXEL OF oPanel

@ C(020),C(005) Say "Conta orçamentária de: "  Size C(060),C(010) COLOR CLR_BLACK PIXEL OF oPanel
@ C(020),C(055) MsGet oCtade  	   Var cCtaDe  Size C(039),C(007) COLOR CLR_BLACK Picture PesqPict("AKD","AKD_CO") F3 CpoRetF3('AKD_CO') PIXEL OF oPanel
@ C(020),C(110) Say "Conta orçamentária até: "  Size C(060),C(010) COLOR CLR_BLACK PIXEL OF oPanel
@ C(020),C(160) MsGet oCtaAte  	   Var cCtaAte  Size C(039),C(007) COLOR CLR_BLACK Picture PesqPict("AKD","AKD_CO") F3 CpoRetF3('AKD_CO') PIXEL OF oPanel

@ C(030),C(005) Say "Item contábil de: "  Size C(060),C(010) COLOR CLR_BLACK PIXEL OF oPanel
@ C(030),C(055) MsGet oItemdE  	   Var cItemDe  Size C(039),C(007) COLOR CLR_BLACK  Picture PesqPict("AKD","AKD_ITCTB") F3 CpoRetF3('AKD_ITCTB') PIXEL OF oPanel
@ C(030),C(110) Say "Item contábil até: "  Size C(060),C(010) COLOR CLR_BLACK PIXEL OF oPanel
@ C(030),C(160) MsGet oItemAte  	   Var cItemAte  Size C(039),C(007) COLOR CLR_BLACK Picture PesqPict("AKD","AKD_ITCTB") F3 CpoRetF3('AKD_ITCTB') PIXEL OF oPanel

@ C(040),C(005) Say "Classe de valor de: "  Size C(060),C(010) COLOR CLR_BLACK PIXEL OF oPanel
@ C(040),C(055) MsGet oCLVLDe  	   Var cCLVLDe  Size C(039),C(007) COLOR CLR_BLACK Picture PesqPict("AKD","AKD_CLVLR") F3 CpoRetF3('AKD_CLVLR') PIXEL OF oPanel
@ C(040),C(110) Say "Classe de valor até: "  Size C(060),C(010) COLOR CLR_BLACK PIXEL OF oPanel
@ C(040),C(160) MsGet oCLVLAte  	   Var cCLVLAte  Size C(039),C(007) COLOR CLR_BLACK Picture PesqPict("AKD","AKD_CLVLR") F3 CpoRetF3('AKD_CLVLR') PIXEL OF oPanel

@ C(050),C(005) Say "Centro de Custo de: "  Size C(060),C(010) COLOR CLR_BLACK PIXEL OF oPanel
@ C(050),C(055) MsGet oCcDe  	 Var cCcDe  Size C(039),C(007) COLOR CLR_BLACK Picture PesqPict("AKD","AKD_CC") F3 CpoRetF3('AKD_CC') PIXEL OF oPanel
@ C(050),C(110) Say "Centro de Custo até: "  Size C(060),C(010) COLOR CLR_BLACK PIXEL OF oPanel
@ C(050),C(160) MsGet oCCAte  	 Var cCCAte  Size C(039),C(007) COLOR CLR_BLACK Picture PesqPict("AKD","AKD_CC") F3 CpoRetF3('AKD_CC') PIXEL OF oPanel

@ C(060),C(005) Say "Operação de: "  Size C(060),C(010) COLOR CLR_BLACK PIXEL OF oPanel
@ C(060),C(055) MsGet oOperDe  	 Var cOperDe  Size C(039),C(007) COLOR CLR_BLACK Picture PesqPict("AKD","AKD_OPER") F3 CpoRetF3("AKD_OPER") PIXEL OF oPanel
@ C(060),C(110) Say "Operação até: "  Size C(060),C(010) COLOR CLR_BLACK PIXEL OF oPanel
@ C(060),C(160) MsGet oOperAte  	 Var cOperAte  Size C(039),C(007) COLOR CLR_BLACK Picture PesqPict("AKD","AKD_OPER") F3 CpoRetF3("AKD_OPER") PIXEL OF oPanel

@ C(070),C(005) Say "Classe De: "      Size C(060),C(010) COLOR CLR_BLACK PIXEL OF oPanel
@ C(070),C(055) MsGet oClasseDe  	     Var cClasseDe  Size C(025),C(007) COLOR CLR_BLACK Picture PesqPict("AKD","AKD_CLASSE") F3 CpoRetF3('AKD_CLASSE') PIXEL OF oPanel
@ C(070),C(110) Say "Classe Ate: "      Size C(060),C(010) COLOR CLR_BLACK PIXEL OF oPanel
@ C(070),C(160) MsGet oClasseAte  	     Var cClasseAte  Size C(025),C(007) COLOR CLR_BLACK Picture PesqPict("AKD","AKD_CLASSE") F3 CpoRetF3('AKD_CLASSE') PIXEL OF oPanel

@ C(080),C(005) Say "Tipo de Saldo: " Size C(060),C(010) COLOR CLR_BLACK PIXEL OF oPanel
@ C(080),C(055) MsGet oTpSaldo  	 Var cTpSaldo  Size C(025),C(007) COLOR CLR_BLACK Picture PesqPict("AKD","AKD_TPSALD") F3 CpoRetF3('AKD_TPSALD') Valid CheckSX3('AKD_TPSALD',cTpSaldo) PIXEL OF oPanel
@ C(080),C(110) Say "% Aplicação: "  Size C(060),C(010) COLOR CLR_BLACK PIXEL OF oPanel
@ C(080),C(160) MsGet oPercent  	 Var nPercent  Size C(025),C(007)  COLOR CLR_BLACK Picture "@R 999.99" PIXEL OF oPanel

oRodape := TPanel():New(0,0,'',oDlg,, .T., .T.,, ,30,30,.T.,.T. )
oRodape:Align := CONTROL_ALIGN_BOTTOM

@ C(008),C(340) BUTTON "Atualizar" SIZE 40,13 ACTION (oGet:aCols := OKAtualiz(aColsAK2,aHeaderAK2)) PIXEL OF oRodape
@ C(008),C(390) BUTTON "Efetivar" SIZE 40,13 ACTION (xEfetiva(aColsAK2,aHeaderAK2)) PIXEL OF oRodape
@ C(008),C(440) BUTTON "Sair" SIZE 40,13 ACTION (oDlg:End()) PIXEL OF oRodape

oGet:= MsNewGetDados():New(aPosObj[2,1]-50,aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],0,"AllwaysTrue", "AllwaysTrue","", ,000, 999,,,,, aHeaderAK2, aColsAK2)
oGet:oBrowse:Align := CONTROL_ALIGN_ALLCLIENT

oDlg:lCentered := .T.
oDlg:Activate()

RestArea(aArea)
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³OKAtualiz º Autor ³ Leonardo Soncin    ³ Data  ³ 30/01/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Saldo Historico - Simulacao                                º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ CNI                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function OKAtualiz(aCols,aHeader)
Local oProcess

oProcess := MsNewProcess():New( { | lEnd | aCols := xProcAt(@lEnd,oProcess,aCols,aHeader) }, "Atualizando", "Aguarde, atualizando...", .F. )
oProcess:Activate()

Return aClone(aCols)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³OKAtualiz º Autor ³ Leonardo Soncin    ³ Data  ³ 30/01/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Saldo Historico - Simulacao                                º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ CNI                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function xProcAt(lEnd,oProcess,aCols,aHeader)
Local aArea := GetArea()
Local _cQuery := ""
Local cAliasTRB := GetNextAlias()
Local cAliasTMP := GetNextAlias()
Local aEstrut 	:= {}
Local cArqTMP  	:= ""
Local nPosCo	:= Ascan(aHeader, {|e| Alltrim(e[2]) = "AK2_CO"} )

//Limpa aCols
aCols := {}
aadd(aCols,Array(Len(aHeader)+1))
For ny := 1 to Len(aHeader)
	aCols[1][ny] := CriaVar(aHeader[ny][2])
Next ny
aCols[1][Len(aHeader)+1] := .F.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Estrutura do arquivo temporario ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aAdd( aEstrut, { "AK2_CO"		,"C", TamSx3("AK2_CO")[1], 0 } )
aAdd( aEstrut, { "AK2_CC"		,"C", TamSx3("AK2_CC")[1], 0 } )
aAdd( aEstrut, { "AK2_ITCTB"	,"C", TamSx3("AK2_ITCTB")[1], 0 } )
aAdd( aEstrut, { "AK2_CLVLR"	,"C", TamSx3("AK2_CLVLR")[1], 0 } )
aAdd( aEstrut, { "AK2_CLASSE"	,"C", TamSx3("AK2_CLASSE")[1], 0 } )
aAdd( aEstrut, { "AK2_OPER"		,"C", TamSx3("AK2_OPER")[1], 0 } )

// Campos de Acordo com o Periodo
For nX := 1 to Len(aPeriodos)
	aAdd( aEstrut, { "P"+Alltrim(Str(Month(Ctod(Substr(aPeriodos[nX],1,10))))) 	,"N", TamSx3("AK2_VALOR")[1], 2 } )
Next nX

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Cria o arquivo temporario ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cArqTMP := CriaTrab( aEstrut, .T. )
dbUseArea( .T.,,cArqTMP, cAliasTmp, .F., .F. )

IndRegua( cAliasTmp, cArqTMP, "AK2_CO+AK2_CC+AK2_ITCTB+AK2_CLVLR+AK2_CLASSE+AK2_OPER",,,"Criando Indice, aguarde..." )
dbClearIndex()
dbSetIndex( cArqTMP + OrdBagExt() )

//Query
cQuery :=  "SELECT AKD_CO, AKD_CC, AKD_ITCTB, AKD_CLVLR, AKD_CLASSE, AKD_OPER, AKD_DATA, "
cQuery +=  "CASE WHEN AKD_TIPO = '1' THEN AKD_VALOR1 ELSE AKD_VALOR1*-1 END AKD_VALOR1 "
cQuery +=  "FROM "+RetSqlName("AKD")
cQuery +=  " WHERE AKD_FILIAL = '"+xFilial("AKD")+"' AND "
//cQuery +=  "AKD_CODPLA = '"+cPlanOri+"' AND AKD_VERSAO = '"+cRevOri+"' AND "
cQuery +=  "AKD_DATA BETWEEN '"+Dtos(dDataDe)+"' AND '"+Dtos(dDataAte)+"' AND "
cQuery +=  "AKD_CO BETWEEN '"+cCtaDe+"' AND '"+cCtaAte+"' AND "
cQuery +=  "AKD_CC BETWEEN '"+cCcDe+"' AND '"+cCcAte+"' AND "
cQuery +=  "AKD_ITCTB BETWEEN '"+cItemDe+"' AND '"+cItemAte+"' AND "
cQuery +=  "AKD_CLVLR BETWEEN '"+cCLVLDe+"' AND '"+cCLVLAte+"' AND "
cQuery +=  "AKD_OPER BETWEEN '"+cOperDe+"' AND '"+cOperAte+"' AND "
cQuery +=  "AKD_CLASSE BETWEEN '"+cClasseDe+"' AND '"+cClasseAte+"' AND "
cQuery +=  "AKD_TPSALD = '"+cTpSaldo+"' AND "
cQuery +=  "AKD_STATUS = '1' AND "
cQuery +=  "AKD_TIPO <> '3' AND "
cQuery +=  "D_E_L_E_T_ = '' "
cQuery +=  "ORDER BY AKD_CO,AKD_CC,AKD_ITCTB,AKD_CLVLR,AKD_CLASSE,AKD_OPER
cQuery := ChangeQuery(cQuery)

If Select(cAliasTRB) > 0
	dbSelectArea(cAliasTRB)
	dbCloseArea()
Endif

dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasTRB,.T.,.T.)

DbSelectArea(cAliasTRB)
dbGotop()

If (cAliasTRB)->(!Eof())
	
	DbSelectArea("AK5")
	DbSelectArea("AK6")
	DbSelectArea("CTT")
	DbSelectArea("CTD")
	DbSelectArea("CTH")
	DbSelectArea(cAliasTRB)
	
	While (cAliasTRB)->(!Eof())
		
		oProcess:IncRegua1()
		
		//Grava dados Agrupados
		dbSelectArea(cAliasTmp)
		dbSetOrder(1)
		dbGotop(cAliasTmp)
		
		// verifica se existe o periodo na planilha
		IF (cAliasTmp)->(FieldPos("P"+Alltrim(Str(Month(STOD((cAliasTrb)->AKD_DATA)))))) == 0
			(cAliasTRB)->(dbSkip())
			Loop
		ENDIF
		
		If dbSeek((cAliasTRB)->(AKD_CO+AKD_CC+AKD_ITCTB+AKD_CLVLR+AKD_CLASSE+AKD_OPER))
			RecLock((cAliasTmp),.F.)
			(cAliasTmp)->&("P"+Alltrim(Str(Month(STOD((cAliasTrb)->AKD_DATA))))) +=  (((cAliasTrb)->AKD_VALOR1 * nPercent)/100)
			(cAliasTmp)->(MsUnLock())
		Else
			RecLock((cAliasTmp),.T.)
			(cAliasTmp)->AK2_CO 	:= (cAliasTrb)->AKD_CO
			(cAliasTmp)->AK2_CC		:= (cAliasTrb)->AKD_CC
			(cAliasTmp)->AK2_ITCTB 	:= (cAliasTrb)->AKD_ITCTB
			(cAliasTmp)->AK2_CLVLR 	:= (cAliasTrb)->AKD_CLVLR
			(cAliasTmp)->AK2_CLASSE	:= (cAliasTrb)->AKD_CLASSE
			(cAliasTmp)->AK2_OPER 	:= (cAliasTrb)->AKD_OPER
			(cAliasTmp)->&("P"+Alltrim(Str(Month(STOD((cAliasTrb)->AKD_DATA))))) := (((cAliasTrb)->AKD_VALOR1 * nPercent)/100)
			(cAliasTmp)->(MsUnLock())
		Endif
		
		dbSelectArea(cAliasTRB)
		(cAliasTRB)->(dbSkip())
	EndDo
Endif

// Atualiza aCols
dbSelectArea(cAliasTMP)
dbGotop()
While (cAliasTMP)->(!Eof())
	oProcess:IncRegua2()
	If !Empty(aCols[Len(aCols),nPosCo])
		aadd(aCols,Array(Len(aHeader)+1))
	Endif
	For nX := 1 to Len(aHeader)
		
		Do Case
			Case Alltrim(aHeader[nX,2]) =="AK2_VALOR"
				aCols[Len(aCols),nX]:= (cAliasTmp)->&("P"+Alltrim(Str(Month(Ctod(Substr(aHeader[nX,1],1,10))))))
			Case AllTrim(aHeader[nX,2])=="AK2_DESCLA"
				AK6->(dbSetOrder(1))
				If AK6->(dbSeek(xFilial("AK6")+(cAliasTmp)->AK2_CLASSE))
					aCols[Len(aCols),nX] := AK6->AK6_DESCRI
				EndIf
			Case AllTrim(aHeader[nX][2])=="AK2_DESCCO"
				AK5->(dbSetOrder(1))
				If AK5->(dbSeek(xFilial("AK5")+(cAliasTmp)->AK2_CO))
					aCols[Len(aCols),nX] := AK5->AK5_DESCRI
				EndIf
			Case AllTrim(aHeader[nX][2])=="AK2_DESCCC"
				CTT->(dbSetOrder(1))
				If CTT->(dbSeek(xFilial("CTT")+(cAliasTmp)->AK2_CC))
					aCols[Len(aCols),nX] := CTT->CTT_DESC01
				EndIf
			Case AllTrim(aHeader[nX][2])=="AK2_DESCIT"
				CTD->(dbSetOrder(1))
				If CTD->(dbSeek(xFilial("CTD")+(cAliasTmp)->AK2_ITCTB))
					aCols[Len(aCols),nX] := CTD->CTD_DESC01
				EndIf
			Case AllTrim(aHeader[nX][2])=="AK2_DESCCL"
				CTH->(dbSetOrder(1))
				If CTH->(dbSeek(xFilial("CTH")+(cAliasTmp)->AK2_CLVLR))
					aCols[Len(aCols),nX] := CTH->CTH_DESC01
				EndIf
			OtherWise
				aCols[Len(aCols),nX]:=FieldGet(FieldPos(aHeader[nX,2]))
		EndCase
		
		aCols[Len(aCols)][Len(aHeader)+1] := .F.
	Next nX
	
	dbSelectArea(cAliasTMP)
	dbSkip()
EndDo

//Libera Alias e Arquivos Temporarios
If Select(cAliasTRB) > 0
	dbSelectArea(cAliasTRB)
	dbCloseArea()
Endif

If Select(cAliasTmp) != 0
	dbSelectArea(cAliasTmp)
	dbCloseArea()
	FErase(cArqTmp+GetDBExtension())
	FErase(cArqTmp+OrdBagExt())
Endif

RestArea(aArea)

Return aCols
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³OKAtualiz º Autor ³ Leonardo Soncin    ³ Data  ³ 30/01/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Saldo Historico - Simulacao                                º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ CNI                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function xEfetiva(aCols,aHeader)
Local oWizard
Local cArquivo
Local aAreaAK1 	:= AK1->(GetArea())
Local aAreaAK2 	:= AK2->(GetArea())
Local aAreaAK3 	:= AK3->(GetArea())
Local aAreaAKE 	:= AKE->(GetArea())
Local lRet		:= .F.
Local lParam, lBrowse:=.T., lParam2
Local aParametros := {	{3,"Distribuição"				, 1,{"Normal", "Específica"}	,160,,.F.}}
Local aConfig :=  {1}
Local aParam2  := {}
Local aConfig2 := {}
Local oProcess
Local nPosCo	:= Ascan(aHeader, {|e| Alltrim(e[2]) = "AK2_CO"} )

Private lAllPeriod := .T.

If AK1->(FieldPos("AK1_XAPROV"))>0
	If AK1->AK1_XAPROV <> "0"
		MsgStop("A planilha orçamentária deve estar com a situação igual a '0 - Em aberto' para que possa importar dados. Verifique!","Atenção")
		Return
	Endif
Endif

If !Empty(aCols[Len(aCols),nPosCo])
	
	oWizard := APWizard():New("Atenção"/*<chTitle>*/,;
	"Este assistente lhe ajudara a distribuir um determinado valor para os periodos da planilha atual."/*<chMsg>*/, "Distribuição de Valores Apurados "/*<cTitle>*/, ;
	"Você devera escolher a forma de distribuição e ao finalizar o assistente, este valor será distribuido conforme os parametros solicitados."+;
	CRLF+CRLF+"Planilha : "+Alltrim(AK1->AK1_CODIGO)+" - "+PadR(AK1->AK1_DESCRI,50),;
	{||.T.}/*<bNext>*/, ;
	{|| .T.}/*<bFinish>*/,;
	/*<.lPanel.>*/, , , /*<.lNoFirst.>*/)
	
	oWizard:NewPanel( "Distribuição"/*<chTitle>*/,;
	"Neste passo voce deverá informar a forma da distribuição para a planilha orçamentária."/*<chMsg>*/, ;
	{||.T.}/*<bBack>*/, ;
	{||.T.}/*<bNext>*/, ;
	{||.T.}/*<bFinish>*/,;
	.T./*<.lPanel.>*/,;
	{||Rat1_Par(oWizard,@lParam, aParametros, aConfig)}/*<bExecute>*/ )
	
	oWizard:NewPanel( "Período destino"/*<chTitle>*/,;
	"Neste passo voce deverá informar o período destino para distribuição."/*<chMsg>*/, ;
	{||.T.}/*<bBack>*/, ;
	{||.T.}/*<bNext>*/, ;
	{||lRet := .T.}/*<bFinish>*/, ;
	.T./*<.lPanel.>*/, ;
	{||Rat2_Par(oWizard, lParam2, aParam2, aPeriodos, aConfig, aConfig2)}/*<bExecute>*/ )
	
	oWizard:Activate( .T./*<.lCenter.>*/,;
	{||.T.}/*<bValid>*/, ;
	{||.T.}/*<bInit>*/, ;
	{||.T.}/*<bWhen>*/ )
	
	If lRet
		oProcess := MsNewProcess():New( { | lEnd | xGrvOrc(@lEnd,oProcess,aCols,aHeader,aConfig[1],aConfig2[1]) }, "Atualizando", "Aguarde, atualizando...", .F. )
		oProcess:Activate()
	Endif
	
Else
	MsgStop("Não existem itens para realizar a efetivação.")
	lRet := .F.
Endif

RestArea(aAreaAK1)
RestArea(aAreaAK2)
RestArea(aAreaAK3)
RestArea(aAreaAKE)

Return(lRet)


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ xGrvOrc  º Autor ³ Leonardo Soncin    ³ Data  ³ 30/01/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Saldo Historico - Simulacao                                º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ CNI                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function xGrvOrc(lEnd,oProcess,aCols,aHeader,nTipo,nPeriodo)
Local nZ, nX
Local nPosCo	:= Ascan(aHeader, {|e| Alltrim(e[2]) = "AK2_CO"} )
Local nPosCc	:= Ascan(aHeader, {|e| Alltrim(e[2]) = "AK2_CC"} )
Local nPosItem	:= Ascan(aHeader, {|e| Alltrim(e[2]) = "AK2_ITCTB"} )
Local nPosOper	:= Ascan(aHeader, {|e| Alltrim(e[2]) = "AK2_OPER"} )
Local nPosClass	:= Ascan(aHeader, {|e| Alltrim(e[2]) = "AK2_CLASSE"})
Local nPosClvlr	:= Ascan(aHeader, {|e| Alltrim(e[2]) = "AK2_CLVLR"}  )
Local cItem		:=	""
Local cNivel	:= "001"
Local aRecAK3	:={}
Local lAglut 	:= .F.
Local dPerIni 	:= Iif(nTipo == 1,AK1->AK1_INIPER,cTod(Substr(aPeriodos[nPeriodo],1,10)))
Local dPerFim 	:= Iif(nTipo == 1,AK1->AK1_FIMPER,cTod(Substr(aPeriodos[nPeriodo],14,16)))

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicializa a gravacao dos lancamentos do SIGAPCO          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ

// Pesquisa e Deleta Orcamento
xDelOrc(nTipo,dPerIni,dPerFim)

//Inicia Gravacao dos Dados Simulados

PcoIniLan('000252')

For nX := 1 to Len(aCols)
	
	oProcess:IncRegua1()
	oProcess:IncRegua2()
	
	aRecAK3	:={}
	
	For nZ := 1 to Len(aPeriodos)
		
		IF aCols[nX,Ascan(aHeader, {|e| Alltrim(e[1]) = Alltrim(aPeriodos[nZ])})] == 0
			Loop
		ENDIF
		
		dPerIni 	:= Iif(nTipo == 1,CTOD(Substr(aPeriodos[nZ],1,10)),CtoD(Substr(aPeriodos[nPeriodo],1,10)))
		dPerFim 	:= Iif(nTipo == 1,CTOD(Substr(aPeriodos[nZ],14,16)),CtoD(Substr(aPeriodos[nPeriodo],14,16)))
		
		//dbSelectArea("AK2")
		//dbSetOrder(1)
		//dbSeek(xFilial("AK2")+cPlanOri+cRevOri+aCols[nX,nPosCo]+DTOS(dPerIni)+"ZZZZ",.T.)
		//dbSkip(-1)
		
		// inicio
		dbSelectArea("AK2")
		dbSetOrder(5)
		
		IF !MsSeek(xFilial("AK2") + cPlanOri + cRevOri + aCols[nX,nPosCo] )
			cItem  := "0001"
		ELSE
			
			_cIDAnt := ""
			cItem   := ""
			
			While !Eof() .And. AK2->AK2_FILIAL + AK2->AK2_ORCAME + AK2->AK2_VERSAO + AK2->AK2_CO  == xFilial("AK2")+cPlanOri+cRevOri+aCols[nX,nPosCo]
				
				If AK2->(AK2_CC+AK2_ITCTB+AK2_CLVLR+AK2_CLASSE+AK2_OPER) == aCols[nX,nPosCC]+aCols[nX,nPosITem]+aCols[nX,nPosCLVLR]+aCols[nX,nPosClass]+aCols[nX,nPosOper]
					cItem	:=	AK2->AK2_ID
				Endif
				
				_cIDAnt := AK2->AK2_ID
				
				AK2->(dbSkip())
			Enddo
			
			IF Empty(cItem)
				cItem := Soma1(_cIDAnt)
			ELSE
				dbSelectArea("AK2")
				dbSetOrder(1)
				IF dbSeek(xFilial("AK2")+cPlanOri+cRevOri+aCols[nX,nPosCo]+DTOS(dPerIni)+cItem)
					lAglut := .T.
				ENDIF
			ENDIF
			
		ENDIF
		
		/*
		If xFilial("AK2")+cPlanOri+cRevOri+aCols[nX,nPosCo]+DTOS(dPerIni) == AK2_FILIAL+AK2_ORCAME+AK2_VERSAO+AK2_CO+DTOS(AK2_PERIOD)
		//If AK2->(AK2_CC+AK2_ITCTB+AK2_CLVLR+AK2_CLASSE+AK2_OPER) == aCols[nX,nPosCC]+aCols[nX,nPosITem]+aCols[nX,nPosCLVLR]+aCols[nX,nPosClass]+aCols[nX,nPosOper]
		If AK2->(AK2_CC+AK2_ITCTB+AK2_CLVLR+AK2_OPER) == aCols[nX,nPosCC]+aCols[nX,nPosITem]+aCols[nX,nPosCLVLR]+aCols[nX,nPosOper]
		cItem := AK2->AK2_ID
		lAglut := .T.
		Else
		cItem := Soma1(AK2->AK2_ID)
		Endif
		Else
		cItem := "0001"
		EndIf
		*/
		
		If !nTipo == 1
			
			If AK2->(AK2_CO+AK2_CC+AK2_ITCTB+AK2_CLVLR+AK2_CLASSE+AK2_OPER+DTOS(AK2_PERIOD))==;
				aCols[nX,nPosCo]+aCols[nX,nPosCC]+aCols[nX,nPosITem]+aCols[nX,nPosCLVLR]+aCols[nX,nPosClass]+aCols[nX,nPosOper]+DTOS(dPerIni)
				lAglut := .T.
			Else
				lAglut := .F.
			Endif
			
		Endif
		
		If !lAglut
			RecLock("AK2",.T.)
			AK2->AK2_FILIAL := xFilial("AK2")
			AK2->AK2_ORCAME := cPlanOri
			AK2->AK2_VERSAO := cRevOri
			AK2->AK2_MOEDA	:= 1
			AK2->AK2_PERIOD	:= dPerIni
			AK2->AK2_DATAI	:= dPerIni
			AK2->AK2_DATAF	:= dPerFim
			AK2->AK2_ID		:= cItem
			AK2->AK2_XSTS	:= "0" // status da UO
			AK2->AK2_CO 	:= aCols[nX,nPosCo]
			AK2->AK2_CC 	:= aCols[nX,nPosCC]
			AK2->AK2_ITCTB 	:= aCols[nX,nPosItem]
			AK2->AK2_CLVLR 	:= aCols[nX,nPosClvlr]
			AK2->AK2_CLASSE := aCols[nX,nPosClass]
			AK2->AK2_OPER 	:= aCols[nX,nPosOper]
			AK2->AK2_VALOR	:= aCols[nX,Ascan(aHeader, {|e| Alltrim(e[1]) = Alltrim(aPeriodos[nZ])})]
		Else
			RecLock("AK2",.F.)
			AK2->AK2_VALOR	+= aCols[nX,Ascan(aHeader, {|e| Alltrim(e[1]) = Alltrim(aPeriodos[nZ])})]
		Endif
		MsUnlock()
		
		dbSelectArea("AK3")
		dbSetOrder(1)
		
		If !dbSeek(xFilial('AK3')+AK2->AK2_ORCAME+AK2->AK2_VERSAO+AK2->AK2_CO)
			cNivel := "001"
			GravaAK3(AK2->AK2_ORCAME,AK2->AK2_VERSAO,AK2->AK2_CO,aRecAK3,@cNivel)
			
			For nt := Len(aRecAK3) to 1 Step -1
				cNivel := Soma1(cNivel)
				AK3->(dbGoto(aRecAK3[nt]))
				RecLock("AK3",.F.)
				AK3->AK3_NIVEL := cNivel
				MsUnlock()
			Next nt
		EndIf
		
		dbSelectArea("AK2")
		
		PcoDetLan("000252","01","PCOA100")
		
	Next nX
Next nZ

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Finaliza a gravacao dos lancamentos do SIGAPCO            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

PcoFinLan('000252')

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ GravaAK3    ºAutor ³ Leonardo Soncin   º Data ³ 21/11/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Inclui as contas orcamentarias ref a tab (AK3)   posicionadoº±±
±±º          ³utiliza recursividade ao chamar a funcao A200Nivel() para   º±±
±±º          ³chamar novamente xIncConta para as contas pai     	      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function GravaAK3(cOrcame,cVersao,cCO,aRecAK3,cNivel)

Local aArea := GetArea()
dbSelectArea("AK5")
dbSetOrder(1)
If MsSeek(xFilial("AK5")+cCO)
	PmsNewRec("AK3")
	AK3->AK3_FILIAL 	:= xFilial("AK3")
	AK3->AK3_ORCAME		:= cOrcame
	AK3->AK3_VERSAO		:= cVersao
	AK3->AK3_CO			:= cCO
	AK3->AK3_PAI		:= If(Empty(AK5->AK5_COSUP),cOrcame,AK5->AK5_COSUP)
	AK3->AK3_TIPO		:= AK5->AK5_TIPO
	AK3->AK3_DESCRI		:= AK5->AK5_DESCRI
	MsUnlock()
	aAdd(aRecAK3,AK3->(RecNo()))
	dbSelectArea("AK3")
	dbSetOrder(1)
	If !Empty(AK5->AK5_COSUP)
		If !dbSeek(xFilial('AK3')+cOrcame+cVersao+AK5->AK5_COSUP)
			GravaAK3(AK2->AK2_ORCAME,AK2->AK2_VERSAO,AK5->AK5_COSUP,aRecAK3,@cNivel)
		Else
			cNivel := AK3->AK3_NIVEL
		EndIf
	EndIf
EndIf
RestArea(aArea)
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³    Rat1_Par ºAutor  ³Paulo Carnelossi  º Data ³ 20/07/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Funcao para escolha da forma do rateio                      º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Rat1_Par(oWizard, lParam, aParametros, aConfig)

If lParam == NIL
	ParamBox(aParametros ,"Parametros", aConfig,,,.F.,120,3, oWizard:oMPanel[oWizard:nPanel])
	lParam := .T.
Else
	Rest_Par(aConfig)
EndIf

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³    Rest_ParºAutor  ³Paulo Carnelossi   º Data ³ 30/07/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Funcao para restauracao dos conteudos das variaveis MV_PAR  º±±
±±º          ³na navegacao entre os paineis do assistente de copia        º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Rest_Par(aParam)
Local nX
Local cVarMem

For nX := 1 TO Len(aParam)
	cVarMem := "MV_PAR"+AllTrim(STRZERO(nX,2,0))
	&(cVarMem) := aParam[nX]
Next

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³    Rat2_Par  ºAutor  ³Paulo Carnelossi  º Data ³ 20/07/05  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Funcao para parametrizar os percentuais nos periodos        º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Rat2_Par(oWizard, lParam2, aParam2 ,aPeriodos, aConfig, aConfig2)

Local nX
Local aAux := {}

aAdd(aAux,Array(Len(aPeriodos)))
aParam2 := {}
aConfig2 := {1}

If aConfig[1] == 2 //Especifico
	For nX := 1 TO Len(aPeriodos)
		aAux[1][nX]:= aPeriodos[nX]
	Next
	aAdd(aParam2,{3,"Período destino",1,aAux[1],100,,.F.})
Else
	aAdd(aParam2,{3,"Período destino",1,{"Todos os períodos da planilha orçamentária."},120,,.F.})
EndIf

If lParam2 == NIL
	ParamBox(aParam2 ,"Parametros", aConfig2,,,.F.,120,3, oWizard:oMPanel[oWizard:nPanel])
	lParam2 := .T.
Else
	Rest_Par(aConfig2)
EndIf

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³  xDelOrc º Autor ³ Leonardo Soncin    º Data ³  27/11/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Deleta dados da Pl. Orcamentaria antes da importacao do csvº±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ CNI                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function xDelOrc(nTipo,dPerIni,dPerFim)
Local cAliasDel := GetNextAlias()
Local cQuery := ""

// Deleta AK2
cQuery := "SELECT R_E_C_N_O_ AS NREG FROM "
cQuery += RetSqlName("AK2")+" "
cQuery += " WHERE AK2_FILIAL = '"+xFilial("AK2")+"' AND AK2_ORCAME = '"+cPlanOri+"' AND AK2_VERSAO = '"+cRevOri+"' AND "
If nTipo == 1
	cQuery +=  "AK2_PERIOD BETWEEN '"+Dtos(FirstDay(dPerIni))+"' AND '"+Dtos(dPerFim)+"' AND " //cQuery +=  "AK2_PERIOD BETWEEN '"+Dtos(dDataDe)+"' AND '"+Dtos(dDataAte)+"' AND "
Else
	cQuery +=  "AK2_PERIOD BETWEEN '"+Dtos(dPerIni)+"' AND '"+Dtos(dPerFim)+"' AND "
Endif
cQuery += " AK2_CO BETWEEN '"+cCtaDe+"' AND '"+cCtaAte+"'  AND "
cQuery += " AK2_CC BETWEEN '"+cCcDe+"' AND '"+cCcAte+"'  AND "
cQuery += " AK2_ITCTB BETWEEN '"+cItemDe+"' AND '"+cItemAte+"'  AND "
cQuery += " AK2_CLVLR BETWEEN '"+cCLVLDe+"' AND '"+cCLVLAte+"'  AND "
cQuery += " AK2_OPER BETWEEN '"+cOperDe+"' AND '"+cOperAte+"'  AND "
cQuery +=  "AK2_CLASSE BETWEEN '"+cClasseDe+"' AND '"+cClasseAte+"' AND "
cQuery += " D_E_L_E_T_ <> '*' "
cQuery := ChangeQuery(cQuery)

If Select(cAliasDel) > 0
	dbSelectArea(cAliasDel)
	dbCloseArea()
Endif

dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasDel,.T.,.F.)

DbSelectArea(cAliasDel)
dbGotop()

If (cAliasDel)->(!Eof())
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Inicializa a gravacao dos lancamentos do SIGAPCO          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	PcoIniLan('000252')
	
	While (cAliasDel)->(!Eof())
		
		dbSelectArea("AK2")
		dbGoto((cAliasDel)->NREG)
		
		PcoDetLan("000252","01","PCOA100",.T.,"00025201")
		Reclock("AK2",.F.,.T.)
		dbDelete()
		Msunlock()
		
		dbSelectArea(cAliasDel)
		dbSkip()
	Enddo
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Finaliza a gravacao dos lancamentos do SIGAPCO            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	PcoFinLan('000252')
Endif

Return
