#Include 'Protheus.ch'

/*/{Protheus.doc} SF06R06X
(long_description)
@author j2a.luizjunior
@since 05/10/2016
@version 1.0
@example
(examples)
@see (links_or_references)
/*/
//Walmir Junior - Fonte do Relatório de Transferência para Terceiro.
User Function SF06R06X
	
	Local   oReport
	Private _cSitTit  := ""
	Private cPerg     := "SF06R06X"
	Private cTitOrig  := ""	
	
	If Funname(0) == "SF06R06X"
		CriaSX1(cPerg)
		If !Pergunte(cPerg,.T.)
			Return(Nil)
		Else
			If !ChkDados(MV_PAR01)
				Help(" ",1,"RECNO")
				Return
			Else
				If _cSitTit == "4"  
					cTitOrig := "Envio de titulos para Cobrança Terceiros"	
				ElseIf _cSitTit == "8"
					cTitOrig := "Envio ao SPC"
				EndIf		
			EndIf	
		EndIf	
	Else
		If Type("_cNumDR") != "U"
			If !ChkDados(_cNumDR)
				Help(" ",1,"RECNO")
				Return
			Else
				If _cSitTit == "4"  
					cTitOrig := "Envio de titulos para Cobrança Terceiros"	
				ElseIf _cSitTit == "8"
					cTitOrig := "Envio ao SPC"
				EndIf		
			EndIf
		EndIf
	EndIf
	
	oReport  := ReportDef()
	oReport:PrintDialog()
	
Return

/*/{Protheus.doc} ReportDef
(long_description)
@author j2a.luizjunior
@since 05/10/2016
@version 1.0
@example
(examples)
@see (links_or_references)
/*/

Static Function ReportDef(cPerg)
	
	Local oReport
	Local cIDCelula	:= ""	
	Local cNomeRel	:= "SF06R06X"
	
	Local cTitRel	:= cTitOrig
	Local cDescRel	:= cTitOrig
	
	If Type("_cNumDR") != 'U'
		If !Empty(_cNumDR) 
		cE1_NUMBOR := _cNumDR
		EndIf
	Else
		cE1_NUMBOR := MV_PAR01
	EndIf	
	
	oReport := TReport():New( cNomeRel, cTitRel, cPerg, { |oReport| ReportPrint(oReport) }, cDescRel )
	
	oReport:SetLandScape(.T.)
	//oReport:SetPortrait() 
	oReport:SetTotalInLine(.F.) 
	oReport:Page()
	
	//-> Titulos
	oTitulo := TRSection():New(oReport, "Titulo" , {"SE1"})	
	oCGC    := TRCell():New(oTitulo,"A1_CGC"     ,"SA1")
	oCGC:SetSize(85)	
	oNome   := TRCell():New(oTitulo,"A1_NOME"    ,"SA1")
	oNome:SetSize(160)				
	oEnd    := TRCell():New(oTitulo,"A1_END"     ,"SA1")
	oEnd:SetSize(160)	
	oBairro := TRCell():New(oTitulo,"A1_BAIRRO"  ,"SA1")
	oBairro:SetSize(80)	
	oCep    := TRCell():New(oTitulo,"A1_CEP"     ,"SA1")
	oCep:SetSize(50)		
	oMun    := TRCell():New(oTitulo,"A1_MUN"     ,"SA1")
	oMun:SetSize(60)		
	oEst    := TRCell():New(oTitulo,"A1_EST"     ,"SA1")
	oEst:SetSize(30)		
	oTel    := TRCell():New(oTitulo,"A1_TEL"     ,"SA1")
	oTel:SetSize(45)	
	oEmiss  := TRCell():New(oTitulo,"E1_EMISSAO" ,"SE1")
	oEmiss:SetSize(45)		
	oVenc   := TRCell():New(oTitulo,"E1_VENCTO"  ,"SE1")
	oVenc:SetSize(45)		
	oNum    := TRCell():New(oTitulo,"E1_NUM"     ,"SE1")
	oNum:SetSize(45)		
	oValor  := TRCell():New(oTitulo,"E1_VALOR"   ,"SE1","Tit Vencidos Valor Atual")
	oValor:SetSize(30)		
	oSit    := TRCell():New(oTitulo,"E1_SITUACA" ,"SE1")
	oSit:SetSize(30)
	oEmail  := TRCell():New(oTitulo,"A1_EMAIL"   ,"SA1")
	oEmail:SetSize(120)		
	oXSPC   := TRCell():New(oTitulo,"E1_XSPC"    ,"SE1")
	oXSPC:SetSize(30)	
	//oXDR    := TRCell():New(oTitulo,"E1_XDR"     ,"SE1")
	//oXDR:SetSize(30)
	
Return(oReport)

/*/{Protheus.doc} ReportPrint
(long_description)
@author j2a.luizjunior
@since 05/10/2016
@version 1.0
@param oReport, objeto, (Descrição do parâmetro)
@return ${return}, ${return_description}
@example
(examples)
@see (links_or_references)
/*/

Static Function ReportPrint(oReport)

	Local cAlias     := GetNextAlias()
	Local cSQL       := ""
	Local cAlSec5    := GetNextAlias() 
	Local oSec1      := oReport:Section(1)
	Local cE1_NUMBOR := ""
	
	
	If Type("_cNumDR") != "U"
		If !Empty(_cNumDR) 
			cE1_NUMBOR := _cNumDR
		EndIf
	Else
		cE1_NUMBOR := MV_PAR01
	EndIf
	
	BeginSql Alias cAlias
		
		SELECT E1_NUM,
		       E1_VENCTO,
		       E1_EMISSAO,
		       A1_END,
		       A1_BAIRRO,
		       A1_CEP,
		       A1_MUN,
		       A1_EST,
		       A1_TEL,
		       A1_CGC,
		       A1_NOME,
		       A1_EMAIL,
		       E1_NUM,
		       E1_VALOR,
		       E1_SITUACA,
		       (CASE WHEN E1_XSPC = ' ' THEN 'NÃO' WHEN E1_XSPC = 'N' THEN 'NÃO' ELSE 'SIM' END) E1_XSPC,
		       E1_XDR		       
		FROM   %Table:SE1% E1
		INNER  JOIN %Table:SA1% A1
		ON     A1.A1_COD    = E1.E1_CLIENTE
		AND    A1.A1_LOJA   = E1.E1_LOJA  
		WHERE  E1.E1_NUMBOR = %Exp:cE1_NUMBOR%
		AND    E1.E1_FILIAL = %xFilial:SE1%
		AND    E1.%NOTDEL%		
		AND    A1.%NOTDEL%
		ORDER BY E1_FILIAL,E1_CLIENTE,E1_LOJA 
	
	EndSql

	While !(cAlias)->(Eof())
	
		oSec1:Init()
		oSec1:Cell("A1_CGC"):SetValue(AllTrim((cAlias)->A1_CGC))
		oSec1:Cell("A1_NOME"):SetValue(AllTrim((cAlias)->A1_NOME))
		oSec1:Cell("A1_END"):SetValue(AllTrim((cAlias)->A1_END))
		oSec1:Cell("A1_BAIRRO"):SetValue(AllTrim((cAlias)->A1_BAIRRO))
		oSec1:Cell("A1_CEP"):SetValue(AllTrim((cAlias)->A1_CEP))
		oSec1:Cell("A1_MUN"):SetValue(AllTrim((cAlias)->A1_MUN))		
		oSec1:Cell("A1_EST"):SetValue(AllTrim((cAlias)->A1_EST))
		oSec1:Cell("A1_TEL"):SetValue(AllTrim((cAlias)->A1_TEL))			
		oSec1:Cell("A1_EMAIL"):SetValue(AllTrim((cAlias)->A1_EMAIL))		
		oSec1:Cell("E1_EMISSAO"):SetValue(DToC(SToD((cAlias)->E1_EMISSAO)))		
		oSec1:Cell("E1_VENCTO"):SetValue(DToC(SToD((cAlias)->E1_VENCTO)))		
		oSec1:Cell("E1_NUM"):SetValue(AllTrim((cAlias)->E1_NUM))
		oSec1:Cell("E1_VALOR"):SetValue((cAlias)->E1_VALOR)		
		oSec1:Cell("E1_SITUACA"):SetValue((cAlias)->E1_SITUACA)
		oSec1:Cell("E1_XSPC"):SetValue((cAlias)->E1_XSPC)		
		//oSec1:Cell("E1_XDR"):SetValue((cAlias)->E1_XDR)	
		
		oSec1:Printline()
		
		If oReport:Cancel()
			Return(Nil)
		EndIf
		
		(cAlias)->(DbSkip())
	EndDo
	
	(cAlias)->(DbCloseArea())
	
	oSec1:Finish()

Return

/*/{Protheus.doc} CriaSx1
(long_description)
@author j2a.luizjunior
@since 05/10/2016
@version 1.0
@param oReport, objeto, (Descrição do parâmetro)
@return ${return}, ${return_description}
@example
(examples)
@see (links_or_references)
/*/

Static Function CriaSx1

	Local aHelp := {}
	
	//-> Texto do help em português,inglês, espanhol
	AAdd(aHelp, {{"Bordero"  }  ,{""},{""}})
	
	u_SFPUTSX1(cPerg,"01","Bordero" ,"","","mv_ch1" ,"C",06,00,00,"G","","" ,"","","mv_par01",""   ,"","","",""   ,"","","","","","","","","","","" ,aHelp[1,1] ,aHelp[1,2] ,aHelp[1,3] ,"")

Return

/*/{Protheus.doc} ChkDados
(long_description)
@author j2a.luizjunior
@since 25/10/2016
@version 1.0
@example
(examples)
@see (links_or_references)
/*/

Static Function ChkDados(pBor)

	Local cBordero := pBor
	Local cAlias   := GetNextAlias()
	Local lRet     := .T.
	Local cSql     := ""
	
	If Select(cAlias) > 0
		(cAlias)->(DbCloseArea())
	EndIf
	
	If !Empty(cBordero)
		cSql += " AND E1_NUMBOR = '" + cBordero + "' "
	Else		 
		lRet := .F.
		Return lRet	
	EndIf
	
	cSql := "%" + cSql + "%"
	
	BeginSql Alias cAlias 
	
		SELECT E1_NUM,
			   E1_SITUACA	
		FROM   %TABLE:SE1%
		WHERE  E1_FILIAL = %xFilial:SE1%
		AND    %NOTDEL% 		
		%Exp:cSql%
		ORDER BY E1_NUM 
		
	EndSql
	
	If Empty((cAlias)->E1_NUM)
		lRet     := .F.
	Else
		_cSitTit := (cAlias)->E1_SITUACA 	
	EndIf

Return lRet