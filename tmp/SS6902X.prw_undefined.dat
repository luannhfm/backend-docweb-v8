#INCLUDE "PROTHEUS.CH"

/*

Lembrete:
- adicionar button em ações relacionadas do contrato para VISUALIZAR todos os documentos digitalizados
- substituir o botão Conhecimento por SS6902X em SIGAGCT > Atualizações > Contratos > Documentos
- Testar testar testar

*/

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³SS6902X   ³ Autor ³ Jonas Nascimento      ³ Data ³07/08/2015³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Aplicacao ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Analista Resp.³  Data  ³ Bops ³ Manutencao Efetuada                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³              ³  /  /  ³      ³                                        ³±±
±±³              ³  /  /  ³      ³                                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/

User Function SS6902X(_cVal1, _cVal2, _cVal3, _cVal4, _nVal5) //Filial + Alias + Nr CNK + Nr Contrato + Comportamento janela
	
	// Variaveis Locais da Funcao
	Local _cEdit1	 := _cVal1
	Local _cEdit2	 := _cVal4
	Local _oEdit1
	Local _oEdit2
	
	//Variável para validação. Por Walmir Junior em 07/11/2016.
	Local _nRec	:= ""
	
	// Variaveis Private da Funcao
	Private _oDlg				// Dialog Principal
	
	/* Define comportamento da janela [_cModView]
		_cModView Descrição funcionamento				 Como invocar (chamar)
		--------- -------------------------------------- -------------------------------------------------------------- 
		1		  Edição por Documento (Incluir/Excluir) U_SS6902X('02MT0012' , 'CNK', '000008','000000000000021', 1)
		2		  Visualização por Documento			 U_SS6902X('02MT0012' , 'CNK', '000008','000000000000021', 2)			
		3		  Visualização por Contrato				 U_SS6902X(			  , 	 ,		   ,				 , 3)	
	*/
	Private _cModView	:= ""

	/* Armazena arquivos visualizados p/ excluir da estação após encerrar a rotina */	
	Private _aExclui	:= {}
	
	/* Variáveis de identificação para buscar na Base de Conhecimento CUSTOMIZADA */
	Private _cFilial	:= _cVal1
	Private _cAlias	:= _cVal2
	Private _Identid	:= _cVal3
	Private _cNrCN9	:= _cVal4
	
	/*Obtem parâmetro de usuários que podem excluir e inserir anexo diretamente. Por Walmir Junior em 10/11/2016.*/
	Private _cUsrAx	:=	GetMv('MV_XUSRAX', ,'')
	
	If !IsInCallStack("U_SS6901X") .And. CN9->CN9_SITUAC $ "05" .And. Empty(_cUsrAx)
		Aviso("[SS6902X] Atenção!","Para excluir ou inserir novos documentos configure o parâmetro 'MV_XUSRAX'.",{"Ok"})
	EndIf
	
	If cValToChar(_nVal5) == "3"
		_cModView	:= cValToChar(_nVal5)
		_cFilial	:= xFilial("CN9")
		_cAlias		:= "CN9"
		_Identid	:= ""
		_cNrCN9	:= CN9->CN9_NUMERO
		_cEdit1	:= xFilial("CN9")
		_cEdit2	:= CN9->CN9_NUMERO
	Else
		_cModView	:= cValToChar(_nVal5)
	EndIf
	
	DEFINE MSDIALOG _oDlg TITLE "Contrato - Documentos digitalizados" FROM C(259),C(191) TO C(690),C(977) PIXEL	//746
		@ C(004),C(005) Say 	"Filial" 			Size C(012),C(008)				COLOR CLR_BLACK PIXEL OF _oDlg
		@ C(004),C(076) Say 	"Nº Contrato" 		Size C(029),C(008)				COLOR CLR_BLACK PIXEL OF _oDlg
		@ C(012),C(005) MsGet 	_oEdit1 Var _cEdit1	Size C(060),C(009)	READONLY	COLOR CLR_BLACK PIXEL OF _oDlg
		@ C(012),C(075) MsGet 	_oEdit2 Var _cEdit2	Size C(060),C(009)	READONLY	COLOR CLR_BLACK PIXEL OF _oDlg
		@ C(200),C(243) Button	"Visualizar" 		Size C(037),C(012)								PIXEL OF _oDlg Action(fViewZG0())
		@ C(200),C(349) Button	"Cancelar" 			Size C(037),C(012)								PIXEL OF _oDlg Action(fExiZG0())
		
		//Alterado para gerar o GetDados antes de adicionar os botões. Por Walmir Junior em 07/11/2016.
		_nRec	:=	fGetDados1()	// Chamadas GetDados do Sistema
		
		If _cModView == "1"
			@ C(011),C(334) Button 	"Adicionar Arquivo" Size C(052),C(012) PIXEL OF _oDlg Action(fAddFile())
			@ C(200),C(296) Button	"Confirmar" 		Size C(037),C(012) PIXEL OF _oDlg Action(fGrvZG0())
		//Permitir inserir novo arquivo e salvar caso não tenham registros no getDados.
		//Por Walmir Junior em 07/11/2016.
		ElseIf !IsInCallStack("U_SS6901X") .And. CN9->CN9_SITUAC $ "05" .And. RetCodUsr() $ _cUsrAx
			@ C(011),C(334) Button 	"Adicionar Arquivo" Size C(052),C(012) PIXEL OF _oDlg Action(fAddFile())
			@ C(200),C(296) Button	"Confirmar" 		Size C(037),C(012) PIXEL OF _oDlg Action(fGrvZG0())
		//ElseIf !IsInCallStack("U_SS6901X") .And. CN9->CN9_SITUAC $ "05"
			@ C(200),C(190) Button	"Excluir" 			Size C(037),C(012) PIXEL OF _oDlg Action(fDelAnx())
		EndIf
	ACTIVATE MSDIALOG _oDlg CENTERED

Return(.T.)


/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³fGetDados1³ Autor ³ Jonas Nascimento      ³ Data ³07/08/2015³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Cria o grid                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Aplicacao ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Analista Resp.³  Data  ³ Bops ³ Manutencao Efetuada                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³              ³  /  /  ³      ³                                        ³±±
±±³              ³  /  /  ³      ³                                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
/*
Alterações realizadas:
	05/02/2016 - Franklin B. Oliveira
		-> Adicionado condição na seleção dos dados a serem visualizados
		"_cModView == 3", serão exibidos apenas os documentos ao contrato
		selecionado "CN9_NUMERO = CNK_CONTRA"
	22/04/2016 - Franklin B. Oliveira
		->Adicionado clausula distinc na seleção de dados a serem visualizados
		"_cModView == 3"
*/
Static Function fGetDados1()
	
	// Variaveis deste Form                                                                                                         
	Local nX			:= 0

	// Vetor responsavel pela montagem da aHeader
	Local aCpoGDa       := {"ZG0_SEQOBJ","ZG0_DESCOB","ZG0_OBJETO","RECNO"}
	Local aHead        	:= {}               // Array a ser tratado internamente na MsNewGetDados como aHeader
	Local aColsEx	 := {}
	
	// Vetor com os campos que poderao ser alterados                                                                                
	Local aAlter       	:= {"ZG0_SEQOBJ","ZG0_DESCOB","ZG0_OBJETO","RECNO"}
	Local nSuperior    	:= C(030)           // Distancia entre a MsNewGetDados e o extremidade superior do objeto que a contem
	Local nEsquerda    	:= C(004)           // Distancia entre a MsNewGetDados e o extremidade esquerda do objeto que a contem
	Local nInferior    	:= C(195)           // Distancia entre a MsNewGetDados e o extremidade inferior do objeto que a contem
	Local nDireita     	:= C(387)           // Distancia entre a MsNewGetDados e o extremidade direita  do objeto que a contem
	
	// Posicao do elemento do vetor aRotina que a MsNewGetDados usara como referencia  
	Local nOpc         	:= GD_INSERT+GD_DELETE+GD_UPDATE
	Local cLinhaOk     	:= "AllwaysTrue"    // Funcao executada para validar o contexto da linha atual do aCols
	Local cTudoOk      	:= "AllwaysTrue"    // Funcao executada para validar o contexto geral da MsNewGetDados (todo aCols)
	Local cIniCpos     	:= ""               // Nome dos campos do tipo caracter que utilizarao incremento automatico.
                                         // Este parametro deve ser no formato "+<nome do primeiro campo>+<nome do            
                                         // segundo campo>+..."                                                               
	Local nFreeze      	:= 000              // Campos estaticos na GetDados.
	Local nMax         	:= 999              // Numero maximo de linhas permitidas. Valor padrao 99
	Local cCampoOk     	:= "AllwaysTrue"    // Funcao executada na validacao do campo
	Local cSuperApagar 	:= ""               // Funcao executada quando pressionada as teclas <Ctrl>+<Delete>
	Local cApagaOk     	:= "AllwaysTrue"    // Funcao executada para validar a exclusao de uma linha do aCols

	// Objeto no qual a MsNewGetDados sera criada                                      
	Local oWnd          	:= _oDlg
	
	/* SQL */
	Local _aArea		:= GetArea()
	Local _cZG0			:= GetNextAlias()
	Local _cSQL			:= ""

	
	Local aCol         	:= {}               // Array a ser tratado internamente na MsNewGetDados como aCols
	Static oGetDados1
	
	If _cModView $ "2;3"
		nOpc := 0		//Sem acesso a GD_INSERT+GD_DELETE+GD_UPDATE
	EndIf
                                                                                                                                
	// Carrega aHead                                                                                                                
	DbSelectArea("SX3")
	SX3->(DbSetOrder(2)) // Campo
	For nX := 1 to Len(aCpoGDa)
		If SX3->(DbSeek(aCpoGDa[nX]))
			Aadd(aHead,{ AllTrim(X3Titulo()),;
				SX3->X3_CAMPO	,;
				SX3->X3_PICTURE,;
				SX3->X3_TAMANHO,;
				SX3->X3_DECIMAL,;
				SX3->X3_VALID	,;
				SX3->X3_USADO	,;
				SX3->X3_TIPO	,;
				SX3->X3_F3 		,;
				SX3->X3_CONTEXT,;
				SX3->X3_CBOX	,;
				SX3->X3_RELACAO})
		Endif
	Next nX
	
	AADD(aHead,{"Recno"			,"R_E_C_N_O_"	,"",5,0,"AllwaysTrue()"	,"","N","","","",""})

	If _cModView $ "1;2"
		DbSelectArea("ZG0")
		ZG0->(dbSetOrder(1))
		ZG0->(dbSeek(_cFilial+_cAlias+_Identid))
	Else
		 _cSQL := "SELECT DISTINCT"+CRLF
		 _cSQL += "  ZG0_SEQOBJ, ZG0_DESCOB, ZG0_OBJETO, ZG0.R_E_C_N_O_ ZG0RECNO"+CRLF
		 _cSQL += "FROM "+CRLF
		 _cSQL += "  "+RetSqlName("CN9")+" CN9 "+CRLF
		 _cSQL += "  INNER JOIN "+RetSqlName("CN1")+" CN1 ON CN1.D_E_L_E_T_ = ' ' AND CN1_FILIAL = '"+xFilial("CN1")+"' AND CN9_TPCTO = CN1_CODIGO "+CRLF
		 _cSQL += "  INNER JOIN "+RetSqlName("CNJ")+" CNJ ON CNJ.D_E_L_E_T_ = ' ' AND CNJ_FILIAL = '"+xFilial("CNJ")+"' AND CN9_TPCTO = CNJ_TPCTO "+CRLF
		 _cSQL += "  INNER JOIN "+RetSqlName("CNK")+" CNK ON CNK.D_E_L_E_T_ = ' ' AND CN9_FILIAL = CNK_FILIAL AND CN9_NUMERO = CNK_CONTRA AND CNJ_TPDOC = CNK_TPDOC "+CRLF
		 _cSQL += "  INNER JOIN "+RetSqlName("ZG0")+" ZG0 ON ZG0.D_E_L_E_T_ = ' ' AND CNK_FILIAL = ZG0_FILIAL AND ZG0_NUMERO = CNK_CODIGO AND ZG0_ENTIDA = 'CNK' "+CRLF
		 _cSQL += "WHERE "+CRLF
		 _cSQL += "  CN9.D_E_L_E_T_ = ' ' "+CRLF
		 _cSQL += "  AND CN9_NUMERO = '"+AllTrim(_cNrCN9)+"' "+CRLF
		 _cSQL += "  AND CN9_FILIAL = '"+xFilial("CN9")+"' "+CRLF
		 _cSQL += "ORDER BY ZG0_SEQOBJ "+CRLF
		 MemoWrite("C:\temp\SS6902X.txt",_cSQL)
		 dbUseArea(.T., 'TOPCONN', TcGenQry(,,ChangeQuery(_cSQL)), _cZG0, .F., .F.)
	EndIf
	
	If Eof()
		AADD(aCol , {'001', Space(TamSX3("ZG0_DESCOB")[1]), Space(TamSX3("ZG0_OBJETO")[1]), 0,.F.})
	EndIf
 
	While !Eof()
		If _cModView $ "1;2"
			AADD(aCol , {ZG0->ZG0_SEQOBJ, ZG0->ZG0_DESCOB, ZG0->ZG0_OBJETO, IIf(_cModView $ "1;2", Recno(), ZG0RECNO),.F.})
			ZG0->(dbSkip())
		Else
			AADD(aCol , {(_cZG0)->ZG0_SEQOBJ, (_cZG0)->ZG0_DESCOB, (_cZG0)->ZG0_OBJETO, IIf(_cModView $ "1;2", Recno(), ZG0RECNO),.F.})
			(_cZG0)->(dbSkip())
		EndIf
	EndDo
	If Select(_cZG0) > 0
		(_cZG0)->(dbCloseArea())		//ZG0->(dbCloseArea())
	EndIf
	RestArea(_aArea)

	oGetDados1:= MsNewGetDados():New(nSuperior,nEsquerda,nInferior,nDireita,nOpc,cLinhaOk,cTudoOk,cIniCpos,;
		aAlter,nFreeze,nMax,"U_fAltGrid",cSuperApagar,"U_DelZG0",oWnd,aHead,aCol)
	
Return aCol[oGetDados1:nAt,4]

/* Função executada quando alterar uma célula da GRID */
User Function fAltGrid()
	
	Local _aCol	:= oGetDados1:aCols
	Local nLinha	:= oGetDados1:nAt
	Local _lRet 	:= .T.

	If _aCol[nLinha,04] > 0
		Aviso(FunName()+"/"+ProcName(),"Alteração não permitida!"+CRLF+"Somente Adicionar/Excluir é permitido.",{"OK"},3)
		_lRet := .F.
	EndIf
	
Return _lRet

/* Função executado ao marcar um item do grid como excluído */
User Function DelZG0()
	Local _aCol 	:= oGetDados1:aCols
	Local nLinha 	:= oGetDados1:nAt
	Local _lRet := .F.

	If !_aCol[nLinha,05]
		If Aviso(FunName()+"/"+ProcName(),"Confirma a exclusão do arquivo?"+CRLF+CRLF+"A exclusão será irreversível após a confirmação!",{"Sim","Não"},3) == 1
			_lRet := .T.
		EndIf
	Else
		_lRet := .T.
	EndIf
	
Return _lRet

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa   ³   C()   ³ Autor   ³ Jonas Nascimento       ³ Data ³07/08/2015³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao  ³ Funcao responsavel por manter o Layout independente da       ³±±
±±³           ³ resolucao horizontal do Monitor do Usuario.                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function C(nTam)
	Local nHRes	:=	oMainWnd:nClientWidth	// Resolucao horizontal do monitor
	If nHRes == 640	// Resolucao 640x480 (soh o Ocean e o Classic aceitam 640)
		nTam *= 0.8
	ElseIf (nHRes == 798).Or.(nHRes == 800)	// Resolucao 800x600
		nTam *= 1
	Else	// Resolucao 1024x768 e acima
		nTam *= 1.28
	EndIf
                                                                                
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿                                               
	//³Tratamento para tema "Flat"³                                               
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ                                               
	If "MP8" $ oApp:cVersion
		If (Alltrim(GetTheme()) == "FLAT") .Or. SetMdiChild()
			nTam *= 0.90
		EndIf
	EndIf
Return Int(nTam)

/* Auto incremento por Objeto para ZL2_SEQOBJ */
User Function INIPXZL2()
	Local _aCol := oGetDados1:aCols
	Local _cRet := ""
	
	_cRet := StrZero(Len(_aCol) , 03)
	
	While aScan(_aCol , {|x| AllTrim(x[1]) == _cRet } ) > 0
		_cRet := Soma1(_cRet)
	EndDo
	
Return(_cRet)

/* Função para adicionar arquivos na grid */
Static Function fAddFile()
	Local _aCol 		:= oGetDados1:aCols
	Local _cArqRet 	:= ""
	Local nLinha 	:= oGetDados1:nAt
	Local _cDirIni	:= GetMv('MV_XGCTDIR', ,'C:\TEMP\')	//Diretorio origem dos arquivos digitalizados a ser adicionados e copiados p/ SRV.
	Local _cItm		:= ""
	
	If _aCol[nLinha,4] > 0 .And. IsInCallStack("U_SS6901X")
		Aviso(FunName()+"/"+ProcName(),"Alteração não permitida!",{"OK"},3)
	Else
		
		//Tratativa para inserir mais de um registro no caso de abrir direto pelo Conhecimento. 
		//Por Walmir Junior em 09/11/2016.
		If !IsInCallStack("U_SS6901X")
			nLinha := Len(_aCol)
			If _aCol[nLinha,4] > 0
				_cItm := StrZero(val(_aCol[nLinha,1])+1,3)
				AADD(_aCol , {_cItm, Space(TamSX3("ZG0_DESCOB")[1]), Space(TamSX3("ZG0_OBJETO")[1]), 0,.F.})
				nLinha := val(_cItm)
				oGetDados1:nAt := nLinha
			EndIf
		EndIf
	
		_cArqRet := cGetFile('Arquivo |*.*','Adicionar arquivos digitalizados',1,_cDirIni,.F.,GETF_LOCALHARD)
		If Len(Space(TamSX3("ZG0_OBJETO")[1])) < Len(_cArqRet)
			Aviso(FunName()+"/"+ProcName(),"Nome do arquivo muito grande!"+CRLF+"Tamanho máximo de 600 caracteres."+CRLF+CRLF+"Solução: Redefina o nome do arquivo e adicione novamente.",{"OK"},3)
		Else
			If .Not. Empty(_cArqRet)
				_aTexto := StrTokArr(_cArqRet, '\',.T.)
				If aScan(_aCol , {|x| AllTrim(x[3]) == AllTrim(_aTexto[Len(_aTexto)]) } ) > 0
					Aviso(FunName()+"/"+ProcName(),"Arquivo [" + AllTrim(_aTexto[Len(_aTexto)]) + "] já existe."+CRLF+"Não é permitido adicionar arquivos com o mesmo nome e extensão.",{"OK"},3)
				Else
					_aCol[nLinha,3] := _cArqRet
					//Adicionada tratativa para preencher descrição. Por Walmir Junior em 07/11/2016.
					If Empty(_aCol[nLinha,2]) .And. !IsInCallStack("U_SS6901X")
						_aCol[nLinha,2] := fGetDsc()
					EndIf
				EndIf
			EndIf
		EndIf
	EndIf
Return

/* Função para realizar a inclusão na ZG0 */
Static Function fGrvZG0()
	Local _aCols	:= oGetDados1:aCols
	Local _nCount	:= 0
	Local _lRet		:= .T.
	Local _cDirFim	:= GetMv('MV_XGCTGED', ,'\system\GCT\GED\')	//Diretorio final dos arquivos digitalizados, já armazenados no SRV
	Local _cMsgRet	:= "Arquivos incluidos com sucesso"
	
	//Variaveis criadas para o controle e inserção direto no Conhecimento. Por Walmir Junior em 07/11/2016. 
	Local _lCont	:= .T.
	Local _lAli		:= .F.
	
	If Len(_aCols) > 0
		//Cria diretório especifico c/ nº do contrato
		MakeDir(_cDirFim+_cNrCN9)
		
		
		//Implementação. Por Walmir Junior em 07/11/2016.
		If Empty(_Identid) .And. CN9->CN9_SITUAC $ "05" .And. RetCodUsr() $ _cUsrAx
			_lCont := .F.
			If !Empty(_aCols[Len(_aCols),3]) .And. CN170Manut("CNK",,3,,CN9->CN9_NUMERO,"001",@_Identid)
				dbSelectArea("CNK")
				dbSetOrder(1)
				dbSeek(xFilial("CNK")+_Identid)
				_lCont	:= .T.
				_lAli	:= .T.
			EndIf
		EndIf
		
		If _lCont
			dbSelectArea('ZG0')
			For nX:=1 to len(_aCols)
				If _aCols[nX,4] > 0
					ZG0->(DbGoTo(_aCols[nX,4]))
					
					If _aCols[nX,05]
						RecLock('ZG0',.F.)
						ZG0->(DbDelete())
						ZG0->(MsUnLock())
						
						//Exclui o arquivo físico no SRV - Verifica se existe, então apaga
						If File(_cDirFim+_cNrCN9+"\"+AllTrim(_aCols[nX,3]))
							FErase(_cDirFim+_cNrCN9+"\"+AllTrim(_aCols[nX,3]))
						Endif
					/*Else
						_nCount++
						RecLock('ZG0',.F.)
						ZG0->ZG0_FILIAL	:= _cFilial
						ZG0->ZG0_ENTIDA	:= _cAlias
						ZG0->ZG0_NUMERO	:= _Identid
						ZG0->ZG0_SEQOBJ	:= _aCols[nX,1]
						ZG0->ZG0_DESCOB	:= _aCols[nX,2]
						ZG0->ZG0_OBJETO	:= _aCols[nX,3]
		
						ZG0->(MsUnLock())*/
					EndIf
				ElseIf !_aCols[nX,5]
					//Copiar arquivos para SRV - CPYT2S()			CPYS2T ( < cOrigem > , < cDestino > , [ lCompacta ] )
					If Len(AllTrim(_aCols[nX,3])) > 0
						If CPYT2S(_aCols[nX,3] ,_cDirFim+_cNrCN9+"\" , .T. )
							RecLock('ZG0',.T.)
							ZG0->ZG0_FILIAL	:= _cFilial
							ZG0->ZG0_ENTIDA	:= Iif(_lAli, "CNK",_cAlias)
							ZG0->ZG0_NUMERO	:= _Identid
							ZG0->ZG0_SEQOBJ	:= _aCols[nX,1]
							ZG0->ZG0_DESCOB	:= _aCols[nX,2]
							_aTexto			:= StrTokArr(_aCols[nX,3], '\',.T.)
							ZG0->ZG0_OBJETO	:= AllTrim(_aTexto[Len(_aTexto)])
							_aCols[nX,3]	:= AllTrim(_aTexto[Len(_aTexto)])
					
							ZG0->(MsUnLock())
							
							_aCols[nX,4]	:= Recno()
							_nCount++
						Else
							_cMsgRet := "Falha ao copiar o arquivo;"+CRLF+(AllTrim(_aCols[nX,3]))
							If _nCount > 0
								_cMsgRet := _cMsgRet+CRLF+CRLF+"Foram adicionados ["+cValToChar(_nCount)+"] arquivos."
							EndIf
							_lRet := .F.
						EndIf
					Else
						_cMsgRet := "Item ["+AllTrim(_aCols[nX,1])+"], adicione um arquivo para gravar!"
						_lRet := .F.
					EndIf
				EndIf
			Next
			ZG0->(dbCloseArea())
			
			If _lRet .AND. _nCount > 0
				_cMsgRet := _cMsgRet+CRLF+CRLF+"Foram adicionados ["+cValToChar(_nCount)+"] arquivos."
			EndIf
		Else
			_oDlg:End()
		EndIf
	Else
		_cMsgRet := "Adicione arquivos para Confirmar a gravação!"
		_lRet := .F.
	EndIf
	
	//Tratativa para não inclusão. Por Walmir Junior em 09/11/2016.
	If !_lCont
		_cMsgRet := "Inclusão não realizada."
	EndIf
	
	Aviso(FunName()+"/"+ProcName(),_cMsgRet,{"OK"},3)
		
	If _lRet
		_oDlg:End()
		fDelArq()	//Função p/ excluir arquivos abertos
	EndIf

Return

/* Função exibir arquivo na estação do usuário */
Static Function fViewZG0()
	Local _aCol 		:= oGetDados1:aCols
	Local nLinha 	:= oGetDados1:nAt
	Local _cDirSRV	:= GetMv('MV_XGCTGED', ,'\system\GCT\GED\')
	Local _cDirEst	:= GetTempPath()	//Diretório temporário para armazenar arq copiado do srv e gerar visualização
	Local _cArqSRV	:= ""
	Local _cArqEst	:= ""
	Local _lRet		:= .T.
	Local _cMsgRet	:= ""
	Local _nRet		:= 0
	
	_cDirSRV := _cDirSRV + _cNrCN9 + "\"				// Url GCT + Url especifica do contrato
	_cArqSRV := _cDirSRV+(AllTrim(_aCol[nLinha,3]))	// Url + nome do arquivo no SRV
	_cArqEst := _cDirEst+(AllTrim(_aCol[nLinha,3]))	// Url + nome do arquivo na Estação
	
	//Verificar se o arquivo existe no SRV
	If File(_cArqSRV)						 //ALERT(File("\system\GCT\GED\fencprov_u_fencprov.txt"))
		If CPYS2T(_cArqSRV, _cDirEst, .T.)		// CpyS2T("\system\DOT\CRM\"+"NRM_FF_007_Proposta_de_comercializacao.dot", "K:\util\", .T.)
			_nRet := ShellExecute("Open", AllTrim(_cArqEst), "", _cDirEst, 1)
			
			If _nRet <= 32	//Verifica se ñ for possível abrir o arquivo
				If _nRet == 31
					_cMsgRet := "Nao existe aplicativo associado a este tipo de arquivo!"
				EndIf
				_lRet := .F.
				_cMsgRet := "Nao foi possivel abrir o objeto [" + AllTrim(_aCol[nLinha,3]) + "]!" +CRLF+ _cMsgRet
			EndIf
			
			aadd( _aExclui, {_cArqEst})
		Else
			_lRet := .F.
			_cMsgRet := "Falha ao copiar o arquivo ["+AllTrim(_aCol[nLinha,3])+"] do Servidor para estação."+CRLF+"Consulte o administrador de sistemas."
		EndIf
	Else
		If _aCol[nLinha,4] > 0
			_cMsgRet := "Arquivo ["+AllTrim(_aCol[nLinha,3])+"] não localizado no Servidor."+CRLF+"Consulte o administrador de sistemas"
		Else
			_cMsgRet := "Somente arquivos já gravados podem ser visualizados!"
		EndIf
		_lRet := .F.
	Endif
	
	
	If !_lRet	//Exibe msg de erro
		Aviso(FunName()+"/"+ProcName(),_cMsgRet,{"OK"},3)
	EndIf
		
Return

/* Função fechar a janela */
Static Function fExiZG0()

	fDelArq()	//Função p/ excluir arquivos abertos
	_oDlg:End()
	
Return

/* Função: excluirá todos os arquivos abertos/visualizados quando encerrar a aplicaçao. */
Static Function fDelArq()

	If Len(_aExclui) > 0
		For _nX := 1 to Len(_aExclui)
			FErase(AllTrim(_aExclui[_nX,1]))
		Next
	EndIf
	
Return

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³fGetDsc	³ Autor ³ WALMIR JUNIOR         ³ Data ³07/11/2016³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Solicita a descrição do anexo				              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fGetDsc()
Local _cRet := Space(50)
Local _oDlDsc 
Local _oEdtDsc

DEFINE MSDIALOG _oDlDsc TITLE "Descrição do Anexo" FROM C(259),C(200) TO C(350),C(400) PIXEL
	@ C(004),C(005) Say 	"Descrição" 		Size C(022),C(008)	COLOR CLR_BLACK PIXEL OF _oDlDsc 
	@ C(012),C(005) MsGet 	_oEdtDsc Var _cRet	Size C(080),C(009)	COLOR CLR_BLACK PIXEL OF _oDlDsc  
	@ C(028),C(005) Button	"Confirmar" 		Size C(037),C(012) PIXEL OF _oDlDsc Action(Iif(Empty(_cRet),Alert("Informe a descrição do anexo."),_oDlDsc:End()))
ACTIVATE MSDIALOG _oDlDsc CENTERED

Return _cRet

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³fDelAnx	³ Autor ³ WALMIR JUNIOR         ³ Data ³07/11/2016³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Exclusão de anexo do contrato				              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fDelAnx()
Local _aAlZG0 := ZG0->(GetArea())
Local _aAlCNK := CNK->(GetArea())
If Aviso("Atenção!", "Você está prestes a deletar o anexo selecionado. "+;
						"Deseja efetuar a operação?", {"Sim","Não"}) == 1
	dbSelectArea("ZG0") 
	//Posiciona aliás conforme posicionamento da grade.
	ZG0->(dbGoTo(oGetDados1:aCols[oGetDados1:nAt,4]))
	dbSelectArea("CNK")    
	CNK->(dbSetOrder(1)) 
	//Localiza CNK.
	If CNK->(DbSeek(FwxFilial("CNK")+ZG0->ZG0_NUMERO))
		//Deleta Documento do Contrato.
		CNK->(RecLock("CNK",.f.))
		CNK->( dbDelete())
		CNK->(MsUnLock())
		CNK->(dbCommit())
		//Deleta Anexo do Documento.
		ZG0->(RecLock("ZG0",.f.))
		ZG0->( dbDelete())
		ZG0->(MsUnLock())
		ZG0->(dbCommit())
	EndIf
	
	RestArea(_aAlZG0)
	RestArea(_aAlCNK)
	//Atualiza a tela.
	fRefresh()
EndIf

Return Nil

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³fRefresh	³ Autor ³ WALMIR JUNIOR         ³ Data ³07/11/2016³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Atualiza os botões da tela.  				              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function fRefresh()
Local _nRec := "" 

_nRec	:=	fGetDados1()	// Chamadas GetDados do Sistema

If _cModView == "1"
	@ C(011),C(334) Button 	"Adicionar Arquivo" Size C(052),C(012) PIXEL OF _oDlg Action(fAddFile())
	@ C(200),C(296) Button	"Confirmar" 		Size C(037),C(012) PIXEL OF _oDlg Action(fGrvZG0())
ElseIf !IsInCallStack("U_SS6901X") .And. CN9->CN9_SITUAC $ "05" .And. RetCodUsr() $ _cUsrAx
	@ C(011),C(334) Button 	"Adicionar Arquivo" Size C(052),C(012) PIXEL OF _oDlg Action(fAddFile())
	@ C(200),C(296) Button	"Confirmar" 		Size C(037),C(012) PIXEL OF _oDlg Action(fGrvZG0())
//ElseIf !IsInCallStack("U_SS6901X") .And. CN9->CN9_SITUAC $ "05"
	@ C(200),C(190) Button	"Excluir" 			Size C(037),C(012) PIXEL OF _oDlg Action(fDelAnx())
EndIf

Return