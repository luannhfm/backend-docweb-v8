#INCLUDE "PROTHEUS.CH"
#INCLUDE "RWMAKE.CH"
#INCLUDE "FONT.CH"
#INCLUDE "COLORS.CH"

/*/{Protheus.doc} SF0504X
//TODO Descrição auto-gerada.
@author author
@since 04/07/2017
@version version
@example
(examples)
@see (links_or_references)
/*/

User Function SF0504X()

	Local cEstat := U_SFGN001A(ProcName(0), "SF0504X")
	Local   aCoors     := FWGetDialogSize(oMainWnd)
	Local   cRotina    := "Impressão de Carteirinha" 
	Local   oTFont     := TFont():New('Courier new',,16,.T.)
	Local   cNome      := ZH1->ZH1_NOME
	Local   cEndereco  := ZH1->ZH1_END
	Local   cCidade    := AllTrim(ZH1->ZH1_MUN) + " - " +	 AllTrim(ZH1->ZH1_EST)
	Local   cBairro    := ZH1->ZH1_BAIRRO
	Local   cCep       := ZH1->ZH1_CEP
	Local   cFone      := ZH1->ZH1_FONRES 
	Local   cEmail     := ZH1->ZH1_EMAIL
	
	Local   cVencto    := ZH1->ZH1_VALID
	Local   oWindow    := Nil
	Local   aHeader    := {}	
	Local   aCDep      := {}	
	 
	Local   oFontN     := TFont():New( "Arial",,-11,,.T. )
	Local   oFont      := TFont():New( "Arial",,-11,,.F. )
	Private cFilAtu    := ZH1->ZH1_FILORI 
	Private cContrato  := AllTrim(ZH1->ZH1_NUMERO)
	Private cRevisa    := ZH1->ZH1_REVISA
	Private cMatricula := ZH1->ZH1_MATRIC
	Private oBmp       := Nil
	Private oBDEP      := Nil
	Private aCols      := {}
	Private oTCH       := LoadBitmap(GetResources(), "LBOK")
	Private oFCH       := LoadBitmap(GetResources(), "LBNO")
	Private aBotao     := {}
	
	Private cCat      ,cDCart   ,cDepTit,cEmpresa,cDescEmp,cCBarras,cValid
	Private oCategoria,oDescCart,oDepTit,oEmpresa,oDescEmp,oBarras ,oImg,oValid
	
	cEmpresa := ""
	cDescEmp := ""
	
	aAdd(aBotao,{"Imagem"  ,{|| AddImg()    },"Add Img" })
	aAdd(aBotao,{"Carteira",{|| ImpCart()   },"Imp Cart"})
	 
	oWindow         := MsDialog():New(aCoors[1], aCoors[2], 617, 1217, cRotina,,, .F.,,,,,, .T.,,, .T.)
	oWindow:bInit   := {||EnchoiceBar(oWindow,{|| oWindow:End()},{|| oWindow:End()},.F.,aBotao)}	
	
	oFWLayer := FWLayer():New() 
	oFWLayer:Init(oWindow)

	oFWLayer:AddLine("Lin01", 21, .F.)                      
	oFWLayer:addCollumn("Col01", 100, .T., "Lin01")
	oFWLayer:addWindow("Col01", "Win01", "Informações do Titular",100,.T.,.F.,,"Lin01")
	
	oFWLayer:AddLine("Lin02", 75, .F.)                      
	oFWLayer:addCollumn("Col01", 100, .T., "Lin02")
	oFWLayer:addWindow("Col01", "Win01",  "Informações do Titulo",100,.T.,.F.,,"Lin02")

	oPnl1           := oFWLayer:GetColPanel("Col01","Lin01")
	oPan1           := TPanel():New(020,005,"",oPnl1,oTFont,.T.,,CLR_YELLOW,CLR_WHITE,602,40)		
	oGrp1           := TGroup():New(000,000,40,602,"",oPan1,CLR_BLACK,CLR_WHITE,.T.,.F.)
	
	oPnl2           := oFWLayer:GetColPanel("Col01","Lin02")
	oPan2           := TPanel():New(020,005,"",oPnl2,oTFont,.T.,,CLR_YELLOW,CLR_WHITE,602,203)
	oGrp2           := TGroup():New(000,000,203,602,"",oPan2,CLR_BLACK,CLR_WHITE,.T.,.F.)
	
	oTitulo         := TSay():New(003,010,{|| "Titular:"                                      },oPan1,,,.F.,.F.,.F.,.T.,CLR_BLUE,CLR_WHITE,120,008)
	oGet1           := TGet():New(012,010,{|u| If(PCount() > 0 , cNome     := u , cNome      )},oPan1,120,008,'',,CLR_BLACK,CLR_WHITE,,,,.T.,"",,,.F.,.F.,,.F.,.F.,"","",,)	
	oGet1:lReadOnly := .T.
	
	oContrato       := TSay():New(003,140,{|| "Contrato:"                                     },oPan1,,,.F.,.F.,.F.,.T.,CLR_BLUE,CLR_WHITE,120,008)
	oGet2           := TGet():New(012,140,{|u| If(PCount() > 0 , cContrato := u , cContrato  )},oPan1,0120,008,'',,CLR_BLACK,CLR_WHITE,,,,.T.,"",,,.F.,.F.,,.F.,.F.,"","",,)	
	oGet2:lReadOnly := .T.

	oVencimento     := TSay():New(003,270,{|| "Vencimento:"                                   },oPan1,,,.F.,.F.,.F.,.T.,CLR_BLUE,CLR_WHITE,120,008)
	oGet3           := TGet():New(012,270,{|u| If(PCount() > 0 , cVencto   := u , cVencto    )},oPan1,060,008,'',,CLR_BLACK,CLR_WHITE,,,,.T.,"",,,.F.,.F.,,.F.,.F.,"","",,)	
	oGet3:lReadOnly := .T.
	
	/*
	oBairro         := TSay():New(003,400,{|| "Bairro:"                                       },oPan1,,,.F.,.F.,.F.,.T.,CLR_BLUE,CLR_WHITE,120,008)
	oGet4           := TGet():New(012,400,{|u| If(PCount() > 0 , cBairro   := u , cBairro    )},oPan1,060,008,'',,CLR_BLACK,CLR_WHITE,,,,.T.,"",,,.F.,.F.,,.F.,.F.,"","",,)	
	oGet4:lReadOnly := .T.

	oFone           := TSay():New(003,530,{||  "Fone:"                                        },oPan1,,,.F.,.F.,.F.,.T.,CLR_BLUE,CLR_WHITE,120,008)
	oGet5           := TGet():New(012,530,{|u| If(PCount() > 0 , cFone     := u , cFone      )},oPan1,060,008,'',,CLR_BLACK,CLR_WHITE,,,,.T.,"",,,.F.,.F.,,.F.,.F.,"","",,)	
	oGet5:lReadOnly := .T.
		
	oEndereço       := TSay():New(024,010,{|| "Endereço:"                                     },oPan1,,,.F.,.F.,.F.,.T.,CLR_BLUE,CLR_WHITE,120,008)
	oGet6           := TGet():New(033,010,{|u| If(PCount() > 0 , cEndereco := u , cEndereco  )},oPan1,0120,008,'',,CLR_BLACK,CLR_WHITE,,,,.T.,"",,,.F.,.F.,,.F.,.F.,"","",,)	
	oGet6:lReadOnly := .T.
	
	oCidade         := TSay():New(024,140,{|| "Cidade:"                                       },oPan1,,,.F.,.F.,.F.,.T.,CLR_BLUE,CLR_WHITE,120,008)
	oGet7           := TGet():New(033,140,{|u| If(PCount() > 0 , cCidade   := u , cCidade    )},oPan1,060,008,'',,CLR_BLACK,CLR_WHITE,,,,.T.,"",,,.F.,.F.,,.F.,.F.,"","",,)	
	oGet7:lReadOnly := .T.

	oCep            := TSay():New(024,270,{|| "Cep:"                                          },oPan1,,,.F.,.F.,.F.,.T.,CLR_BLUE,CLR_WHITE,120,008)
	oGet8           := TGet():New(033,270,{|u| If(PCount() > 0 , cCep      := u , cCep       )},oPan1,060,008,'',,CLR_BLACK,CLR_WHITE,,,,.T.,"",,,.F.,.F.,,.F.,.F.,"","",,)	
	oGet8:lReadOnly := .T.

	oEmail          := TSay():New(024,400,{|| "Email:"                                        },oPan1,,,.F.,.F.,.F.,.T.,CLR_BLUE,CLR_WHITE,120,008)
	oGet9           := TGet():New(033,400,{|u| If(PCount() > 0 , cEmail    := u , cEmail     )},oPan1,110,008,'',,CLR_BLACK,CLR_WHITE,,,,.T.,"",,,.F.,.F.,,.F.,.F.,"","",,)	
	oGet9:lReadOnly := .T.
	*/
	
	oCart              := TBITMAP():CREATE(oPan2)
	oCart:cName        := "oCart"
	oCart:cCaption     := "oCart:"
	oCart:nLeft        := 14
	oCart:nTop         := 14
	oCart:nWidth       := 240
	oCart:nHeight      := 150
	oCart:lShowHint    := .F.
	oCart:lReadOnly    := .F.
	oCart:lVisibleControl := .T.
	oCart:lStretch     := .F.
	oCart:lAutoSize    := .F.
	
	If cFilAnt == "02MT0012"
		oCart:cBmpFile := "\PARQUE\Cart_parque.bmp"
	Else
		oCart:cBmpFile := "\PARQUE\img\Carteirinha_cac_01.bmp"	
	EndIf
	
	oImg               := TBITMAP():CREATE(oPan2)
	oImg:cName         := "oImg"
	oImg:cCaption      := "oImg:"
	oImg:nLeft         := 180
	oImg:nTop          := 60
	oImg:nWidth        := 80
	oImg:nHeight       := 90
	oImg:lShowHint     := .F.
	oImg:lReadOnly     := .F.
	oImg:lVisibleControl := .T.
	oImg:lStretch      := .F.
	oImg:lAutoSize     := .F.
	
	If cFilAnt == "02MT0012"
		oCart:cBmpFile :=  "\PARQUE\Cart_parque.bmp"
		oCategoria     := TSay():New(013,070,{|u| If(PCount() > 0 , cCat     := u  , cCat     ) },oPan2,,oFontN,.F.,.F.,.F.,.T.,CLR_BLACK,CLR_WHITE,120,008)
		oDescCart      := TSay():New(024,010,{|u| If(PCount() > 0 , cDCart   := u  , cDCart   ) },oPan2,,oFont ,.F.,.F.,.F.,.T.,CLR_BLACK,CLR_WHITE,120,008)
		oDepTit        := TSay():New(029,010,{|u| If(PCount() > 0 , cDepTit  := u  , cDepTit  ) },oPan2,,oFontN,.F.,.F.,.F.,.T.,CLR_BLACK,CLR_WHITE,120,008)
		oEmpresa       := TSay():New(036,010,{|u| If(PCount() > 0 , cEmpresa := u  , cEmpresa ) },oPan2,,oFont ,.F.,.F.,.F.,.T.,CLR_BLACK,CLR_WHITE,120,008)
		oDescEmp       := TSay():New(041,010,{|u| If(PCount() > 0 , cDescEmp := u  , cDescEmp ) },oPan2,,oFontN,.F.,.F.,.F.,.T.,CLR_BLACK,CLR_WHITE,120,008)
		oValid         := TSay():New(049,010,{|u| If(PCount() > 0 , cValid   := u  , cValid   ) },oPan2,,oFontN,.F.,.F.,.F.,.T.,CLR_BLACK,CLR_WHITE,120,008)
		oBarras        := TSay():New(057,010,{|u| If(PCount() > 0 , cCBarras := u  , cCBarras ) },oPan2,,oFontN,.F.,.F.,.F.,.T.,CLR_BLACK,CLR_WHITE,120,008)
	Else
		oCart:cBmpFile :="\PARQUE\img\Carteirinha_cac_01.bmp"
		oCategoria     := TSay():New(012,055,{|u| If(PCount() > 0 , cCat     := u  , cCat     ) },oPan2,,oFontN,.F.,.F.,.F.,.T.,CLR_BLACK,CLR_WHITE,120,008)
		oDescCart      := TSay():New(014,008,{|u| If(PCount() > 0 , cDCart   := u  , cDCart   ) },oPan2,,oFont ,.F.,.F.,.F.,.T.,CLR_BLACK,CLR_WHITE,120,008)
		oDepTit        := TSay():New(019,008,{|u| If(PCount() > 0 , cDepTit  := u  , cDepTit  ) },oPan2,,oFontN,.F.,.F.,.F.,.T.,CLR_BLACK,CLR_WHITE,120,008)
		oEmpresa       := TSay():New(024,008,{|u| If(PCount() > 0 , cEmpresa := u  , cEmpresa ) },oPan2,,oFont ,.F.,.F.,.F.,.T.,CLR_BLACK,CLR_WHITE,120,008)
		oDescEmp       := TSay():New(029,008,{|u| If(PCount() > 0 , cDescEmp := u  , cDescEmp ) },oPan2,,oFontN,.F.,.F.,.F.,.T.,CLR_BLACK,CLR_WHITE,120,008)
		oValid         := TSay():New(037,008,{|u| If(PCount() > 0 , cValid   := u  , cValid   ) },oPan2,,oFontN,.F.,.F.,.F.,.T.,CLR_BLACK,CLR_WHITE,120,008)
		oBarras        := TSay():New(045,008,{|u| If(PCount() > 0 , cCBarras := u  , cCBarras ) },oPan2,,oFontN,.F.,.F.,.F.,.T.,CLR_BLACK,CLR_WHITE,120,008)
	EndIf

	aHeader            := {" ","N. Cart","Nome", "Nascimento", "Tipo","Imagem","Local"}
	aCols              := CCols()
		
	oBDEP              := TCBrowse():New(007,130,465,189,,aHeader,aCDep,oPan2,,,,{||},{||},,,,,,,.F.,,.T.,,.F.,,,)
	
	oBDEP:bChange      := {|| AtuIMG(oImg,oBDEP)}
	
	oBDEP:SetArray(aCols)	
	
	oBDEP:bLDblClick   := {||fMarca(oBDEP:nAt)}	
	
	oBDEP:bLine        := {|| {If(  aCols[oBDEP:nAT,01],oTCH,oFCH),;
                                 aCols[oBDEP:nAt,02],;                              
                                 aCols[oBDEP:nAt,03],;
                                 aCols[oBDEP:nAt,04],;
                                 aCols[oBDEP:nAt,05],;
                                 aCols[oBDEP:nAt,06],;
                                 aCols[oBDEP:nAt,07]}}	
	
	oWindow:lEscClose  := .T. 
	oWindow:lCentered  := .T.			
	oWindow:Activate()	

Return

/*/{Protheus.doc} CCols
//TODO Descrição auto-gerada.
@author luiz_
@since 05/07/2017
@version undefined
@example
(examples)
@see (links_or_references)
/*/

Static Function CCols

	Local cEstat := U_SFGN001A(ProcName(0), "SF0504X")
	Local cAlCart := GetNextAlias()
	Local aRet    := {}  
	Local cNome   := ZH1->ZH1_NOME 

	aAdd(aRet,{.F.,ZH1->ZH1_COD,ZH1->ZH1_NOME,ZH1->ZH1_DTNASC,"Titular",ZH1->ZH1_IMFILE,ZH1->ZH1_IMFIL2})

	BeginSql Alias cAlCart
	
		SELECT ZH2_CODDEP,
			   ZH2_NOME,
		       ZH2_DTNASC,
		       ZH2_IMFILE,
		       ZH2_IMFIL2   
		FROM   %Table:ZH2%
		WHERE  ZH2_COD = %Exp:ZH1->ZH1_COD%
		AND    %NOTDEL% 		
				
	EndSql 
	
	If !(cAlCart)->(Eof())
	
		While !(cAlCart)->(Eof())		
			aAdd(aRet,{.F.,(cAlCart)->ZH2_CODDEP,(cAlCart)->ZH2_NOME,SToD((cAlCart)->ZH2_DTNASC),"Dependente",(cAlCart)->ZH2_IMFILE,(cAlCart)->ZH2_IMFIL2})		
			(cAlCart)->(Dbskip())
		EndDo
	
	Else 
	
		If Len(aRet) = 0
			aAdd(aRet,{.F.,"","","","","",""})
		EndIf
	
	EndIf

Return aRet

/*/{Protheus.doc} AtuIMG
(long_description)
@author j2a.luizjunior
@since 07/07/2017
@version 1.0
@example
(examples)
@see (links_or_references)
/*/

Static Function AtuIMG(pTBMP,pBDEP)

	Local cEstat := U_SFGN001A(ProcName(0), "SF0504X")
	Local oTBMP      := pTBMP
	Local oBDEP      := pBDEP
	Local cAuxTipo   := ""
	Local cPath      := "C:\PARQUE\"	
	Local cPathPad   := "\PARQUE\sem_imagem.PNG"
	Local cNewCamImg := ""
	
	If !File(cPath)
		FWMakeDir(cPath,.F.)
	EndIf
	
	//->Atualiza imagem carteira	
	If SubStr(aCols[oBDEP:nAt][5],1,1)	== "T"
	
		DbSelectArea("ZH1")
		DbSetOrder(1)
		If DbSeek(xFilial("ZH1") + aCols[oBDEP:nAt][2])
			While !ZH1->(Eof()) .And. xFilial("ZH1") + aCols[oBDEP:nAt][2] == ZH1->ZH1_FILIAL + ZH1->ZH1_COD
				cDCart    := "Titular:"
				cDepTit   := SubStr(AllTrim(ZH1->ZH1_NOME),1,22)
				cCBarras  := "1" + ZH1->ZH1_COD + "00"
				cAuxTipo  := ZH1->ZH1_CATEGO
				cValid    := "Validade: " + DToC(ZH1->ZH1_VALID)
				ZH1->(DbSkip())
			EndDo		
		EndIf
		
	Else
	
		DbSelectArea("ZH2")
		DbSetOrder(2)
		If DbSeek(xFilial("ZH2") + aCols[oBDEP:nAt][2])
			While !ZH2->(Eof()) .And. xFilial("ZH2") + aCols[oBDEP:nAt][2] == ZH2->ZH2_FILIAL + ZH2->ZH2_CODDEP
				cAuxTipo  := Posicione("ZH1",1,xFilial("ZH1") + ZH2->ZH2_COD,"ZH1_CATEGO")
				cValid    := "Validade: " + DToC(Posicione("ZH1",1,xFilial("ZH1") + ZH2->ZH2_COD,"ZH1_VALID"))
				cDCart    := "Dependente:"
				cDepTit   := SubStr(ZH2->ZH2_NOME,1,22)
				cCBarras  := "2" + ZH2->ZH2_CODDEP + ZH2->ZH2_ITEM 
				ZH2->(DbSkip())
			EndDo		
		EndIf
		
	EndIf
	
	Do Case					
		Case cAuxTipo == "I"
			cCat     := "INDUSTRIA"
			//-> Nome da Empresa
			cEmpresa := "Empresa: "
			cDescEmp := RETFILCAR("I")			
		Case cAuxTipo == "C"
			cCat     := "COMUNIDADE"
			cEmpresa := ""
			cDescEmp := ""			
		Case cAuxTipo == "S"
			cCat     := "SISTEMA FIEMT"
			//-> Nome da Filial
			cEmpresa := "Empresa: "
			cDescEmp := RETFILCAR("S")			
	EndCase
	
	oValid:Refresh()
	oCategoria:Refresh() 
	oDescCart:Refresh()
	oDepTit:Refresh()
	oEmpresa:Refresh()
	oDescEmp:Refresh()
	oBarras:Refresh()
	
	If Empty(AllTrim(aCols[oBDEP:nAt][6]))

		cNewCamImg := cPath + SubStr(cPathPad,(Rat("\",cPathPad))+1,Len(cPathPad))
		
		If !File(cNewCamImg)
			If !CpyS2T(cPathPad,cPath,.T.)
				Aviso(FunName()+"/"+ProcName(),"Impressão ABORTADA, não foi possivel copiar TEMPLATE para Impressão!",{"OK"})
				Return .F.
			EndIf
		EndIf
		
		oTBMP:cBmpFile := cNewCamImg
		
	Else
		oTBMP:cBmpFile := aCols[oBDEP:nAt][7]
	EndIf
	
	oTBMP:Refresh()
	
Return

/*/{Protheus.doc} fMarca
(long_description)
@author j2a.luizjunior
@since 07/07/2017
@version 1.0
@example
(examples)
@see (links_or_references)
/*/

Static Function fMarca(pLin)
	
	Local cEstat := U_SFGN001A(ProcName(0), "SF0504X")
	Local nLin := pLin
	
	For nI := 1 To Len(aCols)
		
		If aCols[nI][1] .And. nI != nLin
			aCols[nI][1] := .F.
		EndIf
			
		If nI = nLin
			aCols[nLin,01] := !(aCols[nLin,01])	
		EndIf	
			
	Next nI

	oBDEP:Refresh()

Return

/*/{Protheus.doc} AddImg
(long_description)
@author j2a.luizjunior
@since 07/07/2017
@version 1.0
@return ${return}, ${return_description}
@example
(examples)
@see (links_or_references)
/*/

Static Function AddImg()

	Local cEstat := U_SFGN001A(ProcName(0), "SF0504X")
	Local   nLin      := oBDEP:nAt	
	Local   cMask     := "Arquivos Texto (*.PNG) |*.png|"
	Local   cPathIMG  := "\PARQUE\"
	Local   cArqIMG   := SuperGetMv("MV_XCRMPRO",,"")
	Private cCaminho  := ""	

	If aCols[nLin,01]
	
		cCaminho := cGetFile(cMask,"")
	
		If Empty(cCaminho)
			Return
		Else
				
			//-> Copia imagem para servidor 
			If !CpyT2S(cCaminho,cPathIMG,.T.)
				Aviso(FunName()+"/"+ProcName(),"Não foi possivel copiar imagem para o servidor.",{"OK"})
				Return .f.
			Endif
			
			cNewCamImg := cPathIMG + SubStr(cCaminho,(Rat("\",cCaminho))+1,Len(cCaminho)) 
			
		EndIf
	
		aCols[oBDEP:nAt][6] := cNewCamImg
		aCols[oBDEP:nAt][7] := cCaminho
		
		If SubStr(aCols[nLin,05],1,1) == "T"
			DbSelectArea("ZH1")
			DbSetOrder(1)
			If DbSeek(xFilial("ZH1") + aCols[nLin,02])			
				While !ZH1->(Eof()) .And. xFilial("ZH1") + aCols[nLin,02] == ZH1->ZH1_FILIAL + ZH1->ZH1_COD					
					If Reclock("ZH1",.F.)					
						ZH1->ZH1_IMFILE := aCols[oBDEP:nAt][6]
						ZH1->ZH1_IMFIL2 := aCols[oBDEP:nAt][7]				
						ZH1->(MsUnlock())					
					EndIf										
					ZH1->(DbSkip())
				EndDo
				
			EndIf
		Else
			DbSelectArea("ZH2")
			DbSetOrder(2)
			If DbSeek(xFilial("ZH2") + aCols[nLin,02])
				While !ZH2->(Eof()) .And. xFilial("ZH2") + aCols[nLin,02] == ZH2->ZH2_FILIAL + ZH2->ZH2_CODDEP 					
					If Reclock("ZH2",.F.)					
						ZH2->ZH2_IMFILE := aCols[oBDEP:nAt][6]
						ZH2->ZH2_IMFIL2 := aCols[oBDEP:nAt][7]					
						ZH1->(MsUnlock())					
					EndIf										
					ZH2->(DbSkip())
				EndDo			
			EndIf		
		EndIf
		
		oImg:cBmpFile := aCols[oBDEP:nAt][6]
		oImg:Refresh()
	
	EndIf

Return	

/*/{Protheus.doc} ImpCart
(long_description)
@author j2a.luizjunior
@since 14/07/2017
@version 1.0
@example
(examples)
@see (links_or_references)
/*/

Static Function ImpCart

	Local cEstat := U_SFGN001A(ProcName(0), "SF0504X")
	Local oPrint  
	Local aImp	     := GetImpWindows(.F.)
	Local cImpRet    := aImp[1]
	Local nLin	     := 0
	Local nCol	     := 0
	Local cImpRetCli := ""
	Local nLargura   := 200
	Local oFontGN    := TFont():New( "Arial",,-08,,.F. )
	Local oFont      := TFont():New( "Arial",,-08,,.T. )
	Local oFontB     := TFont():New( "Arial",,-10,,.T. )
	Local cCabec     := ""	
	Local cConteudo  := ""	
	Local cAuxTipo   := ""
	Local cTipo      := ""	
	Local cPath      := "C:\PARQUE\"	
	Local cValidade  := ""
	
	//For nI := 1 To Len(aCols)
		
		If aCols[oBDEP:nAt][01]
		
			oPrint       := FWMSPrinter():New("Cabeçalho Padrão" ,, .F., /*cStartPath*/, .T.,, @oPrint,cImpRet,.F.,.F.,,)
			
			//oPrint:SetLandscape()		
			//oPrint:StartPage()
			
			If SubStr(aCols[oBDEP:nAt][5],1,1)	== "T"
			
				DbSelectArea("ZH1")
				DbSetOrder(1)
				If DbSeek(xFilial("ZH1") + aCols[oBDEP:nAt][2])
					While !ZH1->(Eof()) .And. xFilial("ZH1") + aCols[oBDEP:nAt][2] == ZH1->ZH1_FILIAL + ZH1->ZH1_COD
						cCabec    := "Titular: "
						cConteudo := Substr(ZH1->ZH1_NOME,1,22)
						cBarras   := "1" + ZH1->ZH1_COD + "00"
						cAuxTipo  := ZH1->ZH1_CATEGO
						cValidade := DToC(ZH1->ZH1_VALID) 
						ZH1->(DbSkip())
					EndDo		
				EndIf
				
			Else
			
				DbSelectArea("ZH2")
				DbSetOrder(2)
				If DbSeek(xFilial("ZH2") + aCols[oBDEP:nAt][2])
					While !ZH2->(Eof()) .And. xFilial("ZH2") + aCols[oBDEP:nAt][2] == ZH2->ZH2_FILIAL + ZH2->ZH2_CODDEP
						cAuxTipo  := Posicione("ZH1",1,xFilial("ZH1") + ZH2->ZH2_COD,"ZH1_CATEGO")
						cValidade := DToC(Posicione("ZH1",1,xFilial("ZH1") + ZH2->ZH2_COD,"ZH1_VALID"))
						cCabec    := "Dependente: "
						cConteudo := Substr(ZH2->ZH2_NOME,1,22)
						cBarras   := "2" + ZH2->ZH2_CODDEP + ZH2->ZH2_ITEM
						ZH2->(DbSkip())
					EndDo		
				EndIf
				
			EndIf
			
			Do Case					
				Case cAuxTipo == "I"
					cTipo := "INDUSTRIA"
				Case cAuxTipo == "C"
					cTipo := "COMUNIDADE"
				Case cAuxTipo == "S"
					cTipo := "SISTEMA FIEMT"
				Case cAuxTipo == "N"	
					cTipo := "NAO INDUSTRIA"
			EndCase
			
			If !File(cPath)
				FWMakeDir(cPath,.F.)
			EndIf
			
			cPathDOT  := "\PARQUE\img\"
			
			If cFilAnt == "02MT0012"
			
				oPrint:SetLandscape()		
				oPrint:StartPage()
			
				cArqDOT   := "Cart_parque.bmp"
				cSavePath := ""				

				If !CpyS2T(cPathDOT+cArqDOT,cPath,.T.)
					Aviso(FunName()+"/"+ProcName(),"Impressão ABORTADA, não foi possivel copiar TEMPLATE para Impressão!",{"OK"})
					Return .F.
				EndIf	
			
				cBmp  := cPath + cArqDOT
				oPrint:SayBitmap(001,001,cBmp,240,150)
				
				If !CpyS2T(aCols[oBDEP:nAt][6],cPath,.T.)
					Aviso(FunName()+"/"+ProcName(),"Impressão ABORTADA, não foi possivel copiar TEMPLATE para Impressão!",{"OK"})
					Return .F.
				EndIf		
				
				cFoto := AllTrim(cPath + SubStr(aCols[oBDEP:nAt][6],(Rat("\",aCols[oBDEP:nAt][6]))+1,Len(aCols[oBDEP:nAt][6])))
				
				oPrint:SayBitmap(040,160,cFoto,80,90)
				//-> Tipo
				oPrint:Say(24, 130 , cTipo            , oFont         )
				//-> Cabeçalho
				oPrint:Say(40, 010 , cCabec           , oFontGN       )
				//-> Conteudo
				oPrint:Say(50, 010 , cConteudo        , oFont         )
				
				If !Empty(cEmpresa)
					//-> Empresa
					oPrint:Say(63, 010 , cEmpresa     , oFontGN       )
					//-> Nome Empresa
					oPrint:Say(73, 010 , cDescEmp     , oFont         )
				EndIf
				
				//-> Validade
				//oPrint:Say(87, 010 , "* Validade até a vigência do convênio"   , oFontGN   )
				oPrint:Say(84, 010 , "Validade: " + cValidade   , oFont   )
				//-> Codigo Barras
				oPrint:Say(98, 010 , cBarras          , oFont         )
			
				oPrint:FWMSBAR("INT25" /*cTypeBar*/, 09/*nRow*/ , 001/*nCol*/ ,cBarras /*cCode*/, oPrint /*oPrint*/, .F. /*lCheck*/,/*Color*/,/*lHorz*/, 0.02/*nWidth*/, 0.8/*nHeigth*/,.F. /*lBanner*/, "Times New Roman"/*cFont*/, "INT25"  /*cMode*/, .F. /*lPrint*/, 0.5/*nPFWidth*/, 0.5/*nPFHeigth*/,/*lCmtr2Pix*/)
			
				oPrint:Print()			
				oPrint:EndPage()
		
				If File(cBmp)
					FErase(cBmp)
				EndIf
		
				If File(cFoto)
					FErase(cFoto)
				EndIf
			
			Else

				oPrint:SetPortrait()		
				oPrint:StartPage()

				//-> Tipo
				oPrint:Say(19, 420 , cTipo              , oFont            )
				//-> Cabeçalho
				oPrint:Say(19, 273 , cCabec + cConteudo , oFontGN          )
				//-> Conteudo
				//oPrint:Say(29, 253 , cConteudo        , oFont            )
				
				If !Empty(cEmpresa)
					//-> Empresa
					oPrint:Say(29, 273 , cEmpresa + cDescEmp , oFontGN     )
					//-> Nome Empresa
					//oPrint:Say(52, 253 , cDescEmp     , oFont            )
				EndIf
				
				//-> Validade				
				oPrint:Say(39, 273 , "Validade: " + cValidade , oFont      )
				//-> Codigo Barras
				oPrint:Say(55, 344 , cBarras                  , oFont      )
			
				oPrint:FWMSBAR("INT25" /*cTypeBar*/, 005/*nRow*/ , 023.3/*nCol*/ ,cBarras /*cCode*/, oPrint /*oPrint*/, .F. /*lCheck*/,/*Color*/,/*lHorz*/, 0.02/*nWidth*/, 0.8/*nHeigth*/,.F. /*lBanner*/, "Times New Roman"/*cFont*/, "INT25"  /*cMode*/, .F. /*lPrint*/, 0.5/*nPFWidth*/, 0.5/*nPFHeigth*/,/*lCmtr2Pix*/)
			
				oPrint:Print()			
				oPrint:EndPage()
				
			EndIf	
			
		EndIf
		
	//Next nI	

Return

/*/{Protheus.doc} RETFILCAR
(long_description)
@author j2a.luizjunior
@since 18/07/2017
@version 1.0
@example
(examples)
@see (links_or_references)
/*/

Static Function RETFILCAR(pParam)

	Local cEstat := U_SFGN001A(ProcName(0), "SF0504X")
	Local cRet    := ""
	Local cParam  := pParam
	Local cFilRet := "" 
	
	If cParam == "S"
		 
		cFilRet   := Posicione("SRA",1, xFilial("SRA") + cMatricula,"RA_FILIAL")
		 
		DbSelectArea("SM0")
		DbGoTop()
		While !SM0->(Eof()) 			
			If AllTrim(SM0->M0_CODFIL) == AllTrim(cFilRet) 
				cRet :=  SM0->M0_FILIAL
			EndIf			
			SM0->(DbSkip())
		EndDo		
		 
	Else
		
		DbSelectArea("CN9")
		DbSetOrder(1)
		If DbSeek(cFilAtu + cContrato + cRevisa)
			cRet   := SubStr(AllTrim(Posicione("SA1",1, xFilial("SA1") + CN9->CN9_CLIENT + CN9->CN9_LOJACL,"A1_NREDUZ")),1,22) 
		EndIf			
	 	
	EndIf
	
Return cRet