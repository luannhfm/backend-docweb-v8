#include "Protheus.ch"

/*/{Protheus.doc} User Function FM73E01F
Função para enviar pesquisa de satisfação da FIEMT no processo de venda.
@type  Function
@author Franklin de Brito de Oliveira
@since 23/06/2021
/*/
User Function FM73E01F()
    local aBkpArea := GetArea()
    local cDestinatario := fGetDestinatario()
    local cAssunto := fGetAssunto()
    local cMensagem := fBuildBodyEMail()
   
    U_SFEnvEmail(/*Remetente*/, cDestinatario, /*Com copia*/, /*Com copia oculta*/, cAssunto, cMensagem)

    RestArea(aBkpArea)

Return Nil

/*/{Protheus.doc} fGetDestinatario
Identifica os destinatários do e-mail
@type Function
@author Franklin de Brito de Oliveira
@since 24/06/2021
@return character, destinatários identificados
/*/
Static Function fGetDestinatario()
    local cDestinatario := ""

    dbSelectArea("AD9")

    if AD9->(dbSeek(AD1->(AD1_FILIAL+AD1_NROPOR+AD1_REVISA)))
        while !AD9->(EoF()) .and. AD9->(AD9_FILIAL+AD9_NROPOR+AD9_REVISA)==AD1->(AD1_FILIAL+AD1_NROPOR+AD1_REVISA)
            cDestinatario += AllTrim(Posicione("SU5", 1, xFilial("SU5")+AD9->AD9_CODCON, "U5_EMAIL"))+";"
            AD9->(dbSkip())
        end       
    endif

Return cDestinatario

/*/{Protheus.doc} fGetAssunto
Identifica o assunto do e-mail
@type Function
@author Franklin de Brito de Oliveira
@since 24/06/2021
@return character, assunto do e-mail
/*/
Static Function fGetAssunto()

Return 'Pesquisa de satisfação FIEMT e IEL | NPS'

/*/{Protheus.doc} fBuildBodyEMail
Monta o corpo do e-mail que será enviado
@type Function
@author Franklin de Brito de Oliveira
@since 24/06/2021
@return character, corpo do e-mail
/*/
Static Function fBuildBodyEMail()
    local cBodyEMail := ""

    cBodyEMail := MemoRead("\workflow\pesquisaDeSatisfacao.html")

Return cBodyEMail
