#Include 'Protheus.ch'

Static _Retorno
Static _oDlg

/*/{Protheus.doc} SF73C01J
Funcao de consulta(F3) especifica(ADZSGE) de produtos.
Utilizado no campo ADZ_PRODUT

@type 		function
@author 	Jose Leite de Barros Neto
@since 	08/02/2017
@version 	1.0
@return 	_lRet, True
/*/
/*/
Franklin B. Oliveira - 25/05/2018
	Removido a coluna TURMA e a pesquisa por TURMA.
/*/
User Function SF73C01J()
 
	Local _aArea := GetArea()
	Local cVarPos  	:= &(ReadVar())
	Local _lRet	 := .T.
	
	VAR_IXB := cVarPos

	//Busca os produtos
	fTelaPrd(@cVarPos)
	
	RestArea(_aArea)

Return(_lRet)


/** {Protheus.doc} fTelaPrd
Funcao que realiza a criacao da tela de busca de produtos

@author: 	Jose Leite de Barros Neto
@since: 	08/02/2017
@Uso: 		SFIEMT
*/
Static Function fTelaPrd(p_cVarPos)

	Local _btConfirm	:= Nil
	Local _btCancel		:= Nil
	Local _cTipo 		:= SPACE(1)
	Local _cFilt 		:= p_cVarPos + Space(30 - Len(p_cVarPos))
	Local nPosList		:= 0

	Private _oDlg		:= Nil
	Private _oBrowse	:= Nil
	Private _aItens		:= {}
		
	//Carrega Produtos
	fQryPrd()
			
	DEFINE MSDIALOG _oDlg TITLE 'Cadastro de Produtos' FROM 000, 000 TO 380,1160 COLORS 0, 16777215 PIXEL
		@ 005,005 SAY 'Campo' PIXEL SIZE 30,9 Of _oDlg
		@ 012,005 MSCOMBOBOX oGet1 VAR _cTipo ITEMS{"C=Código","D=Descrição", "S=Cod. SGE"} SIZE 052, 010 OF _oDlg COLORS 0, 16777215 PIXEL
	
		@ 005,070 SAY 'Consulta' PIXEL SIZE 40,9 Of _oDlg
		@ 012,070 MSGET oGet2 VAR _cFilt SIZE 150, 010 Picture "@!" OF _oDlg COLORS 0, 16777215 PIXEL
	
		@ 012,230 BUTTON "Pesquisar" SIZE 070, 010 PIXEL OF _oDlg ACTION MsgRun("Aguarde, selecionando informações...",, {|| fQryPrd(_cTipo, _cFilt)} )
		
		/* Cria a ListBox */																																															
		@ 025, 004 LISTBOX _oBrowse Fields HEADER "Código", "Tipo", "Descrição", "Módulo", "Prod. SGE", "Carga Horaria" SIZE 573, 142 OF _oDlg PIXEL ColSizes 30, 12, 255,40,40						
		_oBrowse:SetArray(_aItens)

		If .not. Empty(p_cVarPos) .and. (nPosList := aScan(_aItens, {|x| x[1] == AllTrim(p_cVarPos)})) > 0			
			_oBrowse:nAt := nPosList
		EndIf		
		
		_oBrowse:bLDblClick := {|| fBtnOk(), _oBrowse:DrawSelect()} //Duplo clique
		_oBrowse:bLine := {||{ 	_aItens[_oBrowse:nAt,1],;
								 	 	_aItens[_oBrowse:nAt,2],;
										_aItens[_oBrowse:nAt,3],;
										_aItens[_oBrowse:nAt,4],;
										_aItens[_oBrowse:nAt,5],;
										_aItens[_oBrowse:nAt,6]}}
		
		@ 175, 005 BUTTON _btConfirm	PROMPT "OK"		 	ACTION fBtnOk()		SIZE 037, 012 OF _oDlg PIXEL
		@ 175, 050 BUTTON _btCancel		PROMPT "Cancelar"	ACTION _oDlg:End()	SIZE 037, 012 OF _oDlg PIXEL
		@ 175, 095 BUTTON _btVisual		PROMPT "Visualizar"	ACTION fVisual()	SIZE 037, 012 OF _oDlg PIXEL
		
	ACTIVATE MSDIALOG _oDlg CENTERED		
		
Return


/** {Protheus.doc} fBtnOk
Funcao executada ao clicar no botao OK da tela, retornando os dados de
codigo de produto, codigo da turma e codigo do modulo.

@author: 	Jose Leite de Barros Neto
@since: 	08/02/2017
@Uso: 		SFIEMT
*/
Static Function fBtnOk()

Local _oModel		:= FwModelActive()
Local _oModelADZ	:= _oModel:GetModel("ADZPRODUTO")
//+------------------------------------------------------------------+
//| Antonio Dantas                                        16/11/2018 | 
//| Sustituicao do bloco de codigo abaixo em decorrencia da mudanca  |
//| de tecnologia do formulario CRM para MVC na Versao 12.           |
//+------------------------------------------------------------------+
//&&--  
//&&--  Local _nP01 	:= aScan(aHeader,{|X| UPPER(AllTrim(X[2]))=="ADZ_XTURMA"})
//&&--  Local _nP02 	:= aScan(aHeader,{|X| UPPER(AllTrim(X[2]))=="ADZ_XMODUL"})
//&&--  Local nPosPSge 	:= aScan(aHeader,{|X| UPPER(AllTrim(X[2]))=="ADZ_XPRDSG"})
//&&--  Local nPosCHMo	:= aScan(aHeader,{|X| UPPER(AllTrim(X[2]))=="ADZ_XCHMOD"})
//&&--  
//&&--  //aCols[n,_nP01] 		:= _aItens[_oBrowse:nAt,4] //Cod Turma
//&&--  aCols[n,_nP02] 		:= _aItens[_oBrowse:nAt,4] //Cod Modulo
//&&--  aCols[n,nPosPSge]	:= _aItens[_oBrowse:nAt,5] //Código produto SGE
//&&--  aCols[n,nPosCHMo] 	:= _aItens[_oBrowse:nAt,6] //Carga horária modulo
//&&--  _oDlg:End()
//&&--  
//--
VAR_IXB := _aItens[_oBrowse:nAt,1] //Cod Produto
_oModelADZ:LoadValue("ADZ_XMODUL", _aItens[_oBrowse:nAt,4] )
_oModelADZ:LoadValue("ADZ_XPRDSG", _aItens[_oBrowse:nAt,5] )
_oModelADZ:LoadValue("ADZ_XCHMOD", _aItens[_oBrowse:nAt,6] )

_oDlg:End()

Return


/** {Protheus.doc} fQryPrd
Funcao responsavel pela busca dos produtos de acordo com parametros passados.

@author: 	Jose Leite de Barros Neto
@since: 	08/02/2017
@Uso: 		SFIEMT
*/
Static Function fQryPrd(p_cTipo, p_cFiltro)
	
	Local aAreaOld		:= GetArea()
	Local _cAlias		:= GetNextAlias()
	Local _cAliasRM		:= GetNewPar("MV_XSGEABD", "")
	Local _cQuery		:= ""	

	Default p_cTipo		:= ""
	Default p_cFiltro	:= ""
	
	_aItens := {}
	aSize( _aItens, 0 )
	
	If Select(_cAlias) > 0
		(_cAlias)->(DbCloseArea())
	EndIf
	
	_cQuery := "SELECT " + CRLF
	
	If "03MT" $ cFilAnt
		_cQuery += "  TRIM(B1_COD) AS CODIGO_PRODUTO, "+ CRLF
		_cQuery += "  TRIM(B1_DESC) AS NOME_PRODUTO, " + CRLF  
		_cQuery += "  TRIM(B1_TIPO) AS TIPO_PRODUTO, " + CRLF
		_cQuery += "  TRIM(B1_XPRDSGE) AS PRODUTO_SGE, " + CRLF
		_cQuery += "  B1_XCHSIGE, " + CRLF
		/*
		Franklin B. Oliveira - 25/05/2018
		_cQuery += "  CASE "+ CRLF
		_cQuery += "    WHEN TURMA is null THEN ' ' " + CRLF
		_cQuery += "    ELSE TURMA" + CRLF
		_cQuery += "  END CODIGO_TURMA, " + CRLF
		*/
		_cQuery += "  CASE "+ CRLF
    	_cQuery += "  	WHEN MODULO is null THEN TO_CHAR(' ') "+ CRLF
    	_cQuery += "  	ELSE TO_CHAR(MODULO) "+ CRLF
  		_cQuery += "  END CODIGO_MODULO "+ CRLF
		_cQuery += "FROM "+ RetSqlName("SB1") +" "+ CRLF
		/*
		Franklin B. Oliveira - 25/05/2018
		_cQuery += "  LEFT JOIN RM.VISAO_CRM_TURMA"+ _cAliasRM + CRLF 
		_cQuery += "  ON CODIGO_UNIDADE_OPERACIONAL = '"+ cFilAnt +"' AND CODIGO_CURSO = TRIM(B1_XPRDSGE) " + CRLF
		*/
		_cQuery += " LEFT JOIN RM.VISAO_CURSOS_CRM" + _cAliasRM + CRLF
		_cQuery += "	ON CODIGO_CURSO = TRIM(B1_XPRDSGE) " + CRLF
	Else
		_cQuery += "  TRIM(B1_COD) AS CODIGO_PRODUTO, " + CRLF 
		_cQuery += "  TRIM(B1_DESC) AS NOME_PRODUTO, " + CRLF 
		_cQuery += "  TRIM(B1_TIPO) AS TIPO_PRODUTO, " + CRLF
		_cQuery += "  TRIM(B1_XPRDSGE) AS PRODUTO_SGE, " + CRLF
		_cQuery += "  B1_XCHSIGE, " + CRLF
		/*
		Franklin B. Oliveira - 25/05/2018
		_cQuery += "  ' ' AS CODIGO_TURMA, " + CRLF
		*/
		_cQuery += "  ' ' AS CODIGO_MODULO " + CRLF
		_cQuery += "FROM "+RetSqlName(+"SB1")+" "+ CRLF
	EndIf
	
	_cQuery += "WHERE " + CRLF
	_cQuery += "  B1_XPRDVEN <> 'N' " + CRLF
	_cQuery += "  AND (B1_XPRDFIL = '"+ SubStr(cFilAnt,1,2)+"' OR B1_XPRDFIL = '06')" + CRLF
	
	If p_cTipo == 'C'
		_cQuery += "  AND B1_COD LIKE '%"+ AllTrim(p_cFiltro) +"%' " + CRLF
		_cOrder := "ORDER BY B1_COD, B1_DESC"
	ElseIf p_cTipo == 'D'
		_cQuery += "  AND B1_DESC LIKE '%"+ AllTrim(p_cFiltro) +"%' "+ CRLF
		_cOrder := "ORDER BY B1_DESC, B1_COD "+ CRLF
	ElseIf p_cTipo == 'S'
		_cQuery += "  AND B1_XPRDSGE LIKE '%"+ AllTrim(p_cFiltro) +"%' "+ CRLF
		_cOrder := "ORDER BY B1_XPRDSGE, B1_DESC, B1_COD "+ CRLF
	Else
		_cOrder := "ORDER BY B1_COD, B1_DESC "+ CRLF
	EndIf
	
	_cQuery += "  AND B1_MSBLQL = '2'" + CRLF		//1=Sim;2=Não
	_cQuery += "  AND D_E_L_E_T_ <> '*'" + CRLF
	_cQuery += _cOrder
	
	dbUseArea(.T.,'TOPCONN',TcGenQry(,,_cQuery), _cAlias,.F.,.F.)
	
	If .Not. (_cAlias)->(EOF())
		/* Popula Array da GRID - ListBox */
		While .Not. (_cAlias)->(EOF())
			aAdd(_aItens, {	AllTrim((_cAlias)->CODIGO_PRODUTO)		,;
								AllTrim((_cAlias)->TIPO_PRODUTO)	,;
								AllTrim((_cAlias)->NOME_PRODUTO)	,;
								AllTrim((_cAlias)->CODIGO_MODULO)	,;
								AllTrim((_cAlias)->PRODUTO_SGE)		,;
								(_cAlias)->B1_XCHSIGE		})
			(_cAlias)->(dbSkip())
		EndDo
	EndIf

	If Select(_cAlias) > 0
		(_cAlias)->(DbCloseArea())
	EndIf
	
	If Len(_aItens) == 0
		aAdd(_aItens, {	''	,;
						''	,;
						''	,;
						''	,;
						''	,;
						''	})
		Alert("Nenhum produto foi encontrado, favor verificar o Filtro!")
	EndIf
	
	If .Not. Empty(p_cTipo) .OR. Len(_aItens) == 0
		_oBrowse:SetArray(_aItens)
		_oBrowse:bLine := {||{_aItens[_oBrowse:nAt,1],;
						 		_aItens[_oBrowse:nAt,2],;
								_aItens[_oBrowse:nAt,3],;
								_aItens[_oBrowse:nAt,4],;
								_aItens[_oBrowse:nAt,5],;
								_aItens[_oBrowse:nAt,6]}}
	EndIf
	
	RestArea(aAreaOld)

Return


/** {Protheus.doc} fVisual
Funcao responsavel pela visualizacao do produto selecionado

@author: 	Jose Leite de Barros Neto
@since: 	13/02/2017
@Uso: 		SFIEMT
*/
Static Function fVisual()
	
	Local _cProd := _aItens[_oBrowse:nAt,1]
	
	DbSelectArea("SB1")
	DbSetOrder(1)
	If SB1->(DbSeek(xFilial("SB1") + _cProd))
		AxVisual("SB1", SB1->(RECNO()), 2)
	EndIf
	
	SB1->(DbCloseArea())
	
Return