#Include 'Protheus.ch'
#INCLUDE "Topconn.ch"

User Function SS33R5S()

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//?eclaracao de variaveis                   ?	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Private oReport  := Nil
	Private oSecCab  := Nil
	Private cPerg    := "SS33R5S"
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//?riacao e apresentacao das perguntas      ?	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	u_SFPUTSX1(cPerg,"01","Cliente de...................?","","","mv_ch1","C",009,00,00,"G","","SA1"	,"","","mv_par01","","","","","","","","","","","","","","","","",,,,"")
	u_SFPUTSX1(cPerg,"02","Cliente ate..................?","","","mv_ch2","C",009,00,00,"G","","SA1"	,"","","mv_par02","","","","","","","","","","","","","","","","",,,,"")
	u_SFPUTSX1(cPerg,"03","Data De......................?","","","mv_ch3","D",008,00,00,"G","",""		,"","","mv_par03","","","","","","","","","","","","","","","","",,,,"")
	u_SFPUTSX1(cPerg,"04","Data Ate.....................?","","","mv_ch4","D",008,00,00,"G","",""		,"","","mv_par04","","","","","","","","","","","","","","","","",,,,"")
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//?efinicoes/preparacao para impressao      ?	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	ReportDef()
	oReport:PrintDialog()
 
Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ?±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±?±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±?±±?esc.     ?Definição da estrutura do relat?io.                       º±?±±?         ?                                                           º±?±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±?±±?so       ?                                                           º±?±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±?±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±?ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß?*/
Static Function ReportDef()
 
	oReport := TReport():New("SS33R5S","Relatorio	Fatura Sintetica",cPerg,{|oReport| PrintReport(oReport)},"Relatorio de Fatura Sintetica")
	oReport:SetLandscape(.T.)
 
	oSecCab := TRSection():New( oReport , "Relatorio Sintetico", {"QRY"} )
	TRCell():New( oSecCab, "ZZM_CODCLI" , "QRY", "Cod. Cli" , /*Picture*/,10 ,/*lPixel*/,/*{|| code-block de impressao }*/)
	TRCell():New( oSecCab, "ZZM_CNPJ"	 , "QRY", /*X3Titulo*/,"@R 99.999.999/9999-99", /*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
	TRCell():New( oSecCab, "ZZM_NOME"	 , "QRY", "Empresa" , /*Picture*/,40 ,/*lPixel*/,/*{|| code-block de impressao }*/)
	TRCell():New( oSecCab, "VALOR"	, "QRY", /*X3Titulo*/,"@R 999999.99" ,15,/*lPixel*/,/*{|| code-block de impressao }*/)

 
	TRFunction():New(oSecCab:Cell("ZZM_CNPJ"),/*cId*/,"COUNT"     ,/*oBreak*/,"Quantidade de Empresas","@R 999999",/*uFormula*/,.F.           ,.T.           ,.F.        ,oSecCab)
	TRFunction():New(oSecCab:Cell("VALOR"),/*cId*/,"SUM"     ,/*oBreak*/,"Valor Total de GA","@R 999999.99",/*uFormula*/,.F.           ,.T.           ,.F.        ,oSecCab)
	
Return Nil

Static Function PrintReport(oReport)
 
	Local cQryOp     := ""
	
		// -- Seta Titulo do Relat?io -- //    
	   oReport:SetCustomText( {||CriaCab(oReport ) })
 
	Pergunte(cPerg,.F.)
	
	oSecCab:BeginQuery()
	BeginSql Alias "QRY"
		SELECT ZZM_CODCLI, ZZM_CNPJ, ZZM_NOME, SUM(ZZN_VUNIT) AS VALOR
		FROM %table:ZZM% ZZM 		
		INNER JOIN %table:ZZN% ZZN ON ZZN_FILIAL = ZZM_FILIAL AND ZZN_ATEND = ZZM_ATEND
		WHERE ZZM_FILIAL = %xFilial:ZZM% 
			AND ZZM.%NotDel%
			AND ZZN.%NotDel%
			AND ZZM.ZZM_CODCLI >= %Exp:mv_par01%
			AND ZZM.ZZM_CODCLI <= %Exp:mv_par02%
			AND ZZM.ZZM_DATA >= %Exp:DtoS(MV_PAR03)%
			AND ZZM.ZZM_DATA <= %Exp:DtoS(MV_PAR04)%
		GROUP BY ZZM_CODCLI, ZZM_CNPJ,ZZM_NOME 
		ORDER BY ZZM_NOME
	EndSql					
	oSecCab:EndQuery()					
	
	oSecCab:Print()

	DbCloseArea("QRY")
     
Return Nil

/*/{Protheus.doc} CriaCab
Altera o Cabe?lho do Relatorio

@author 	Sergio Ricardo Leite Salustiano
@since 		25/11/2014
@version 	1.0

@param oReport, objeto

@return aCabec, Array
/*/

Static Function CriaCab( oReport )

	Local aArea	:= GetArea()
	Local aCabec	:= {}
	Local cChar	:= chr(160)  // caracter dummy para alinhamento do cabe?lho
	Local cTitulo	:= ""


	cTitulo:= "Periodo de: " +DTOC(MV_PAR03)+" Ate "+DTOC(MV_PAR04)

          
	aCabec := {	"__LOGOEMP__" , cChar + "         " + SM0->M0_NOMECOM + "         " + cChar + RptFolha+ TRANSFORM(oReport:Page(),'999999');
		,  'SS33R5S' + cChar + "         " + "RELATORIO DE FATURA SINTETICA" + "         "+ cChar + RptEmiss + " " + Dtoc(dDataBase);
		, "         " + cChar +  "         " + UPPER(AllTrim(cTitulo)) +  "         "+ cChar + RptHora + " " + Time() }
		       

	RestArea( aArea )
	
Return( aCabec )

