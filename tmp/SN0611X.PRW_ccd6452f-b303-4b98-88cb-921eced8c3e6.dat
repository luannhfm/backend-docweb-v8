#INCLUDE "PROTHEUS.CH"
#INCLUDE "TOPCONN.CH"
#INCLUDE "RWMAKE.CH"
#INCLUDE "FONT.CH"
#INCLUDE "COLORS.CH"  
#INCLUDE "PARMTYPE.CH"     
#INCLUDE "FWMBROWSE.CH"
#INCLUDE "FWMVCDEF.CH"
#INCLUDE "FWCOMMAND.CH"    
/*
+--------------------------------------------------------------------------+
|{Protheus.doc}<SN0611X>                                                   |
|Browser Para Importacao de Alunos para Pre-Cadastro de Convenios.         |
|                                                                          |
|    ATENCAO:                                                              |
|    --------                                                              |
|    Para utilizar a faciliade verifique o criacao e declaracao do         |
|    parametro: Repositorio de Logs do Pronatec                            |
|    MV_XLOGPRO  = "\Pronatec\Logs\"                                       |
|                                                                          |
|@Author<Antonio Dantas>                                                   |
|@since<15/04/2014>                                                        |
|@version<001>                                                             |
|@return<Nil>,<Nil>                                                        |
|                                                                          |
|@example                                                                  |
|<Nil>                                                                     |
|                                                                          |
|@see                                                                      |
|<Nil>                                                                     |
+-----------+--------------------------------------------------------------+
|Uso        |FIEMT - FederaCAO das Industrias no Estado de Mato Grosso     |
+-----------+--------------------------------------------------------------+
*/
User Function SN0611X()
Local _cAlias  := "ZP1"
Local _cTitle  := "Pré Cadastro de Convênios"
Local _oBrowse := FWMBrowse():New()
Private aRotina := MenuDef() 
_oBrowse:SetAlias(_cAlias)
_oBrowse:SetFilterDefault("ZP1_XPRECA=='S' .and. ZP1_XSTATU= 'A'") 
_oBrowse:SetDescription(_cTitle)
_oBrowse:Activate()   
Return NIL

/*
+--------------------------------------------------------------------------+
|{Protheus.doc}<MenuDef>                                                   |
|Definicao do MenuDef                                                      |
|@Author<Antonio Dantas>                                                   |
|@since<15/03/2014>                                                        |
|@version<Nil>                                                             |
|                                                                          |
|@parameters:                                                              |
|@return                                                                   |
|<   _aRotina (a) - Vetor com as opcoes de Menu                            |
|>                                                                         |
+-----------+--------------------------------------------------------------+
|Uso        |FIEMT - FederaCAO das Industrias no Estado de Mato Grosso     |
+-----------+--------------------------------------------------------------+
*/
Static Function MenuDef()
Local _aRotina := {}
ADD OPTION _aRotina TITLE "Pesquisar"  	ACTION "PESQBRW"       		OPERATION 1 ACCESS 0
ADD OPTION _aRotina TITLE "Manutenção" 	ACTION "VIEWDEF.SN0611X" 	OPERATION 2 ACCESS 0
ADD OPTION _aRotina TITLE "Imprimir"   	ACTION "VIEWDEF.SN0611X"	OPERATION 8 ACCESS 0
Return _aRotina

/*
+--------------------------------------------------------------------------+
|{Protheus.doc}<MenuDef>                                                   |
|Definicao do ModelDef                                                     |
|@Author<Antonio Dantas>                                                   |
|@since<15/03/2014>                                                        |
|@version<Nil>                                                             |
|                                                                          |
|@parameters:                                                              |
|@return                                                                   |
|<   _oModel (o) - Objeto com a Definicao do Modelo                        |
|>                                                                         |
+-----------+--------------------------------------------------------------+
|Uso        |FIEMT - FederaCAO das Industrias no Estado de Mato Grosso     |
+-----------+--------------------------------------------------------------+
*/
Static Function ModelDef()
Local _oStruZP1 := FWFormStruct(1,"ZP1")
Local _oStruZP3 := FWFormStruct(1,"ZP3")  
//-- ********************************************************************
//--   New("SN06611XX"), refere-se ao nome do Ponto de Entrada PADRAO
//-- ********************************************************************
Local _oModel   := MPFormModel():New("SN0611XX")
_oModel:AddFields("ZP1MASTER", /*cOwner*/,_oStruZP1)
_oModel:AddGrid("ZP3DETAIL","ZP1MASTER",_oStruZP3)
_oModel:SetPrimaryKey({"ZP1_FILIAL","ZP1_XCONV"})
_oModel:SetRelation("ZP3DETAIL", {{"ZP3_FILIAL", "FwxFilial('ZP3')"},{"ZP3_XCONVE","ZP1_XCONV"},{"ZP3_XSTATU","'P'"}},ZP3->(IndexKey(2)))
_oModel:SetDescription("Pré Cadastro de Convênios")
//-- Defini o Modelo como nao Editavel
_oModel:GetModel("ZP1MASTER"):SetDescription("Convênios")
//-- Defini o Modelo como nao Editavel
_oModel:GetModel("ZP3DETAIL"):SetDescription("Conveniados")
_oModel:GetModel("ZP3DETAIL"):SetNoInsertLine(.T.)
_oModel:GetModel("ZP3DETAIL"):SetNoDeleteLine(.T.)
_oModel:GetModel("ZP3DETAIL"):SetNoUpdateLine(.T.)
_oModel:GetModel("ZP3DETAIL"):SetOnlyQuery(.T.)    			//-- Indica que o model e somente para consulta
Return _oModel


/*
+--------------------------------------------------------------------------+
|{Protheus.doc}<MenuDef>                                                   |
|Definicao do ViewDef                                                      |
|@Author<Antonio Dantas>                                                   |
|@since<20/03/2014>                                                        |
|@version<Nil>                                                             |
|                                                                          |
|@parameters:                                                              |
|@return                                                                   |
|<   _oView (o) - Objeto com a Definicao do Visao do Modelo                |
|>                                                                         |
+-----------+--------------------------------------------------------------+
|Uso        |FIEMT - FederaCAO das Industrias no Estado de Mato Grosso     |
+-----------+--------------------------------------------------------------+
*/
Static Function ViewDef()
Local _oStruZP1 := FWFormStruct(2,"ZP1")
Local _oStruZP3 := FWFormStruct(2,"ZP3",/*{|_cFild| xDefFilds(_cFild)}*/)
Local _oModel   := FWLoadModel("SN0611X")           	
Local _oView    := FWFormView():New()
//-- Define Visao do Convenios          
_oView:SetModel(_oModel)
_oView:AddField("VIEW_ZP1",_oStruZP1,"ZP1MASTER")
_oView:CreateHorizontalBox("SUPERIOR",60)
_oView:SetOwnerView("VIEW_ZP1","SUPERIOR")
_oView:EnableTitleView("VIEW_ZP1")
//-- Defini visao dos Beneficiarios para Pre Cadastro
_oView:AddGrid("VIEW_ZP3",_oStruZP3,"ZP3DETAIL")
_oView:CreateHorizontalBox("INFERIOR",40)
_oView:SetOwnerView("VIEW_ZP3","INFERIOR")
_oView:EnableTitleView("VIEW_ZP3")    
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³  Inclui um Botao no formulario.                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
_oView:AddUserButton("Integrar SIGE"	,'CLIPS',{|_oView| fIntSige()} ) 
Return _oView
 
/*
+--------------------------------------------------------------------------+
|{Protheus.doc}<fIntSige>                                                  |
| Abre interface "Tela" para identificar e validar o periodo. Chama e      |
| executar a View de integracao com SIGE para importar dos Beneficiarios   |
| "Aluno/Matriculas" para pre-cadastro.                                    |
|                                                                          |
|@Author<Antonio Dantas>                                                   |
|@since<16/04/2014>                                                        |
|@version<Nil>                                                             |
|                                                                          |
|@parameters:                                                              |
|@Receive                                                                  |
|@return                                                                   |
+-----------+--------------------------------------------------------------+
|Uso        |FIEMT - FederaCAO das Industrias no Estado de Mato Grosso     |
+-----------+--------------------------------------------------------------+
*/
Static Function fIntSige()
Local _cPeriodo		:= space(TamSX3("ZP4_XCOD")[1])  
Local _cDescrPer	:= space(TamSX3("ZP4_XDESCR")[1])  
Local _dDataINI		:= CTOD("  /  /  ")
Local _dDataFIM		:= CTOD("  /  /  ")   
Local _oModel   	:= FWModelActive()
Local _oStruZP1 	:= _oModel:GetModel("ZP1MASTER")
Local _cConvenio 	:= _oStruZP1:GetValue("ZP1_XCONV") 
Local _cTpSige 		:= _oStruZP1:GetValue("ZP1_XSIGE") 
Private _oMdlZP3 	:= _oModel:GetModel("ZP3DETAIL")		//-- Matriculas
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Declara PRIVATE os Objetos da Sessao                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SetPrvt("_oDlgSIGE","_oSayCP","_oGetCodCP","_oGetDesc","_oSayDtIni","_oGetDTIni","_oSayDtFim","_oGetDTFim","_oBtnOK","_oBtnESC")
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Definicao do Dialog e todos os seus componentes.                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
_oDlgSIGE   := MSDialog():New( 161,242,371,811,"Integração SIGE",,,.F.,,,,,,.T.,,,.T. )
//-- Gets relativos a Identificacao do Periodo [Codigo e Descricao]
_oSayCP		:= TSay():New( 032,032,{||"Periódo:"},_oDlgSIGE,,,.F.,.F.,.F.,.T.,CLR_BLACK,CLR_WHITE,020,008)
_oGetCodCP	:= TGet():New( 030,054,{|u| If(PCount()>0,_cPeriodo:=u,_cPeriodo)},_oDlgSIGE		,035,008,'@!',{|| fVldPerio(@_cPeriodo,@_cDescrPer,@_dDataINI,@_dDataFIM) },CLR_BLACK,CLR_WHITE,,,,.T.,"",,,.F.,.F.,,.F.,.F.,"ZP4","_cPeriodo",,)
_oGetDesc	:= TGet():New( 030,095,{|u| If(PCount()>0,_cDescrPer:=u,_cDescrPer)},_oDlgSIGE	,165,008,'@!',,CLR_BLACK,CLR_WHITE,,,,.T.,"",,,.F.,.F.,,.F.,.F.,"","_cDescrPer",,)
_oGetDesc:Disable()
//-- Gets relativos Data Inicial
_oSayDtIni  := TSay():New( 050,032,{||"Data Inicial:"},_oDlgSIGE,,,.F.,.F.,.F.,.T.,CLR_BLACK,CLR_WHITE,032,008)
_oGetDTIni  := TGet():New( 048,064,{|u| If(PCount()>0,_dDataINI:=u,_dDataINI)},_oDlgSIGE		,045,008,'@D',,CLR_BLACK,CLR_WHITE,,,,.T.,"",,,.F.,.F.,,.F.,.F.,"","_dDataINI",,)
_oGetDTIni:Disable()
//-- Gets relativos Data Final 
_oSayDtFim  := TSay():New( 050,117,{||"Final:"},_oDlgSIGE,,,.F.,.F.,.F.,.T.,CLR_BLACK,CLR_WHITE,015,008)
_oGetDTFim  := TGet():New( 048,133,{|u| If(PCount()>0,_dDataFIM:=u,_dDataFIM)},_oDlgSIGE		,045,008,'@D',,CLR_BLACK,CLR_WHITE,,,,.T.,"",,,.F.,.F.,,.F.,.F.,"","_dDataFIM",,)
_oGetDTFim:Disable()
//-- Botoes da Interface 
_oBtnOK     := TButton():New( 075,186,"Confirma"	,_oDlgSIGE,{|| Processa({|| u_ViewMatr(_oModel,_cConvenio,_cTpSige,_dDataINI,_dDataFim)} ,"Importando Beneficiários do SIGE...") ,_oDlgSIGE:End() }	,037,012,,,.T.,.T.,,("Confirmar?"))
_oBtnESC    := TButton():New( 075,226,"Cancela"	,_oDlgSIGE,{|| _oDlgSIGE:End() }								,037,012,,,.T.,.T.,,("Confirmar?"))
_oDlgSIGE:Activate(,,,.T.)
Return 

/*
+--------------------------------------------------------------------------+
|{Protheus.doc}<fVldPerio>                                                 |
| Funcao de Validacao do Codigo do Periodo informada e inicializacao       |
| das variaveis da Interface da funcao que chamou.                         |
|                                                                          |
|@Author<Antonio Dantas>                                                   |
|@since<16/04/2014>                                                        |
|@version<Nil>                                                             |
|                                                                          |
|@parameters:                                                              |
|@Receive                                                                  |
|< _cPeriodo (c) - Codigo do Periodo que sera Validado                     |
| _cDescrPer (c) - Descricao do Perido selecionado                         |
|  _dDataINI (d) - Data Inicial do Periodo Selecionado                     |
|  _dDataFIM (d) - Data Final do Periodo Selecionado                       |
|>                                                                         |
|@return                                                                   |
|<   _lReturn  (L) - (.T.) - Codigo informado/Selecionado e Valido         |
|                    (.F.) - Codigo informado/Selecionado NAO e Valido     |
|>                                                                         |
+-----------+--------------------------------------------------------------+
|Uso        |FIEMT - FederaCAO das Industrias no Estado de Mato Grosso     |
+-----------+--------------------------------------------------------------+
*/
Static Function fVldPerio(_cPeriodo,_cDescrPer,_dDataINI,_dDataFIM)
Local _lReturn		:= .F.
_lReturn := ExistCpo("ZP4",_cPeriodo)
If _lReturn
	dbSelectArea("ZP4")
	ZP4->(dbSetOrder(1))
	ZP4->(dbSeek(FwxFilial("ZP4")+_cPeriodo))
	_cPeriodo			:= ZP4->ZP4_XCOD
	_cDescrPer			:= ZP4->ZP4_XDESCR
	_oGetDesc:cText 	:= ZP4->ZP4_XDESCR
	_oGetDesc:Refresh()
	_dDataINI			:= ZP4->ZP4_XDTINI
	_oGetDTIni:cText 	:= ZP4->ZP4_XDTINI
	_oGetDTIni:Refresh()
	_dDataFIM 			:= ZP4->ZP4_XDTFIM 
	_oGetDTFim:cText 	:= ZP4->ZP4_XDTFIM 
	_oGetDTFim:Refresh()
Endif
Return _lReturn



/*
+--------------------------------------------------------------------------+
|{Protheus.doc}<ViewMatr>                                                  |
| Rotina de integracao SIGE x PROTHEUS, importa informacoes das Matriculas |
| do SIGE para Protheus relacionados as Matriculas para pagamento do       |
| PRONATEC dentro de um determinado periodo fornecido como argumento.      |
| Imputa dados nas seguintes Tabelas:                                      |
|   [ZP0] - ALUNOS                                                         |
|   [ZP3] - Conveniados                                                    |
|                                                                          |
|@Author<Antonio Dantas>                                                   |
|@since<16/04/2014>                                                        |
|@version<Nil>                                                             |
|                                                                          |
|@parameters:                                                              |
|@Receive                                                                  |
|<    _cTpSige (c) - Tipo de Pagamento SIGE (Convenio)                     |
|    _dDataINI (d) - Data inicial do Periodo a Consultar                   |
|    _dDataFim (d) - Data Final do Periodo a Consultar                     |
|>                                                                         |
|@return                                                                   |
|<   _lReturn  (L) - (.T.) - A consulta encontrou dados                    |
|                    (.F.) - A consulta NAO encontrou dados.               |
|>                                                                         |
+-----------+--------------------------------------------------------------+
|Uso        |FIEMT - FederaCAO das Industrias no Estado de Mato Grosso     |
+-----------+--------------------------------------------------------------+
*/
User Function ViewMatr(_oModel,_cConvenio,_cTpSige,_dDataINI,_dDataFim)
Local _lReturn		:= .t. 
Local _nPos 		:= 0   
Local _nCtaREG		:= 0
Local _nCtaNEW		:= 0  
Local _nCtaCPF		:= 0    
Local _nCount		:= 0
Local _aFilds		:= {}
Local _nCtaA 		:= 0  
Local _nTLin 		:= 0
Local _lPreCad 		:= .F.  
Local _lCPF_OK 		:= .F.  
Local _lNoFirstT	:= .F.
Local _cOcorCPF 	:= ""
Local _cUnOper		:= ""      
Local _cCPF 		:= ""
Local _lAchou 		:= .F.   
Local _cSIGE_TP 	:= ""        
Local _ChrBreak		:= Chr(13)+Chr(10)  
Local _cTextLOG		:= ""  
Local _nOperMOD		:= 0
Local _oMdlZP3		:= Nil       
Local _cPathRoot	:= GetNewPar("MV_XLOGPRO","\Pronatec\Logs\")   
Local _aDeParaUN	:=	{	{"1","03MT0002"	,"Senai Cuiaba"				} ,;
							{"2","03MT0012"	,"Escola da Construção"		} ,;
							{"3","03MT0004"	,"Varzea Grande"			} ,;
							{"4","03MT0003"	,"Rondopolis"				} ,;
							{"5","03MT0005"	,"Barra do Garças"			} ,;
							{"6","03MT0010"	,"Juína"					} ,;
						 	{"7","03MT0008"	,"Cáceres"					} ,;
							{"8","03MT0007"	,"Sinop"   					} ,;
							{"A","03MT0011"	,"SENAI COLIDER"			} ,;
							{"B","03MT0001"	,"SENAI DEPART RE"			} }
Private _nRegs		:= 0
Private _cFileLog	:= CriaTrab(Nil,.F.)
Private _nHdl_log	:= fCreate(_cPathRoot + cFilAnt + "_" + FunName()+"_"+ProcName() + _cFileLog +"_LOG.txt")    
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Salva o Codigo da Operacao e modifica operacao para 4 - ALteracao.       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
_nOperMOD 			:= _oModel:NOPERATION
_oModel:NOPERATION 	:= 4
//-- se Modificada na Funcao de Integracao com SIGE     
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ PERMITE que a Grid de Conveniados seja editavel                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
_oModel:GetModel("ZP3DETAIL"):SetNoInsertLine(.F.)
_oModel:GetModel("ZP3DETAIL"):SetNoDeleteLine(.F.)
_oModel:GetModel("ZP3DETAIL"):SetNoUpdateLine(.F.) 
_oModel:GetModel("ZP3DETAIL"):SetOnlyQuery(.F.)    			//-- Indica que o model e somente para consulta      
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ *****ATENCAO:                                                            ³
//³ Por mais que possa parece estranho, e isso mesmo, DESATIVAMOS e ATIVAMOS ³
//³ o model para aplicas a mudanca das propriedades, senao, nao funciona !!  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
_oModel:GetModel("ZP3DETAIL"):DeActivate() 	  		//-- Desativa 
_oModel:GetModel("ZP3DETAIL"):Activate()			//-- aTIVA	
_oMdlZP3 := _oModel:GetModel("ZP3DETAIL")	   		//-- Matriculas
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Limpa a GRID de Dados para ZP7 - Matriculas                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
_oMdlZP3:clearData() 
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta cabecalho do Lod de Importacao                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
_cTextLOG := "#RELATORIO DE LOG - PRE CADASTRO - "+DTOC(date())+" as "+time()+CHR(13)+CHR(10) 
_cTextLOG += "       Periodo: "+ZP4->ZP4_XCOD+CHR(13)+CHR(10) 
fWrite(_nHdl_Log,_cTextLOG,Len(_cTextLOG)) != Len(_cTextLOG)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//| Identifica o Codigo correspondete da Unidade Operacional no SIGE   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
_nPos := aScan(_aDeParaUN,{|x| x[2]==cFilAnt})
If _nPos > 0
	_cUnOper := _aDeParaUN[_nPos,1]
Else                                                 	
	Aviso(FunName()+"/"+ProcName(),"Unidade Operacional não Idenficada no De Para de Filiais, Favor Verificar com Administrador do Sistema!",{"OK"})
	_lReturn := .F.
	Return _lReturn
Endif	
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//| Executa consulta SQL na base SIGE para importar informcoes.        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If _lReturn .and. Iif(_lReturn, u_GetMatric(_cUnOper,_dDataINI,_dDataFim),.f.)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//| Loop na Tabela de Matriculas para Gravar as informacoes nas        ³
	//| respectivas tabelas do Prothes                                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	//-- Numero de registros a processar [] foi inicializado na funcao [GetMatric()]
	ProcRegua(_nRegs)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//| Reposiciona o ponteiro para inicio do arquivo temporario.        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	TRBMAT->(dbGoTop()) 
	Do While TRBMAT->(!eof())    
		_nCtaREG++   
		If _nCtaREG == 1
			//-- Apresenta um Cabecalho
			_cTextLOG :=	PadR("Tp SIGE" 		,TamSx3("ZP1_XSIGE")[1])+";"+;
							PadR("Evento" 		,TamSx3("ZP7_XEVENT")[1])+";"+;
							PadR("Matricula"	,TamSx3("ZP7_XMATRI")[1])+";"+;
							PadR("Codigo" 		,TamSx3("ZP0_XCOD")[1])+";"+;
							PadR("Nome"			,TamSx3("ZP0_XNOME")[1])+";"+;
							PadR("CPF" 			,TamSx3("ZP0_XCPF")[1])+";"+;
							PadR("Dt Nasc." 	,TamSx3("ZP0_XDTNAS")[1])+";"+;
							PadR("Tip Pgto" 	,TamSx3("TIPO_PGTO")[1])+";"+;	
							PadR("Ocorrencia"	,40)+CHR(13)+CHR(10)      
			fWrite(_nHdl_Log,_cTextLOG,Len(_cTextLOG)) != Len(_cTextLOG)
		Endif		
		//-- Implementa Termometro
		IncProc("Total de Registros:"+Alltrim(Transform(_nRegs,"@E 999,999,999"))+"|  Lidos:"+Alltrim(Transform(_nCtaREG,"@E 999,999,999")))   
		_cCPF 		:= PadR(Alltrim(TRBMAT->ZP0_XCPF),TamSX3("ZP0_XCPF")[1])  
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//| Garante que so ira Validar Matriculas do Convenio Selecionado      ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If _cTpSige != PadR(Alltrim(TRBMAT->TIPO_PGTO),TamSX3("ZP1_XSIGE")[1])   
			TRBMAT->(dbskip())
			Loop
		Endif
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//| Se CPF estiver em branco                                           ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If Alltrim(Alltrim(TRBMAT->ZP0_XCPF)) == ""
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//| Se nao for valido, grava no LOG                                    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			_cTextLOG := 	TRBMAT->ZP1_XSIGE+";"+;
							TRBMAT->ZP7_XEVENT+";"+;
							TRBMAT->ZP7_XMATRI+";"+;
							TRBMAT->ZP0_XCOD+";"+;
							TRBMAT->ZP0_XNOME+";"+;
							TRBMAT->ZP0_XCPF+";"+;
							DTOC(TRBMAT->ZP0_XDTNAS)+";"+;
							TRBMAT->TIPO_PGTO+";"+;	
							"Ocorrência: CPF em Branco. INVALIDO."+CHR(13)+CHR(10)      
			fWrite(_nHdl_Log,_cTextLOG,Len(_cTextLOG)) != Len(_cTextLOG)
			TRBMAT->(dbskip())
			Loop
		Endif      
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//| Verifrica se o Beneficiario esta foi cadastrado Neste convenio     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("ZP3")					//-- Conveniados
		ZP3->(dbSetOrder(1)) 	 			//-- CPF + Convenio 
		If (ZP3->(dbSeek(FwxFilial("ZP3")+_cCPF+_cTpSige)))
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//| Se nao for valido, grava no LOG                                    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			_cTextLOG := 	TRBMAT->ZP1_XSIGE+";"+;
							TRBMAT->ZP7_XEVENT+";"+;
							TRBMAT->ZP7_XMATRI+";"+;
							TRBMAT->ZP0_XCOD+";"+;
							TRBMAT->ZP0_XNOME+";"+;
							TRBMAT->ZP0_XCPF+";"+;
							DTOC(TRBMAT->ZP0_XDTNAS)+";"+;
							TRBMAT->TIPO_PGTO+";"+;	
							"Ocorrência: Beneficiario já foi importado Convenios."+CHR(13)+CHR(10)      
			fWrite(_nHdl_Log,_cTextLOG,Len(_cTextLOG)) != Len(_cTextLOG)
			TRBMAT->(dbskip())
			Loop
		Endif 
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//|                                                                    ³
		//|  [ZP0 - ALUNOS] - Trata as informacoes da tabela de Alunos         ³
		//|                                                                    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//| Trata o cadasro do Aluno, se ja existe atualiza os dados, se nao   ³
		//| inclui o registro                                                  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("ZP0")
		ZP0->(dbSetOrder(1)) 	 			//--  CPF do Aluno 
		_lAchou := (ZP0->(dbSeek(FwxFilial("ZP0")+_cCPF)))
		ZP0->(RecLock("ZP0",!_lAchou))
		If !_lAchou
			Replace ZP0->ZP0_FILIAL	With FWxFilial("ZP0")	 			//-- Filial
			Replace ZP0->ZP0_XCOD	With TRBMAT->ZP0_XCOD	 			//-- Codigo  
			_nCtaNEW++
		Endif 
		Replace	ZP0->ZP0_XNOME	With TRBMAT->ZP0_XNOME	//-- Nome
		Replace	ZP0->ZP0_XCPF	With TRBMAT->ZP0_XCPF	//-- CPF
		Replace	ZP0->ZP0_XRG	With TRBMAT->ZP0_XRG		//-- Identidade
		Replace	ZP0->ZP0_XDTNAS	With TRBMAT->ZP0_XDTNAS	//-- Nascimento
		Replace	ZP0->ZP0_XMAE	With TRBMAT->ZP0_XMAE	//-- Nome da Mae
		Replace	ZP0->ZP0_XENDER	With TRBMAT->ZP0_XENDER	//-- Endereco
		Replace	ZP0->ZP0_XNR	With ""					//-- Numero
		Replace	ZP0->ZP0_XLOGRA	With ""					//-- Logradouro
		Replace	ZP0->ZP0_XCOMPL	With TRBMAT->ZP0_XCOMPL	//-- Complemento
		Replace	ZP0->ZP0_XBAIRR	With TRBMAT->ZP0_XBAIRR	//-- Bairro
		Replace	ZP0->ZP0_XCIDAD	With TRBMAT->ZP0_XCIDAD	//-- Cidade
		Replace	ZP0->ZP0_XUF	With TRBMAT->ZP0_XUF		//-- UF
		Replace	ZP0->ZP0_XCEP	With TRBMAT->ZP0_XCEP	//-- CEP
		Replace	ZP0->ZP0_XTEL1	With TRBMAT->ZP0_XTEL1	//-- Telefone 1
		Replace	ZP0->ZP0_XTEL2	With TRBMAT->ZP0_XTEL2	//-- Telefone 2
		Replace	ZP0->ZP0_XMAIL	With TRBMAT->ZP0_XMAIL	//-- E-mail
		Replace	ZP0->ZP0_XNASCI	With TRBMAT->ZP0_XNASCI	//-- Nascionalida
		Replace	ZP0->ZP0_XNATUR	With TRBMAT->ZP0_XNATUR	//-- Naturalidade
		Replace	ZP0->ZP0_XUFNAT	With TRBMAT->ZP0_XUFNAT	//-- UF Naturalid
		Replace	ZP0->ZP0_XBCO	With TRBMAT->ZP0_XBCO	//-- Banco
		Replace	ZP0->ZP0_XAGENC	With TRBMAT->ZP0_XAGENC	//-- Agencia
		Replace	ZP0->ZP0_XDVAGE	With TRBMAT->ZP0_XDVAGE	//-- DV Agencia
		Replace	ZP0->ZP0_XCTA	With TRBMAT->ZP0_XCTA	//-- C. Corrente
		Replace	ZP0->ZP0_XDVCTA	With TRBMAT->ZP0_XDVCTA	//-- DV CTA
		Replace	ZP0->ZP0_XOPBCO	With TRBMAT->ZP0_XOPBCO	//-- Cod Operacao
		Replace	ZP0->ZP0_XCARTA	With TRBMAT->ZP0_XCARTA	//-- Nr Cartao
		Replace	ZP0->ZP0_XCPFRE	With TRBMAT->ZP0_XCPFRE	//-- CPF Responsa
 		ZP0->(MsUnLock())
 		ZP0->(dbCommit())

		dbSelectArea("ZP3")
		ZP3->(dbSetOrder(1)) 	 		   	
		ZP3->(RecLock("ZP3",.t.))
		Replace ZP3->ZP3_FILIAL	With FWxFilial("ZP3")	 	//-- Filial
		Replace ZP3->ZP3_XMATRI	With TRBMAT->ZP7_XMATRI 	//-- Matricula    
		Replace ZP3->ZP3_XALUNO	With TRBMAT->ZP0_XCOD	 	//-- Aluno
		Replace ZP3->ZP3_XCPF	With TRBMAT->ZP0_XCPF	 	//-- CPF Aluno  
		Replace ZP3->ZP3_XNOME 	With TRBMAT->ZP0_XNOME		//-- Nome do Conveniado
		Replace ZP3->ZP3_XCONVE	With _cConvenio	 			//-- Convenio
		Replace ZP3->ZP3_XIDCON	With TRBMAT->ZP0_XCARTA		//-- ID Cartao
		Replace ZP3->ZP3_XEVENT	With TRBMAT->ZP7_XEVENT	 	//-- Evento
		Replace ZP3->ZP3_XDESEV	With ""	 					//-- Descricao
		Replace ZP3->ZP3_XMODAL	With TRBMAT->ZP7_XMODAL	 	//-- Modalidade
		Replace ZP3->ZP3_XDESMO	With TRBMAT->ZP7_XDESMO	 	//-- Descr Modali
		Replace ZP3->ZP3_XATECN	With TRBMAT->ZP7_XATECN	 	//-- Area Tecnolo
		Replace ZP3->ZP3_XDESAT	With TRBMAT->ZP7_XDESAT	 	//-- Descr A Tecn
		Replace ZP3->ZP3_XCURSO	With TRBMAT->ZP7_XCURSO	 	//-- Curso
		Replace ZP3->ZP3_XDESCS	With TRBMAT->ZP7_XDESCS	 	//-- Descr Curso
		Replace ZP3->ZP3_XTURNO	With TRBMAT->ZP7_XTURNO	 	//-- Turno
		Replace ZP3->ZP3_XSTATU	With "P"	 				//-- Situacao - P=Em preparacao
 		ZP3->(MsUnLock())
		ZP3->(dbCommit())

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Nao considera o Primeiro Registro como Inclussao na GRID, pois ja existe um      ³
		//³ registro em Branco que sera modificado                                           ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If _lNoFirstT
			_oMdlZP3:AddLine()
		Endif		
		_lNoFirstT	:= .T.
		_nCount		:= ZP3->(FCount())
		_nCtaA 		:= 0
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Gera [_aColsNEW], que sera apresentado ao operador. Devera obrigatoriamente ser  ³
		//³ definida PRIVATE na funcao que chamou                                            ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		For _nCtaA := 1 to _nCount
			_oMdlZP3:LoadValue(ZP3->(FieldName(_nCtaA)),ZP3->(FieldGet(_nCtaA)))
		Next _nCtaA
		TRBMAT->(dbskip())  
	Enddo
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Indica que terminou todas as importacoes de forma OK, deve ser inicializada      ³
	//³ PRIVATE na funcao que chamou.                                                    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	_lViewOK := .T.
Endif    
If Select("TRBMAT")>0
	TRBMAT->(DbCloseArea())
Endif  
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Devolve o tipo de OPERACAO                                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
_oModel:NOPERATION := _nOperMOD
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ NAO PERMITE que a Grid de Conveniados seja editavel                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
_oModel:GetModel("ZP3DETAIL"):SetNoInsertLine(.T.)
_oModel:GetModel("ZP3DETAIL"):SetNoDeleteLine(.T.)
_oModel:GetModel("ZP3DETAIL"):SetNoUpdateLine(.T.)
//_oModel:GetModel("ZP3DETAIL"):SetOnlyQuery(.T.)    //-- Indica que o model e somente para consulta
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ ***** ATENCAO:                                                           ³
//³ Por mais que possa parece estranho, e isso mesmo, DESATIVAMOS e ATIVAMOS ³
//³ o model para aplicas a mudanca das propriedades, senao, nao funciona !!  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
_oModel:GetModel("ZP3DETAIL"):DeActivate() 	   		//-- Desativa 
_oModel:GetModel("ZP3DETAIL"):Activate()			//-- aTIVA	  
fClose(_nHdl_Log) 
If Aviso(FunName()+"/"+ProcName(),"Integração Encerrada. Deseja imprimir o arquivo de Log ["+_cFileLog+"_LOG.txt] neste momento?", {"Sim","Não"}) == 1
	u_SN069BX(_cPathRoot + cFilAnt + "_" + FunName() +"_"+ ProcName() + _cFileLog +"_LOG.txt")
Endif    
Return _lReturn 