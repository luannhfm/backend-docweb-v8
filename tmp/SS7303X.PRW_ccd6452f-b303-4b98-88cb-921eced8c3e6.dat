#INCLUDE "PROTHEUS.CH"
#INCLUDE "TOPCONN.CH"
#INCLUDE "RWMAKE.CH"
#INCLUDE "FONT.CH"
#INCLUDE "COLORS.CH"
#INCLUDE "PARMTYPE.CH"
#INCLUDE "FWMBROWSE.CH"
#INCLUDE "FWMVCDEF.CH"
#INCLUDE "FWCOMMAND.CH"
/*
--------------------------------------------------------------------------------
{Protheus.doc} <SS7303X>
 Modelo MVC para: 14 - Cadastro dos Polos de Vendas
 Pai	: ZCD - Cadastro dos Polos de Vendas 
 Filha	: ZCE - Cidades dos Polos de Vendas 	  

@author<Antonio Dantas>
@since<24/02/2015>
@version<1.00>
@receive<Nil>
@return<Nil>
@example<Nil>
@see<Nil>
--------------------------------------------------------------------------------
*/
User Function SS7303X()
Local _cAlias  	:= "ZCD"
Local _cTitle  	:= "Polos de Vendas"
Local _oBrowse 	:= FWMBrowse():New()
Private aRotina	:= MenuDef()
_oBrowse:SetAlias(_cAlias)
_oBrowse:SetDescription(_cTitle)
//-- Define a Legenda para modelo
_oBrowse:AddLegend("ZCD_ATIVO == 'S' "	,"GREEN" 	,"Ativa" 			)
_oBrowse:AddLegend("ZCD_ATIVO == 'N' "	,"RED"		,"Desativada" 	)
_oBrowse:Activate()
Return NIL

/*
--------------------------------------------------------------------------------
{Protheus.doc} <MenuDef>
 Definicao do MenuDef

@author<Antonio Dantas>
@since<24/02/2015>
@version<1.00>
@receive<Nil>
@return< _aRotina (a) - Vetor com as opcoes de Menu >
@example<Nil>
@see<Nil>
--------------------------------------------------------------------------------
*/
Static Function MenuDef()
Local _aRotina 		:= {}
ADD OPTION _aRotina TITLE "Pesquisar"	ACTION "PESQBRW"			OPERATION 1 ACCESS 0
ADD OPTION _aRotina TITLE "Visualizar"	ACTION "VIEWDEF.SS7303X"	OPERATION 2 ACCESS 0
ADD OPTION _aRotina TITLE "Incluir"	ACTION "VIEWDEF.SS7303X"	OPERATION 3 ACCESS 0
ADD OPTION _aRotina TITLE "Alterar"	ACTION "VIEWDEF.SS7303X"	OPERATION 4 ACCESS 0
ADD OPTION _aRotina TITLE "Excluir"	ACTION "VIEWDEF.SS7303X"	OPERATION 5 ACCESS 0
ADD OPTION _aRotina TITLE "Imprimir"	ACTION "VIEWDEF.SS7303X"	OPERATION 8 ACCESS 0  
ADD OPTION _aRotina TITLE "Copiar"		ACTION "VIEWDEF.SS7303X"	OPERATION 9 ACCESS 0
Return _aRotina

/*
--------------------------------------------------------------------------------
{Protheus.doc} <ModelDef>
 Definicao do ModelDef

@author<Antonio Dantas>
@since<24/02/2015>
@version <1.00>
@receive<Nil>
@return< _oModel (o) - Objeto com a Definicao do Modelo >
@example<Nil>
@see<Nil>
--------------------------------------------------------------------------------
*/
Static Function ModelDef()
Local _oStruZCD := FWFormStruct(1,"ZCD")
Local _oStruZCE := FWFormStruct(1,"ZCE")
Local _oModel   := Nil
_oModel := MPFormModel():New("SS7303XP",/*PreValid*/,/*Valid*/,/*Grava*/ )
_oModel:AddFields("ZCDMASTER"	, /*cOwner*/	,_oStruZCD,/*Pre-Validacao*/, /*Pos-Validacao*/)
_oModel:AddGrid("ZCEDETAIL"		, "ZCDMASTER"	,_oStruZCE,{|_oMdlGrd, _nLin, _cAction, _cField| fVldLine(_oMdlGrd, _nLin, _cAction, _cField )} )
//-- 
_oModel:SetPrimaryKey({"ZCD_FILIAL","ZCD_CODIGO"})
_oModel:SetRelation("ZCEDETAIL", {{"ZCE_FILIAL", "FwxFilial('ZCE')"},{"ZCE_POLO", "ZCD_CODIGO"}},ZCE->(IndexKey(1)))
_oModel:SetDescription("Item das Metas de Venda")
_oModel:GetModel("ZCDMASTER"):SetDescription("Polos")
_oModel:GetModel("ZCEDETAIL"):SetDescription("Cidades")
_oModel:GetModel("ZCEDETAIL"):SetUniqueLine({"ZCE_FILIAL","ZCE_POLO","ZCE_CODMUN"}) 
Return _oModel


/*
--------------------------------------------------------------------------------
{Protheus.doc} <ViewDef>
 Definicao do ViewDef

@author<Antonio Dantas>
@since<24/02/2015>
@version <1.00>
@receive<Nil>
@return< _oView (o) - Objeto com a Definicao do Visao do Modelo  >
@example<Nil>
@see<Nil>
--------------------------------------------------------------------------------
*/
Static Function ViewDef()
Local _oStruZCD 		:= FWFormStruct(2,"ZCD",/*{|_cFild| xDefFilds(_cFild)}*/)
Local _oStruZCE 		:= FWFormStruct(2,"ZCE",/*{|_cFild| xDefFilds(_cFild)}*/)
Local _oModel   		:= FWLoadModel("SS7303X")
Local _oView    		:= FWFormView():New()
//+---------------------------------------------------------------------+
//|  Monta Box Horizontal - Superior                                    |
//+---------------------------------------------------------------------+
_oView:SetModel(_oModel)
_oView:CreateHorizontalBox("SUPERIOR",50)
//-- 1.1) ZCD - Polos de Vendas  
_oView:CreateVerticalBox("ZCD_POL",100,"SUPERIOR")
_oView:AddField("VIEW_ZCD",_oStruZCD,"ZCDMASTER")
_oView:EnableTitleView("VIEW_ZCD","Polos de Vendas")
_oView:SetOwnerView("VIEW_ZCD","ZCD_POL")
//+---------------------------------------------------------------------+
//|  Monta Box Horizontal - Inferior                                    |
//+---------------------------------------------------------------------+
_oView:CreateHorizontalBox("INFERIOR",50)
//-- 1.2) ZCE - Cidades do Polo de Vendas 
_oView:CreateVerticalBox("ZCE_CID",100,"INFERIOR")
_oView:AddGrid("VIEW_ZCE",_oStruZCE,"ZCEDETAIL", /*bLinePre*/,/*bLinePost*/, /*bPreVal*/,/*bPosVal*/, /*BLoad*/) 
_oView:EnableTitleView("VIEW_ZCE","Cidades do Polo de Vendas")
_oView:SetOwnerView("VIEW_ZCE","ZCE_CID")
//-- Removes os campos da Grid
_oStruZCE:RemoveField("ZCE_FILIAL")
_oStruZCE:RemoveField("ZCE_POLO")
_oStruZCE:RemoveField("ZCE_TP")  
Return _oView

/*
--------------------------------------------------------------------------------
{Protheus.doc} <fVldLine>
 Valida a já existencia da Cidade informada para o Polo.
 Não permite modificar ou excluir a CIDADE SEDE.

@author<Antonio Dantas>
@since<25/02/2015>
@version <1.00>
@receive<Nil>
<   _oMdlGrd (o) - oModelGrid
       _nLin (n) - Numero da Linha do Grid "Apos Carregado o Model"
    _cAction (c) - Define a Acao na GRID: 
                   CANSETVALUE - Pre-Validação Antes de Imputar o valor
                      SETVALUE - Validação apos o ENTER; Informou o valor 
                        DELETE - Apagou a Linha
                      UNDELETE - Recuperou uma linha deletada
     _cField (c) - Nome do campo afetado.
>
@return
<   _lReturn (l) - (.t.) Pode Executar a Acao
                   (.f.) Nao Pode executar a Acao 
>
@example<Nil>
@see<Nil>
--------------------------------------------------------------------------------
*/
Static Function fVldLine(_oMdlGrd, _nLin, _cAction, _cField)
Local _lReturn		:= .T.       
Local _oModel			:= FWModelActive()      
Local _nOper			:= _oModel:GetOperation()   		//-- Define a Acao no FORMULARIO
Local _oMdlItem		:= _oModel:GetModel("ZCEDETAIL")	//-- Item da Meta    
Local _aHearItem		:= _oMdlItem:aHeader
Local _aColsItem		:= _oMdlItem:aCols  
Local _N				:= _oMdlItem:NLine					//-- Posicao do Ponteiro na Sessao 
Local _nPCod			:= aScan(_aHearItem,{|X| UPPER(AllTrim(X[2]))=="ZCE_CODMUN"})
Local _nPTp			:= aScan(_aHearItem,{|X| UPPER(AllTrim(X[2]))=="ZCE_TP"})
Local _cTipo			:= _aColsItem[_N,_nPTp]  
//+--------------------------------------------------------------------+
//| ATENÇÃO:                                                           |
//| Tive que incluir aqui a verificação para campo "ZCE_DESCRI", uma   |
//| vez que quanto o campo "ZCE_CODMUN" e preenchido GATILHA o campo   |
//| "ZCE_DESCRI" o gatilho modifica o FOCO, antes da saida do campo.   |
//+--------------------------------------------------------------------+
//-- Se estiver 3=Incluindo ou 4=Alterando 
If _nOper == 3 .or. _nOper == 4 
	Do Case
		Case Upper(_cAction) == "CANSETVALUE"	//-- Pre-Validação Antes de Imputar o valor no campo
			//Nil 
		Case Upper(_cAction) == "SETVALUE"		//-- Validação apos o ENTER; Informou o valor no campo
			If _cTipo == "S" .and. ( _cField == "ZCE_CODMUN" .or. _cField == "ZCE_DESCRI" )
				Help(,,"HELP",,"Cidade SEDE não pode ser modificada!",1,0)    
				_lReturn := .f. 
			Else
				If _cField == "ZCE_CODMUN" .or. _cField == "ZCE_DESCRI"
					_lReturn := fVldCid(M->ZCE_CODMUN,_N,_aColsItem,_nPCod) 
				Endif 
			Endif 				
		Case Upper(_cAction) == "DELETE"		//-- Apagou a Linha
			If _cTipo == "S" 
				Help(,,"HELP",,"Cidade SEDE não pode ser EXCLUIDA!",1,0)    
				_lReturn := .f. 
			Endif 				
		Case Upper(_cAction) == "UNDELETE"		//-- Recuperou uma linha apagada
			//Nil 
	EndCase
Endif
Return _lReturn

/*
--------------------------------------------------------------------------------
{Protheus.doc} <fVldCid>
 Valida o Codigo da Cidade que esta sendo incluida.
 Regra: Não permite Incluir se a Cidade já Existir para este Polo  
 	
@author<Antonio Dantas>
@since<25/02/2015>
@version <1.00>
@receive<Nil>
<     _cCodCid (c) - Codigo da Cidade que esta sendo Incluida 
       _NLinha (n) - Posição (LINHA) em que se encontra o registro que esta 
                     sendo validado
    _aColsItem (a) - Array contendo as informações do [aCols] da grid em edição
        _nPCid (n) - Posição do campo "Codigo da Cidade" na Grid/Array
>
@return
<   _lReturn (l) - (.t.) Se o codigo estiver OK
                   (.f.) Se existir algum empedimento para inclusão do
                         codigo da Cidade  
>
@example<Nil>
@see<Nil>
--------------------------------------------------------------------------------
*/
Static Function fVldCid(_cCodCid,_NLinha,_aColsItem,_nPCid) 
Local _lReturn 	:= .t.
Local _nTAMaCol := Len(_aColsItem[_NLinha])
_nCtaA := 0
For _nCtaA := 1 to Len(_aColsItem)
	If _nCtaA != _NLinha
		If _aColsItem[_nCtaA,_nPCid] == _cCodCid
			_lReturn := .f.
			If _aColsItem[_nCtaA,_nTAMaCol] 
				Help(,,"HELP",,"Confirme primeiro a exclusão da CIDADE lançada para este POLO!",1,0)    
			Else
				Help(,,"HELP",,"Já existe esta CIDADE lançada para este POLO. Verifique!",1,0)    
			Endif
		Endif 
	Endif  
Next _nCtaA
Return _lReturn


/*
--------------------------------------------------------------------------------
{Protheus.doc} <fPutZCE>
  Inclui no array das "Cidades do Polo" a Cidade da Filial.  Esta Cidade é 
  OBRIGATORIA e IMUTAVEL.
	
@author<Antonio Dantas>
@since<25/02/2015>
@version <1.00>
@receive<Nil>
@return
< _lReturn (l) - (.t.) Formulario Valido
                 (.f.) Formulario Invalido
>
@example<Nil>
@see<Nil>
--------------------------------------------------------------------------------
*/
User Function fPutZCE()
Local _lReturn 	:= .t.
Local _aArea		:= GetArea() 
Local _aASM0		:= SM0->(GetArea()) 
Local _cNumCid	:= ""
Local _cUF			:= ""
//-- 
Local _oModel		:= FWModelActive() 
Local _nOper		:= _oModel:GetOperation()    
//-- 
Local _oStruZCD	:= _oModel:GetModel("ZCDMASTER")
Local _cFilial 	:= _oStruZCD:GetValue("ZCD_XFILIA")
Local _cPolo	 	:= _oStruZCD:GetValue("ZCD_CODIGO")
//-- 
Local _oStruZCE	:= _oModel:GetModel("ZCEDETAIL")
dbSelectArea("SM0")
SM0->(dbSetOrder(1))
If (SM0->(dbSeek(cEmpAnt+_cFilial)))
	_cNumCid	:= PadR(Alltrim(SM0->M0_CIDENT),TamSX3("CC2_MUN")[1])
	_cUF		:= SM0->M0_ESTENT
Endif
//-- 
dbSelectArea("CC2")
CC2->(DbOrderNickName("CC2NOMEST"))
If (CC2->(dbSeek(FwxFilial("CC2")+_cNumCid+_cUF)))
	//+---------------------------------------------------------------------------+
	//| Preenche a Grid de Cidades do Polo com as Informações da Cidado da Filial |
	//+---------------------------------------------------------------------------+
	_oStruZCE:AddLine()
	_oStruZCE:LoadValue("ZCE_FILIAL", FwxFilial("ZCE")		)
	_oStruZCE:LoadValue("ZCE_POLO"  , _cPolo					)
	_oStruZCE:LoadValue("ZCE_UF"    , CC2->CC2_EST   		)
	_oStruZCE:LoadValue("ZCE_CODMUN", CC2->CC2_CODMUN   		)
	_oStruZCE:LoadValue("ZCE_DESCRI", CC2->CC2_MUN   		)
	_oStruZCE:LoadValue("ZCE_POPULA", 0	)
	_oStruZCE:LoadValue("ZCE_CONTRI", 0	)
	_oStruZCE:LoadValue("ZCE_OBS"   , "Cidade SEDE."			)
	_oStruZCE:LoadValue("ZCE_TP"    , "S"	)
Else
	Aviso(FunName()+"/"+ProcName(),"Cidade SEDE da Filial não foi encontrada!",{"OK"})
	_lReturn 	:= .f.
Endif 
//-- 
RestArea(_aArea)
RestArea(_aASM0) 
Return _lReturn