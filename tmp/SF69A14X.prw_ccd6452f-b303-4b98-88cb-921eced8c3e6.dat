#include 'protheus.ch'

/*/{Protheus.doc} Função para trazer as turmas para o cadastro da Necessidade de Docente
    @type  Function SF69A14X
    @author Daniel Castro - TOTVS
    @since 29/06/2021
    @version version
    /*/

//=====================================================================
User Function SF69A14X(oModel)

//	Local oModelZBB     := oModel:GetModel( 'ZBBMASTER' )
	Local oModelZBC     := oModel:GetModel( 'ZBCDETAIL' )
	Local oModelZBH     := oModel:GetModel( 'ZBHDETAIL' )
	Local z
	Local aProd         := {}
	Local x

	Private aVetor		:= {}

	For z := 1 To oModelZBC:Length()
		oModelZBC:GoLine( z )
		aAdd(aProd, {.F.,oModelZBC:GetValue("ZBC_PRODUT"),;
			oModelZBC:GetValue("ZBC_DESC"),;
			oModelZBC:GetValue("ZBC_QUANT")})
	Next z
	aVetor := aClone(aProd)

	For z := 1 To oModelZBH:Length()
		oModelZBH:GoLine( z )
		If !Empty(oModelZBH:GetValue("ZBH_TURMA")) .And. !oModelZBH:IsDeleted(z)
			Processa( {|| Montatela(oModelZBH:GetValue("ZBH_TURMA")) }, "Aguarde...", "Carregando dados...",.F.)

			For x := 1 to Len(aVetor)
				If aVetor[x,1] .And. !Empty(aVetor[x,2])
					oModelZBH:SetValue("ZBH_PRODUT", aVetor[x,2])
					oModelZBH:SetValue("ZBH_QTD", aVetor[x,4])
					aVetor[x,1] := .F.
					Exit
				EndIf
			Next x
		EndIf

	Next z

Return(.T.)


//======================================================

Static Function MontaTela(cTurma)

	Local cVar     := Nil
	Local oDlg     := Nil
	Local cTitulo  := "Turma " + Alltrim(cTurma)
	Local oOk      := LoadBitmap( GetResources(), "CHECKED" )   //CHECKED    //LBOK  //LBTIK
	Local oNo      := LoadBitmap( GetResources(), "UNCHECKED" ) //UNCHECKED  //LBNO
	Local oChk     := Nil

	Private lChk     := .F.
	Private oLbx := Nil

	DEFINE MSDIALOG oDlg TITLE cTitulo FROM 0,0 TO 320,600 PIXEL

	@ 10,10 LISTBOX oLbx VAR cVar FIELDS HEADER ;
		" ", "PRODUTO","DESCRIÇÃO","QUANTIDADE" ;
		SIZE 295,105 OF oDlg PIXEL ON dblClick(aVetor[oLbx:nAt,1] := !aVetor[oLbx:nAt,1],oLbx:Refresh())

	oLbx:SetArray( aVetor )
	oLbx:bLine := {|| {Iif(aVetor[oLbx:nAt,1],oOk,oNo),;
		aVetor[oLbx:nAt,2],;
		aVetor[oLbx:nAt,3],;
		aVetor[oLbx:nAt,4]}}

	@ 140,10 CHECKBOX oChk VAR lChk PROMPT "Marca/Desmarca" SIZE 60,007 PIXEL OF oDlg ;
		ON CLICK(aEval(aVetor,{|x| x[1]:=lChk}),oLbx:Refresh())

	DEFINE SBUTTON FROM 137,230 TYPE 1 ACTION oDlg:End() ENABLE OF oDlg
	DEFINE SBUTTON FROM 137,260 TYPE 2 ACTION oDlg:End() ENABLE OF oDlg

	ACTIVATE MSDIALOG oDlg CENTER

Return
