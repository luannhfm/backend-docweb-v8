#include  'TOTVS.ch'

/*/{Protheus.doc} SFATUSXK
    @description Atualiza SXK com o intuito de trabar a contabilização online
    @type User Function
    @author Rafael Karczevski
    @since 09/08/2019
    @version 1.0
    /*/
User Function SFATUSXK()

    Processa( {|| ExecFunc(), "Aguarde...", "Gravando informações." })

Return

Static Function ExecFunc()

    Local cIdUser
    Local lTem
    Local cFile := '\bkp\new_sxk0101.dbf'
    Local nX
    Local aGrpInf := AllGroups()
    Local nCount := 0
    Local nCoun2 := 0

    dbUseArea(.t., "dbfcdxads", cFile, "DbfFile", .t., .f.)

    If DbfFile->(eof())
        MsgAlert("Arquivo modelo não encontrado."+CRLF+"Adicione o arquivo 'new_sxk0101.dbf' na pasta bkp.","Fonte: XXX")
    Else
        DbfFile->(dbGoTop())
        dbEval({|| nCount++ })
        ProcRegua( nCount )

        DbSelectArea("SXK")
        SXK->(dbSetOrder(2))
        For nX := 1 to Len(aGrpInf)

            DbfFile->(dbGoTop())
            IncProc( "Atualizando perguntas dos grupos. " + cValToChar(nX) + " de " + cValToChar(nCount))
            ProcRegua( nCount )
            nCoun2 := 0
            While DbfFile->(! Eof())
                //Inicio da gravação
                nCoun2++
                cIdUser := "G" + AllTrim(aGrpInf[nX][1][1])
                IncProc( cValToChar(nX) + "/"+ cValToChar(Len(aGrpInf)) + " - Atualizando " + AllTrim(aGrpInf[nX][1][3])+ " " + cValToChar(nCoun2) + " de " + cValToChar(nCount))
                lTem := MsSeek(DbfFile->XK_GRUPO + DbfFile->XK_SEQ + cIdUser )
                RecLock("SXK", !lTem)
                    SXK->XK_GRUPO   := DbfFile->XK_GRUPO
                    SXK->XK_SEQ     := DbfFile->XK_SEQ
                    SXK->XK_IDUSER  := cIdUser
                    SXK->XK_CONTEUD := DbfFile->XK_CONTEUD
                SXK->(MsUnlock())
                DbfFile->(DbSkip())
            EndDo

        Next
    EndIf

Return