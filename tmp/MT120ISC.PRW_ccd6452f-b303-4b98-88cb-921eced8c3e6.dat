#INCLUDE "TOTVS.CH"

/*/{Protheus.doc} MT120ISC
    @description Atualizacao do preço unitário e total do PC conforme SC
    @type User Function
    @author Microsiga
    @since 05/08/11
    @version 1.0
    /*/
User Function MT120ISC()

    // Adicionado por Paulo Schwind - 10/07/2019
    Local aAreaSC1   := SC1->(GetArea())
    Local aAreaSC7   := SC7->(GetArea())

    Local nPosNSC := aScan(aHeader,{|x| AllTrim(x[2]) == 'C7_NUMSC'})
    Local nPosITSc:= aScan(aHeader,{|x| AllTrim(x[2]) == 'C7_ITEMSC'})
    Local nPosXClS:= aScan(aHeader,{|x| AllTrim(x[2]) == 'C7_XCLSSC'})
    Local nPosXPlc:= aScan(aHeader,{|x| AllTrim(x[2]) == 'C7_XCLPLC'})
    Local _i

    // Considera somente Solicitação de Compra
    IF nTipoPed !=	2
        GDFieldPut("C7_PRECO",SC1->C1_VUNIT,n)
        GDFieldPut("C7_TOTAL",SC1->(C1_VUNIT*C1_QUANT),n)
    ENDIF

        // Adicionado - Paulo Schwind 10/07/2019
        // Se a Classificação não foi informada no Pedido, pega da SC  C1_XCLSSC
        For _i := 1 TO Len(aCols)
        
            IF ! Empty( aCols[ _i , nPosNSC ] ) .and. ! Empty( aCols[ _i , nPosITSC ] )
                aCols[ _i , nPosXClS ] := POSICIONE("SC1",1,xFilial("SC1") + aCols[ _i , nPosNSC ] + aCols[ _i , nPosITSC ], "C1_XCLSSC" )  
                aCols[ _i , nPosXPlc ] := POSICIONE("SC1",1,xFilial("SC1") + aCols[ _i , nPosNSC ] + aCols[ _i , nPosITSC ], "C1_XCLPLC" )  
            Endif

        Next _i

    RestArea(aAreaSC1)
    RestArea(aAreaSC7)

Return()
