#INCLUDE "protheus.ch"

STATIC _LSAIR := .F.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ SIFINX01 º Autor ³ Microsiga          º Data ³  24/07/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Amarracao Empresa/Filial x Cliente x Fornecedor            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ P11 - SISTEMA INDUSTRIA                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

User Function SIFINX01()
Private cCadastro := "Amarração Entidade x Cliente x Fornecedor"
Private aRotina := { {"Pesquisar","AxPesqui",0,1} ,;
{"Visualizar","U__FINX01Man(2)",0,2} ,;
{"Manutenção","U__FINX01Man(3)",0,3} ,;
{"Excluir","U__FINX01Man(5)",0,5} }

Private cString := "SZE"

dbSelectArea("SZE")
dbSetOrder(1)

dbSelectArea(cString)
mBrowse( 6,1,22,75,cString)

Return()

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³_FINX01Man ºAutor  ³Microsiga          º Data ³  24/07/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Tela de Manutencao                                         º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ P11 - SISTEMA INDUSTRIA                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

User Function _FINX01Man(nOpc)
Local nX 	 	 := 0
Local aAlterEnch := {}
Local cIniCpos   := ""
Local cFieldOk   := "AllwaysTrue"
Local cLinhaOk   := "U_FINX01LOK()"
Local cTudoOk    := "AllwaysTrue"
Local cSuperDel  := ""
Local cDelOk     := "AllwaysTrue"
Local nOpcX		 := 0
Local aButtons   := {}
Private aHeader  := {}
Private aCols    := {}
Private oDlg
Private oBrw
Private oEnch
Private aTELA[0][0]
Private aGETS[0]

IF _LSAIR
	_LSAIR := .F.
	Return()
ENDIF

cSeek := SZE->ZE_FILIAL
cWhile := "SZE->ZE_FILIAL"
FillGetDados(4,"SZE",1,cSeek,{|| &cWhile },{|| .T. },/*aNaoCpo*/,/*aSimCpo*/,,,,,aHeader,aCols)
// Atualiza o item da primeira linha
IF Empty(GdFieldGet("ZE_ITEM",1,,aHeader,aCols))
	GDFieldPut("ZE_ITEM",StrZero(1,TamSX3("ZE_ITEM")[1]),1,aHeader,aCols)
ENDIF

aEval(aCols,{|x| x[GDFieldPos("ZE_NOMEMP",aHeader)] := Posicione("SM0",1,cEmpAnt+x[GDFieldPos("ZE_CODEMP",aHeader)],"M0_NOME") })
aEval(aCols,{|x| x[GDFieldPos("ZE_NOMCLI",aHeader)] := Posicione("SA1",1,XFilial("SA1")+x[GDFieldPos("ZE_CODCLI",aHeader)]+x[GDFieldPos("ZE_LOJCLI",aHeader)],"A1_NOME") })
aEval(aCols,{|x| x[GDFieldPos("ZE_FORNEC",aHeader)] := Posicione("SA2",1,XFilial("SA2")+x[GDFieldPos("ZE_CODFOR",aHeader)]+x[GDFieldPos("ZE_LOJFOR",aHeader)],"A2_NOME") })

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Montagem da Tela de Consulta                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

aSizeAut := MsAdvSize()

DEFINE FONT oFnt 	NAME "Arial" SIZE 0, -11 BOLD

oDlg := MSDIALOG():New(0,0,500,1000, cCadastro,,,,,,,,,.T.)

If nOpc == 3
	INCLUI := .T.
	ALTERA := .F.
	nStyle := GD_INSERT+GD_UPDATE+GD_DELETE
Else
	INCLUI := .F.
	ALTERA := .F.
	nStyle := 0
Endif

oBrw := MsNewGetDados():New(1,1,1,1,nStyle,cLinhaOk,cTudoOk,"+ZE_ITEM",,,9999, cFieldOk, cSuperDel,cDelOk,oDlg, aHeader, aCols)
oBrw:oBrowse:Align := CONTROL_ALIGN_ALLCLIENT

// Habilita botão somente na opção de manutenção
IF nOpc == 3
	Aadd(aButtons,{'S4WB013N',{|| xFIN01Atu() },"Empresas","Carregar Empresas"})
ENDIF

oDlg:bInit	:= {|| EnchoiceBar(oDlg, {|| nOpcX:=1 , IIf(oBrw:TudoOk() .and. Obrigatorio(aGets, aTela),oDlg:End(),nOpcX:=0)}, {||nOpcx := 0, oDlg:End()},,aButtons)}
oDlg:lCentered := .T.
oDlg:Activate()

If nOpcX == 1 .and. (nOpc == 3 .or. nOpc == 5)
	IF Aviso("Confirmação","Confirma "+IIF(nOpc==3,"atualização","exclusão")+" dos dados ?",{"Sim","Não"}) == 1
		//Gravaçao dos Dados
		MsgRun('Atualizando registros. Aguarde...',, {|| xFIN01GRV(nOpc) } )
	ENDIF
	IF nOpc == 3
		_LSAIR := .T.
	ENDIF
Endif

Return()

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ xFIN01GRVº Autor ³ Microsiga          º Data ³  24/07/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Gravacao dos Dados                                         º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ P11 - SISTEMA INDUSTRIA                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function xFIN01GRV(nOpc)
Local bCampo := {|nCPO| Field(nCPO) }

// Backup do TTS
lSavTTsInUse := __TTSInUse

// Ativa TTS
__TTSInUse := .T.

Begin Transaction

If !nOpc == 5
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Gravacao das Distribuicoes     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	For i := 1 to Len(oBrw:aCols)
		
		// Desconsidera linhas sem empresa
		IF Empty(GdFieldGet("ZE_CODEMP",i,,oBrw:aHeader,oBrw:aCols))
			Loop
		ENDIF
		
		_cCodItem := GdFieldGet("ZE_ITEM",i,,oBrw:aHeader,oBrw:aCols)
		
		SZE->(dbSetOrder(1))
		IF !SZE->(dbSeek(XFilial("SZE")+_cCodItem))
			IF GdDeleted(i,oBrw:aHeader,oBrw:aCols)
				Loop
			ENDIF
			RecLock("SZE",.t.)
			SZE->ZE_FILIAL := xFilial("SZE")
		ELSE
			RecLock("SZE",.f.)
			IF GdDeleted(i,oBrw:aHeader,oBrw:aCols)
				SZE->(dbDelete())
				SZE->(msUnlock())
				Loop
			ENDIF
		ENDIF
		
		For x := 1 to Len(oBrw:aHeader)
			If ( oBrw:aHeader[x,10] <> "V" )
				SZE->(FieldPut(FieldPos(oBrw:aHeader[x,2]),oBrw:aCols[i,x]))
			EndIf
		Next
		
		SZE->(msUnlock())
	Next
	
Else
	
	// Exclusao dos registros
	SZE->(dbSetOrder(1))
	SZE->(MsSeek(xFilial("SZE")))
	While SZE->(!Eof()) .and. SZE->ZE_FILIAL == XFilial("SZE")
		Eval({|| RecLock("SZE",.f.), SZE->(dbDelete()), SZE->(MsUnLock()) })
		SZE->(dbSkip())
	EndDo
	
Endif

End Transaction

// Restaura TTS
__TTSInUse := lSavTTsInUse

Return()

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³xFIN01AtuºAutor  ³Microsiga           º Data ³  25/06/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Carrega lista de empresas                                  º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ P11 - SISTEMA INDUSTRIA                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function xFIN01Atu()

IF Aviso("Confirmação","Confirma importação da lista de empresas ?",{"Sim","Não"}) <> 1
	Return()
ENDIF

IF Ascan(oBrw:aCols,{|x| !Empty(x[GDFieldPos("ZE_CODEMP",oBrw:aHeader)]) }) <> 0 .and. Aviso("Alerta","Já existem dados. Deseja sobrepor ?",{"Sim","Não"}) <> 1
	Return()
ENDIF

dbUseArea( .T.,,'SIGAMAT.EMP','EMP',.t., .t.)

oBrw:aCols := {}

While EMP->(!Eof())
	aAdd(oBrw:aCols,Array(Len(oBrw:aHeader)+1))
	For nX := 1 To Len(oBrw:aHeader)
		IF !(Alltrim(oBrw:aHeader[nX][2]) $ "ZE_REC_WT|ZE_ALI_WT" )
			oBrw:aCols[Len(oBrw:aCols),nX] := CriaVar(oBrw:aHeader[nX,2])
		ENDIF
	Next nX
	oBrw:aCols[Len(oBrw:aCols),Len(oBrw:aHeader)+1] := .F.
	
	GDFieldPut("ZE_ITEM",StrZero(Len(oBrw:aCols),TamSX3("ZE_ITEM")[1]),Len(oBrw:aCols),oBrw:aHeader,oBrw:aCols)
	GDFieldPut("ZE_CODEMP",EMP->M0_CODFIL,Len(oBrw:aCols),oBrw:aHeader,oBrw:aCols)
	GDFieldPut("ZE_NOMEMP",EMP->M0_NOME,Len(oBrw:aCols),oBrw:aHeader,oBrw:aCols)
	
	EMP->(dbSkip())
Enddo

EMP->(dbCloseArea())

oBrw:aCols := aClone(oBrw:aCols)
oBrw:oBrowse:Refresh()
Return()

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³FINX01LOK  ºAutor  ³Microsiga          º Data ³  31/07/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Validacao da linha                                         º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ P11 - SISTEMA INDUSTRIA                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

User Function FINX01LOK()
Local _lRet := .t.

_aDuplic := {}
_lDuplic := .F.

For i := 1 to Len(oBrw:aCols)
	
	IF GDDeleted(i,oBrw:aHeader,oBrw:aCols)
		Loop
	ENDIF
	
	IF !_lDuplic .and. Ascan(_aDuplic,GdFieldGet("ZE_CODEMP",i,,oBrw:aHeader,oBrw:aCols)) == 0
		Aadd(_aDuplic,GdFieldGet("ZE_CODEMP",i,,oBrw:aHeader,oBrw:aCols))
	ELSE
		_lDuplic := .T.
		Exit
	ENDIF
Next

IF _lDuplic
	Aviso("Atenção","Existe(m) empresa(s) em duplicidade. Verifique!",{"Voltar"})
	_lRet := .f.
ENDIF

Return(_lRet)
