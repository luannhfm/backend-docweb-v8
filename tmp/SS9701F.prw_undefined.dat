#Include 'Protheus.ch'
#Include 'Parmtype.ch'
#Include 'Topconn.ch'

/*/{Protheus.doc} SS9701F
	Função utilizada no inicializador padrão do campo ZZG_CÓDIGO
		retornando um código de R.D.A. para o novo cadastro.

@author Franklin de Brito de Oliveira
@since 11/01/2017
@type function
@Return cReturn, Caracter, Código da nova R.D.A.
/*/
User Function SS9701F()

Local aArea		:= GetArea()
Local cQuery	:= ""
Local cReturn	:= "" 

	cQuery := "SELECT MAX(ZZG_CODIGO) AS ZZG_CODIGO FROM " + RetSqlName("ZZG")
	cQuery += " WHERE D_E_L_E_T_ = ' ' " 
	cQuery += "AND ZZG_FILIAL = '" + xFilial("ZZG") + "'"
	
	If Select("MAXZZG") > 0
		MAXZZG->( DbCloseArea() )
	Endif
	
	TCQUERY cQuery NEW ALIAS "MAXZZG"
	
	DbSelectArea("MAXZZG")
	
	If Empty( MAXZZG->ZZG_CODIGO )
		cReturn := Soma1(StrZero(0, TamSx3("ZZG_CODIGO")[1]))
	Else
		cReturn := Soma1( MAXZZG->ZZG_CODIGO )
	EndIf
	
	MAXZZG->( DbCloseArea() )
	
	RestArea(aArea)
	
Return (cReturn)