#INCLUDE 'RWMAKE.CH'
#INCLUDE 'PROTHEUS.CH'

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ NOMEENT  ºAutor  ³ - JULIO STORINO -  º Data ³  11/30/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ RETORNA O NOME DA ENTIDADE DO ARQUIVO XX8                  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Parametro de Entrada                                       º±±
±±º          ³    cEmp - C - Codigo da Empresa que se deseja a informacao º±±
±±º          ³    cFil - C - Codigo da Filial que se deseja a informacao  º±±
±±º          ³    nEnt - N - Entidade que se deseja a informacao sendo    º±±
±±º          ³                                                            º±±
±±º          ³    0 - Nome do Grupo                                       º±±
±±º          ³    1 - Nome da Unidade de Negocio                          º±±
±±º          ³    2 - Nome da Empresa                                     º±±
±±º          ³    3 - Nome da Filial                                      º±±
±±º          ³                                                            º±±
±±º          ³    10 - Valida Todos os Componentes / retorna Nome Grupo   º±±
±±º          ³    11 - Valida Todos os Componentes / retorna Nome Unidade º±±
±±º          ³    12 - Valida Todos os Componentes / retorna Nome Empresa º±±
±±º          ³    13 - Valida Todos os Componentes / retorna Nome Filial  º±±
±±º          ³    * Se Algum Componente nao validar, retorna nome Vazio   º±±
±±º          ³ Retorno                                                    º±±
±±º          ³    cNome - Nome da Entidade Consultada                     º±±
±±º          ³                                                            º±± 
±±º          ³ * - Valores Default cEmp=01 cFil=01001001 cEnt=3           º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

User Function SF0521X(cEmp,cFil,nEnt)

Local cEstat := U_SFGN001A(ProcName(0), "SF0521X")
Local _cNome		:= ""
Local _nRecXX8		:= XX8->(Recno())

Local _nTEmp		:= 0
Local _nTUni		:= 0
Local _nTFil		:= 0

Local _cCGrp		:= ""
Local _cCEmp		:= ""
Local _cCUni		:= ""
Local _cCCod		:= ""

Local _cNGrp		:=	""
Local _cNUni		:=	""
Local _cNEmp		:=	""
Local _cNFil		:= ""
		
Default cEmp		:= cEmpAnt
Default cFil		:= cFilAnt
Default nEnt		:= 3

XX8->(DbSetOrder(4))
XX8->(DbGoTop())

//Localiza primeiro o registro 0 - Grupo de Empresa para pegar a mascara
If XX8->(DbSeek(Space(12)+Space(12)+Space(12)+PadR(cEmp,12)+'0'))
	_cLay		:= XX8->XX8_LEIAUT
	_nTEmp 	:= ContaLet('E',_cLay)
	_nTUni 	:= ContaLet('U',_cLay)
	_nTFil 	:= ContaLet('F',_cLay)	
Else
	Return( 'NiHil')
EndIf

//Monto as Strings de Procura conforme a entidade.	
Do Case
	Case nEnt = 0  //Grupo de Empresa
		_cCGrp	:= Space(12)
		_cCEmp 	:= Space(12)
		_cCUni	:= Space(12)
		_cCCod	:= PadR(cEmp,12)
	Case nEnt = 1	//Unidade de Negocio
		_cCGrp	:= PadR(cEmp,12)
		_cCEmp 	:= Space(12)
		_cCUni	:= Space(12)
		_cCCod	:= PadR(Substr(cFil,At('E',_cLay),_nTEmp),12)
	Case nEnt = 2	//Empresa
		_cCGrp	:= PadR(cEmp,12)
		_cCEmp 	:= PadR(Substr(cFil,At('E',_cLay),_nTEmp),12)
		_cCUni	:= Space(12)
		_cCCod	:= PadR(Substr(cFil,At('U',_cLay),_nTUni),12)
	Case nEnt = 3	//Filial
		_cCGrp	:= PadR(cEmp,12)
		_cCEmp 	:= PadR(Substr(cFil,At('E',_cLay),_nTEmp),12)
		_cCUni	:= PadR(Substr(cFil,At('U',_cLay),_nTUni),12)
		_cCCod	:= PadR(Substr(cFil,At('F',_cLay),_nTFil),12)
	Case nEnt > 9
		_cNGrp	:=	U_NOMEENT(cEmp,cFil,0)	//Nome do Grupo
		_cNUni 	:= If(Empty(PadR(Substr(cFil,At('E',_cLay),_nTEmp),12)),"",U_NOMEENT(cEmp,cFil,1))	//Nome da Unidade
		_cNEmp	:= If(Empty(PadR(Substr(cFil,At('U',_cLay),_nTUni),12)),"",U_NOMEENT(cEmp,cFil,2))	//Nome da Empresa
		_cNFil	:= If(Empty(PadR(Substr(cFil,At('F',_cLay),_nTFil),12)),"",U_NOMEENT(cEmp,cFil,3))	//Nome da Filial
		Do Case
			Case nEnt = 10
				_cNome := _cNGrp
			Case nEnt = 11
				_cNome := _cNUni
			Case nEnt = 12
				_cNome := _cNEmp
			Case nEnt = 13
				_cNome := _cNFil
		EndCase		
EndCase

//Faço a Busca propriamente dita.
If nEnt < 10
	XX8->(DbGoTop())
	If XX8->(DbSeek(_cCGrp+_cCEmp+_cCUni+_cCCod+cValToChar(nEnt)))
		_cNome := AllTrim(XX8->XX8_DESCRI)
	Else
		_cNome := 'NiHil'
	EndIf
EndIf

XX8->(DbGoTo(_nRecXX8))

Return( _cNome )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ CONTALET ºAutor  ³ - JULIO STORINO -  º Data ³  11/30/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ CONTA AS LETRAS                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function ContaLet(cLet,cString)

Local cEstat := U_SFGN001A(ProcName(0), "SF0521X")
Local _nCont 	:= 0
Local _nI		:= 0

For _nI := 1 To Len(cString)
	
	If cLet == Substr(cString,_nI,Len(cLet))
		_nCont++
	EndIf

Next _nI

Return( _nCont )