#include 'protheus.ch'
#include 'parmtype.ch'
#Include 'TOTVS.CH'
#Include 'TOPCONN.CH'

/*/{Protheus.doc} F590CAN
// solução para que não limpe o campo E1_NUMBCO "Nosso Número" ao remover de borderô ou cancelar borderô, 
para títulos com PREFIXO "SGE" ou "RMC".
@author J2A 
@since 11/12/2019
@version 1

@type function
/*/

user function F590CAN()

	Local cNumBor := PARAMIXB[2]
	Local cTipo := PARAMIXB[1]
	Local lRet := .T.
	LOCAL cQuery := ""
	
	If Select('TBRD') > 0
		TBRD->(DbCloseArea())
	EndIf

	If cTipo=="R"

		cQuery := "SELECT EA_FILIAL, EA_NUM, EA_PREFIXO, EA_PARCELA, EA_TIPO " 
		cQuery += "FROM " + RetSqlName("SEA") + " EA "
		cQuery += "INNER JOIN " + RetSqlName("SE1") + " E1 ON EA_FILIAL = E1_FILIAL AND EA_NUM = E1_NUM "
		cQuery += "AND EA_PREFIXO = E1_PREFIXO AND EA_PARCELA = E1_PARCELA AND E1_TIPO = EA_TIPO  "
		cQuery += "WHERE EA_FILIAL = '" + cFilAnt + "' AND EA_NUMBOR = '"+ cNumBor + "' AND EA_PREFIXO IN ('SGE','RMC') "
		cQuery += "AND E1_NUMBOR = ' ' AND E1_NUMBCO = ' ' AND E1_SALDO > 0 AND  EA.D_E_L_E_T_ <> ' ' "
		cQuery := ChangeQuery( cQuery )  

		MEMOWRITE ("C:\TEMP\F590CAN.TXT", cQuery)

		TcQuery cQuery NEW ALIAS "TBRD"

		DbselectArea ("TBRD")

		TBRD->(dbgotop())

		DbSelectArea("SE1")
		DbSetOrder(1) 

		while !TBRD->(Eof())

			If DbSeek(xFilial("SE1")+TBRD->EA_PREFIXO+TBRD->EA_NUM+TBRD->EA_PARCELA+TBRD->EA_TIPO)

				If RecLock("SE1", .F.)

					SE1->E1_NUMBCO := SE1->E1_XIDCNAB
				
					MSUNLOCK()

				EndIf
			EndIF
			
			TBRD->(dbSkip())

		EndDo
		
		TBRD->(DbCloseArea())
		
		SE1->(DbCloseArea())

	EndIf


Return lRet

