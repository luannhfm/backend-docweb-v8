#Include 'Protheus.ch'
#Include 'RwMake.ch

User Function SF0401X()
	Local _oReport
	Private _cPerg		:= 'SF0401X'
	Private _cAlias	:= GetNextAlias()
	Private _oCurso, _oDiscipli
	
	If FindFunction('TRepInUse')
		_oReport := ReportDef()	
		_oReport:PrintDialog()
	EndIf

Return

Static Function ReportDef()
	Local _oReport
	
	AjustaSX1(_cPerg)
	
	Pergunte(_cPerg, .t.)
	
	_oReport := TReport():New('SF0401X','Relatório de amarração Curso/Disc X Produto', _cPerg, {|_oReport| PrintReport(_oReport)}, '')
	
	_oReport:SetLeftMargin(2)
	_oReport:SetPortrait()
	_oReport:oPage:SetPaperSize(9)
	_oReport:SetEnvironment(1)
	
	_oCurso := TRSection():New(_oReport, "Curso", {_cAlias}, {'Curso', 'Descrição Curso'})
	TRCell():New(_oCurso, 'ZZB_CODCUR'	, _cAlias)
	TRCell():New(_oCurso, 'ZZB_DESCUR'	, _cAlias)
	
	_oDiscipli := TRSection():New(_oReport, "Disciplina", {_cAlias})//, {'  ','Cod Disciplina','Disciplina','Cod Produto','Produto','Status'})
	TRCell():New(_oDiscipli, "COL"		 , _cAlias, ''						,, 1) 
	TRCell():New(_oDiscipli, "ZZB_CODDIS", _cAlias, 'Dis.'					,, 3) 
	TRCell():New(_oDiscipli, "ZZB_DESDIS", _cAlias, 'Descrição Disciplina'	,, 70) 
	TRCell():New(_oDiscipli, "ZZB_CODPRO", _cAlias,	'Cod. Produto'			,) 
	TRCell():New(_oDiscipli, "ZZB_DESPRO", _cAlias, 'Descrição Produto'		,, 70) 
	//TRCell():New(_oDiscipli, "ZZB_CODDIS", _cAlias) 
	TRCell():New(_oDiscipli, "ZZB_STATUS", _cAlias, 'Status'				,) 
	
	_oDiscipli:Cell('COL'):SetBorder(2,0,,.T.)
	_oDiscipli:Cell('COL'):SetBorder(2,0,,.F.)
	
Return(_oReport)

Static Function PrintReport(_oReport)
	Local _nCont	:= 0
	Local _cCodCrs := ""
	
	MsgRun('Selecionando registros...', 'Aguarde', {||GeraQuery(_oReport)})
	_cCodCrs := AllTrim((_cAlias)->ZZB_CODCUR)
	
	While !(_cAlias)->(EOF())
		_oReport:IncMeter()
		_oReport:SetMsgPrint((_cAlias)->ZZB_CODCUR)
		If _oReport:Cancel()
			_oReport:PrintText('Cancelado pelo usuário')
			Exit
		EndIf
		_oCurso:Init()
		_oCurso:PrintLine()
		_oCurso:Finish()
		While AllTrim(_cCodCrs) == AllTrim((_cAlias)->ZZB_CODCUR)
			_oDiscipli:Init()
			_oDiscipli:PrintLine()		//-- Imprime e alinha
			(_cAlias)->(dbSkip())
		Enddo
		_oDiscipli:Finish()
		_cCodCrs := AllTrim((_cAlias)->ZZB_CODCUR)
		_oReport:SkipLine()
	EndDo
	//_oReport:SkipLine()
Return

Static Function GeraQuery(_oReport)
	Local _cSql		:= ""
	Local _nOrder	:= _oReport:nOrder
	
	_oReport:nOrder := 0
	
	//MakeSqlExpr(_cPerg)
	
	_cSql := "SELECT"+CRLF
	_cSql += "  ZZB_FILIAL, ZZB_CODCUR, ZZB_DESCUR, ZZB_CODDIS, ZZB_DESDIS, ZZB_ITEM, ZZB_CODPRO, ZZB_DESPRO, ZZB_STATUS"+CRLF
	_cSql += "FROM"+CRLF
	_cSql += "  "+RetSqlName("ZZB")+" "+CRLF
	_cSql += "WHERE"+CRLF
	_cSql += "  D_E_L_E_T_ = ' '"+CRLF
	_cSql += "  AND ZZB_CODPRO BETWEEN '"+Mv_par01+"' AND '"+Mv_par02+"'"+CRLF
	_cSql += "  AND ZZB_CODCUR BETWEEN '"+Mv_par03+"' AND '"+Mv_par04+"'"+CRLF
	_cSql += "  AND ZZB_CODDIS BETWEEN '"+Mv_par05+"' AND '"+Mv_par06+"'"+CRLF
	MemoWrite("C:\temp\"+FunName()+"_"+ProcName()+".TXT",_cSql)
	If Select(_cAlias)>0
		(_cAlias)->(DbCloseArea())
	Endif
	DbUseArea(.t.,"TOPCONN",TcGenQry(,,_cSql),_cAlias,.t.,.t.)

Return

Static Function AjustaSX1(_cPerg)
	
	u_SFPUTSX1(_cPerg, '01', 'Produto DE?'		,'','','mv_ch1','C',TamSX3("B1_COD")[1]		,00,00,'G','','SB1','','','mv_par01','','','','','','','','','','','','','','','','',,'','','')
	u_SFPUTSX1(_cPerg, '02', 'Produto ATE?'		,'','','mv_ch2','C',TamSX3("B1_COD")[1]		,00,00,'G','','SB1','','','mv_par02','','','','','','','','','','','','','','','','',,'','','')
	
	u_SFPUTSX1(_cPerg, '03', 'Curso DE?'		,'','','mv_ch3','C',TamSX3("ZZB_CODCUR")[1]	,00,00,'G','','ZZB001','','','mv_par03','','','','','','','','','','','','','','','','',,'','','')
	u_SFPUTSX1(_cPerg, '04', 'Curso ATE?'		,'','','mv_ch4','C',TamSX3("ZZB_CODCUR")[1]	,00,00,'G','','ZZB001','','','mv_par04','','','','','','','','','','','','','','','','',,'','','')
	
	u_SFPUTSX1(_cPerg, '05', 'Disciplina DE?'	,'','','mv_ch5','C',TamSX3("ZZB_CODDIS")[1]	,00,00,'G','','ZZB002','','','mv_par05','','','','','','','','','','','','','','','','',,'','','')
	u_SFPUTSX1(_cPerg, '06', 'Disciplina ATE?'	,'','','mv_ch6','C',TamSX3("ZZB_CODDIS")[1]	,00,00,'G','','ZZB002','','','mv_par06','','','','','','','','','','','','','','','','',,'','','')
	
Return	

		
		
		
		
		
		
		
		
		
		
		
		
		
		
		