#INCLUDE 'TOTVS.CH'

/*/{Protheus.doc} SF01A01X
	@description Fonte da customização na rotina de aprovação de Solicitacao de Baixa de Ativos Fixos. 
	@type User Function
	@author Walmir Junior
	@since 22/02/17
	@version 1.0
@history 10/01/2020, Franklin de Brito de Oliveira, Inclusão de gatilho nos campos descritivos
@history 15/01/2020, Franklin de Brito de Oliveira, Ajuste na validação dos campos na aprovação de baixa
/*/
User Function SF01A01X()

	Local _oDlg
	Local _aRet		:= {,,,}
	Local _lEdt		:= M->(NM_MOTBX) $ GetNewPar("MV_XMBXVEN", "01")
	//Preserva condição do Alias SNM
	Local _aArea	:= GetArea("SNM")
	//Campos do Processo de Solicitação de Baixa
	Local _cNPro	:= "           "
	Local _cNRes	:= "           "
	//Campos Aprovação da Transferência
	Local _cCCDt	:= Space(20)
	Local cDescCCD	:= Space(TamSX3("CTT_DESC01")[1])
	Local _cLDst	:= Space(6)
	Local _cResp 	:= Space(30)
	Local _cCdRsp 	:= Space(6)
	Local _cItDt 	:= Space(20)
	Local cDescItD 	:= Space(TamSX3("CTD_DESC01")[1])
	local cDescLoc	:= Space(50)
	local cDescRes	:= Space(50)
	Local nOpc		:= 0
	Local bVlGtCCD	:= {|| (cDescCCD := Posicione("CTT", 1, xFilial("CTT") + _cCCDt , "CTT_DESC01"), .T.) }
	//Campos da Solicitação.
	Private _cCndPg	:= "   "

	//+-------------+
	//|	Monta Tela	|
	//+-------------+
	If M->(NM_TIPOSOL) == '1'
		DEFINE DIALOG _oDlg FROM 0,0 TO 200,400 TITLE "Aprovação de Solicitação de Baixa" OF oMainWnd PIXEL
		
		@ 002,002 GROUP TO 040,200 LABEL "" OF _oDlg PIXEL
		
		@ 004,004 Say "Informe os dados a seguir"	SIZE 197,008 OF _oDlg PIXEL
		
		@ 015,006 Say "Numero do Processo" 			SIZE 197,008 OF _oDlg PIXEL
		@ 024,006 MsGet _cNPro SIZE 50,8 OF _oDlg PIXEL
		@ 015,070 Say "Resolução " 					SIZE 197,008 OF _oDlg PIXEL
		@ 024,070 MsGet _cNRes SIZE 50,8 OF _oDlg PIXEL
		
		@ 040,002 GROUP TO 080,200 LABEL "" OF _oDlg PIXEL
		
		@ 045,006 Say "Cond. Pg" 			SIZE 197,008 OF _oDlg PIXEL
		@ 054,006 MsGet _cCndPg SIZE 50,8 OF _oDlg VALID (_lEdt .And. ExistCpo("SE4",_cCndPg)) PIXEL F3 "SE4" WHEN _lEdt
		
		@ 110,130 BUTTON "Cancelar" SIZE 030,013 PIXEL OF _oDlg ACTION ( nOpc := 0, _oDlg:End() )
		@ 085,170 BUTTON "Ok" SIZE 030,013 PIXEL OF _oDlg ACTION ( Iif(fVldDdos(M->(NM_TIPOSOL), , , , , _cNPro, _cNRes, _cCndPg, _lEdt), ( nOpc := 1, _oDlg:End()), 1+1) )
	Else
		DEFINE DIALOG _oDlg FROM 0,0 TO 250,400 TITLE "Aprovação de Solicitação de Transferência" OF oMainWnd PIXEL
		
		@ 002,002 GROUP TO 105,200 LABEL "" OF _oDlg PIXEL
		
		@ 004,004 Say "Informe os dados a seguir"	SIZE 197,008 OF _oDlg PIXEL
		
		@ 016,006 Say "CC Destino"				SIZE 197,008 OF _oDlg PIXEL
		@ 025,006 MsGet oGetCCD Var _cCCDt SIZE 050,008		OF _oDlg PIXEL F3 "CTT002"
			 oGetCCD:bValid := bVlGtCCD
		@ 025,070 MsGet oDesCC Var cDescCCD SIZE 100,008 OF _oDlg PIXEL When .F.
		
		@ 036,006 Say "Item Destino"			SIZE 197,008 OF _oDlg PIXEL
		@ 045,006 MsGet _cItDt SIZE 050,008		OF _oDlg PIXEL ;
			VALID (cDescItD := Posicione("CTD", 1, xFilial("CTD") + _cItDt , "CTD_DESC01"), .T.) F3 "CTD02X"
		@ 045,070 MsGet oDescItC Var cDescItD SIZE 100,008 OF _oDlg PIXEL When .F.
		
		@ 056,006 Say "Local Destino "			SIZE 197,008 OF _oDlg PIXEL
		@ 065,006 MsGet oGetLoc  VAR _cLDst 	SIZE 050,008 OF _oDlg PIXEL ;
		 	VALID ( cDescLoc := Posicione("SNL", 1, SNM->NM_FILDEST + _cLDst , "NL_DESCRIC"), .T.) F3 "SNLX"
		@ 065,070 MsGet oDescLoc VAR cDescLoc	SIZE 100,008 OF _oDlg PIXEL WHEN .F.
		
		@ 076,006 Say "Responsável "			SIZE 197,008 OF _oDlg PIXEL
		@ 085,006 MsGet oGetRsp VAR _cCdRsp 	SIZE 050,008	OF _oDlg PIXEL ;
			VALID ( cDescRes := Posicione("RD0", 1, xFilial("RD0") + _cCdRsp , "RD0_NOME"), .T.) F3 "RD0NOM"
		@ 085,070 MsGet oDescRes VAR cDescRes	SIZE 100,008 OF _oDlg PIXEL WHEN .F.
				
		@ 110,130 BUTTON "Cancelar" SIZE 030,013 PIXEL OF _oDlg ACTION ( nOpc := 0, _oDlg:End() )
		@ 110,170 BUTTON "Ok" SIZE 030,013 PIXEL OF _oDlg ACTION ( Iif(fVldDdos(M->(NM_TIPOSOL), _cCCDt, _cItDt, _cLDst, _cCdRsp), ( nOpc := 1, _oDlg:End()), 1+1) )
	EndIf

	ACTIVATE MSDIALOG _oDlg
	
	If nOpc == 1
		If M->(NM_TIPOSOL) == '1'
			_aRet := {_cNPro,_cNRes, _cCndPg,SNM->NM_FILIAL + SNM->NM_CODIGO,""}
		Else
			_aRet := ExecAlt({_cCCDt,_cLDst, _cResp, SNM->NM_FILIAL + SNM->NM_CODIGO, _cCdRsp, _cItDt})
		EndIf
	Else
		_aRet := {}
	EndIf
	
	RestArea(_aArea)

Return _aRet

/*/{Protheus.doc} ExecAlt
	@type Static Function
	@author Rafael Karczevski
	@since 03/09/2019
	@version 1.0
	/*/
Static Function ExecAlt(p_aRet)

Local aRet := p_aRet
Local nSeq := "0"
	
	dbSelectArea("SN1")
	SN1->(dbSetOrder(1))
	If SN1->( MsSeek( SNM->NM_FILDEST + SNM->NM_CBASE + SNM->NM_ITEM ) )
		RecLock("SN1",.F.)
			SN1->N1_LOCAL := aRet[2]
		SN1->(MsUnlock())
		dbSelectArea("SN3")
		SN3->(dbSetOrder(1))
		If SN3->( MsSeek( SNM->NM_FILDEST + SNM->NM_CBASE + SNM->NM_ITEM ) )
			RecLock("SN3",.F.)
				SN3->N3_CCUSTO := aRet[1]
				SN3->N3_SUBCTA := aRet[6]
			SN3->(MsUnlock())
			dbSelectArea("SND")
			SND->(dbSetOrder(3))
			If SND->( MsSeek( SNM->NM_FILDEST + SNM->NM_CBASE + SNM->NM_ITEM ) )
				While (SND->ND_CBASE == SNM->NM_CBASE)
					If SND->ND_DTFIM == cToD("  /  /  ")
						RecLock("SND",.F.)
							SND->ND_DTFIM := Date()
							SND->ND_STATUS := "2"
							nSeq := SND->ND_SEQUENC
							nSeq := Iif(nSeq == Nil, "1", nSeq)
						SND->(MsUnlock())
					EndIf
					SND->(dbSkip())
				End
			EndIf
			RecLock("SND",.T.)
				SND->ND_FILIAL	:= SNM->NM_FILDEST
				SND->ND_CBASE	:= SNM->NM_CBASE
				SND->ND_ITEM	:= SNM->NM_ITEM
				SND->ND_CODRESP	:= aRet[5]
				SND->ND_STATUS	:= "1"
				SND->ND_DTINI	:= Date()
				SND->ND_DTFIM	:= cToD("  /  /  ")
				SND->ND_SEQUENC	:= StrZero(Val(nSeq) + 1, 6)
			SND->(MsUnlock())
		EndIf
	EndIf

	SN1->(dbCloseArea())
	SN3->(dbCloseArea())
	SND->(dbCloseArea())

Return aRet

Static Function fVldDdos(p_cTpSol, _cCCDt, _cItDt, _cLDst, _cCdRsp, _cNPro, _cNRes, _cCndPg, _lEdt)
Local lRet		:= .T.
Local cAlTMP	:= GetNextAlias()

	If p_cTpSol == '1' //Baixa
		If Empty(_cNPro) .Or. Empty(_cNRes) .Or. ( Empty(_cCndPg) .And. _lEdt )
			lRet := .F.
			Alert("Atenção! É Obrigatório preencher TODOS os campos da Aprovação.")
		EndIf
	Else 
		If Empty(_cCCDt) .Or. Empty(_cItDt) .Or. Empty(_cLDst) .Or. Empty(_cCdRsp)	
			lRet := .F.
			Alert("Atenção! É Obrigatório preencher TODOS os campos da Aprovação.")
		EndIf
		If lRet
			BeginSql Alias cAlTMP
				SELECT
					CTA_ITEM
				FROM %Table:CTA%
				WHERE 
					CTA_FILIAL = %Exp:xFilial("CTA")%
					AND CTA_CUSTO = %Exp:_cCCDt%
					AND CTA_ITEM = %Exp:_cItDt%
					AND %NOTDEL%
			EndSql
			(cAlTMP)->(dbGoTop())
		
			If (cAlTMP)->(eof())
				lRet := .F.
				Alert("Não existe amarração entre centro de custo e item contabil!")
			EndIf
		
			(cAlTMP)->(dbCloseArea())
		EndIf
	EndIf

Return lRet