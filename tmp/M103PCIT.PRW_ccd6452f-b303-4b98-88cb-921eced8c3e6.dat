#Include "TOTVS.Ch"

/*/{Protheus.doc} M103PCIT
    @description Pto de entrada utilizado para preencher o campo D1_XCLSSC na importação de Pedidos
    @type User Function
    @author Paulo César P.Schwind
    @since 11/07/2019
    @version 1.0
    /*/
User Function M103PCIT() 

	Local aAreaSC7 := SC7->(GetArea())
	Local aAreaSD1 := SD1->(GetArea())

	Local nPosNPed := aScan(aHeader,{|x| AllTrim(x[2]) == 'D1_PEDIDO'})
	Local nPosITPD := aScan(aHeader,{|x| AllTrim(x[2]) == 'D1_ITEMPC'})
	Local nPosXClS := aScan(aHeader,{|x| AllTrim(x[2]) == "D1_XCLSSC" } ) 
	Local nPosXPlc := aScan(aHeader,{|x| AllTrim(x[2]) == "D1_XCLPLC" } ) 
	Local _i

	For _i := 1 TO Len(aCols)

		// Verifica se o campo com o numero do Pedido está informado e a Classificação está Vazia
		// Busca no Pedido de Compras
		IF ! Empty( aCols[ _i , nPosNPed ] ) .AND. Empty( aCols[ _i , nPosXClS ] )

			aCols[ _i , nPosXClS ] := POSICIONE("SC7",1,xFilial("SC7") + aCols[ _i , nPosNPed ] + aCols[ _i , nPosITPD ], "C7_XCLSSC" )
			aCols[ _i , nPosXPlc ] := POSICIONE("SC7",1,xFilial("SC7") + aCols[ _i , nPosNPed ] + aCols[ _i , nPosITPD ], "C7_XCLPLC" )
			
		Endif   

	Next _i

	RestArea(aAreaSC7)
	RestArea(aAreaSD1)

Return