#Include 'Protheus.ch'

//-------------------------------------------------
/*/{Protheus.doc} F460E5
Executado antes de cancelemento dos títulos gerados 
pela liquidação

@author Allan da Silva Faria
@since 19/04/2016
@version 1.0
/*/
//-------------------------------------------------
User Function F460E5()

Local _cDadosSE5 := ParamIXB
Local _aHistCob  := Array(8)

	//------------------------------------------------------
	//-- Exclui Histórico de cobrança - Título Origem 
	//------------------------------------------------------
	_aHistCob[1]:= SE5->E5_CLIFOR
	_aHistCob[2]:= SE5->E5_LOJA
	_aHistCob[3]:= SE5->E5_PREFIXO 
	_aHistCob[4]:= SE5->E5_NUMERO
	_aHistCob[5]:= SE5->E5_PARCELA
	_aHistCob[6]:= SE5->E5_TIPO
	GetInfE5(@_aHistCob)

	//---------------------------------------
	//-- Chama Função que excluir historico
	//---------------------------------------
	DELHISTOR(_aHistCob,"FINA460")

Return Nil

//-------------------------------------------------
/*/{Protheus.doc} GetInfE5
Retorna informações da SE5 da liquidação

@author Allan da Silva Faria
@since 20/04/2016
@version 1.0
@param _aDados, Array, Passado por referncia, dados de pesquisa.
/*/
//-------------------------------------------------
Static Function GetInfE5(_aDados)

Local _cTrab:= GetNextAlias()

If Select(_cTrab)>0
	(_cTrab)->(dbCloseArea())
EndIf

//------------------
//-- Busca SE5 liq
//------------------
BeginSQL Alias _cTrab

	column E5_DATA as Date

	SELECT E5_SEQ,E5_DATA
	FROM %table:SE5%
	WHERE %notDel%
		AND E5_FILIAL = %xFilial:SE5%
		AND E5_CLIFOR = %exp:_aDados[1]%
		AND E5_LOJA   = %exp:_aDados[2]%
		AND E5_PREFIXO= %exp:_aDados[3]%
		AND E5_NUMERO = %exp:_aDados[4]%
		AND E5_PARCELA= %exp:_aDados[5]%
		AND E5_TIPO	  = %exp:_aDados[6]%
		AND E5_MOTBX  = 'LIQ'
		AND Trim(E5_DOCUMEN) = %exp:AllTrim(cLiqCan)%
EndSQL

dbSelectArea(_cTrab)
(_cTrab)->(dbGoTop())

If (_cTrab)->(!EOF())
	_aDados[7]:= (_cTrab)->E5_DATA
	_aDados[8]:= (_cTrab)->E5_SEQ
EndIf

If Select(_cTrab)>0
	(_cTrab)->(dbCloseArea())
EndIf

Return Nil

//-----------------------------------------------------
/*/{Protheus.doc} DelHistor
Deleta historico.

@author Allan da Silva Faria
@since 18/04/2016
@version 1.0
@param _aDados	, Array		, Dados do movimetação que gerou o historico
@param _cFunName, Caracter	, Nome da rotina que gerou o historico
/*/
//-----------------------------------------------------

Static Function DelHistor(_aDados,_cFunName)

	Local _aArea := ZF1->(GetArea()) 

	DEFAULT _cFunName := FunName()

	_cFunName := PADR(_cFunName,TamSX3("ZF1_ORIGEM")[1])	

	dbSelectArea("ZF1")
	ZF1->(dbSetOrder(2))
	ZF1->(dbGoTop())      //-- FILIAL - CLIENTE - LOJA - PREFIXO - NUM - PARCELA - TIPO - DATA - ORIGEM - SEQ
	If ZF1->(dbSeek(FWxFilial("ZF1")+_aDados[1]+_aDados[2]+_aDados[3]+_aDados[4]+_aDados[5]+_aDados[6]+DTOS(_aDados[7])+_cFunName+_aDados[8]))
		RecLock("ZF1",.F.)
		ZF1->(dbDelete())
		ZF1->(MsUnlock())
	EndIf

	RestArea(_aArea)

Return Nil
