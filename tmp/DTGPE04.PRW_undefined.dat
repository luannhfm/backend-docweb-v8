#include "PROTHEUS.CH"

//-- Diretivas indicando as colunas para Matriz de Verbas
#define MN_QTDCOL	 11

#define MN_MARCA    1
#define MN_MES      2
#define MN_ANO      3
#define MN_CC       4
#define MN_DESCCC   5
#define MN_ITEM     6
#define MN_DESCITE  7
#define MN_HRMES    8
#define MN_HORAS    9
#define MN_PERCEN  10
#define MN_RECNO   11
                                                   	
/*/f/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
<Descricao> : Manutenção de Horas Rateadas
<Autor> : Fábrica DOIT SP
<Data> : 16/05/2014
<Parametros> : Nil
<Retorno> : Nil
<Processo> : FIEMT – Folha de Pagamento
<Tipo> : 
<Obs> : 
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
*/

User Function DTGPE04(cFilRat,cMatRat)

//-- Dialog
Local oDlg
Local oPanel
Local nItem,nLine,nPos

//-- Controle de dimensoes de objetos
Local aObjects	  := {}
Local aInfo		  := {}
Local aPosObj	     := {}  
Local aSize		  := {}
Local lOk		     := .F.
Local oOk          := LoadBitMap(GetResources(),"LBOK")
Local oNo          := LoadBitMap(GetResources(),"LBNO")
Local nL			  := 0

Private cUserRat	  := GETNEWPAR("MV_USERRAT", "")

Private aItens     := {}
Private aBotoes    := {}

Private oCodFunc   := Nil
Private oNomeFunc  := Nil
Private oHorasMes  := Nil
Private oMesBase   := Nil
Private oAnoBase   := Nil

Private oMarcaTudo := Nil
Private oListItens := Nil
Private aTitItens  := {}

Private aMesBase   	:= {}
Private aAnoBase   	:= {}
Private cCodFunc   	:= cMatRat
Private cNomeFunc  	:= POSICIONE("SRA",1,cFilRat+cMatRat,"RA_NOME")
Private cMesBase   	:= ""
Private cPeriodo		:= GETMV("MV_FOLMES")
Private cAnoBase   	:= SUBSTR(cPeriodo,1,4) //StrZero( Year( dDataBase ), 4 )  
Private nHrsMesTot 	:= ''   
Private nTotPerc	    := 0  
Private nTotTrab	    := 0
Private nTotHor		:= 0
Private nHrMes    	:= 0

Private lMarcaTudo  := .F.

Private nDiaCorte	    := Day(DATE())
Private nValida		:= GETMV("DT_BLQRAT") //Dia de corte do paramento de rateio
Private cMatRef		:= cMatRat
Private cFilRef		:= cFilRat

//Bloqueia lançamento de rateio

If !(RetCodUsr() $ cUserRat)
	If nDiaCorte > nValida                                    
		
		Alert( "Manutenções de estrutura de rateio, só podem ser realizados até dia: "+STRZERO(nValida,2) )
		RETURN .T.
	
	EndIf
Endif


//---------------------------------
//
//--------------------------------------------------

Aadd( aTitItens, ""                )
Aadd( aTitItens, "Mês"             )
Aadd( aTitItens, "Ano"             )
Aadd( aTitItens, "Centro de Custo" )
Aadd( aTitItens, "Descrição"       )
Aadd( aTitItens, "Item Contábil"   )
Aadd( aTitItens, "Descrição"       )
Aadd( aTitItens, "Horas Mês"       )
Aadd( aTitItens, "Horas Alocado"   )
Aadd( aTitItens, "Percentual (%)"  )

//---------------------------------
//
//--------------------------------------------------

For nL := 2000 To Year( dDataBase )

   Aadd( aAnoBase, Str( nL, 4 ) )

Next

Aadd( aMesBase, "Janeiro"   )
Aadd( aMesBase, "Fevereiro" )
Aadd( aMesBase, "Março"     )
Aadd( aMesBase, "Abril"     )
Aadd( aMesBase, "Maio"      )
Aadd( aMesBase, "Junho"     )
Aadd( aMesBase, "Julho"     )
Aadd( aMesBase, "Agosto"    )
Aadd( aMesBase, "Setembro"  )
Aadd( aMesBase, "Outubro"   )
Aadd( aMesBase, "Novembro"  )
Aadd( aMesBase, "Dezembro"  )

cMesBase := aMesBase[ VAL(SUBSTR(cPERIODO,5,2))] //aMesBase[ Month( dDataBase ) ]

//-- Dimensoes padroes
aSize   := MsAdvSize()
AAdd( aObjects, { 100, 020, .T., .F., .T. } )
AAdd( aObjects, { 100, 100, .T., .T. } )
AAdd( aObjects, { 100, 020, .F., .F. } )
aInfo   := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 5, 5 }
aPosObj := MsObjSize( aInfo, aObjects,.T.)

MsgRun( "Aguarde, selecionando informações...",, {|| GPE04Qry()} )

If .T.

	aBotoes := { { "Incluir", {|| GPE04Opc( "I" ) }, "Incluir" },;
	             { "Alterar", {|| GPE04Opc( "A" ) }, "Alterar" },;
	             { "Excluir", {|| GPE04Opc( "E" ) }, "Excluir" } }
		
	DEFINE MSDIALOG oDlg TITLE "Estrutura de Rateio de Verbas" FROM aSize[7],00 TO aSize[6],aSize[5] PIXEL
		
		@ 005,005 Say "Matrícula:"                                           Of oDlg Pixel
		@ 013,005 MsGet oCodFunc    Var cCodFunc  When .F.	      Size 030,07 Of oDlg Pixel

		@ 005,045 Say "Funcionário:"                                         Of oDlg Pixel
		@ 013,045 MsGet oNomeFunc   Var cNomeFunc When .F.	      Size 300,07 Of oDlg Pixel

		@ 005,355 Say "Mês:"                                                 Of oDlg Pixel
		@ 013,355 ComboBox oMesBase Var cMesBase  Items aMesBase Size 100,36 Of oDlg Pixel

		@ 005,465 Say "Ano:"                                                 Of oDlg Pixel
		@ 013,465 ComboBox oAnoBase Var cAnoBase  Items aAnoBase Size 070,36 Of oDlg Pixel

		@ 013,545 Button "Filtrar"                               Size 040,11 Of oDlg Pixel Action ( GPE04Qry(.T.,cFilRat,cMatRat) )

      //------------------ Documentos

		aPosObj[2,1] += 5

		oFolder := TFolder():NEW( aPosObj[2,1],004, { "Centro de Custo / Item Contábil" },, oDlg, 1,,, .t.,, aPosObj[2,4],aPosObj[3,3] )

		@ 005,005 CHECKBOX oMarcaTudo Var lMarcaTudo PROMPT "Marcar/Desmarcar todos os registros..." Size 168, 08 On CLICK( GPE04Todos( lMarcaTudo ) ) OF  oFolder:aDialogs[1] PIXEL

		@ 020,005 LISTBOX oListItens Fields HEADER;
			  aTitItens[01],;
			  aTitItens[02],;
			  aTitItens[03],;
			  aTitItens[04],;
 			  aTitItens[05],;
 			  aTitItens[06],;
 			  aTitItens[07],;
 			  aTitItens[08],;
 			  aTitItens[09],;
 			  aTitItens[10] SIZE ( aPosObj[2,4] - 10 ), ((aPosObj[2,3]-aPosObj[2,1])-5) OF oFolder:aDialogs[1] PIXEL 
			
		oListItens:bLDblClick := { || GPE04Todos( .F., oListItens:nAT ) }
		oListItens:SetArray( aItens )
		oListItens:bLine   := { || {       Iif( aItens[ oListItens:nAT,MN_MARCA  ], oOk, oNo )   ,;
										                aItens[ oListItens:nAT,MN_MES    ]               ,;
										                aItens[ oListItens:nAT,MN_ANO    ]               ,;
										                aItens[ oListItens:nAT,MN_CC     ]               ,;
										                aItens[ oListItens:nAT,MN_DESCCC ]               ,;
										                aItens[ oListItens:nAT,MN_ITEM   ]               ,;
										                aItens[ oListItens:nAT,MN_DESCITE]               ,;
										     Transform( aItens[ oListItens:nAT,MN_HRMES  ], "@E 999.99"   )	,;
										     Transform( aItens[ oListItens:nAT,MN_HORAS  ], "@E 999.99"   ) 	,;
										     Transform( aItens[ oListItens:nAT,MN_PERCEN ], "@E 999.99") }}

	//ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar( oDlg, {|| oDlg:End() }, {|| oDlg:End() },, aBotoes ) 
	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar( oDlg, {|| IF(U_DTGPE04ChkPerc('V'),oDlg:End(),oDlg:Refresh())}, {|| IF(U_DTGPE04ChkPerc('V'),oDlg:End(),oDlg:Refresh())},, aBotoes )

EndIf
 
Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ GPE04QRY        ³ Marcelo Coutinho    º Data ³  16/05/14    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                             º±±
±±º          ³                                                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ DTGPE04                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function GPE04QRY( lFiltro,cFilRat,cMatRat )
Local cAliasQry  := GetNextAlias()
Local aAreaAtu   := GetArea()
Local cQuery     := ''
Local nDelItens  := Len( aItens )

Local nSizeCTT   := 4
Local nSizeCTD   := 4   
Local nI := 0
Default lFiltro  := .F.  

//--------------------------------------
//
//-----------------------------------------------------------

For nI := 1 To nDelItens

    aDel( aItens, nI )
Next

aSize( aItens, 0 )

//--------------------------------------
//                                                            
//-----------------------------------------------------------

cQuery :=        "Select ZD6.ZD6_MESANO, ZD6.ZD6_CC, CTT.CTT_DESC01, ZD6.ZD6_ITEM, CTD.CTD_DESC01, ZD6.ZD6_HRMES, ZD6.ZD6_HRTRAB, ZD6.ZD6_PERCEN, ZD6.R_E_C_N_O_ ZD6_RECNO"

cQuery += CRLF + "  From " + RetSqlName("ZD6") + " ZD6 "

cQuery += CRLF + "  Left Join " + RetSqlName("CTT") + " CTT On SubStr( CTT.CTT_FILIAL, 1, " + Str( nSizeCTT, 2 ) + " ) = SubStr( ZD6.ZD6_FILIMP, 1, " + Str( nSizeCTT, 2 ) + " ) And CTT.CTT_CUSTO = ZD6.ZD6_CC    And CTT.D_E_L_E_T_ = ' '" 
cQuery += CRLF + "  Left Join " + RetSqlName("CTD") + " CTD On SubStr( CTD.CTD_FILIAL, 1, " + Str( nSizeCTD, 2 ) + " ) = SubStr( ZD6.ZD6_FILIMP, 1, " + Str( nSizeCTD, 2 ) + " ) And CTD.CTD_ITEM  = ZD6.ZD6_ITEM  And CTD.D_E_L_E_T_ = ' '"

cQuery += CRLF + " Where ZD6.D_E_L_E_T_ = ' '"
cQuery += CRLF + "   And ZD6.ZD6_MAT    = '" + cMatRef  + "'"
cQuery += CRLF + "   And SubStr( ZD6.ZD6_FILIMP, 1, " + Str( nSizeCTT, 2 ) + " ) = '" + SubStr( cFilRef, 1, nSizeCTT ) + "'"   

If lFiltro
   cQuery += CRLF + " And ZD6.ZD6_MESANO = '" + cAnoBase + StrZero( oMesBase:nAt, 2 ) + "'"
EndIf

cQuery += CRLF + " Order By ZD6.ZD6_MESANO DESC, ZD6.ZD6_CC, ZD6.ZD6_ITEM "

cQuery := ChangeQuery( cQuery )

dbUseArea( .T., "TOPCONN", TCGenQry(,,cQuery), cAliasQry, .T., .T. )     
TCSetField(cAliasQry,"ZD6_HRMES" ,"N",TamSX3("ZD6_HRMES" )[1],TamSX3("ZD6_HRMES")[2])
TCSetField(cAliasQry,"ZD6_HRTRAB","N",TamSX3("ZD6_HRTRAB")[1],TamSX3("ZD6_HRTRAB")[2])
TCSetField(cAliasQry,"ZD6_PERCEN","N",TamSX3("ZD6_PERCEN")[1],TamSX3("ZD6_PERCEN")[2])

//--------------------------------------
//
//-----------------------------------------------------------

While (cAliasQry)->(!Eof())

	Aadd( aItens, Array( MN_QTDCOL ) )

	nPosItens := Len( aItens )

	aItens[nPosItens][MN_MARCA  ] := .F.
	aItens[nPosItens][MN_MES    ] := SubStr( (cAliasQry)->( ZD6_MESANO ), 5, 2 )
	aItens[nPosItens][MN_ANO    ] := SubStr( (cAliasQry)->( ZD6_MESANO ), 1, 4 )
	aItens[nPosItens][MN_CC     ] :=         (cAliasQry)->( ZD6_CC     )
	aItens[nPosItens][MN_DESCCC ] :=         (cAliasQry)->( CTT_DESC01 )
	aItens[nPosItens][MN_ITEM   ] :=         (cAliasQry)->( ZD6_ITEM   )
	aItens[nPosItens][MN_DESCITE] :=         (cAliasQry)->( CTD_DESC01 )
	aItens[nPosItens][MN_HRMES  ] :=         (cAliasQry)->( ZD6_HRMES  )
	aItens[nPosItens][MN_HORAS  ] :=         (cAliasQry)->( ZD6_HRTRAB )
	aItens[nPosItens][MN_PERCEN ] :=         (cAliasQry)->( ZD6_PERCEN )
	aItens[nPosItens][MN_RECNO  ] :=         (cAliasQry)->( ZD6_RECNO  )

	(cAliasQry)->(dbSkip())
End

If Len( aItens ) == 0

   Aadd( aItens, Array( MN_QTDCOL ) )

	aItens[Len( aItens )][MN_MARCA  ] := .F.
	aItens[Len( aItens )][MN_MES    ] := ""//StrZero( Month( dDataBase ), 2 )
	aItens[Len( aItens )][MN_ANO    ] := ""//StrZero(  YEar( dDataBase ), 4 )
	aItens[Len( aItens )][MN_CC     ] := ""
	aItens[Len( aItens )][MN_DESCCC ] := ""
	aItens[Len( aItens )][MN_ITEM   ] := ""
	aItens[Len( aItens )][MN_DESCITE] := ""
	aItens[Len( aItens )][MN_HRMES  ] := 0
	aItens[Len( aItens )][MN_HORAS  ] := 0
	aItens[Len( aItens )][MN_PERCEN ] := 0.00
	aItens[Len( aItens )][MN_RECNO  ] := 0
EndIf

(cAliasQry)->(dbCloseArea())

RestArea(aAreaAtu)

Return NIL

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ GPE04Todos  ºAutor  ³ Marcelo Coutinho   º Data ³  16/05/14 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Marca / Desmarca Todos                                      º±±
±±º          ³                                                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ DTGPE04                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function GPE04Todos( lMarca, nPosMarca )

Local nX := 0
Local cPerMes := ''
Local cPerAno := ''
Local lAlert	:= .F.
Default lMarca    := .F.
Default nPosMarca := 0

For nX := 1 To Len( aItens )

	cPerAno := aItens[nX][MN_ANO]
	cPerMes := aItens[nX][MN_MES]
	cPerReg := cPerAno+cPerMes

	If nPosMarca # nX	
	
		If cPerReg = cPeriodo .OR. (RetCodUsr() $ cUserRat)
			aItens[nX][MN_MARCA] := lMarca
		EndIf
		
		IF cPerReg <> cPeriodo .AND. nPosMarca = 0 
			lAlert := .T.
		EndIf

	ElseIf nPosMarca > 0
	
		
		If cPerReg = cPeriodo .OR. (RetCodUsr() $ cUserRat)
			aItens[nPosMarca][MN_MARCA] := !aItens[nPosMarca][MN_MARCA]
		Else
			lAlert := .T.
		EndIf	

	EndIf
Next

If lAlert .AND. nPosmarca = 0 .AND. !(RetCodUsr() $ cUserRat)
	Alert( "A opção Marcar Todos, só irá selecionar registros do periodo "+Substr(cPeriodo,1,4)+"/"+Substr(cPeriodo,5,2)+" !" )
EndIf
If lAlert .AND. nPosmarca > 0 .AND. !(RetCodUsr() $ cUserRat)
	Alert( "Registro indisponível para manutenção!" )
EndIf

oListItens:Refresh()
     
Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ GPE04Todos  ºAutor  ³ Marcelo Coutinho   º Data ³  16/05/14 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Marca / Desmarca Todos                                      º±±
±±º          ³                                                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ DTGPE04                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function GPE04Opc(cOpc)

Local oDlg
Local oMes
Local oAno
Local oCC
Local oCCDesc
Local oItem
Local oItemDesc
Local oHrMes
Local oHrAloc
Local oPerc

Local cMes      := SUBSTR(cPERIODO,5,2) //StrZero(  Month( dDataBase   ), 2 )
Local cAno      := SUBSTR(cPERIODO,1,4) //StrZero(   Year( dDataBase   ), 4 )  
Local cItem     :=   Space( TamSX3( "CTD_ITEM"  )[1] )
Local cCCDesc   := ""
Local cItemDesc := ""
Local nHrAloc   := 0
Local nPerc     := 0
Local cMesAno   := ""
Local nTotComp  := 1
Local nExclui   := 0

Local nAltera   := 0 

Local cTitulo   := Iif( cOpc == "I", "Inclusão", Iif( cOpc == "A", "Alteração", "Exclusão" ) ) + " de itens para Rateio" 
Local nI := 0
Private cCC       :=   Space( TamSX3( "CTT_CUSTO" )[1] )

If lMarcaTudo .And. cOpc # "I"

	For nI := 1 To Len( aItens)

      cMesAno := aItens[ nI ][ MN_ANO ] + aItens[ nI ][ MN_MES ]

		If aItens[ nI ][ MN_MARCA ]

			If cOpc == "E"

				nExclui += 1

			ElseIf cOpc == "A"

				nAltera += 1

			EndIf

		EndIf

		If nI < Len( aItens )

			If cMesAno # aItens[ nI ][ MN_ANO ] + aItens[ nI ][ MN_MES ] .And. aItens[ nI ][ MN_MARCA ]

            nTotComp += 1

			EndIf
		EndIf
	Next

	If nTotComp > 1
	
		If cOpc == "E" .And. nExclui > 0

			Alert( "Para Excluir vários registros, é necessário que estejam na mesma competência (Mês/Ano)!" )

			Return .T.

		ElseIf cOpc == "A"

			Alert( "Para Alterar, é necessário que os registros estejam filtrados em uma única competência (Mês/Ano)!" )

			Return .T.

		EndIf

	Else
	
		If cOpc == "A" .And. nAltera > 0

			Alert( "Para opção Alterar, é necessário que apenas um registro esteja selecionado!" )

			Return .T.

		EndIf

	EndIf

EndIf

If cOpc # "I" .And. Ascan( aItens, { |x| x[ MN_MARCA ] == .T. } ) = 0

	Alert( "Selecione um registro para " + Iif( cOpc == "E", "Exclusão!", "Alteração!" ) )

   Return .T.

EndIf

If cOpc == "A"

	nPos := Ascan( aItens, { |x| x[MN_MARCA] == .T. } )

	If nPos > 0

		cMes      := aItens[ nPos ][ MN_MES     ]
		cAno      := aItens[ nPos ][ MN_ANO     ]
		cCC       := aItens[ nPos ][ MN_CC      ]
		cCCDesc   := aItens[ nPos ][ MN_DESCCC  ]
		cItem     := aItens[ nPos ][ MN_ITEM    ]
		cItemDesc := aItens[ nPos ][ MN_DESCITE ]
		nHrMes    := aItens[ nPos ][ MN_HRMES   ]
		nHrAloc   := aItens[ nPos ][ MN_HORAS   ]
		nPerc     := aItens[ nPos ][ MN_PERCEN  ]

	EndIf
	
	If cPeriodo > cAno+cMes .AND. !(RetCodUsr() $ cUserRat)
	
			Alert( "Não se pode alterar registros anteriores a competência "+cMes+"/"+cAno+" !" )
			Return .T.
		
	EndIf
	
EndIf

//-------------------------------------------------------
//
//------------------------------------------------------------------------------

If cOpc # "E"
	
	U_DTGPE04ChkPerc("I")
    
 	nHrMes := IIF(nHrsMesTot=0,SRA->RA_HRSMES,nHrsMesTot)
 	
	DEFINE MSDIALOG oDlg FROM 000,000 TO 250,540 PIXEL TITLE cTitulo

		oPanel := tPanel():New( 003, 003, "", oDlg,,,,, CLR_WHITE, 265, 105, .T.) 
	
		@ 005, 005 SAY "Mês"                                                                                                                               SIZE 030, 009 OF oPanel PIXEL
		@ 013, 005 MSGET oMes      VAR cMes          WHEN .F.                                                                                              SIZE 020, 010 OF oPanel PIXEL

		@ 005, 040 SAY "Ano"                                                                                                                               SIZE 030, 009 OF oPanel PIXEL
		@ 013, 040 MSGET oAno      VAR cAno          WHEN .F.                                                                                              SIZE 050, 010 OF oPanel PIXEL
	
		@ 030, 005 SAY "Centro de Custo"                                                                                                                   SIZE 100, 009 OF oPanel PIXEL
		@ 038, 005 MSGET oCC       VAR cCC           F3 "CTA1"        VALID ( cCCDesc   := GPE04Psq( "C", cCC   ), oCCDesc:Refresh(),   !Empty(cCCDesc))   SIZE 050, 010 OF oPanel PIXEL
		@ 038, 060 MSGET oCCDesc   VAR cCCDesc       WHEN .F.                                                                                              SIZE 200, 010 OF oPanel PIXEL

		@ 055, 005 SAY "Item Contábil"                                                                                                                     SIZE 100, 009 OF oPanel PIXEL
		@ 063, 005 MSGET oItem     VAR cItem         F3 "CTA2"        ;
			VALID ( cItemDesc := GPE04Psq( "I", cItem ), oItemDesc:Refresh(), !Empty(cItemDesc) .AND. CtbAmarra("",cCC,cItem,"",.F.,.T.)) 				   SIZE 050, 010 OF oPanel PIXEL

		@ 063, 060 MSGET oItemDesc VAR cItemDesc     WHEN .F.                                                                                              SIZE 200, 010 OF oPanel PIXEL

		@ 080, 005 SAY "Horas Mês"                                                                                                                         SIZE 100, 009 OF oPanel PIXEL
		IF SRA->RA_CATFUNC = 'M'
			@ 088, 005 MSGET oHrMes    VAR nHrMes    PICTURE "999.99"  WHEN .F.																			   SIZE 030, 010 OF oPanel PIXEL
		ELSE                                                       
			@ 088, 005 MSGET oHrMes    VAR nHrMes    PICTURE "999.99"     																					   SIZE 030, 010 OF oPanel PIXEL
		ENDIF

		@ 080, 065 SAY "Horas Alocadas"                                                                                                                    SIZE 100, 009 OF oPanel PIXEL
		@ 088, 065 MSGET oHrAloc   VAR nHrAloc       PICTURE "999.99"   VALID ( nPerc := ROUND(( nHrAloc * 100 / nHrMes ),2), oHrAloc:Refresh(), (nHrAloc <= nHrMes .AND. nHrAloc > 0) )    SIZE 030, 010 OF oPanel PIXEL

		@ 080, 130 SAY "Percentual (%)"                                                                                                                    SIZE 100, 009 OF oPanel PIXEL
		@ 088, 130 MSGET oPerc     VAR nPerc         When .F.                                                                                              SIZE 040, 010 OF oPanel PIXEL


		DEFINE SBUTTON FROM 110,205 TYPE 1 OF oDlg ENABLE ACTION ( IIF(CtbAmarra("",cCC,cItem,"",.F.,.T.),(GPE04Atu( cOpc, cAno, cMes, cCC, cItem, nHrMes, nHrAloc, nPerc ), oDlg:End()),NIL) ) WHEN !Empty(cOpc) .And. !Empty(cAno) .And. !Empty(cMes) .And. !Empty(cCC) .And. !Empty(cItem) .And. nHrMes > 0 .And. nHrAloc > 0 //.And. nPerc>0
		DEFINE SBUTTON FROM 110,235 TYPE 2 OF oDlg ENABLE ACTION oDlg:End()

	ACTIVATE MSDIALOG oDlg CENTERED

Else
	
	If cPeriodo > cAno+cMes .AND. !(RetCodUsr() $ cUserRat)
	
			Alert( "Não se pode alterar registros anteriores a competência "+cMes+"/"+cAno+" !" )
			Return .T.
		
	EndIf

	If MsgYesNo( "Confirma a Exclusão do(s) registro(s) selecionado(s)?" )

 		GPE04Atu( "E" )

	EndIf

EndIf
     
Return 

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ GPE04Atu    ºAutor  ³ Marcelo Coutinho   º Data ³  16/05/14 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Manutenção de REgistro Importados                           º±±
±±º          ³                                                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ DTGPE04                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function GPE04Atu( cMov, cAno, cMes, cCC, cItem, nHrMes, nHrAloc, nPerc )

Local lRet := .T.
Local nX := 0
If cMov # "E"

	If cMov == "I"

		If !( ZD6->( dbSeek( xFilial( "ZD6" ) + cAno + cMes + SRA->RA_MAT + cCC + cITem ) ) )
            
            lRet := .T.
                               
			U_DTGPE04Perc("A") //Valida percentual total
    		nTotHor  := nTotHor + nHrAloc   
    		nTotPerc := ROUND(( nTotHor * 100 / nHrMes ),2)
    
    		IF nTotPerc > 100
       
    			Alert("Percentual total de horas trabalhadas final, supera 100% ou a quantidade de horas mes é inferior ao total informado!  Favor verificar.")
    			lRet := .F.
    	
    		EndIf

            
    		IF (nHrMes > 240 .AND. lRet = .T.)
    	
    			Alert("Horas trabalhadas no mês, não podem ser maior que 240 hs.")
    			lRet := .F.

    		EndIf   
    
    		IF (nHrsMesTot <> nHrMes .AND. lRet = .T. .AND. nHrsMesTot <> 0)
    	
    			lRet := .F.
    			If MsgYesNo( "Está sendo modificada a quantidade de horas mês do funcionário. Deseja manter alteração?" )
    				lRet := .T.
    			EndIf          
    	
    		EndIf      
    	
    
    		If lRet = .T.
	
	    		RecLock("ZD6", .t.)

    		EndIf
    		
			
		Else

			Alert( "Dados já cadastrados! Verifique as informações." )

			lRet := .F.

		EndIf
	Else

		nPos := Ascan( aItens, { |x| x[MN_MARCA] == .T. } )

		If nPos > 0

			ZD6->( dbGoTo( aItens[ nPos ][ MN_RECNO ] ) )

			RecLock("ZD6", .F.)

	   EndIf
   EndIf
        

	If lRet 
		
       ZD6->ZD6_FILIAL := SRA->RA_FILIAL//xFilial("ZD6")
		ZD6->ZD6_MESANO := cAno + cMes
		ZD6->ZD6_MAT    := SRA->RA_MAT
		ZD6->ZD6_CC     := cCC
		ZD6->ZD6_ITEM   := cItem
		ZD6->ZD6_HRMES  := nHrMes
		ZD6->ZD6_HRTRAB := nHrAloc
		ZD6->ZD6_PERCEN := nPerc
		ZD6->ZD6_USRIMP := UsrRetName(RetCodUsr())
		ZD6->ZD6_DATIMP := dDataBase
		ZD6->ZD6_HORIMP := Time()
		ZD6->ZD6_FILIMP := SUBSTR(SRA->RA_FILIAL,1,4)

		ZD6->( MsUnLock() )
		
		IF nHrsMesTot <> nHrMes //Atualiza percentual dos registros, caso horas mes tenha sido alterado na inserção
		    
		
			U_DTGPE04ChkPerc("A")
		
		EndIf
        

		GPE04QRY()

	EndIf

ElseIf cMov == "E"

	For nX := 1 To Len( aItens )

		If aItens[ nX ][ MN_MARCA ]

			ZD6->( dbGoTo( aItens[ nX ][ MN_RECNO ] ) )

			RecLock("ZD6", .F.)		
			
      			ZD6->( DbDelete() )
			
			ZD6->( MsUnLock() )

      EndIf

	Next

	GPE04QRY()

EndIf

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ GPE04Psq    ºAutor  ³ Marcelo Coutinho   º Data ³  16/05/14 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Pesquisa Centro de Custo / Item Contábil                    º±±
±±º          ³                                                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ DTGPE04                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function GPE04Psq( cTp, cCod )

Local cDescricao := ""

If cTp == "C"

	dbSelectArea("CTT")
	dbOrderNickName("DTCTT01")

   CTT->( dbSeek( cCod + xFilial("CTT") ) )

	cDescricao := CTT->CTT_DESC01

ElseIf cTp == "I"

	dbSelectArea("CTD")
	dbOrderNickName("DTCTD01")

   If CTD->( dbSeek( cCod + xFilial("CTD") ) ) 
		If SubStr(cCod,1,2) == SubStr(cValToChar(Year(dDataBase)),3,2)
			cDescricao := CTD->CTD_DESC01
		Else
			Alert("Item Contabil informado não é do Ano Corrente, Favor Verificar!")
		EndIf
	EndIf

EndIf

Return cDescricao
/*/f/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
<Descricao> : Rotina para controle de versao
<Data> : 24/05/2014
<Parametros> : Nenhum
<Retorno> : cRet
<Processo> : Controle de versao
<Tipo> (Menu,Trigger,Validacao,Ponto de Entrada,Genericas,Especificas ) : V
<Autor> : Doit Sistemas
<Obs> :
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
*/

User Function DTGPE04V()

Local cRet := ""
cRet := "20140523"
 
Return cRet     

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ DTGPE04Perc ºAutor  ³ William Santos     º Data ³  09/06/14 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Controle Total de Horas no mes                              º±±
±±º          ³                                                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ DTGPE04                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/                                                       

User Function DTGPE04Perc()

nTotPerc := 0 
nTotHor	 := 0
      
cQuery :=	"Select ZD6_MESANO, ZD6_MAT, ZD6_HRMES, ZD6_HRTRAB, ZD6_PERCEN FROM "+RetSqlName("ZD6")+" "
/* Jose Leite - 29-01-14
	Estava acontecendo de existir matricula em outras filiais, logo,
	nao deixava realizar a inclusao do rateio.
	
	Foi adicionado a instrucao para filtrar a filial no where
	
	-old
	cQuery +=	"Where ZD6_MESANO = '"+cPeriodo+"' AND ZD6_MAT = '"+cCodFunc+"' AND D_E_L_E_T_ <> '*' "
*/
//Inicio
cQuery +=	"Where ZD6_FILIAL = '"+cFilRef+"' AND ZD6_MESANO = '"+cPeriodo+"' AND ZD6_MAT = '"+cCodFunc+"' AND D_E_L_E_T_ <> '*' "
//Fim
cQuery += 	"ORDER BY ZD6_HRMES DESC"

cQuery := ChangeQuery( cQuery )
dbUseArea( .T., "TOPCONN", TCGenQry(,,cQuery), "TABPERC", .T., .T. ) 

nHrsMesTot := TABPERC->ZD6_HRMES

While TABPERC->(!EOF())
    nTotHor	 += TABPERC->ZD6_HRTRAB
    nTotPerc += TABPERC->ZD6_PERCEN
	TABPERC->(DBSKIP())
EndDo  

TABPERC->(DBCLOSEAREA())    

Return


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ DTGPE04AtuPerc ºAutor ³ William Santos   º Data ³  09/06/14 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Controle Total de Horas no mes                              º±±
±±º          ³                                                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ DTGPE04                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/                                                       

User Function DTGPE04ChkPerc(nOpcao)

Local nTotal := 0
Local nHorTotais := 0
      
cQuery :=	"Select ZD6_MESANO, ZD6_MAT, ZD6_HRMES, ZD6_HRTRAB, ZD6_PERCEN,ZD6_CC,ZD6_ITEM FROM "+RetSqlName("ZD6")+" "
/* Jose Leite - 29-01-14
	Estava acontecendo de existir matricula em outras filiais, logo,
	nao deixava realizar a inclusao do rateio.
	
	Foi adicionado a instrucao para filtrar a filial no where
	
	-old
	cQuery +=	"Where ZD6_MESANO = '"+cPeriodo+"' AND ZD6_MAT = '"+cCodFunc+"' AND D_E_L_E_T_ <> '*' "
*/
//Inicio
cQuery +=	"Where ZD6_FILIAL = '"+cFilRef+"' AND ZD6_MESANO = '"+cPeriodo+"' AND ZD6_MAT = '"+cCodFunc+"' AND D_E_L_E_T_ <> '*' "
//Fim
cQuery += 	"ORDER BY ZD6_HRMES DESC"

cQuery := ChangeQuery( cQuery )
dbUseArea( .T., "TOPCONN", TCGenQry(,,cQuery), "TAB1", .T., .T. ) 

nHrsMesTot := TAB1->ZD6_HRMES

If nOpcao = 'A'
	While TAB1->(!EOF())
		
		cAno := SUBSTR(TAB1->ZD6_MESANO,1,4)
		cMes := SUBSTR(TAB1->ZD6_MESANO,5,2)
	    cCC	 := TAB1->ZD6_CC
	    cItem :=TAB1->ZD6_ITEM   
	    
	    aAREA := GETAREA()
	    DBSELECTAREA("ZD6")
	    DBSETORDER(1)
	    DBGOTOP()
	
		ZD6->( dbSeek( xFilial( "ZD6" ) + cAno + cMes + SRA->RA_MAT + cCC + cITem ) )
	    
	    RecLock("ZD6", .F.)
	    
	    ZD6->ZD6_PERCEN := (TAB1->ZD6_HRTRAB * 100 / nHrMes)
	    ZD6->ZD6_HRMES	:= nHrMes  
	    
	    ZD6->( MsUnLock() )
	    RESTAREA(aAREA)
		
		TAB1->(DBSKIP())
	EndDo    
EndIf

If nOpcao = 'V'         
	
	nHorTotais := TAB1->ZD6_HRMES          
	
	While TAB1->(!EOF())
		nTotal += TAB1->ZD6_HRTRAB
		TAB1->(DBSKIP())
	EndDo  
	
	IF (nTotal <> nHorTotais  .AND. nTotal <> 0)
		MsgAlert('Quantidade de horas de alocação final inferior a 100%. Favor verificar!')
		TAB1->(DBCLOSEAREA()) 
		Return .F.
	EndIf  

EndIf      
	
	TAB1->(DBCLOSEAREA())  
	
Return .T.   