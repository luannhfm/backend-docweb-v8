#INCLUDE 'PROTHEUS.CH'
#INCLUDE 'RWMAKE.CH'
#INCLUDE 'FONT.CH'
#INCLUDE 'COLORS.CH' 
#INCLUDE 'TOPCONN.CH'

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ TcSupOk   ³ Autor ³Leonardo P de Castro  ³ Data ³ 06/11/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Verifica se usuario logado eh um operador do tipo superior  ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ P10                                                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

User Function TcSupOk()

Local _aArea  := ZAF->(GetArea())
Local _lRet   := .T.
Local _cUsuID := __cUserID 

If _cUsuID != "000000" //Administrador do sistema, acesso total

	DbSelectArea("ZAF")
	ZAF->(DbSetOrder(4))
	
	If ZAF->(DbSeek(xFilial("ZAF") + _cUsuID))
		If ZAF->ZAF_NIVEL = "S"
			If ZAF->ZAF_ATIVO = "N"
				_lRet := .F.
			EndIF
		Else
			_lRet := .F.
		EndIF
	Else
		_lRet := .F.
	EndIF 
EndIF
	
RestArea(_aArea)

Return(_lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³TcHoraOk  ºAutor  ³Leonardo P de Castroº Data ³  07/11/09   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Funcao de validacao das horas informadas pelo usuario.      º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParam.    ³_cHora - Hora que devera ser validada                       º±±
±±º          ³_cIni  - String da hora inicial do intervalo a ser validada º±±
±±º          ³_cFim  - String da hora final do intervalo a ser validada   º±±
±±º          ³_lMens - Define se a mensagem de hora invalida sera exibida º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ P10                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

User Function TcHoraOk(_cValor, _cIni, _cFim, _lMens)

Local _lRet	 := .F.			// Retorno logico da funcao
Local _nHor	 := 0			// Hora a ser validade
Local _nMin	 := 0			// Minuto a ser validado
Local _nHIni := 0			// Hora inicial do intervalo
Local _nMIni := 0			// Minuto inicial do intervalo
Local _nHFim := 0			// Hora final do intervalo
Local _nMFim := 0			// Minuto final do intervalo
Local _cVar	 := ReadVar()	// Nome da variavel da enchoice

Default _cValor	:= &(ReadVar())
Default _cIni	:= "00:00"
Default _cFim	:= "23:59"
Default _lMens	:= .T.

_nHor := Val(Subs(_cValor,1,2))
_nMin := Val(Subs(_cValor,4,2))

_cValor	:= StrZero(_nHor,2) + ":" + StrZero(_nMin,2)
If !Empty(_cVar)
	&_cVar	:= _cValor
Endif

_nHIni := Val(Subs(_cIni,1,2))
_nMIni := Val(Subs(_cIni,4,2))

_nHFim	:= Val(Subs(_cFim,1,2))
_nMFim	:= Val(Subs(_cFim,4,2))

If	(_nHor >= _nHIni .AND. _nHor <= _nHFim) .AND.;
	(_nMin >= _nMIni .AND. _nMin <= _nMFim)
	
	_lRet := .T.
Else
	If _lMens
		Help(" ",1,"TCHORANOK",,_cIni + " - " + _cFim,4,0)
	Endif
Endif

Return(_lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ TcParam  ºAutor  ³Leonardo P de Castroº Data ³  10/11/09   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Rotina que retorna no formato SQL as informacoes dos parame-º±±
±±º          ³tros da tabela ZAO                                          º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³	Array[01] = Gera lista ?                                  º±±
±±º          ³	Array[02] = Total dívida ?                                º±±
±±º          ³	Array[03] = Abandona atendimentos               		  º±±
±±º          ³	Array[04] = Prefixo ?     								  º±±
±±º          ³	Array[05] = Natureza ?           						  º±±
±±º          ³	Array[06] = Tipo ?          							  º±±
±±º          ³	Array[07] = Prazo maximo ?           					  º±±
±±º          ³	Array[08] = Cosiderar tipos ?           				  º±±
±±º          ³	Array[09] = Filial de ?                 				  º±±
±±º          ³	Array[10] = Filial ate ?                				  º±±
±±º          ³	Array[11] = Prazo 2 Parcela ?            				  º±±
±±º          ³	Array[12] = vencimento maximo para titulos muito atrasados?±±
±±º          ³	Array[13] = percentual maximo de desc para tit atras ?    º±±
±±º          ³	Array[14] = perc min para a entr em tit muito vencidos ?  º±±
±±º          ³	Array[15] = val min das parcelas para tit muito venc ?    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³P10                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

User Function TcParam()

Local _aRet := {"","","","","","",0,"","","",0,STOD(""),0,0,0}
Local _cFilial := "01MT0001,05MT0001"
Local _cTipTit := "FI"   

DbSelectArea("ZAO")
ZAO->(DbSetOrder(1))

If ZAO->(DbSeek(xFilial("ZAO") + "PARAM01   "))
	//Gera lista ?
	_aRet[1] := AllTrim(ZAO->ZAO_CONT)
	//Total divida ?   
	ZAO->(DbSkip())
	_aRet[2] := AllTrim(ZAO->ZAO_CONT)
	//Abandona atendimento ?
	ZAO->(DbSkip())
	_aRet[3] := AllTrim(ZAO->ZAO_CONT)
	//Prefixo ?        
	ZAO->(DbSkip())
	_aRet[4] := AllTrim(ZAO->ZAO_CONT)
	//Natureza ?      
	ZAO->(DbSkip())
	_aRet[5] := AllTrim(ZAO->ZAO_CONT)
	//Tipo ?                   
	ZAO->(DbSkip())
	_aRet[6] := AllTrim(ZAO->ZAO_CONT)
	//Prazo maximo                
	ZAO->(DbSkip())
	_aRet[7] := Val(AllTrim(ZAO->ZAO_CONT))
	//Considerar tipos?                 
	ZAO->(DbSkip())
	_cTipTit := AllTrim(ZAO->ZAO_CONT)
	//Prazo 2 Parcela
	ZAO->(DbSkip())
	_aRet[11] := Val(AllTrim(ZAO->ZAO_CONT))
	
	// vencimento maximo para titulos muito atrasados 
	ZAO->(DbSkip())
	_aRet[12] := STOD(AllTrim(ZAO->ZAO_CONT))
	// percentual maximo de desconto para titulos muito atrasados 
	ZAO->(DbSkip())
	_aRet[13] := Val(AllTrim(ZAO->ZAO_CONT))
	// percentual minimo para a entrada em titulos muito vencidos
	ZAO->(DbSkip())
	_aRet[14] := Val(AllTrim(ZAO->ZAO_CONT))
	// valor minimo das parcelas para titulos muito vencidos
	ZAO->(DbSkip())
	_aRet[15] := Val(AllTrim(ZAO->ZAO_CONT))
EndIF
	//Filal de 
	_aRet[9]  := Space(Len(cfilant))
	//Filial Ate
	_aRet[10] := Replicate("Z",Len(cfilant))
/*	Retirado este For pois a filial tem que ser de branco a ZZZZZ
For _nX := 1 To Len(_cFilial)
		If !(SubStr(_cFilial,_nX,1) $ ",.;:\|/!@#$%¨&*()-_+='<>´`^~Çç{}[]?") .And. Len(_aRet[9]) = 2
			_aRet[10]	+= SubStr(_cFilial,_nX,1)
		End
		If !(SubStr(_cFilial,_nX,1) $ ",.;:\|/!@#$%¨&*()-_+='<>´`^~Çç{}[]?") .And. Len(_aRet[9]) <= 2 .And. Len(_aRet[10]) < 1
			_aRet[9]	+= SubStr(_cFilial,_nX,1)
		End
	Next _nX
*/	
	_aRet[8]	:= "'"
	
	For _nY := 1 To Len(_cTipTit)
		If SubStr(_cTipTit,_nY,1) $ "ABCDEFGHIJKLMNOPQRSTVWXYZ0123456789 "
			_aRet[8]	+= SubStr(_cTipTit,_nY,1)
		Else
			_aRet[8]	+= "'"+IIF(_nY < Len(_cTipTit),",'","")
		EndIf
	Next _nY
	
	_aRet[8]	+= "'"

Return( _aRet ) 

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³TcSaudacaoºAutor  ³Leonardo P de Castroº Data ³  11/11/09   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Rotina que retorna uma saudacao sendo: Bom dia, Boa tarde ouº±±
±±º          ³Boa noite.                                                  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³P10                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

User Function TcSaudacao()

Local _cRet := ""
Local _cHora := Time()

If _cHora >= "06:00:00" .And. _cHora < "12:00:00"
	_cRet := "Bom dia"
ElseIf _cHora >= "12:00:00" .And. _cHora < "18:00:00"
	_cRet := "Boa tarde"
ElseIf _cHora >= "18:00:00" .And. _cHora <= "23:59:59"
	_cRet := "Boa noite"                   
ElseIf _cHora >= "00:00:00" .And. _cHora <= "05:59:59"
	_cRet := "Boa noite"
EndIF

Return(_cRet)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ TcInfo    ³ Autor ³Leonardo P de Castro  ³ Data ³ 11/11/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Obtem as informacoes do Operador logado no sistema          ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ P10                                                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

User Function TcInfo()

Local _aArea  := ZAF->(GetArea())
Local _aRet   := {"","","","","","",0,0,0} //Codigo, ID, Nome, N Reduz, Nivel, Ativo?, Desc. Jur, Desc. Mul, Perc Comis
Local _cUsuID := __cUserID 

DbSelectArea("ZAF")
ZAF->(DbSetOrder(4))

If ZAF->(DbSeek(xFilial("ZAF") + _cUsuID))
	_aRet[1] := ZAF->ZAF_COD	
	_aRet[2] := ZAF->ZAF_CODUSU
	_aRet[3] := ZAF->ZAF_NOME
	_aRet[4] := ZAF->ZAF_NREDUZ
	_aRet[5] := ZAF->ZAF_NIVEL
	_aRet[6] := ZAF->ZAF_ATIVO
	_aRet[7] := ZAF->ZAF_PRCJUR
	_aRet[8] := ZAF->ZAF_PRCMUL
	_aRet[9] := ZAF->ZAF_COMIS
EndIF 
	
RestArea(_aArea)

Return(_aRet)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ TcFases   ³ Autor ³Leonardo P de Castro  ³ Data ³ 12/11/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Obtem as fases de cobranca que o operador esta apto a operar³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ P10                                                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

User Function TcFases( _cCodOp )

Local _aRet := {}
Local _cSql := ""

_cSql := ""
_cSql += " SELECT 
_cSql += "		* 
_cSql += " FROM 
_cSql += 		RetSqlName("ZAG")
_cSql += " WHERE 
_cSql += "		ZAG_FILIAL = '" + xFilial("ZAG") + "' AND 
_cSql += "		ZAG_CODOPE = '" + _cCodOp + "' AND
_cSql += "		D_E_L_E_T_ != '*' 
_cSql += " ORDER BY "
_cSql += " 		ZAG_CODFAS "
_cSql := ChangeQuery(_cSql)  

If Select("tZAG") > 0
	DbSelectArea("tZAG")
	tZAG->(DbCloseArea())
EndIF

TcQuery _cSql New Alias tZAG

If tZAG->(!Eof())
	
	While tZAG->(!Eof())		
	
		DbSelectArea("ZAE")
		ZAE->(DbSetOrder(1)) 
		
		If ZAE->(DbSeek(xFilial("ZAE") + tZAG->ZAG_CODFAS))
		
			If tZAG->ZAG_VALIDA >= DtoS(dDataBase)
				aAdd(_aRet,tZAG->ZAG_CODFAS + " - " + ZAE->ZAE_DESCRI)	
			EndIF
		EndIF
		
		tZAG->(DbSkip())
	EndDo
EndIF

Return(_aRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³TcSemaf           ³Leonardo P de Castroº Data ³  13/11/09   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Rotina validar se o atendimento esta liberado.              º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/ 

User Function TcSemaf( _cRot, _nReg, _nRecZAN, _lMsg )

Local _aArea := GetArea()
Local _lRet  := .T.               

DbSelectArea("ZAN")
DbSetOrder(1)
_cRot := _cRot + Space(Len(ZAN->ZAN_ROTINA)-Len(_cRot))

If !DbSeek(xFilial("ZAN")+_cRot+StrZero(_nReg,10,0))
	RecLock("ZAN",.T.)
	Replace ZAN->ZAN_FILIAL  With xFilial("ZAN")
	Replace ZAN->ZAN_ROTINA  With _cRot  
	Replace	ZAN->ZAN_USUARIO With cUserName
	Replace ZAN->ZAN_REGIST  With StrZero(_nReg,10,0)
	MsUnlock()
	_nRecZAN := Recno()
Else
	If _lMsg
		Alert("Este registro já esta sendo atendido pelo usuário: " + ZAN->ZAN_USUARIO)
	EndIF
	_lRet  := .F.
Endif

RestArea(_aArea)

Return (_lRet)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ TcMotivo ³ Autor ³Leonardo P de Castro  ³ Data ³ 12/11/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Obtem os motivos de cobranca que poderam ser utilizadas nest³±±
±±³          ³a fase.                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ P10                                                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

User Function TcMotivo( _cCodFas )

Local _aRet := {}
Local _cSql := ""

_cSql := ""
_cSql += " SELECT "
_cSql += " 		ZAI.ZAI_CODMOT, ZAH.ZAH_DESCRI "
_cSql += " FROM "
_cSql +=  		RetSqlName("ZAI") + " ZAI "
_cSql += " INNER JOIN " + RetSqlname("ZAH") + " ZAH "
_cSql += " ON ZAI.ZAI_FILIAL = ZAH.ZAH_FILIAL "
_cSql += " AND ZAI.ZAI_CODMOT = ZAH.ZAH_COD "
_cSql += " WHERE "
_cSql += " 		ZAI.ZAI_CODFAS = '" + _cCodFas + "' AND "
_cSql += " 		ZAI.D_E_L_E_T_ != '*'  AND "
_cSql += " 		ZAH.D_E_L_E_T_ != '*' "
_cSql += " ORDER BY "
_cSql += " 		ZAI.ZAI_CODMOT "
_cSql := ChangeQuery(_cSql)  

	If Select("tZAI") > 0
		DbSelectArea("tZAI")
		tZAI->(DbCloseArea())
	EndIF

TcQuery _cSql New Alias tZAI

If tZAI->(!Eof())
	
	While tZAI->(!Eof())		
	
		aAdd(_aRet, tZAI->ZAI_CODMOT + " - " + tZAI->ZAH_DESCRI)	
		
		tZAI->(DbSkip())
	EndDo
EndIF

Return(_aRet)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ TcCondPgt ³ Autor ³Leonardo P de Castro  ³ Data ³ 12/11/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Verifica se existe condicoes de pagamento para renegociacoes³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ P10                                                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

User Function TcCondPgt()

Local _lRet := .T.
Local _cSql := ""

_cSql := ""
_cSql += " SELECT "
_cSql += " 		COUNT(*) AS QUANT "
_cSql += " FROM "
_cSql +=  		RetSqlName("SE4")
_cSql += " WHERE "
_cSql += " 		D_E_L_E_T_ <> '*' AND "
_cSql += " 		E4_XTELECO = 'S' "
_cSql := ChangeQuery(_cSql)  

	If Select("tSE4") > 0
		DbSelectArea("tSE4")
		tSE4->(DbCloseArea())
	EndIF

TcQuery _cSql New Alias tSE4

If tSE4->QUANT = 0
	_lRet := .F.
EndIF

Return(_lRet)
