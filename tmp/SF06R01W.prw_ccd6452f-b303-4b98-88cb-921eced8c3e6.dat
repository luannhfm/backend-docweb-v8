#include 'protheus.ch'
#include 'parmtype.ch'
#include "rwmake.ch"


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  |SF06R01W    ºAutor  ³Walmir Junior       º Data ³03/05/2020 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³RELATORIO DE BORDERÔS ALTERADOS - INSTRUÇÃO DE COBRANÇAS.   º±±
±±º          ³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³                                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/


User Function SF06R01W(_pFil, _pCodLg, _pOcor)
Local oReport
Private _cAlias := GetNextAlias()
Private _oBord
Private _oTit

Private _cFil 	:= _pFil
Private _cCodLg := _pCodLg
Private _cOcorr := _pOcor

oReport := ReportDef()
oReport:PrintDialog()        

Return

Static Function ReportDef()
Local oReport

oReport := TReport():New(	"Borderôs Alterados","Instruções de Cobranças ("+_cOcorr+")",,{|oReport| PrintReport(oReport)},;
							"Relação de borderôs alterados para envio de Instrução de Cobranças")
oReport:nLeftMargin := 2
//oReport:nFontBody := 8
//oReport:nLineHeight := 45
oReport:SetPortrait() // retrato
oReport:oPage:SetPaperSize(9) // papel A4
oReport:SetEnvironment(2) // local

_oBord := TRSection():New(oReport,"Borderôs" ,{_cALias})

TRCell():New(_oBord,"ZGA_DATA"	 ,_cALias, "Data" 			,,12,,{|| SToD((_cALias)->ZGA_DATA)})
TRCell():New(_oBord,"ZGA_FILIAL" ,_cALias, "Filial" 		)
TRCell():New(_oBord,"ZGA_CODIGO" ,_cALias, "Codigo" 		)
TRCell():New(_oBord,"ZGA_USUARI" ,_cALias, "Usuário" 		)
TRCell():New(_oBord,"ZGA_NUMBOR" ,_cALias, "Num Borderô"	)

_oTit := TRSection():New(oReport,"Titulos Alterados" ,{_cALias})

TRCell():New(_oTit,"ZGA_DATA"	 ,_cALias, "Data" 	,,12,,{|| SToD((_cALias)->ZGA_DATA)})
TRCell():New(_oTit,"ZGA_FILIAL" ,_cALias, "Filial" 	)
TRCell():New(_oTit,"ZGA_CODIGO" ,_cALias, "Codigo" 	)
TRCell():New(_oTit,"ZGA_USUARI" ,_cALias, "Usuário" )
TRCell():New(_oTit,"ZGA_NUMBOR" ,_cALias, "Num Borderô"	)
TRCell():New(_oTit,"FILIAL" 	,_cALias, "Filial Titulo",,15 )
TRCell():New(_oTit,"PREFIXO" 	,_cALias, "Prefixo"	)
TRCell():New(_oTit,"TITULO" 	,_cALias, "Titulo"	)
TRCell():New(_oTit,"PARCELA" 	,_cALias, "Parcela"	)
TRCell():New(_oTit,"TIPO" 		,_cALias, "Tipo"	)
TRCell():New(_oTit,"E1_CLIENTE" ,_cALias, "Cliente"	)
TRCell():New(_oTit,"E1_NOMCLI" 	,_cALias, "Nome Cliente",,30 )

Return(oReport)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³funcao que realiza o filtro e imprimi os dados na tela³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

Static Function PrintReport(oReport)
Local _cAux := ""
GeraQuery()

(_cALias)->(dbGoTop())

_oBord:Init()

While !(_cAlias)->( EoF() )
	If _cAux != (_cAlias)->( ZGA_FILIAL + ZGA_CODIGO + ZGA_NUMBOR )
		_cAux := (_cAlias)->( ZGA_FILIAL + ZGA_CODIGO + ZGA_NUMBOR )
		_oBord:PrintLine()
	EndIf
	(_cAlias)->( DBSkip() )
EndDo
_oBord:Finish()

(_cALias)->(dbGoTop())
_oTit:Print()

(_cALias)->(dbClosearea())

Return

//ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
// Funcao que faz o select de acordo com os parametros do relatorio.
//ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
Static Function GeraQuery()
Local _cSql := ""
//Local nOrdem := _oBord:nOrder
//_oBord:nOrder := 0

_cSql += " SELECT ZGA_DATA, ZGA_FILIAL, ZGA_CODIGO, ZGA_NUMBOR, ZGA_USUARI, "+CRLF
_cSql += " SUBSTR(ZGA_CHVSE1,1,8) FILIAL, SUBSTR(ZGA_CHVSE1,9,3) PREFIXO, SUBSTR(ZGA_CHVSE1,12,9) TITULO, "+CRLF
_cSql += " SUBSTR(ZGA_CHVSE1,21,3) PARCELA, SUBSTR(ZGA_CHVSE1,24,3) TIPO , SE1.E1_CLIENTE, SE1.E1_NOMCLI "+CRLF
_cSql += " FROM "+RetSQLName("ZGA")+" ZGA LEFT JOIN " +CRLF
_cSql += " "+RetSQLName("SE1")+" SE1 ON SE1.D_E_L_E_T_ = ' ' AND ZGA_FILIAL = E1_FILIAL AND SUBSTR(ZGA_CHVSE1,9,3) = E1_PREFIXO " +CRLF
_cSql += " 						 AND SUBSTR(ZGA_CHVSE1,12,9) = E1_NUM AND SUBSTR(ZGA_CHVSE1,21,3) = E1_PARCELA " +CRLF
_cSql += " 						 AND SUBSTR(ZGA_CHVSE1,24,3) = E1_TIPO " +CRLF
_cSql += " WHERE ZGA.D_E_L_E_T_ = ' '  "+CRLF
_cSql += " AND ZGA_FILIAL = '"+ _cFil +"' "+CRLF
_cSql += " AND ZGA_CODIGO = '"+ _cCodLg +"' "+CRLF
_cSql += " ORDER BY ZGA_DATA, ZGA_FILIAL, ZGA_CODIGO, ZGA_NUMBOR, 7,8,9 "+CRLF

If Select(_cALias) > 0
	(_cALias)->(dbClosearea())
Endif

MemoWrite("D:\LOGSQL\" + FunName() + "_" + ProcName() + ".txt" , _cSql)

DbUseArea(.T., "TOPCONN", TCGenQry(,,ChangeQuery(_cSql)), _cALias, .F., .F.)

Return