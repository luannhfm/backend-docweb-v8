#Include 'Protheus.ch'
#INCLUDE 'TOPCONN.CH'

/*/{Protheus.doc} SF02R98X
Listagem de Pedidos de Compras por Situação

@author Paulo César P. Schwind
@since 03/05/2019
@return Nil

@type function
/*/
User Function SF02R98X()
	Local oReport     := nil
	Private cPerg     := Padr("SF02R98X",10)
    Private cTitRel

	ValidPerg()
	Pergunte(cPerg,.T.)
	if LastKey()=27
		Return
	Endif

	If MV_PAR07 == 1
		cTitRel := " - FINALIZADOS "
	ElseIf MV_PAR07 == 2
		cTitRel := " - EM ABERTO "
	ElseIf MV_PAR07 == 3
		cTitRel := " - TODOS "
	Endif

	oReport := RptDef(cPerg)
	oReport:PrintDialog()
Return

/*/{Protheus.doc} RptDef
	Definição da classe Treport
@author Paulo César P. Schwind
@since 03/05/2019
@return oReport, objeto Treport
@param cNome, characters, Nome do relatório e do arquivo de perguntas
@type function

@history 25/06/2019, Franklin de Brito de Oliveira, Alterado o tamanho da coluna C7_PRODUTO, da section2.
/*/
Static Function RptDef(cNome)
	Local oReport := Nil
	Local oSection1:= Nil
	Local oSection2:= Nil
	
	Local oBreak
	Local oFunction

	oReport := TReport():New(cNome,"Relatório de Pedidos"+cTitRel,cNome,{|oReport| ReportPrint(oReport)},"Relatório de Pedidos"+cTitRel)
	oReport:SetLandscape()

	oSection1:= TRSection():New(oReport, "Pedidos", {"SC7"}, NIL, .F., .T.)
    oReport:cFontBody := 'Arial'
    oReport:nFontBody := 9
    oReport:lBold := .T.		
	TRCell():New(oSection1,"C7_FILIAL"  ,"TRBSC7","Filial"	    ,"@!",08)
	TRCell():New(oSection1,"C7_NUMSC"  	,"TRBSC7","Solicitação"	,"@!",10)
	TRCell():New(oSection1,"C7_NUM"	    ,"TRBSC7","Pedido"  	,"@!",10)
	TRCell():New(oSection1,"C7_ENCER"	,"TRBSC7","Situação"  	,"@!",10)
	TRCell():New(oSection1,"C7_COMPRA"	,"TRBSC7","Comprador"  	,"@!",25)
	TRCell():New(oSection1,"C7_EMISSAO"	,"TRBSC7","Emissão"	    ,    ,08)
	TRCell():New(oSection1,"C7_FORNECE"	,"TRBSC7","Fornecedor"	,    ,08)
	TRCell():New(oSection1,"C7_LOJA"	,"TRBSC7","Loja"	    ,"@!",80)

    oReport:cFontBody := 'Arial'
    oReport:nFontBody := 6
    oReport:lBold := .F.		

	oSection2:= TRSection():New(oReport, "Itens Pedido", {"SC7"}, NIL, .F., .T.)
	TRCell():New(oSection2,"C7_ITEM"   ,"TRBSC7","Item" ,"9999",004)
	TRCell():New(oSection2,"C7_PRODUTO","TRBSC7","Produto" ,"@!",018)
	TRCell():New(oSection2,"C7_DESCRI" ,"TRBSC7","Descricao" ,"@!",100)
	TRCell():New(oSection2,"C7_UM"     ,"TRBSC7","UM" ,"@!",003)
	TRCell():New(oSection2,"C7_QUANT"  ,"TRBSC7","Quant" ,"@E 9999999.99",015)
	TRCell():New(oSection2,"C7_PRECO"  ,"TRBSC7","Preco" ,"@E 9,999,999.99",15)
	TRCell():New(oSection2,"C7_TOTAL"  ,"TRBSC7","Total" ,"@E 9,999,999.99",15)
	TRCell():New(oSection2,"C7_XXMARCA","TRBSC7","Marca" ,"@!",025)

    TRFunction():New(oSection2:Cell("C7_TOTAL"),NIL,"SUM")

	oReport:SetTotalInLine(.F.)
    //oReport:lHeaderVisible := .T.
	oSection1:SetPageBreak(.F.)
	oSection1:SetTotalText(" ")
					
Return(oReport)

Static Function ReportPrint(oReport)
	Local oSection1 := oReport:Section(1)
	Local oSection2 := oReport:Section(2)
	Local cQuery    := ""
	Local cNumPed   := ""
	Local cFilPed   := ""

	cQuery := "	SELECT C7_FILIAL, C7_NUMSC, C7_NUM, C7_ITEM, C7_PRODUTO, C7_DESCRI, C7_UM, C7_QUANT, C7_PRECO, C7_TOTAL, "
	cQuery += "                C7_DATPRF, C7_FORNECE, C7_LOJA, C7_COND, C7_EMISSAO, C7_COMPRA, C7_ENCER, C7_XXMARCA "
	cQuery += "	FROM "+RetSqlName("SC7")
	cQuery += "	WHERE D_E_L_E_T_=' ' "
	cQuery += "	AND C7_FILIAL  >= '"+MV_PAR01+"'"
	cQuery += "	AND C7_FILIAL  <= '"+MV_PAR02+"'"
	cQuery += " AND C7_EMISSAO >= '"+DTOS(MV_PAR03)+"'"
	cQuery += " AND C7_EMISSAO <= '"+DTOS(MV_PAR04)+"'"
	cQuery += "	AND C7_COMPRA  >= '"+MV_PAR05+"'"
	cQuery += "	AND C7_COMPRA  <= '"+MV_PAR06+"'"

	If MV_PAR07 == 1
		cQuery += "AND C7_ENCER = 'E' "
	ElseIf MV_PAR07 == 2
		cQuery += "AND C7_ENCER = ' ' "
	ElseIf MV_PAR07 == 3
		cQuery += "AND C7_ENCER = ' ' OR C7_ENCER = 'E'"
	Endif

	cQuery += "	ORDER BY C7_FILIAL, C7_NUM, C7_ITEM "

	IF Select("TRBSC7")
		DbSelectArea("TRBSC7")
		DbCloseArea()
	ENDIF
	TCQUERY cQuery NEW ALIAS "TRBSC7"
	TcSetField("TRBSC7","C7_EMISSAO","D",10)

	dbSelectArea("TRBSC7")

	oReport:SetMeter(TRBSC7->(LastRec()))

	TRBSC7->(dbGoTop())
	While ! TRBSC7->(EOF())

		If oReport:Cancel()
			Exit
		EndIf

		oSection1:Init()
		oReport:IncMeter()

		IncProc("Imprimindo Pedido "+AllTrim(TRBSC7->C7_NUM))
		
		oSection1:Cell("C7_FILIAL"):SetValue(TRBSC7->C7_FILIAL)
		oSection1:Cell("C7_NUMSC"):SetValue(TRBSC7->C7_NUMSC)
		oSection1:Cell("C7_NUM"):SetValue(TRBSC7->C7_NUM)
		oSection1:Cell("C7_COMPRA"):SetValue(TRBSC7->C7_COMPRA+" "+POSICIONE("SY1",1,xFilial("SY1")+TRBSC7->C7_COMPRA,"Y1_NOME"))
		oSection1:Cell("C7_EMISSAO"):SetValue(TRBSC7->C7_EMISSAO)
		oSection1:Cell("C7_FORNECE"):SetValue(TRBSC7->C7_FORNECE)
		oSection1:Cell("C7_LOJA"):SetValue(TRBSC7->C7_LOJA+" - "  +POSICIONE("SA2",1,xFilial("SA2")+TRBSC7->C7_FORNECE+TRBSC7->C7_LOJA,"A2_NOME"))
		oSection1:Printline()

		oSection2:init()
		oReport:ThinLine()
		oReport:FatLine()
		
		cFilPed := TRBSC7->C7_FILIAL
		cNumPed := TRBSC7->C7_NUM

		While ! TRBSC7->(EOF()) .and. TRBSC7->C7_FILIAL == cFilPed .and. TRBSC7->C7_NUM == cNumPed
			oReport:IncMeter()

			IncProc("Imprimindo item "+AllTrim(TRBSC7->C7_ITEM))

			oSection2:Cell("C7_ITEM"):SetValue(TRBSC7->C7_ITEM)
			oSection2:Cell("C7_PRODUTO"):SetValue(TRBSC7->C7_PRODUTO)
			oSection2:Cell("C7_DESCRI"):SetValue(TRBSC7->C7_DESCRI)
			oSection2:Cell("C7_UM"):SetValue(TRBSC7->C7_UM)
			oSection2:Cell("C7_QUANT"):SetValue(TRBSC7->C7_QUANT)
			oSection2:Cell("C7_PRECO"):SetValue(TRBSC7->C7_PRECO)
			oSection2:Cell("C7_TOTAL"):SetValue(TRBSC7->C7_TOTAL)
			oSection2:Cell("C7_XXMARCA"):SetValue(TRBSC7->C7_XXMARCA)
			
			oSection2:Printline()

			TRBSC7->(dbSkip())
		EndDo
		
		oReport:ThinLine()
		oReport:FatLine()
		oSection1:Finish()
		//FoReport:FatLine()
		
		oSection2:Finish()
		oReport:FatLine()
	
	Enddo
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ValidPerg ºAutor  ³Paulo Schwind       º Data ³  02/05/2019 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ValidPerg()
	Local _aArea := GetArea()
	Local _cPerg := cPerg
	Local _aRegs:={}
	Local _nI := 0
	Local _nJ := 0

	dbSelectArea('SX1')
	dbSetOrder(1)

	Aadd(_aRegs,{_cPerg,"01","Filial De                     ","                              ","                              ","mv_ch1","C",08,0,0,"G","                                                            ","mv_par01       ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","          ","                                                            ","SM0","S","   ","          "})
	Aadd(_aRegs,{_cPerg,"02","Filial Até                    ","                              ","                              ","mv_ch2","C",08,0,0,"G","                                                            ","mv_par02       ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","          ","                                                            ","SM0","S","   ","          "})
	Aadd(_aRegs,{_cPerg,"03","Emissão De                    ","                              ","                              ","mv_ch3","D",08,0,0,"G","                                                            ","mv_par03       ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","          ","                                                            ","   ","S","   ","          "})
	Aadd(_aRegs,{_cPerg,"04","Emissao Até                   ","                              ","                              ","mv_ch4","D",08,0,0,"G","                                                            ","mv_par04       ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","          ","                                                            ","   ","S","   ","          "})
	Aadd(_aRegs,{_cPerg,"05","Comprador De                  ","                              ","                              ","mv_ch5","C",03,0,0,"G","                                                            ","mv_par05       ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","          ","                                                            ","SY1","S","   ","          "})
	Aadd(_aRegs,{_cPerg,"06","Comprador Até                 ","                              ","                              ","mv_ch6","C",03,0,0,"G","                                                            ","mv_par06       ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","          ","                                                            ","SY1","S","   ","          "})
	Aadd(_aRegs,{_cPerg,"07","Situação dos Pedidos          ","                              ","                              ","mv_ch7","C",01,0,1,"C","                                                            ","mv_par07       ","Finalizados    ","               ","               ","                                                            ","               ","Em Aberto      ","               ","               ","                                                            ","               ","Todos          ","               ","               ","                                                            ","               ","               ","               ","               ","                                                            ","               ","               ","               ","          ","                                                            ","   ","S","   ","          "})

	For _nI:=1 to Len(_aRegs)
		If ! DbSeek(_cPerg+_aRegs[_nI,2])
			RecLock('SX1',.T.)
			For _nJ:=1 to FCount()
				If _nJ <= Len(_aRegs[_nI])
					FieldPut(_nJ,_aRegs[_nI,_nJ])
				Endif
			Next _nJ
			MsUnlock()
		Endif
	Next _nI
	RestArea(_aArea)
Return

