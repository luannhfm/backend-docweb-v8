#Include 'Protheus.ch'
#Include 'FWMVCDEF.CH'

/*/{Protheus.doc} SS3305S
Cadastro de Tabela de Preços

@author 	Sergio Ricardo Leite Salustiano	
@since 		30/05/2014
@version 	1.0

@return Nil, Nulo
/*/
User Function SS3305S()

	Local oBrowse

	oBrowse := FWMBrowse():New()
	oBrowse:SetAlias('ZZJ')
	oBrowse:SetDescription('Tabela de Preços')
	oBrowse:Activate()

Return ( Nil )

Static Function MenuDef()

	Local aRotina := {}

	ADD OPTION aRotina Title 'Visualizar' Action 'VIEWDEF.SS3305S' OPERATION 2 ACCESS 0
	ADD OPTION aRotina Title 'Incluir'    Action 'VIEWDEF.SS3305S' OPERATION 3 ACCESS 0
	ADD OPTION aRotina Title 'Alterar'    Action 'VIEWDEF.SS3305S' OPERATION 4 ACCESS 0
	ADD OPTION aRotina Title 'Excluir'    Action 'VIEWDEF.SS3305S' OPERATION 5 ACCESS 0
	ADD OPTION aRotina Title 'Imprimir'   Action 'VIEWDEF.SS3305S' OPERATION 8 ACCESS 0
	ADD OPTION aRotina Title 'Copiar'     Action 'VIEWDEF.SS3305S' OPERATION 9 ACCESS 0

Return ( aRotina )

Static Function ModelDef()

	Local oStruZZJ := FWFormStruct(1, 'ZZJ')
	Local oStruZZL := FWFormStruct(1, 'ZZL')
	Local oModel

	oModel:= MPFormModel():New('SS3305SM')
	oModel:AddFields('ZZJMASTER',/*cOwner*/, oStruZZJ)
	oModel:AddGrid('ZZLDETAIL','ZZJMASTER',oStruZZL)
	oModel:SetRelation('ZZLDETAIL',{{'ZZL_FILIAL','xFilial("ZZL")'}, {'ZZL_CODTAB','ZZJ_CODTAB'}},ZZL->(IndexKey(1)))
	oModel:SetPrimaryKey({"ZZJ_FILIAL","ZZJ_CODTAB"})
	oModel:SetDescription('Tabela de Preço')
	oModel:GetModel('ZZJMASTER'):SetDescription('Tabela de Preço')
	oModel:GetModel('ZZLDETAIL'):SetDescription('Itens da Tabela de Preço')

Return ( oModel )

Static Function ViewDef()

	Local oModel   := FWLoadModel('SS3305S')
	Local oStruZZJ := FWFormStruct(2, 'ZZJ')
	Local oStruZZL := FWFormStruct(2, 'ZZL')
	Local oView

	oView:=FWFormView():New()
	oView:SetModel(oModel)
	oView:AddField('VIEW_ZZJ', oStruZZJ, 'ZZJMASTER')
	oView:AddGrid ('VIEW_ZZL', oStruZZL, 'ZZLDETAIL')
	oView:AddIncrementField( 'VIEW_ZZL', 'ZZL_ITEM' )
	oView:CreateHorizontalBox ('SUPERIOR', 20)
	oView:CreateHorizontalBox ('INFERIOR', 80)
	oView:SetOwnerView ('VIEW_ZZJ', 'SUPERIOR')
	oView:SetOwnerView ('VIEW_ZZL', 'INFERIOR')

Return ( oView )