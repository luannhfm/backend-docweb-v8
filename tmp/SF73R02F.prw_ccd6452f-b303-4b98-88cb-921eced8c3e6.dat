#include "protheus.ch"

/*/{Protheus.doc} SF73R02F
Relatório com a finalidade de subsidiar o acompanhamento do processo de vendas.
@type function
@author Franklin de Brito de Oliveira
@since 10/11/2021
/*/
User Function SF73R02F()
Local oReport
Private cPerg := "SF73R02F"
    	Pergunte(cPerg,.F.)
    	oReport := ReportDef()
    	oReport:PrintDialog()
Return NIL

/*/{Protheus.doc} ReportDef
Função com as definições do relatório
@type function
@author Franklin de Brito de Oliveira
@since 10/11/2021
@return object, objeto da classe TReport com as definições do relatório
/*/
Static Function ReportDef()
local cDescription := "Relatório demonstrativo das vendas em suas respectivas etapas."
Local oReport
Local oSection
    
    oReport := TReport():New("SF73R02F", "Etapa das vendas", cPerg, {|oReport| PrintReport(oReport)}, cDescription)
    oReport:SetLandscape()
    oReport:HideParamPage()
    
    oSection := TRSection():New(oReport,"Oportunidades", {"AD1", "ADY", "ADZ", "SA1", "SA3"})
    oSection:SetAutoSize(.F.)
    oSection:SetLineBreak(.T.)
    
    TRCell():New(oSection, "A3_NOME", "SA3", "Vendedor")
    oSection:Cell("A3_NOME"):SetSize(40)
    
    TRCell():New(oSection, "AD1_NROPOR", "AD1", "Oportunidade/Proposta")
    oSection:Cell("AD1_NROPOR"):SetSize(12)

    TRCell():New(oSection, "AD1_XCONTR", "AD1", "Contrato")
    oSection:Cell("AD1_XCONTR"):SetSize(20)
    
    TRCell():New(oSection, "AC2_DESCRI", "AC2", "Etapa")
    oSection:Cell("AC2_DESCRI"):SetSize(20)

    TRCell():New(oSection, "AIJ_DUREST", "AIJ", "Tempo na etapa")
    oSection:Cell("AIJ_DUREST"):SetSize(10)

    TRCell():New(oSection, "AD1_STATUS", "AD1", "Status")
    oSection:Cell("AD1_STATUS"):SetSize(16)

    TRCell():New(oSection, "X5_DESCRI", "SX5", "Motivo de perda")
    oSection:Cell("X5_DESCRI"):SetSize(10)
    
    TRCell():New(oSection, "AD1_DTINI", "AD1", "Inicio oportunidade")
    oSection:Cell("AD1_DTINI"):SetSize(10)

    TRCell():New(oSection, "CN9_DTINIC", "CN9", "Inicio contrato")
    oSection:Cell("CN9_DTINIC"):SetSize(10)

    TRCell():New(oSection, "ADZ_DESCRI", "ADZ", "Produto")
    oSection:Cell("ADZ_DESCRI"):SetSize(100)

    TRCell():New(oSection, "ADZ_QTDVEN", "ADZ", "Qtd Produto")
    oSection:Cell("ADZ_QTDVEN"):SetSize(12)

    TRCell():New(oSection, "ADZ_TOTAL", "ADZ", "Vlr. Total Produto")
    oSection:Cell("ADZ_TOTAL"):SetSize(14)

    TRCell():New(oSection, "AD1_PROSPE", "AD1", "Prospect/Cliente")
    oSection:Cell("AD1_PROSPE"):SetSize(100)

    TRCell():New(oSection, "EMAIL", "SA1", "E-Mail")
    oSection:Cell("EMAIL"):SetSize(100)

    TRCell():New(oSection, "TELEFONE", "SA1", "Telefone")
    oSection:Cell("TELEFONE"):SetSize(18)

Return oReport

/*/{Protheus.doc} PrintReport
Função com a consulta e impressão do relatório
@type function
@author Franklin de Brito de Oliveira
@since 10/11/2021
@param oReport, object, objeto da classe TReport com as definições do relatório
/*/
Static Function PrintReport(oReport)
Local oSection := oReport:Section(1)
Local cPart := ""

    //Transforma parametros do tipo Range em expressao SQL para ser utilizada na query 	
    MakeSqlExpr(cPerg)
    oSection:BeginQuery()
    If !Empty(mv_par01)
        cPart := "% AND SA3.A3_COD = '" + mv_par01 + "' %"
    EndIf

    if !Empty(mv_par02)
        cPart := "% AND AIJ.AIJ_STAGE = '" + mv_par02 + "' %"
    EndIf

    if !Empty(mv_par03)
        cPart := "% AND " + mv_par03 + " %"
    EndIf

    if !Empty(mv_par04)
        cPart := "% AND AD1.AD1_DTINI = '" + DToS(mv_par04) + "' %"
    endif

    if !Empty(mv_par05)
        cPart := "% AND CN9.CN9_DTINIC = '" + DToS(mv_par05) + "' %"
    endif

    if !Empty(mv_par06)
        cPart := "% AND ADZ.ADZ_PRODUT = '" + mv_par06 + "' %"
    endif

    if Empty(cPart)
        cPart := "%%"
    EndIf

    BeginSql alias "QRYAD1"
        SELECT 
            AD1.AD1_FILIAL,
            TRIM(SA3.A3_NOME) AS A3_NOME,
            TRIM(AD1.AD1_NROPOR) || '/' || TRIM(ADY.ADY_PROPOS) AS AD1_NROPOR,
            CASE AD1.AD1_XCONTR
                WHEN '               ' THEN '-'
                ELSE TRIM(AD1.AD1_XCONTR) || '/' || TRIM(AD1.AD1_XREVCT)
            END AS AD1_XCONTR,
            nvl(AC2.AC2_DESCRI, '-') AS AC2_DESCRI,
            NVL(
            CASE AIJ.AIJ_DTENCE
                WHEN '        ' THEN ROUND(SYSDATE - TO_DATE(AIJ_DTINIC, 'YYYYMMDD'))
                ELSE ROUND(TO_DATE(AIJ_DTENCE, 'YYYYMMDD') - TO_DATE(AIJ_DTINIC, 'YYYYMMDD'))
            END, 0) AS AIJ_DUREST,
            CASE AD1.AD1_STATUS
                WHEN '1'   THEN 'ABERTO'
                WHEN '2'   THEN 'PERDIDO'
                WHEN '3'   THEN 'SUSPENSO'
                WHEN '9'   THEN 'GANHA'
                WHEN 'E'   THEN 'GANHA SEM PEDIDO'
                WHEN 'F'   THEN 'GANHA COM PEDIDO'
                ELSE '-'
            END AS AD1_STATUS,
            NVL(TRIM(SX5A6.X5_DESCRI), '-') AS X5_DESCRI,
            TO_DATE(AD1.AD1_DTINI, 'YYYYMMDD') AS AD1_DTINI,
            NVL(TO_CHAR(TO_DATE(CN9.CN9_DTINIC, 'YYYYMMDD')), '-') AS CN9_DTINIC,
            NVL(TRIM(ADZ.ADZ_DESCRI), '-') AS ADZ_DESCRI,
            NVL(ADZ.ADZ_QTDVEN, 0) AS ADZ_QTDVEN,
            NVL(ADZ.ADZ_TOTAL, 0) AS ADZ_TOTAL,
            CASE AD1.AD1_PROSPE
                WHEN '         ' THEN AD1.AD1_CODCLI || TRIM(SA1.A1_NOME)
                ELSE AD1.AD1_PROSPE || TRIM(SUS.US_NOME)
            END AS AD1_PROSPE,
            CASE AD1.AD1_PROSPE
                WHEN '         ' THEN TRIM(SA1.A1_EMAIL)
                ELSE TRIM(SUS.US_EMAIL)
            END AS EMAIL,
            CASE AD1.AD1_PROSPE
                WHEN '         ' THEN SA1.A1_DDD || ' ' || SA1.A1_TEL
                ELSE SUS.US_DDD || ' ' || SUS.US_TEL
            END AS TELEFONE
        FROM 
            %Table:AD1% AD1
        LEFT OUTER JOIN %Table:SA3% SA3 ON SA3.%NotDel%
                                    AND SUBSTR(SA3.A3_FILIAL, 1, 4) = SUBSTR(AD1.AD1_FILIAL, 1, 4)
                                    AND SA3.A3_COD = AD1.AD1_VEND
        LEFT OUTER JOIN %Table:ADY% ADY ON ADY.%NotDel%
                                    AND AD1.AD1_FILIAL = ADY.ADY_FILIAL
                                    AND AD1.AD1_NROPOR = ADY.ADY_OPORTU
                                    AND AD1.AD1_REVISA = ADY.ADY_REVISA
        LEFT OUTER JOIN %Table:ADZ% ADZ ON ADZ.%NotDel%
                                    AND ADZ.ADZ_FILIAL = ADY.ADY_FILIAL
                                    AND ADZ.ADZ_PROPOS = ADY.ADY_PROPOS
                                    AND ADZ.ADZ_REVISA = ADY.ADY_PREVIS
        LEFT OUTER JOIN %Table:AIJ% AIJ ON AIJ.%NotDel%
                                    AND AIJ.AIJ_FILIAL = AD1.AD1_FILIAL
                                    AND AIJ.AIJ_NROPOR = AD1.AD1_NROPOR
                                    AND AIJ.AIJ_REVISA = AD1.AD1_REVISA
                                    AND AIJ.AIJ_PROVEN = AD1.AD1_PROVEN
                                    AND AIJ.AIJ_STAGE = AD1.AD1_STAGE
        LEFT OUTER JOIN %Table:AC2% AC2 ON AC2.%NotDel%
                                  AND SUBSTR(AC2.AC2_FILIAL, 1, 4) = SUBSTR(AD1.AD1_FILIAL, 1, 4)
                                  AND AC2.AC2_PROVEN = AD1.AD1_PROVEN
                                  AND AC2.AC2_STAGE = AD1.AD1_STAGE
        LEFT OUTER JOIN %Table:SX5%   SX5A6 ON SX5A6.%NotDel%
                                    AND SUBSTR(SX5A6.X5_FILIAL, 1, 4) = SUBSTR(AD1.AD1_FILIAL, 1, 4)
                                    AND SX5A6.X5_TABELA = 'A6'
                                    AND SX5A6.X5_CHAVE = AD1.AD1_FCI
        LEFT OUTER JOIN %Table:CN9% CN9 ON CN9.%NotDel%
                                    AND CN9.CN9_FILIAL = AD1.AD1_FILIAL
                                    AND CN9.CN9_NUMERO = AD1.AD1_XCONTR
                                    AND CN9.CN9_REVISA = AD1.AD1_XREVCT
        LEFT OUTER JOIN %Table:SA1% SA1 ON SA1.%NotDel%
                                    AND SA1.A1_COD = AD1.AD1_CODCLI
                                    AND SA1.A1_LOJA = AD1.AD1_LOJCLI
        LEFT OUTER JOIN %Table:SUS% SUS ON SUS.%NotDel%
                                    AND SUS.US_COD = AD1.AD1_PROSPE
                                    AND SUS.US_LOJA = AD1.AD1_LOJPRO
        WHERE
            AD1.%NotDel%
            AND AD1_FILIAL = %xFilial:AD1%
            %Exp:cPart%
        ORDER BY
            AD1.AD1_FILIAL,
            AD1.AD1_NROPOR
    EndSql
    oSection:EndQuery()
	oSection:Print()

Return Nil
