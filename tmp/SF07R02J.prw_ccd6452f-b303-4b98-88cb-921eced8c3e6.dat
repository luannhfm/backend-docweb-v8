#INCLUDE "PROTHEUS.CH"
#INCLUDE "Topconn.CH"
 
/*/{Protheus.doc} SF07R02J
Função responsável pela geração do Relatório de estrutura de rateio - PCO

@author 	Jose Leite de Barros Neto
@since 	24/09/2014
@version 	1.0

@return Nil, Nulo
/*/
User Function SF07R02J()

	Local oReport                               
	
	Private cTitle		:= "Estrutura de Rateio de Verbas"	
	Private cPerg		:= "SF07R02J"
	Private aEmpLog		:= FWEmpLoad( .T. )
	Private cEmpresas	:= '('
	Private aEmpAux		:= {}
	Private cPeriodo	:= GETMV("MV_FOLMES")
	
	If TRepInUse()
	
		fCriaSx1()
	
		If pergunte( "SF07R02J" )
			oReport := ReportDef(cPerg)
			oReport:PrintDialog()
		EndIf
		
	EndIf

Return( Nil )

/** {Protheus.doc} ReportDef
Funcao que define as colunas

@author: 	Jose Leite de Barros Neto
@since: 	24/09/2014
@Uso: 		SFIEMT
*/
Static Function ReportDef(cPerg)
	
	Local oReport
	Local oSection
	Local oBreak
	
	oReport := TReport():New(cPerg,cTitle,cPerg,{|oReport| PrintReport(oReport)},cTitle)
	oReport:SetLandScape()
	
	oSection:= TRSection():New(oReport,OemToAnsi(""),{"ZD6"})
		
	oFilial:= TRCell():New(oSection, "_cFilial","TRA", "Filial")
	oFilial:SetSize(10)
	
	oMat:= TRCell():New(oSection, "_cMat","TRA", "Mat")
	oMat:SetSize(8)
	
	oNome:= TRCell():New(oSection, "_cNome","TRA", "Nome")
	oNome:SetSize(50)
	
	oCusto:= TRCell():New(oSection, "_cCusto","TRA", "C. Custo")
	oCusto:SetSize(20)
	
	oDesc:= TRCell():New(oSection, "_cDesc","TRA", "Descrição")
	oDesc:SetSize(50)
	
	oItem:= TRCell():New(oSection, "_cItem","TRA", "Item Contábil")
	oItem:SetSize(20)
	
	oDesc2:= TRCell():New(oSection, "_cDesc2","TRA", "Descrição")
	oDesc2:SetSize(50)
	
	oHistM:= TRCell():New(oSection, "_cHistM","TRA", "Hist./Mês")
	oHistM:SetSize(50)
	
	oHoraM:= TRCell():New(oSection, "_cHoraM","TRA", "Hrs. Mês")
	oHoraM:SetSize(10)
	
	oHoraT:= TRCell():New(oSection, "_cHoraT","TRA", "Hrs. Trab.")
	oHoraT:SetSize(10)
	
	oPerc:= TRCell():New(oSection, "_cPerc","TRA", "Percentual")
	oPerc:SetSize(10)
	
	oBreak	:= TRBreak():New(oSection, oSection:Cell("_cFilial"), "Quant.:",.F.)
	TRFunction():New(oSection:Cell("_cFilial"),,"COUNT",oBreak,,"@E 99,999.99",,.F.,.F.,.F.)
	
	oBreak:SetPageBreak(.T.)
	
Return( oReport )

/** {Protheus.doc} ReportDef
Funcao que gera o relatorio

@author: 	Jose Leite de Barros Neto
@since: 	03/07/2014
@Uso: 		SFIEMT
*/
Static Function PrintReport(oReport)	
	
	Local oSection 	:= oReport:Section(1)
	Local nSizeCTD 	:= 4
	Local nSizeCTT 	:= 4
	Local _cFilial	:= ""
	Local _cMat		:= ""
	Local _cNome		:= ""
	Local _cCusto	:= ""
	Local _cDesc		:= ""
	Local _cItem		:= ""
	Local _cDesc2	:= ""
	Local _cHistM	:= ""
	Local _cHoraM	:= ""
	Local _cHoraT	:= ""
	Local _cPerc		:= ""
	Local cQuery 	:= ""
	Local cAux		:= ""
	
	For nX:= 1 To Len(aEmpLog)                                   

		If substr(aEmpLog[nX][3],1,2) <> cAux
	  		cAux := substr(aEmpLog[nX][3],1,2)
			AADD(aEmpAux,cAux)
	  	EndIf
	  	
	Next 
	
	For nX := 1 to Len(aEmpAux)
		cEmpresas += "'"+aEmpAux[nX]+"'," 
	Next

	cEmpresas := SubStr(cEmpresas,1,(Len(cEmpresas)-1))+")"
	
	cQuery :=        "SELECT ZD6_FILIAL,ZD6_MESANO PER,ZD6_MAT,SRA.RA_NOME,ZD6_CC,CTT.CTT_DESC01,ZD6_ITEM,ctd.ctd_desc01,ZD6_HRMES,ZD6_HRTRAB,ZD6_PERCEN"
	cQuery += CRLF + "  From " + RetSqlName("ZD6") + " ZD6 "
	cQuery += CRLF + "  Left Join " + RetSqlName("SRA") + " SRA On SRA.D_E_L_E_T_ = ' ' And SubStr( SRA.RA_FILIAL, 1, "  + Str( nSizeCTT, 2 ) + " ) = SubStr( ZD6.ZD6_FILIMP, 1, " + Str( nSizeCTT, 2 ) + ") And SRA.RA_MAT = ZD6.ZD6_MAT"
	cQuery += CRLF + "  Left Join " + RetSqlName("CTD") + " CTD On CTD.D_E_L_E_T_ = ' ' And SubStr( CTD.CTD_FILIAL, 1, " + Str( nSizeCTD, 2 ) + " ) = SubStr( SRA.RA_FILIAL, 1, " + Str( nSizeCTD, 2 ) + " ) And CTD.CTD_ITEM = ZD6.ZD6_ITEM"
	cQuery += CRLF + "  Left Join " + RetSqlName("CTT") + " CTT On CTT.D_E_L_E_T_ = ' ' And SubStr( CTT.CTT_FILIAL, 1, " + Str( nSizeCTT, 2 ) + " ) = SubStr( SRA.RA_FILIAL, 1, " + Str( nSizeCTT, 2 ) + " ) And CTT.CTT_CUSTO = ZD6.ZD6_CC"
	
	cQuery += CRLF + " Where ZD6.D_E_L_E_T_ = ' '"   
	
	IF MV_PAR04 = 1                     
		cQuery += CRLF + "   And ZD6.ZD6_MESANO = '" + StrZero( Year( MV_PAR01 ), 4 ) + StrZero( Month( MV_PAR01 ), 2 ) + "'"
	EndIf
	
	cQuery += CRLF + "   And ZD6.ZD6_MAT    BETWEEN '" + MV_PAR02 + "' And '" + MV_PAR03 + "'"    
	cQuery += CRLF + "   And SUBSTR(ZD6.ZD6_FILIMP,1,2) IN    " + cEmpresas                      
	cQuery += CRLF + " AND ZD6_MAT||ZD6_MESANO IN (SELECT ZD6_MAT||MAX(ZD6_MESANO) FROM "+RetSqlName("ZD6")+" GROUP BY ZD6_MAT) "
	
	If MV_PAR05 = 2 //Considera demitidos
		cQuery += CRLF + "     AND SRA.RA_SITFOLH <> 'D' "
	Endif
	
	cQuery += CRLF + " ORDER BY ZD6.ZD6_FILIAL, ZD6.ZD6_MAT, ZD6.ZD6_MESANO, ZD6.ZD6_ITEM"
	
	cQuery := ChangeQuery( cQuery )
	
	TCQUERY cQuery NEW ALIAS "TRA"
	
	DbSelectArea("TRA")
	DbGoTop()
	
	oReport:SetMeter(RecCount())
	oSection:Init()
	
	While .Not. TRA->( EOF() )
	
		If oReport:Cancel()
			Exit
		EndIf
		
		_cFilial	:= TRA->ZD6_FILIAL 
		_cMat		:= TRA->ZD6_MAT
		_cNome		:= AllTrim( TRA->RA_NOME )
		_cCusto	:= AllTrim( TRA->ZD6_CC )
		_cDesc		:= AllTrim( TRA->CTT_DESC01 )
		_cItem		:= AllTrim( TRA->ZD6_ITEM )
		_cDesc2	:= AllTrim( TRA->CTD_DESC01 )
		_cHistM	:= IIF(TRA->PER = cPeriodo,"M","H")
		_cHoraM	:= Transform( TRA->ZD6_HRMES  , "@E 999.99" )
		_cHoraT	:= Transform( TRA->ZD6_HRTRAB  , "@E 999.99" )
		_cPerc		:= Transform( TRA->ZD6_PERCEN  , "@E 999.99" ) + "%"
		
		oSection:Cell("_cFilial"):SetValue(_cFilial)
		oSection:Cell("_cMat"):SetValue(_cMat)
		oSection:Cell("_cNome"):SetValue(_cNome)
		oSection:Cell("_cCusto"):SetValue(_cCusto)
		oSection:Cell("_cDesc"):SetValue(_cDesc)
		oSection:Cell("_cItem"):SetValue(_cItem)
		oSection:Cell("_cDesc2"):SetValue(_cDesc2)
		oSection:Cell("_cHistM"):SetValue(_cHistM)
		oSection:Cell("_cHoraM"):SetValue(_cHoraM)
		oSection:Cell("_cHoraT"):SetValue(_cHoraT)
		oSection:Cell("_cPerc"):SetValue(_cPerc)
		
		oSection:PrintLine()
		oReport:IncMeter()
		
		TRA->( DbSkip() )
	End
	
	TRA->(DbCloseArea())
	oSection:Finish()
	
Return( Nil )

/** {Protheus.doc} fCriaSx1
Funcao para criacao das perguntas

@param: 	<Nil>
@author 	Auremar Duarte
@since 	21/08/2014
@Uso: 		SFIEMT
*/
Static Function fCriaSx1()

	/*
		MV_PAR01  Database		    ?   
		MV_PAR02  Funcionario de	?  
		MV_PAR03  Funcionario ate	?  
		MV_PAR04  Estrutura     	? 
		MV_PAR05  Cons. Demitidos   ?            
		MV_PAR06  Lista Inativos	?          
	*/

	u_SFPUTSX1( cPerg, "01","Database             "	  ,"","","mv_ch1","D",8                   	,0,0,"G","",""		,"","","mv_par01","","","","","","","","","","","","","","","","",{},{},{})
	u_SFPUTSX1( cPerg, "02","Funcionario De       "	  ,"","","mv_ch2","C",TamSX3("RA_MAT")[1]	,0,0,"G","","SRA"	,"","","mv_par02","","","","","","","","","","","","","","","","",{},{},{})
	u_SFPUTSX1( cPerg, "03","Funcionario Ate      "	  ,"","","mv_ch3","C",TamSX3("RA_MAT")[1]	,0,0,"G","","SRA"	,"","","mv_par03","","","","","","","","","","","","","","","","",{},{},{})
	u_SFPUTSX1( cPerg, "04","Estrutura            "	  ,"","","mv_ch4","C",1						 	,0,0,"C","",""		,"","","mv_par04","1=Mensal","","","","2=Historial","","","","","","","","","","","",{},{},{})
	u_SFPUTSX1( cPerg, "05","Considera Demitidos? "	  ,"","","mv_ch5","N",1						 	,0,0,"C","",""		,"","","mv_par05","1=Sim"   ,"","","","2=Não","","","","","","","","","","","",{},{},{})
	
Return( Nil )