#Include 'Protheus.ch'
#Include 'fwmvcdef.ch'
#Include 'Topconn.ch'

/*/{Protheus.doc} SF02A04S
Cadastro de Produto x Area de Atuação
@type function
@author 	SERGIO RICARDO LEITE SALUSTIANO
@since 		30/11/2020
/*/

user function SF02A04S()

	Local oBrowse

	Private aRotina := MenuDef()

	//Instancio a classe FWMBrowse
	oBrowse := FWMBrowse():New()

	//Setando o Alias a ser utilizado
	oBrowse:SetAlias( 'ZDH' )

	//DescriÃ§Ã£o do CabeÃ§alho
	oBrowse:SetDescription('Produto X Area de Atuacao')

	oBrowse:Activate()

Return( Nil )

/** {Protheus.doc} MenuDef
Funcao para Montar o Menu
@type function
@author SERGIO SALUSTIANO
@since 30/11/2020
*/
Static Function MenuDef()

	Local aRotina := {}

	aAdd( aRotina, { 'Pesquisar' , 'PesqBrw'        , 0, 1, 0, .T. } )
	aAdd( aRotina, { 'Visualizar', 'ViewDef.SF02A04S', 0, 2, 0, Nil } )
	aAdd( aRotina, { 'Incluir'   , 'ViewDef.SF02A04S', 0, 3, 0, Nil } )
	aAdd( aRotina, { 'Alterar'   , 'ViewDef.SF02A04S', 0, 4, 0, Nil } )
	aAdd( aRotina, { 'Excluir'   , 'ViewDef.SF02A04S', 0, 5, 0, Nil } )
	aAdd( aRotina, { 'Imprimir'  , 'ViewDef.SF02A04S', 0, 8, 0, Nil } )
	aAdd( aRotina, { 'Copiar'    , 'ViewDef.SF02A04S', 0, 9, 0, Nil } )

Return( aRotina )

/** {Protheus.doc} ModelDef
Funcao para Montar o Modelo de Dados
@type function
@author SERGIO SALUSTIANO
@since 30/11/2020
*/
Static Function ModelDef()

	Local oModel
	Local oStruZDHMASTER := FWFormStruct( 1, 'ZDH', { |X| .Not. (AllTrim(X) $ 'ZDH_ITEM, ZDH_CODARE, ZDH_DESCAR') } )
	Local oStruZDHDETAIL := FWFormStruct( 1, 'ZDH', { |X| AllTrim(X) $ 'ZDH_ITEM, ZDH_CODARE, ZDH_DESCAR' } )

	oModel := MPFormModel():New( 'SF0204SM' )

	oModel:AddFields( 'ZDHMASTER'	,				, oStruZDHMASTER )
	oModel:AddGrid('ZDHDETAIL'		, 'ZDHMASTER'	, oStruZDHDETAIL )

	oModel:SetRelation( 'ZDHDETAIL', { { 'ZDH_FILIAL', 'xFilial( "ZDH" )' }, {'ZDH_CODPRD', 'ZDH_CODPRD' }}, ZDH->( IndexKey( 1 ) ) )

	oModel:GetModel( 'ZDHDETAIL' ):SetUniqueLine( { "ZDH_ITEM"} )

	oModel:GetModel( 'ZDHDETAIL' ):SetOptional( .T. )

	oModel:SetDescription( 'Amarracao Produto X Area de Atuacao' )

	oModel:GetModel( 'ZDHMASTER' ):SetDescription( 'Produto' )
	oModel:GetModel( 'ZDHDETAIL' ):SetDescription( 'Area de Atuacao' )

	oModel:SetPrimaryKey( { "ZDH_FILIAL", "ZDH_CODPRD"} )

	
Return( oModel )

/** {Protheus.doc} ViewDef
Funcao para Montar a Interface
@type function
@author SERGIO SALUSTIANO
@since 30/11/2020
*/
Static Function ViewDef()

	Local oView
	Local oModel  := FWLoadModel( 'SF02A04S' )

	Local oStruZDHMASTER := FWFormStruct( 2, 'ZDH', { |X| .Not. (AllTrim(X) $ 'ZDH_ITEM, ZDH_CODARE, ZDH_DESCAR') } )
	Local oStruZDHDETAIL := FWFormStruct( 2, 'ZDH', { |X| AllTrim(X) $ 'ZDH_ITEM, ZDH_CODARE, ZDH_DESCAR' } )

	oView := FWFormView():New()

	oView:SetModel( oModel )

	oView:AddField( 'VZDHMASTER', oStruZDHMASTER, 'ZDHMASTER' )
	oView:AddGrid( 'VZDHDETAIL' , oStruZDHDETAIL, 'ZDHDETAIL' )
	oView:AddIncrementField( 'VZDHDETAIL', 'ZDH_ITEM' )
	oView:CreateHorizontalBox( 'CABECALHO', 25 )
	oView:CreateHorizontalBox( 'DETALHE', 75 )
	
	oView:SetOwnerView( 'VZDHMASTER', 'CABECALHO' )
	oView:SetOwnerView( 'VZDHDETAIL', 'DETALHE' )
	
	oView:EnableTitleView( 'VZDHMASTER' )
	oView:EnableTitleView( 'VZDHDETAIL' )
	
	oView:AddIncrementField( 'VZDHDETAIL', 'ZDH_ITEM' )
	//oView:AddIncrementField( 'VZDHDETAIL', 'ZDH_SEQITE' )
	
	oView:SetCloseOnOk( {|| .t.} )

Return( oView )

