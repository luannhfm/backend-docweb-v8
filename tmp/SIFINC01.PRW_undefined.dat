#INCLUDE "PROTHEUS.CH"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³SIFINC01  ºAutor  ³Microsiga           º Data ³  29/03/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Mostra resumo de titulos marcados no bordero               º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ P11 - SISTEMA INDUSTRIA                                   º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

User Function SIFINC01()
Local _cArea   := GetArea()
Local _aTitulo := {}

(Alias())->(dbGoTop())

While (Alias())->(!Eof())
	// Verifica se esta marcado
	IF !Empty((Alias())->E2_OK)
		IF ( _nPos := Ascan(_aTitulo,{ |x| x[1] == (Alias())->E2_FORNECE .and. x[2] == (Alias())->E2_LOJA } ) ) > 0
			_aTitulo[_nPos,4] += (Alias())->E2_SALDO
		ELSE
			Aadd(_aTitulo,{ (Alias())->E2_FORNECE,(Alias())->E2_LOJA,(Alias())->E2_NOMFOR,(Alias())->E2_SALDO })
		ENDIF
	ENDIF
	(Alias())->(dbSkip())
Enddo

// Verifica se existem titulos marcados
IF Len(_aTitulo) == 0
	MsgInfo("Nenhum título for marcado.")
ELSE
	
	DEFINE MSDIALOG oDlgTit TITLE "Total por Fornecedor" FROM 0,0 TO 300,450 PIXEL
	
	@ 10,10 LISTBOX oLbx FIELDS HEADER "Código","Loja","Nome Fornecedor","Total" SIZE 382,120 OF oDlgTit PIXEL
	
	oLbx:SetArray( _aTitulo )
	oLbx:bLine := {|| {_aTitulo[oLbx:nAt,1],_aTitulo[oLbx:nAt,2],_aTitulo[oLbx:nAt,3],Transform(_aTitulo[oLbx:nAt,4],PesqPict("SE2","E2_SALDO",,2))}}
	
	oLbx:Align := CONTROL_ALIGN_ALLCLIENT
	
	ACTIVATE MSDIALOG oDlgTit ON INIT (EnchoiceBar(oDlgTit,{|| oDlgTit:End() },{|| oDlgTit:End() })) CENTERED
	
ENDIF

RestArea(_cArea)
Return()
