#include 'TOTVS.ch'
#include 'FWMVCDEF.ch'

/*/{Protheus.doc} User Function CRMA980
Ponto de entrada no padrão MVC do cadastro de clientes.
@type  Function
@author Franklin de Brito de Oliveira
@since 03/08/2021
@return variant, Conforme ID do local de execução do ponto de entrada
/*/
User Function CRMA980()
Local aParam     := PARAMIXB
Local xRet       := .T.
Local oObj       := ''
Local cIdPonto   := ''
Local cIdModel   := ''
Default l030Auto := .F.

Private INCLUI

    If FunName() == "RPC"
        l030Auto := .T.
    EndIf
    If aParam <> NIL
        oObj       := aParam[1]
        cIdPonto   := aParam[2]
        cIdModel   := aParam[3]
        INCLUI := oObj:GetOperation() == MODEL_OPERATION_INSERT

        if cIdPonto == 'MODELVLDACTIVE'
            if l030Auto
                oStructSA1 := oObj:GetModel("SA1MASTER"):GetStruct()
                oStructSA1:SetProperty("A1_COD" , MODEL_FIELD_WHEN, {|| .T.})
                oStructSA1:SetProperty("A1_LOJA" , MODEL_FIELD_WHEN, {|| .T.})
                oStructSA1:SetProperty("A1_XPORTE", MODEL_FIELD_WHEN, {|| .T.})
                oStructSA1:SetProperty("A1_XSITREC", MODEL_FIELD_WHEN, {|| .T.})
                oStructSA1:SetProperty("A1_DTNASC", MODEL_FIELD_WHEN, {|| .T.})
                oStructSA1:SetProperty("A1_CNAE", MODEL_FIELD_WHEN, {|| .T.})
                oStructSA1:SetProperty("A1_XQTEMP", MODEL_FIELD_WHEN, {|| .T.})
                oStructSA1:SetProperty("A1_XCODSIN", MODEL_FIELD_WHEN, {|| .T.})
            endif
            xRet := .T.
        //Antes da alteração de qualquer campo do modelo.
        elseif cIdPonto == 'MODELPRE'
            xRet := .T.
        //Na validação total do modelo.
        elseif cIdPonto == 'MODELPOS'
            xRet := .T.
        //Antes da alteração de qualquer campo do formulário.
        ElseIf cIdPonto == 'FORMPRE'
            xRet := .T.
        //Na validação total do formulário.
        ElseIf cIdPonto == 'FORMPOS'
            Do Case
            //Substituição do ponto de entrada MA030TOK: Permite validações adicionais no cadastro de cliente
            Case oObj:GetOperation() == MODEL_OPERATION_INSERT
                xRet := U_MA030TOK()
            //Substituição do ponto de entrada MA030TOK: Permite validações adicionais no cadastro de cliente
            Case oObj:GetOperation() == MODEL_OPERATION_UPDATE
                xRet := U_MA030TOK()
            Otherwise
                xRet := .T.
            EndCase
        //Antes da alteração da linha do formulário FWFORMGRID.
        ElseIf cIdPonto == 'FORMLINEPRE'
            xRet := .T.
        //Na validação total da linha do formulário FWFORMGRID.
        ElseIf cIdPonto == 'FORMLINEPOS'
            xRet := .T.
        //Após a gravação total do modelo e dentro da transação.
        ElseIf cIdPonto == 'MODELCOMMITTTS'
            Do Case
            //Substituição do ponto de entrada M030INC: Ponto de Entrada chamado após a inclusão do cliente
            Case oObj:GetOperation() == MODEL_OPERATION_INSERT
                U_M030INC()
            //Substituição do ponto de entrada MALTCLI: Ponto de Entrada após a alteração do cliente
            Case oObj:GetOperation() == MODEL_OPERATION_UPDATE
                U_MALTCLI()
            //Substituição do ponto de entrada M030EXC: Ponto de Entrada executado após a exclusão do cliente, e dentro da transação
            Case oObj:GetOperation() == MODEL_OPERATION_DELETE
                U_M030EXC() 
            EndCase
        //Após a gravação total do modelo e fora da transação.
        ElseIf cIdPonto == 'MODELCOMMITNTTS'
            
        //Após a gravação da tabela do formulário.
        ElseIf cIdPonto == 'FORMCOMMITTTSPOS'

        //No cancelamento do botão.
        ElseIf cIdPonto == 'MODELCANCEL'
            xRet := .T.
        //Para a inclusão de botões na ControlBar.
        ElseIf cIdPonto == 'BUTTONBAR'

        EndIf
    EndIf

Return xRet
