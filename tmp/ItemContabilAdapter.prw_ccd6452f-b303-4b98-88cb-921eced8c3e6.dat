#include 'totvs.ch'
#include 'parmtype.ch'

/*/{Protheus.doc} ItemContabilAdapter
Classe Adapter para o serviço
@author  Walmir Junior
@since   06/10/2020
@version 1.0
/*/

CLASS ItemContabilAdapter FROM FWAdapterBaseV2
	METHOD New()
	METHOD GetListItemContabil()
EndClass

//-------------------------------------------------------------------
/*/{Protheus.doc} New
Método construtor
@param cVerb, verbo HTTP utilizado
@author  Walmir Junior
@since   06/10/2020
@version 1.0
/*/
//-------------------------------------------------------------------
Method New( cVerb ) CLASS ItemContabilAdapter
	_Super:New( cVerb, .T. )
return

//-------------------------------------------------------------------
/*/{Protheus.doc} GetListItemContabil
Método que retorna uma lista de centros de custo.
@author  Walmir Junior
@since   06/10/2020
@version 1.0
/*/
//-------------------------------------------------------------------
Method GetListItemContabil() CLASS ItemContabilAdapter
	Local aArea 	AS ARRAY
	Local cWhere	AS CHAR
	

	aArea   := FwGetArea()
	AddMapFields( self )
	::SetQuery( GetQuery() )

	cWhere :=	" D_E_L_E_T_ = ' ' AND CTD_BLOQ = '2' AND "+;
				"(	( CTD_DTEXIS = ' ' Or CTD_DTEXIS <= '" + GravaData(dDataBase,.F.,8) + "' ) AND "+;
				"	( CTD_DTEXSF = ' ' Or CTD_DTEXSF >= '" + GravaData(dDataBase,.F.,8) + "')	) "

	::SetWhere( cWhere )
	
    ::SetOrder( " CTD_FILIAL, CTD_ITEM, CTD_DESC01 " )

	//Executa a consulta, se retornar .T. tudo ocorreu conforme esperado
	If ::Execute() 
		// Gera o arquivo Json com o retorno da Query
		::FillGetResponse()
	EndIf
	
    FwrestArea(aArea)

return

//-------------------------------------------------------------------
/*/{Protheus.doc} AddMapFields
Função para geração do mapa de campos
@param oSelf, object, Objeto da prórpia classe
@author  Walmir Junior
@since   06/10/2020
@version 1.0
/*/
//-------------------------------------------------------------------
Static Function AddMapFields( oSelf )
	
	oSelf:AddMapFields( 'filial'    	, 'CTD_FILIAL'	, .T., .T., { 'CTD_FILIAL'  , 'C', TamSX3( 'CTD_FILIAL' )[1] , 0 } )
	oSelf:AddMapFields( 'item'			, 'CTD_ITEM'	, .T., .F., { 'CTD_ITEM'   	, 'C', TamSX3( 'CTD_ITEM'  )[1]  , 0 } )	
	oSelf:AddMapFields( 'descricao'	    , 'CTD_DESC01'	, .T., .F., { 'CTD_DESC01'  , 'C', TamSX3( 'CTD_DESC01' )[1] , 0 } )
	oSelf:AddMapFields( 'classe'	    , 'CTD_CLASSE'	, .T., .F., { 'CTD_CLASSE'  , 'C', TamSX3( 'CTD_CLASSE' )[1] , 0 } )
	oSelf:AddMapFields( 'bloqueado'	    , 'CTD_BLOQ'	, .T., .F., { 'CTD_BLOQ'  	, 'C', TamSX3( 'CTD_BLOQ' )[1]	 , 0 } )

return 

//------------------------------------------------------------------
/*/{Protheus.doc} GetQuery
Retorna a query usada no serviço
@param oSelf, object, Objeto da prórpia classe
@author  Walmir Junior
@since   06/10/2020
@version 1.0
/*/
//-------------------------------------------------------------------
Static Function GetQuery()
	Local cQuery AS CHARACTER
	
	cQuery := " SELECT #QueryFields#"
    cQuery +=   " FROM " + RetSqlName( 'CTD' ) + " CTD "
    cQuery += " WHERE #QueryWhere#"

Return cQuery
