#include 'protheus.ch'


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³MTA097MNU ºAutor  ³Adriano Luis Brandaoº Data ³  19/07/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Ponto de entrada para chamada de funcoes de liberacao de    º±±
±±º          ³documentos do tipo Solicitacao de compras.                  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP - CNI                                                   º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

User Function MTA097MNU()

// Consulta
IF (_nPos := aScan(aRotina,{|x| Upper(AllTrim(x[2])) == Upper("A097Visual")})) > 0
	aRotina[_nPos,2] := "U_SICOMA04(1)"
ENDIF
// Liberação
IF (_nPos := aScan(aRotina,{|x| Upper(AllTrim(x[2])) == Upper("A097Libera")})) > 0
	aRotina[_nPos,2] := "U_SICOMA04(2)"
ENDIF
// Estornar
IF (_nPos := aScan(aRotina,{|x| Upper(AllTrim(x[2])) == Upper("A097Estorna")})) > 0
	aRotina[_nPos,2] := "U_SICOMA04(3)"
ENDIF

aAdd(aRotina, {"Consulta Just.Compras","U_MTA097JC",0,4})
aAdd(aRotina, {"Imprime Pedido","U_MTA097PC",0,4})    

Return()

/** {Protheus.doc} MTA097JC
Funcao para realizar Consulta Just.Compras

@param: 	<Nil>
@author 	Doit
@since 		29/04/2014
@Uso: 		SFIEMT
*/	                                            
User Function MTA097JC()

Local cJustCom:= ""
Local cObserv  := ""
Local cTexto   := ""
Local oMemo
Local oFont
Local cFileLog := ""
Local cMask    := "", i

IF SCR->CR_TIPO == 'SC'
   SC1->(DBSETORDER(1))
   If SC1->(dbSeek(SCR->CR_FILIAL  + PadR(SCR->CR_NUM, TamSX3("C1_NUM")[1])))               
      cJustCom:=SC1->C1_XJUSTIF
   ENDIF   
ELSEIF SCR->CR_TIPO == 'PC'
   SC7->(DBSETORDER(1))
   If SC7->(dbSeek(SCR->CR_FILIAL  + PadR(SCR->CR_NUM, TamSX3("C7_NUM")[1])))               
      SC1->(DBSETORDER(1))
      If SC1->(dbSeek(SC7->C7_FILIAL  + PadR(SC7->C7_NUMSC, TamSX3("C1_NUM")[1])))               
         cJustCom:=SC1->C1_XJUSTIF
      ENDIF   
   ENDIF
ENDIF   
                   

cTexto += Replicate( "-", 65 ) +chr(13)+chr(10)
cTexto += Replicate( "*", 65 ) +chr(13)+chr(10)
cTexto += "   Conteúdo do campo Justificativa de Compras "+chr(13)+chr(10)
cTexto += Replicate( "*", 65 ) +chr(13)+chr(10)
cTexto += Replicate( "-", 65 ) + CRLF + CRLF + CRLF


for i:=1 to len(cJustCom)
	
	cObserv += substr(cJustCom,i,1)
	
	if i % 80 == 0
		cObserv += chr(13)+chr(10)
	endif
	
next i

If !Empty(alltrim(cObserv))
	
	cTexto += cObserv
	
	cFileLog := MemoWrite( CriaTrab( , .F. ) + ".log", cTexto )
	
	Define Font oFont Name "Arial" Size 8, 12 
	
	Define MsDialog oDlg Title "Totvs" From 3, 0 to 340, 725 Pixel
	
	@ 5, 5 Get oMemo Var cTexto Memo When .F. Size 352, 145 Of oDlg Pixel //FONT oDlg:oFont COLOR CLR_BLACK,CLR_HGRAY 
	oMemo:bRClicked := { || AllwaysTrue() }
	oMemo:oFont     := oFont 
	
	Define SButton From 153, 275 Type  1 Action oDlg:End() Enable Of oDlg Pixel // Apaga
	
	Activate MsDialog oDlg Center
	
EndIf

Return()

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³MTA097PC  ºAutor  ³Antonio Cordeiro    º Data ³  15/05/14   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Ponto de entrada impressao do pedido de compra no browse    º±±
±±º          ³da liberacao dos documentos.                                º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP - CNI                                                   º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
User Function MTA097PC()

_aArea    :=GetArea()
_aAreaSC7 :=SC7->(GETAREA())

IF ALLTRIM(SCR->CR_TIPO) =='PC'
   SC7->(DBSETORDER(1))
   IF SC7->(DBSEEK(SCR->CR_FILIAL+SUBSTR(SCR->CR_NUM,1,TamSX3("C7_NUM")[1])))
      U_DTCOMR04('SC7',SC7->(RECNO()),1)
      //MATR110('SC7',SC7->(RECNO()),1)
   ELSE
      MsgAlert('Filial / Pedido nao encontrado: '+SCR->CR_FILIAL+' '+SCR->CR_NUM,'Atencao','Info') 
   ENDIF
ELSE
   MsgAlert('Este documento: '+SCR->CR_NUM+' não é um pedido de compra','Atencao','Info')
ENDIF   
RestArea(_aAreaSC7)
RestArea(_aArea)

Return()