#Include 'protheus.ch'

/*/{Protheus.doc} A012FNG
Ponto de Entrada na rotina ATFA012 (Cadastro de ativos), 
permitir gatilhar campos customizados da tabela FNG quando informado o grupo de bens, 
chamado a cada linha da SN3 criada a partir do grupo de bens.
@author Franklin de Brito de Oliveira
@since 15/10/2020
@return Nil
@see https://tdn.totvs.com/pages/releaseview.action?pageId=382558407
@type user function
@history 15/10/2020, Franklin de Brito de Oliveira, preenchimento dos campos N3_CCUSTO, N3_CUSTBEM, N3_SUBCTA e N3_SUBCCON com os campos relacionados no documento de entrada.
/*/
User Function A012FNG()
Local aArea		:= GetArea()
Local oModel	:= FWModelActive()
Local oMdlSN1	:= oModel:GetModel( 'SN1MASTER' )
Local oMdlSN3	:= oModel:GetModel( 'SN3DETAIL' )
	cCodForn := oMdlSN1:GetValue('N1_FORNEC')
	cLojForn := oMdlSN1:GetValue('N1_LOJA')
	cNSerie	:= oMdlSN1:GetValue('N1_NSERIE')
	cNFiscal := oMdlSN1:GetValue('N1_NFISCAL')
	cNFItem := oMdlSN1:GetValue('N1_NFITEM')
	cCodProd := oMdlSN1:GetValue('N1_PRODUTO')
	DbSelectArea("SD1")
	DbSetOrder(1)//D1_FILIAL+D1_DOC+D1_SERIE+D1_FORNECE+D1_LOJA+D1_COD+D1_ITEM
	//Posiciono no item no documento de entrada associado ao bem
	If SD1->( DbSeek( xFilial("SD1") + cNFiscal + cNSerie + cCodForn + cLojForn + cCodProd + cNFItem ) )
		//Preencho os dados de centro de custo e item contabil do bem com os dados preenchidos no documento de entrada
		oMdlSN3:SetValue("N3_CCUSTO", SD1->D1_CC)
		oMdlSN3:SetValue("N3_CUSTBEM", SD1->D1_CC)
		oMdlSN3:SetValue("N3_SUBCTA", SD1->D1_ITEMCTA)
		oMdlSN3:SetValue("N3_SUBCCON", SD1->D1_ITEMCTA)
	EndIf
	SD1->( DbCloseArea() )
	RestArea(aArea)
Return Nil