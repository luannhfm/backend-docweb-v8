#Include 'Protheus.ch'
#Include 'TopConn.ch'

/*/{Protheus.doc} SS69R02J
Relatorio Clientes x Servicos (GCT/CRM)

@type 		function
@author 	Jose Leite de Barros Neto
@since 	04/04/2016
@version 	1.0
@return 	Nil, Nulo
/*/
User Function SS69R02J()
	
	Local _oReport		:= Nil
	Local _cPerg 		:= FunName()
	
	If TRepInUse()
		fCriaSx1( _cPerg )
		If Pergunte( _cPerg )
			_oReport := ReportDef( _cPerg )
			_oReport:PrintDialog()
		EndIf
	EndIf

Return


/** {Protheus.doc} ReportDef
Funcao que define as colunas

@author: 	Jose Leite de Barros Neto
@since: 	04/04/2016
@Uso: 		SFIEMT
*/
Static Function ReportDef(p_cPerg)
	
	Local oReport	:= Nil
	Local oSection	:= Nil
	Local oBreak		:= Nil
	Local cTitle 	:= "Relatorio Clientes x Servicos"  
	
	oReport := TReport():New(p_cPerg,cTitle,p_cPerg,{|oReport| PrintReport(oReport)},cTitle)
	oReport:SetLandScape()
	
	oSection:= TRSection():New(oReport,OemToAnsi(""),{"CN9","ADY","ADZ","SA1"})
	
	oUndExec:= TRCell():New(oSection, "_cUndExe","TRA", "Unid. Executora")
	oUndExec:SetSize(15)
	
	oNomCli:= TRCell():New(oSection, "_cNomCli","TRA", "Cliente")
	oNomCli:SetSize(60)
	
	oCGC:= TRCell():New(oSection, "_cCGC","TRA", "CNPJ")
	oCGC:SetSize(30)
	
	oProdut:= TRCell():New(oSection, "_cProdut","TRA", "Serviço(s)")
	oProdut:SetSize(50)
	
	oAnoCtr:= TRCell():New(oSection, "_cAnoCtr","TRA", "Ano Fech. CTR.")
	oAnoCtr:SetSize(12)
	
Return( oReport )


/** {Protheus.doc} ReportDef
Funcao que gera o relatorio

@author: 	Jose Leite de Barros Neto
@since: 	04/04/2016
@Uso: 		SFIEMT
*/
Static Function PrintReport(oReport)
	
	Local oSection 	:= oReport:Section(1)
	Local _cQuery 	:= ""
	Local _cUndExe	:= ""
	Local _cNomCli	:= ""
	Local	_cCGC		:= ""
	Local	_cProdut	:= ""
	Local	_cAnoCtr	:= ""
	
	If Select("TRA") > 0
		DbSelectarea("TRA")
		DbCloseArea()
	Endif
	
	_cQuery := " SELECT ADZ_XUNEXE, ADY_VEND, A1_NOME, A1_CGC,		"	
	_cQuery += CRLF + "	ADZ_PRODUT, SUBSTR(CN9_DTINIC,1,4) CN9_DTINIC,			"
	_cQuery += CRLF + "	CASE CN9_SITUAC													"
	_cQuery += CRLF + "		WHEN '01' THEN 'Cancelado'								"
	_cQuery += CRLF + "		WHEN '02' THEN 'Em Elaboração'							"
	_cQuery += CRLF + "		WHEN '03' THEN 'Emitido'									"
	_cQuery += CRLF + "		WHEN '04' THEN 'Em Aprovação'							"
	_cQuery += CRLF + "		WHEN '05' THEN 'Vigente'									"
	_cQuery += CRLF + "		WHEN '06' THEN 'Paralisado'								"
	_cQuery += CRLF + "		WHEN '07' THEN 'Sol. Finalização'						"
	_cQuery += CRLF + "		WHEN '08' THEN 'Finalizado'								"
	_cQuery += CRLF + "		WHEN '09' THEN 'Revisão'									"
	_cQuery += CRLF + "		WHEN '10' THEN 'Revisado'								"
	_cQuery += CRLF + "		WHEN '11' THEN 'Aguardando Finalização'				"
	_cQuery += CRLF + "		ELSE ' '														"
	_cQuery += CRLF + "	END AS CN9_SITUAC												"
	_cQuery += CRLF + "FROM  " + RetSqlName("CN9") + " CN9 "
	_cQuery += CRLF + "	INNER JOIN " + RetSqlName("SA1") + " SA1 ON SA1.A1_FILIAL = '"+ xFilial("SA1") +"'		" 	
	_cQuery += CRLF + "									AND SA1.A1_COD = CN9.CN9_CLIENT											"	
	_cQuery += CRLF + "									AND SA1.A1_LOJA = CN9.CN9_LOJACL										"		
	_cQuery += CRLF + "									AND SA1.D_E_L_E_T_ <> '*'												"	
	_cQuery += CRLF + "	LEFT JOIN " + RetSqlName("ADY") + " ADY ON ADY.ADY_FILIAL = CN9.CN9_FILIAL					"
	_cQuery += CRLF + "									AND ADY.ADY_OPORTU = CN9_XOPORT											"
	_cQuery += CRLF + "									AND ADY.ADY_REVISA = CN9_XREVOP											"
	_cQuery += CRLF + "									AND ADY.D_E_L_E_T_ <> '*'												"	
	_cQuery += CRLF + "	LEFT JOIN " + RetSqlName("ADZ") + " ADZ ON ADZ.ADZ_FILIAL = ADY.ADY_FILIAL					"
	_cQuery += CRLF + "									AND ADZ.ADZ_PROPOS = ADY.ADY_PROPOS									"	
	_cQuery += CRLF + "									AND ADZ.ADZ_REVISA = ADY.ADY_PREVIS									"	
	_cQuery += CRLF + "									AND ADZ.D_E_L_E_T_ <> '*'												"	
	_cQuery += CRLF + "WHERE																												"
	_cQuery += CRLF + "	CN9_DTINIC  >= '"+ DtoS(MV_PAR01) +"'																	"
	_cQuery += CRLF + "	AND CN9_DTFIM   <= '"+ DtoS(MV_PAR02) +"'															"
	_cQuery += CRLF + "	AND CN9.D_E_L_E_T_ <> '*'																				"	
	_cQuery += CRLF + "	AND ADZ_XUNEXE BETWEEN '"+ MV_PAR03 +"' AND '"+ MV_PAR04 +"'									"
	_cQuery += CRLF + "	AND ADY_VEND BETWEEN '" + MV_PAR05 + "' AND '" + MV_PAR06 + "'									"			
	
	If .Not. Empty(Alltrim(MV_PAR07))
		_cQuery += CRLF + " AND CN9.CN9_SITUAC IN "+ FormatIn(AllTrim(MV_PAR07),";")
	Endif
	
	_cQuery += CRLF + "GROUP	BY	ADZ_XUNEXE, ADY_VEND, A1_NOME, 																	"
	_cQuery += CRLF + "			A1_CGC, ADZ_PRODUT, SUBSTR(CN9_DTINIC,1,4), CN9_SITUAC									"
	_cQuery += CRLF + "ORDER BY ADZ_XUNEXE, ADY_VEND																				"
	
	TCQUERY _cQuery NEW ALIAS "TRA"
		
	DbSelectArea("TRA")
	TRA->(DbGoTop())
		
	oReport:SetMeter(RecCount())
	oSection:Init()
		
	While .Not. TRA->( EOF() )
		
		If oReport:Cancel()
			Exit
		EndIf
		
		_cUndExe	:= AllTrim(TRA->ADZ_XUNEXE)
		_cNomCli	:= AllTrim(TRA->A1_NOME)
		_cCGC		:= AllTrim(TRA->A1_CGC)
		_cProdut	:= AllTrim(Posicione("SB1",1,xFilial("SB1")+TRA->ADZ_PRODUT,"B1_DESC"))
		_cAnoCtr	:= AllTrim(TRA->CN9_DTINIC)
		
		oSection:Cell("_cUndExe"):SetValue(_cUndExe)
		oSection:Cell("_cNomCli"):SetValue(_cNomCli)
		oSection:Cell("_cCGC"):SetValue(_cCGC)
		oSection:Cell("_cProdut"):SetValue(_cProdut)
		oSection:Cell("_cAnoCtr"):SetValue(_cAnoCtr)
		
		oSection:PrintLine()
		oReport:IncMeter()
			
		TRA->( DbSkip() )
	End
		
	TRA->(DbCloseArea())
	oSection:Finish()
	
Return( Nil )


/** {Protheus.doc} fCriaSx1
Funcao para criacao das perguntas

@param: 	<Nil>
@author 	Jose Leite de Barros Neto
@since 	04/04/2016
@Uso: 		SFIEMT
*/
Static Function fCriaSx1( p_cPerg )
	
	Local aHelp := {}

	AAdd(aHelp, {{"Informe a Data de Emissao Inicial"		}, {""}, {""}})
	AAdd(aHelp, {{"Informe a Data de Emissao Final"		}, {""}, {""}})
	AAdd(aHelp, {{"Informe a Unidade Executora Inicial"	}, {""}, {""}})
	AAdd(aHelp, {{"Informe a Unidade Executora Final"		}, {""}, {""}})
	AAdd(aHelp, {{"Informe o Consultor Inicial"				}, {""}, {""}})
	AAdd(aHelp, {{"Informe o Consultor Inicial"				}, {""}, {""}})
	AAdd(aHelp, {{"Informe a Situação do Contrato"			}, {""}, {""}})
	
	u_SFPUTSX1( p_cPerg, "01","Data Vigencia Inicial?	"  ,"","","mv_ch1","D",8						 		,0,0,"G",""						,""			,"","","mv_par01","","","","","","","","","","","","","","","","",aHelp[1,1],aHelp[1,2],aHelp[1,3],"")
	u_SFPUTSX1( p_cPerg, "02","Data Vigencia Final?		"  ,"","","mv_ch2","D",8						 		,0,0,"G",""						,""			,"","","mv_par02","","","","","","","","","","","","","","","","",aHelp[2,1],aHelp[2,2],aHelp[2,3],"")
	u_SFPUTSX1( p_cPerg, "03","Unidade Executora de?	"  ,"","","mv_ch3","C",TamSX3("ADZ_XUNEXE")[1]	,0,0,"G",""						,"SM0"		,"","","mv_par03","","","","","","","","","","","","","","","","",aHelp[3,1],aHelp[3,2],aHelp[3,3],"")
	u_SFPUTSX1( p_cPerg, "04","Unidade Executora Ate?	"  ,"","","mv_ch4","C",TamSX3("ADZ_XUNEXE")[1]	,0,0,"G",""						,"SM0"		,"","","mv_par04","","","","","","","","","","","","","","","","",aHelp[4,1],aHelp[4,2],aHelp[4,3],"")	
	u_SFPUTSX1( p_cPerg, "05","Consultor de?				"  ,"","","mv_ch5","C",TamSX3("ADY_VEND")[1]	,0,0,"G",""						,"SA3"		,"","","mv_par05","","","","","","","","","","","","","","","","",aHelp[5,1],aHelp[5,2],aHelp[5,3],"")
	u_SFPUTSX1( p_cPerg, "06","Consultor Ate?				"  ,"","","mv_ch6","C",TamSX3("ADY_VEND")[1]	,0,0,"G",""						,"SA3"		,"","","mv_par06","","","","","","","","","","","","","","","","",aHelp[6,1],aHelp[6,2],aHelp[6,3],"")
	u_SFPUTSX1( p_cPerg, "07","Situação do Contrato ?	"  ,"","","mv_ch7","C",99							,0,0,"G","u_MrkCN9()"			,""			,"","","mv_par07","","","","","","","","","","","","","","","","",aHelp[7,1],aHelp[7,2],aHelp[7,3],"")
	
Return( Nil )