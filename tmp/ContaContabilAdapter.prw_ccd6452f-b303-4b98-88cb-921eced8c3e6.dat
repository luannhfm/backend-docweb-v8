#include 'totvs.ch'
#include 'parmtype.ch'

/*/{Protheus.doc} ContaContabilAdapter
Classe Adapter para o serviço
@author  Walmir Junior
@since   06/10/2020
@version 1.0
/*/

CLASS ContaContabilAdapter FROM FWAdapterBaseV2
	METHOD New()
	METHOD GetListContaContabil()
EndClass

//-------------------------------------------------------------------
/*/{Protheus.doc} New
Método construtor
@param cVerb, verbo HTTP utilizado
@author  Walmir Junior
@since   06/10/2020
@version 1.0
/*/
//-------------------------------------------------------------------
Method New( cVerb ) CLASS ContaContabilAdapter
	_Super:New( cVerb, .T. )
return

//-------------------------------------------------------------------
/*/{Protheus.doc} GetListContaContabil
Método que retorna uma lista de Conta Contabil.
@author  Walmir Junior
@since   06/10/2020
@version 1.0
/*/
//-------------------------------------------------------------------
Method GetListContaContabil() CLASS ContaContabilAdapter
	Local aArea 	AS ARRAY
	Local cWhere	AS CHAR
	

	aArea   := FwGetArea()
	AddMapFields( self )
	::SetQuery( GetQuery() )

	cWhere :=	"	D_E_L_E_T_ = ' ' AND CT1_BLOQ = '2' AND "+;
				"	(( CT1_DTEXIS = ' ' OR CT1_DTEXIS <= '" + GravaData(dDataBase,.F.,8) + "' ) AND (CT1_DTEXSF = ' ' OR CT1_DTEXSF >= '" + GravaData(dDataBase,.F.,8) + "')) AND "+;
				"	((CT1_DTBLIN = ' ' OR CT1_DTBLIN >= '" + GravaData(dDataBase,.F.,8) + "') OR (CT1_DTBLFI = ' ' OR CT1_DTBLFI <= '" + GravaData(dDataBase,.F.,8) + "')) "
			
	::SetWhere( cWhere )
	
    ::SetOrder( " CT1_FILIAL, CT1_CONTA, CT1_DESC01 " )

	//Executa a consulta, se retornar .T. tudo ocorreu conforme esperado
	If ::Execute() 
		// Gera o arquivo Json com o retorno da Query
		::FillGetResponse()
	EndIf
	
    FwrestArea(aArea)

return

//-------------------------------------------------------------------
/*/{Protheus.doc} AddMapFields
Função para geração do mapa de campos
@param oSelf, object, Objeto da prórpia classe
@author  Walmir Junior
@since   06/10/2020
@version 1.0
/*/
//-------------------------------------------------------------------
Static Function AddMapFields( oSelf )
	
	oSelf:AddMapFields( 'filial'    	, 'CT1_FILIAL'	, .T., .T., { 'CT1_FILIAL'  , 'C', TamSX3( 'CT1_FILIAL' )[1] , 0 } )
	oSelf:AddMapFields( 'conta'			, 'CT1_CONTA'	, .T., .F., { 'CT1_CONTA'   , 'C', TamSX3( 'CT1_CONTA'  )[1] , 0 } )	
	oSelf:AddMapFields( 'descricao'	    , 'CT1_DESC01'	, .T., .F., { 'CT1_DESC01'  , 'C', TamSX3( 'CT1_DESC01' )[1] , 0 } )
	oSelf:AddMapFields( 'classe'	    , 'CT1_CLASSE'	, .T., .F., { 'CT1_CLASSE'  , 'C', TamSX3( 'CT1_CLASSE' )[1] , 0 } )
	oSelf:AddMapFields( 'bloqueado'	    , 'CT1_BLOQ'	, .T., .F., { 'CT1_BLOQ'	, 'C', TamSX3( 'CT1_BLOQ' )[1] 	 , 0 } )

return 

//------------------------------------------------------------------
/*/{Protheus.doc} GetQuery
Retorna a query usada no serviço
@param oSelf, object, Objeto da prórpia classe
@author  Walmir Junior
@since   06/10/2020
@version 1.0
/*/
//-------------------------------------------------------------------
Static Function GetQuery()
	Local cQuery AS CHARACTER
	
	cQuery := " SELECT #QueryFields#"
    cQuery +=   " FROM " + RetSqlName( 'CT1' ) + " CT1 "
    cQuery += " WHERE #QueryWhere#"

Return cQuery
