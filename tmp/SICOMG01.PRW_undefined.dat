#Include "Protheus.Ch"
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ SICOMG01 ³ Autor ³ CADUBITSKI            ³ Data ³ Jan/2012 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Gatilho no campo C1_PRODUTO, para atualizar a finalidade da³±±
±±³          ³ compra e conta contabil.                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

User Function SICOMG01()

Local aArea 		:= GetArea()
Local lOracle		:= "ORACLE"$Upper(TCGetDB())
Local cARQPROD	:= Upper(Alltrim(GetMv("MV_ARQPROD"))) //Configura se os dados de indicadores de produto serao considerados pela tabela "SB1" ou "SBZ"
Local cFil		:= ""
Local cCod		:= ""
Local cFinCom		:= ""
Local cConta		:= ""
Local cDel		:= ""
Local cFrom		:= ""

cAli := GetNextAlias()//Carrega o proximo alias disponivel

//Monta as variaveis a serem utilizadas na select conforme o MV_ARQPROD se SB1 ou SBZ
cFil		:= "% "+SubStr(cARQPROD,2,2)+"_FILIAL = '"+xFilial(cARQPROD)+"' %"
cCod		:= "% "+SubStr(cARQPROD,2,2)+"_COD %"
cCodWh		:= "% "+SubStr(cARQPROD,2,2)+"_COD = '"+M->C1_PRODUTO+"' %"
cFinCom	:= "% "+SubStr(cARQPROD,2,2)+"_FINCOM %"
cDel		:= "% "+cARQPROD+".D_E_L_E_T_ = ' ' %"
cConta		:= 	Iif(lOracle,"% NVL(ZO_XCONTC,' ') %","% ISNULL(ZO_XCONTC,' ') %")
cFrom		:= "% "+RetSqlName(cARQPROD)+" "+cARQPROD+" %"
		
//Verifica a conta contabil vinculado a finalidade de compra conforme MV_ARQPROD se SB1 ou SBZ
BeginSQL Alias cAli

	SELECT 
		%Exp:cCod%		AS COD,
		%Exp:cFinCom%	AS FINCOM,
		%Exp:cConta%	AS CONTA
		
	FROM %Exp:cFrom%
	
	LEFT OUTER JOIN %table:SZO% SZO ON 
		ZO_FILIAL = %xFilial:SZO%  
		AND ZO_FINCOM = %Exp:cFinCom%
		AND SZO.%NotDel% 
	
	WHERE 
		%Exp:cFil%
		AND %Exp:cCodWh%
		AND %Exp:cDel%

EndSQL

//MemoWrite("\SYSTEM\SICOMG01.SQL",GetLastQuery()[2])

DbSelectArea((cAli))

If Select((cAli)) > 0
	If !Empty((cAli)->CONTA)
		GDFieldPut("C1_CONTA" , (cAli)->CONTA,  n)
		GDFieldPut("C1_FINCOM", (cAli)->FINCOM, n)
	EndIf
EndIf

DbCloseArea((cAli))

RestArea(aArea)

Return(M->C1_PRODUTO)
