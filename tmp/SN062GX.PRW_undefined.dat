#INCLUDE "PROTHEUS.CH"
#INCLUDE "APWIZARD.CH"
#INCLUDE "RWMAKE.CH"
#INCLUDE "FONT.CH"
#INCLUDE "COLORS.CH"
#INCLUDE "TOPCONN.CH"
#INCLUDE "TOTVS.CH"
#INCLUDE "FWMVCDEF.CH"		
#INCLUDE "FWMVCDEF.CH"
#INCLUDE "FWMBROWSE.CH"
/*
+--------------------------------------------------------------------------+
|{Protheus.doc}<SN062GX>                                                   |
|Permite a Selecao de uma SESSAO qualquer para visualizar MATRICULAS       |
|@Author<Antonio Dantas>                                                   |
|@since<14/06/2014>                                                        |
|@version<Nil>                                                             |
|                                                                          |
|@parameters:                                                              |
|@receive                                                                  |
|<>                                                                        |
|@return                                                                   |
|<Nil>                                                                     |
+-----------+--------------------------------------------------------------+
|Uso        |FIEMT - Federacao das Industrias no Estado de Mato Grosso     |
+-----------+--------------------------------------------------------------+
*/
User Function SN062GX()
Local _oMBrowse 	:= FWMBrowse():New()
Local _cFilterD		:= ""
Private aRotina 	:= MenuDef() 
Private _cCadastro 	:= "Visualizar Matriculas" 
Private _cSessao 	:= Space(TamSX3("ZP6_XCOD")[1]) 
Private _cAlias		:= "ZP7"
//-- Identifica qual a sessao sera pesquisada
fGetSess(@_cSessao) 
If Empty(Alltrim(_cSessao))
	Return .t.
Endif 
_cFilterD := "ZP7_FILIAL == '"+FwxFilial("ZP7")+"' .and. ZP7_XSESSA == '"+_cSessao+"'"
//--
_oMBrowse:AddLegend( "ZP7->ZP7_XSTATU == 'B'"	,"DISABLE" 			, "Bloqueado Para Remessa"			)
_oMBrowse:AddLegend( "ZP7->ZP7_XSTATU == 'T'"	,"BR_VERMELHO.BMP"	, "Transferida para Proxima Sessao"	)
_oMBrowse:AddLegend( "ZP7->ZP7_XSTATU == 'C'"	,"BR_VERDE.BMP"		, "Corrigido"				  		)
_oMBrowse:AddLegend( "ZP7->ZP7_XSTATU == 'L'"	,"ENABLE" 			, "Liberado Para Remessa"			)
_oMBrowse:AddLegend( "ZP7->ZP7_XSTATU == 'x'"	,"BR_PRETO" 		, "Não Liberado p/Remessa"			)
_oMBrowse:AddLegend( "ZP7->ZP7_XSTATU == 'E'"	,"BR_LARANJA"		, "Remessa Gerada"              	) 
_oMBrowse:AddLegend( "ZP7->ZP7_XSTATU == 'O'"	,"BR_AZUL"			, "Ordem de Pagamento Agendada"		)
_oMBrowse:AddLegend( "ZP7->ZP7_XSTATU == 'R'"	,"CANCEL_15.PNG"	, "Rejeitado"						)
_oMBrowse:AddLegend( "ZP7->ZP7_XSTATU == 'P'"	,"BSTART.PNG"		, "Pago"							)
_oMBrowse:AddLegend( "ZP7->ZP7_XSTATU == 'D'"	,"BEND.PNG"			, "Devolvido"						)
//--
dbSelectArea("ZP7")
ZP7->(dbSetOrder(12)) 	//-- Ordena Por: [Descr da Modalidade]+[Descr Area Tec]+[Descr Curso]+[Turno]+[Evento]+[Nome do Aluno]
ZP7->(dbGotop())
//--
_oMBrowse:SetAlias("ZP7")    
_oMBrowse:SetFilterDefault(_cFilterD)  
_oMBrowse:SetDescription(_cCadastro)   
_oMBrowse:DisableDetails()
_oMBrowse:Activate()	
Return


/*
+--------------------------------------------------------------------------+
|{Protheus.doc}<MenuDef>                                                   |
|Definicao do MenuDef                                                      |
|@Author<Antonio Dantas>                                                   |
|@since<14/06/2014>                                                        |
|@version<Nil>                                                             |
|                                                                          |
|@parameters:                                                              |
|@receive                                                                  |
|<>                                                                        |
|@return                                                                   |
|<   _aRotina (a) - Vetor com as opcoes de Menu                            |
|>                                                                         |
+-----------+--------------------------------------------------------------+
|Uso        |FIEMT - FederaCAO das Industrias no Estado de Mato Grosso     |
+-----------+--------------------------------------------------------------+
*/
Static Function MenuDef()
Local _aRotina := {}
ADD OPTION _aRotina TITLE "Visualizar" 	ACTION "VIEWDEF.SN062GX" 	OPERATION 2 ACCESS 0
Return _aRotina               


/*
+--------------------------------------------------------------------------+
|{Protheus.doc}<ModelDef>                                                  |
|Definicao do ModelDef                                                     |
|@Author<Antonio Dantas>                                                   |
|@since<14/06/2014>                                                        |
|@version<Nil>                                                             |
|                                                                          |
|@parameters:                                                              |
|@receive                                                                  |
|<>                                                                        |
|@return                                                                   |
|<   _oModel (o) - Objeto com a Definicao do Modelo                        |
|>                                                                         |
+-----------+--------------------------------------------------------------+
|Uso        |FIEMT - FederaCAO das Industrias no Estado de Mato Grosso     |
+-----------+--------------------------------------------------------------+
*/
Static Function ModelDef()
Local _oModel			:= Nil 
Local _oStructMD		:= FWFormStruct(1,_cAlias)
//-- Objeto de modelo de dados
_oModel := MPFormModel():New("SN062GXZ", /*Pre-Validacao*/, /*Pos-Validacao*/, /*Commit*/,/*Cancel*/)
//-- A  estrutura  do  modelo  de  dados  (Model) 
_oModel:AddFields("ZP7MASTER",/*cOwner*/,_oStructMD,/*Pre-Validacao*/,/*Pos-Validacao*/)
//-- Chave Primaria
_oModel:SetPrimaryKey({"ZP7_FILIAL","ZP7_XPERIO","ZP7_XSEG","ZP7_XSESSA","ZP7_XMATRI","DTOS(ZP7_XDATAG)"})
Return _oModel


/*
+--------------------------------------------------------------------------+
|{Protheus.doc}<ViewDef>                                                   |
|Definicao do ViewDef                                                      |
|@Author<Antonio Dantas>                                                   |
|@since<14/06/2014>                                                        |
|@version<Nil>                                                             |
|                                                                          |
|@parameters:                                                              |
|@receive                                                                  |
|<>                                                                        |
|@return                                                                   |
|<   _oView (o) - Objeto com a Definicao do Visao do Modelo                |
|>                                                                         |
+-----------+--------------------------------------------------------------+
|Uso        |FIEMT - FederaCAO das Industrias no Estado de Mato Grosso     |
+-----------+--------------------------------------------------------------+
*/
Static Function ViewDef()
Local _oModel 		:= FWLoadModel("SN062GX")
Local _oStructZP7	:= FWFormStruct(2,_cAlias)
Local _oView		:= FWFormView():New()
_oView:SetModel(_oModel)
_oView:AddField("SN062GXZ",_oStructZP7,"ZP7MASTER")   
_oView:CreateHorizontalBox("MATRICULAS",100)
_oView:SetOwnerView("SN062GXZ", "MATRICULAS")
_oView:EnableControlBar(.T.)  
Return _oView    

/*
+--------------------------------------------------------------------------+
|{Protheus.doc}<fGetSess>                                                  |
|Abre interface (Tela) p/operador selecionar ou informar codigo da SESSAO  |
|@Author<Antonio Dantas>                                                   |
|@since<14/06/2014>                                                        |
|@version<Nil>                                                             |
|                                                                          |
|@parameters:                                                              |
|@receive                                                                  |
|<  _cSessao (c) - Codigo da Sessao que sera validado                      |
|                  *Recebida por REFERENCIA para ser modificada na funcao  |
|>                                                                         |
|@return                                                                   |
|<   _cSessao (c) - Codigo da Sessao que sera validado                     |
|>                                                                         |
+-----------+--------------------------------------------------------------+
|Uso        |FIEMT - FederaCAO das Industrias no Estado de Mato Grosso     |
+-----------+--------------------------------------------------------------+
*/
Static Function fGetSess(_cSessao)
//-- **********************************************************************
//--    Declaração de Variaveis Private dos Objetos
//-- **********************************************************************
SetPrvt("_oDlgSS","_oSaySS","_oGetSS","_oBtnOK","_oBtnESC")      
dbSelectArea("ZP6")
ZP6->(dbSetorder(1))
//-- **********************************************************************
//--    Definicao do Dialog e todos os seus componentes.
//-- **********************************************************************
_oDlgSS	:= MSDialog():New( 202,326,359,699,_cCadastro,,,.F.,,,,,,.T.,,,.T. )
_oSaySS	:= TSay():New( 024,050,{||"Cod. da Sessão: "},_oDlgSS,,,.F.,.F.,.F.,.T.,CLR_BLACK,CLR_WHITE,045,008)
_oGetSS	:= TGet():New( 021,095,{|u| If(PCount()>0,_cSessao:=u,_cSessao)}	,_oDlgSS,060,008,"@!"	,{|| Empty(Alltrim(_cSessao)) .OR. VldZP6(@_cSessao) }	,CLR_BLACK,CLR_WHITE,,,,.T.,"",,,.F.,.F.,,.F.,.F.,"ZP6","_cSessao",,)
_oBtnOK     := TButton():New(048,120,"OK"			,_oDlgSS,{|| _oDlgSS:End() }	,037,012,,,,.T.,,"",,,,.F. )
_oDlgSS:Activate(,,,.T.) 
Return _cSessao


/*
+--------------------------------------------------------------------------+
|{Protheus.doc}<fGetSess>                                                  |
|Valida o codigo da Sessao passada como parametros                         |
|@Author<Antonio Dantas>                                                   |
|@since<20/03/2014>                                                        |
|@version<Nil>                                                             |
|                                                                          |
|@parameters:                                                              |
|@receive                                                                  |
|<  _cSessao (c) - Codigo da Sessao que sera validado                      |     
|                  *Recebida por REFERENCIA para ser modificada na funcao  |
|>                                                                         |
|@return                                                                   |
|<  _lReturn (l) - (.t.) Codigo valido/ (.f.) Codigo Invalido              |
|>                                                                         |
+-----------+--------------------------------------------------------------+
|Uso        |FIEMT - FederaCAO das Industrias no Estado de Mato Grosso     |
+-----------+--------------------------------------------------------------+
*/
 Static Function VldZP6(_cSessao)
Local _aArea	:= GetArea() 
Local _aAreaZP6	:= ZP6->(GetArea())
Local _lReturn 	:= .t.
_cSessao := StrZero(Val(_cSessao),TamSX3("ZP6_XCOD")[1])
dbSelectArea("ZP6")
ZP6->(dbSetOrder(1)) 
If !(ZP6->(dbSeek(FwxFilial("ZP6")+_cSessao)))
	Aviso(FunName()+"/"+ProcName(),"Codigo da Sessão informada é invalido!",{"OK"})
	_lReturn 	:= .f.
Endif     
RestArea(_aArea)
RestArea(_aAreaZP6)
Return _lReturn 