#include "totvs.ch"
#include "restful.ch"

//-------------------------------------------------------------------
/*/{Protheus.doc} Lancamentos Contabeis
Declaração do GET de Lancamentos Contábeis
@type class
@author Walmir Junior
@since 18/09/2020
@version 1.0
/*/
//-------------------------------------------------------------------
WSRESTFUL lancontabilfolha DESCRIPTION 'endpoint Lancamentos Contabeis Folha - VENUS ' FORMAT "application/json,text/html"
    WSDATA Page              AS INTEGER OPTIONAL
    WSDATA aQueryString      AS ARRAY    OPTIONAL
    
 	WSMETHOD GET LancContabilFolha;
	    DESCRIPTION "Retorna uma lista de Lancamentos Contabeis";
	    WSSYNTAX "/api/lancontabilfolha" ;
        PATH "/api/lancontabilfolha" ;
	    PRODUCES APPLICATION_JSON

    WSMETHOD GET Itens;
        DESCRIPTION "Retorna lista de itens contabeis de um funcionario" ;
        WSSYNTAX "/api/lancontabilfolha/{id}" ;
        PATH "/api/lancontabilfolha/{id}";
        PRODUCES APPLICATION_JSON
 	
END WSRESTFUL

//-------------------------------------------------------------------
/*/{Protheus.doc} GET LancContabilFolha
Método GET com id LancContabilFolha
@type method
@author Walmir Junior
@since 18/09/2020
@version 1.0
/*/
//-------------------------------------------------------------------
WSMETHOD GET LancContabilFolha QUERYPARAM Page WSREST lancontabilfolha

If Len(::aURLParms) > 0
    ::SetResponse('{"id":' + ::aURLParms[1] + ', "name":"test"}')
EndIf

Return getLancContabilFolha(self)

//-------------------------------------------------------------------
/*/{Protheus.doc} GET Itens
Método GET com id Itens
@type method
@author Walmir Junior
@since 07/04/2021
@version 1.0
/*/
//-------------------------------------------------------------------
WSMETHOD GET Itens QUERYPARAM Page WSREST lancontabilfolha

If Len(::aURLParms) > 0 .And. ::aURLParms[1] != 'itens'
    SetRestFault(500,"Erro inesperado!")
    Return .F.
EndIf

Return getItensContabeisColaborador(self)

//-------------------------------------------------------------------
/*/{Protheus.doc} GET getLancContabilFolha
Função para tratamento da requisicao GET
@type function
@author Walmir Junior
@since 18/09/2020
@version 1.0
/*/
//-------------------------------------------------------------------
Static Function getLancContabilFolha( oWS )
    Local lRet  as logical
    Local oLanc as object
    Local nPosFilt := 0
    DEFAULT oWS:Page := 1  

    lRet := .T.
    oLanc := LanContabilFolhaAdapter():new( 'GET' )
    oLanc:cMethod := "getLancContabilFolha"
    nPosFilt := AScan(oWS:aQueryString, {|x| AllTrim(Upper(x[1]))=="FILTER" })
    If nPosFilt > 0
        If "PERIODO EQ" $ AllTrim(Upper(oWS:aQueryString[nPosFilt][2]))
            nAtIFilt := At("PERIODO EQ", AllTrim(Upper(oWS:aQueryString[nPosFilt][2])))
            cPeriodo := SubStr(AllTrim(Upper(oWS:aQueryString[nPosFilt][2])), nAtIFilt+12, 6)
        endIf
    endIf

    if cPeriodo < '202204'
        oLanc:lIsFolRM := .F.
    else
        oLanc:lIsFolRM := .T.
    endif

    oLanc:setPage(oWS:Page)
    oLanc:setPageSize(200)
    oLanc:setUrlFilter(oWS:aQueryString)
    oLanc:GetListLanCtbilFolha()
    //Se tudo ocorreu bem, retorna os dados via Json
    If oLanc:lOk
        oWS:SetResponse(EncodeUTF8(oLanc:getJSONResponse()))
    Else
    //Ou retorna o erro encontrado durante o processamento
        SetRestFault(oLanc:GetCode(), oLanc:GetMessage())
        lRet := .F.
    EndIf

    oLanc:DeActivate()
    oLanc := nil
   
Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} GET getItensContabeisColaborador
Função para tratamento da requisicao GET
@type function
@author Walmir Junior
@since 07/04/2021
@version 1.0
/*/
//-------------------------------------------------------------------
Static Function getItensContabeisColaborador( oWS )
    Local lRet  as logical
    Local oLanc as object
    DEFAULT oWS:Page      := 1  

    lRet        := .T.
    oLanc := LanContabilFolhaAdapter():new( 'GET' )
    oLanc:setPage(oWS:Page)
    oLanc:setPageSize(200)
    oLanc:setUrlFilter(oWS:aQueryString)
    oLanc:GetListItensColaborador()
    //Se tudo ocorreu bem, retorna os dados via Json
    If oLanc:lOk
        oWS:SetResponse(EncodeUTF8(oLanc:getJSONResponse()))
    Else
    //Ou retorna o erro encontrado durante o processamento
        SetRestFault(oLanc:GetCode(),oLanc:GetMessage())
        lRet := .F.
    EndIf
    //faz a desalocação de objetos e arrays utilizados
    oLanc:DeActivate()
    oLanc := nil

Return lRet
