#Include 'TOTVS.ch'

/*/{Protheus.doc} ${F290BTIT}
Ponto de entrada para definir quais colunas serão apresentadas na seleção de titulos da rotina faturas a pagar
@author j2a.caiolima
@since 28/02/2019
/*/
User Function F290BTIT()
	Local _aRet := aClone(PARAMIXB)
	
	_aRet := fOrgPos(_aRet)
	
Return(_aRet)

/*/{Protheus.doc} fOrgPos
Funcao para organizar o array de faturas a pagar
@author j2a.caiolima
@since 28/02/2019
/*/
Static Function fOrgPos(_aCampos)
	// backup da ultima posicao do array (recno)
	_aColRec := ATAIL(_aCampos)
	aDel(_aCampos, Len(_aCampos))
	
	fDelPos(_aCampos, "ABATMTS"   ,1)
	fDelPos(_aCampos, "ABATSOMADO",1)
	fDelPos(_aCampos, "E2_FILIAL" ,1)
	fDelPos(_aCampos, "E2_PREFIXO",1)
	
	// restauro a ultima posição com o recno
	_aCampos[Len(_aCampos)] := _aColRec
		
	//Aviso("VarInfo", VarInfo("_aCampos",_aCampos,,.F.) , {"ok"},3 )
	
Return(_aCampos)

/*/{Protheus.doc} fDelPos
Funcao que deleta uma posicao no array e adiciona essa posição na penultima posicao
@author j2a.caiolima
@since 28/02/2019
/*/
Static Function fDelPos(_aCampos,_cCampo, _nLenSub)
	// faço um backup da posicao a ser eliminada
	// deleto a ultima posição
	// e restauro essa posição na penultima posicao, pois a ultima precisa ser o recno
	_nPosDel := aScan(_aCampos, {|x| Upper(x[1]) = Upper(_cCampo) } )
	If _nPosDel > 0
		// Chamado GTI2000992 - Corrigido posicionamento para manipulacao do array
		_aColBkp := _aCampos[_nPosDel]
		aDel(_aCampos, _nPosDel)
		If _aCampos[Len(_aCampos)-_nLenSub] = nil
			_aCampos[Len(_aCampos)-_nLenSub] := _aColBkp
		EndIf
	EndIf
Return nil
