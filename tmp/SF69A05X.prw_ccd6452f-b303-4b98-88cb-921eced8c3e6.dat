#Include "TOTVS.ch"

/*/{Protheus.doc} User Function SF69A05X
    Inclusão de título provisório em contas a receber 
    @type  Function
    @author Daniel Castro - TOTVS
    @since 04/11/2020
    @version version
    /*/


User Function SF69A05X()

	Local aArea		:= GetArea()
	Local cTpCtr	:= GetMv("MV_XCTRSE1")



	If SubString(xFilial("CN9"),1,4) == "03MT" .And. Posicione("CN1",1,xFilial("CN1") + CN9->CN9_TPCTO, "CN1_ESPCTR") == "2" //Contrato de Venda

		dbSelectArea("CNF")
		CNF->(dbSetOrder(2))
		CNF->(dbSeek(xFilial("CNF") + CN9->CN9_NUMERO + CN9->CN9_REVISA))

		BeginSql Alias "TMPSE1"

				SELECT R_E_C_N_O_ AS RECNO
				FROM %table:SE1% SE1
				WHERE SE1.E1_FILIAL= %xfilial:SE1%
				AND SE1.E1_MDCONTR = %exp:CNF->CNF_CONTRA%
				AND SE1.E1_MDCRON = %exp:CNF->CNF_NUMERO%
				AND SE1.E1_TIPO = %exp:'PR'%
				AND SE1.%notDel% 

		EndSql

		dbSelectArea("TMPSE1")
		TMPSE1->(dbGotop())

		If !Empty(TMPSE1->RECNO)

			While TMPSE1->(!EOF())
				SE1->(dbGoto(TMPSE1->RECNO))

				RecLock("SE1", .F.)

					If CN9->CN9_TPCTO $ Alltrim(cTpCtr)
						SE1->E1_TIPO 	:= "BOL"
						SE1->E1_PREFIXO := "STI"
					EndIf
					
					SE1->E1_XOBSBOL	:= Alltrim(SE1->E1_MDCONTR) +"|"+ Alltrim(SE1->E1_NOMCLI)

				SE1->(MsUnlock())

				TMPSE1->(dbSkip())
			EndDo

			TMPSE1->(dbCloseArea())

		EndIf

	EndIf

	RestArea(aArea)

Return
