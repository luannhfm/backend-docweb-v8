#INCLUDE "PROTHEUS.CH"
#INCLUDE "TOPCONN.CH"
#INCLUDE "RWMAKE.CH"
#INCLUDE "FONT.CH"
#INCLUDE "COLORS.CH"  
#INCLUDE "PARMTYPE.CH"     
#INCLUDE "FWMBROWSE.CH"
#INCLUDE "FWMVCDEF.CH"
#INCLUDE "FWCOMMAND.CH"

#DEFINE CRLF chr(13)+chr(10)    

/*/{Protheus.doc} SF7321X
#CONTROLE_DN - Modelo 3 MVC para Multiplos Cadastros 
Rotinas Genericas para Tratamentos das Tabelas Importadas do DN. 
Chamada a partir da rotina SF7320X.PRW.
@type function
@author Antonio Dantas
@since 30/09/2015
@history 22/09/2020, Franklin de Brito de Oliveira, Correção de campos obrigatórios nos cadastros com Alias SX5.
/*/
User Function SF7321X(_cOper,_aOpcoes)

Local _oBrowse 		:= FWMBrowse():New()
Private aRotina 	:= MenuDef() 
Private _nOper		:= aScan(_aOpcoes,{|X| AllTrim(X) == _cOper})
Private _cTableX	:= Alltrim(Substr(_cOper,1,3))
Private _cAlias	:= Iif(_nOper==1,"ZCF","SX5") 
Private _aFilOpt	:= {{"Produto"	,""				}	,;
 						{"Solução"	,"@R 9"			}	,;	
 						{"Linha"	,"@R 9.9"		}	,;	
 						{"Familia"	,"@R 9.9.9"		}	,;	
 						{"Categoria","@R 9.9.9.9"	}	,;	
 						{"Entidade"	,"@!"			} 	,;	
 						{"Unidade"	,"@!"			}	}	
//--
DbSelectArea(_cAlias)                                                                                  	
DbSetOrder(1)
(_cAlias)->(DbGoTop())
_oBrowse:SetAlias(_cAlias)
If _nOper > 1 
	//_oBrowse:SetFilterDefault("Substr(SX5->X5_FILIAL,1,4) == '"+Substr(Fwxfilial("SX5"),1,4)+"' .And. SX5->X5_TABELA == '"+_cTableX+"'" )
	_oBrowse:SetFilterDefault("SX5->X5_TABELA == '"+_cTableX+"'" )
Endif 
_oBrowse:SetDescription("Cadastros: "+Rtrim(_cOper))
//-- 
_oBrowse:lLocate	:= .t.
//-- Para nao apresentar a Palheta de Detalhes do Registro
_oBrowse:oBrowseUI:lDetails	:= .f.	 
//-- Nao Permite que o Usuario Modifique (Altere) registros de outras filiais
_oBrowse:SetChgAll(.f.) 			  
//-- Não Permite que o usuario Visualize registro de outras filiais
_oBrowse:SetSeeAll(.f.) 			 
//-- Ativa o browse
_oBrowse:Activate()
If _nOper > 1 
	(_cAlias)->(dbClearFilter())
Endif 
Return NIL

/*
--------------------------------------------------------------------------------
{Protheus.doc} <MenuDef>
Responsavel pela criacao dos botoes, alterar, incluir, excluir e demais.
@type function
@author Antonio Dantas 
@since 01/10/2015
@return array, array com as opções disponíveis no menu
-------------------------------------------------------------------------------
*/                                                                       
Static Function MenuDef()

Local cEstat := U_SFGN001A(ProcName(0), "SF7321X")
Local _aRotina 	:= {}
ADD OPTION _aRotina TITLE "Pesquisar"  	ACTION "PESQBRW"         	OPERATION 1 ACCESS 0
ADD OPTION _aRotina TITLE "Visualizar" 	ACTION "VIEWDEF.SF7321X" 	OPERATION 2 ACCESS 0
ADD OPTION _aRotina TITLE "Incluir"    	ACTION "VIEWDEF.SF7321X" 	OPERATION 3 ACCESS 0
ADD OPTION _aRotina TITLE "Alterar"    	ACTION "VIEWDEF.SF7321X" 	OPERATION 4 ACCESS 0
ADD OPTION _aRotina TITLE "Excluir"   	ACTION "VIEWDEF.SF7321X" 	OPERATION 5 ACCESS 0
ADD OPTION _aRotina TITLE "Imprimir"   	ACTION "VIEWDEF.SF7321X" 	OPERATION 8 ACCESS 0
ADD OPTION _aRotina TITLE "Copiar"     	ACTION "VIEWDEF.SF7321X" 	OPERATION 9 ACCESS 0

Return _aRotina

/*
--------------------------------------------------------------------------------
{Protheus.doc} <ModelDef>
   Contem a construcao e a definicao do Model, lembrando que o Modelo de
   dados (Model) contem as regras de negocio.
@type function
@author Antonio Dantas 
@since 01/10/2015
@return object, Objeto com a definição do Model
-------------------------------------------------------------------------------
*/  
Static Function ModelDef()

Local cEstat := U_SFGN001A(ProcName(0), "SF7321X")
Local _oModel	:= Nil 
Local _oStTMP	:= FWFormStruct(1,_cAlias)
//Cria gatilhos de preenchimento quando tabela SX5
If _nOper <> 1
	aAux := FwStruTrigger(;
		'X5_DESCRI' ,;
		'X5_DESCSPA' ,;
		'M->X5_DESCRI',;
		.F.,;
	)
	_oStTMP:AddTrigger(;
		aAux[1] , ; // [01] identificador (ID) do campo de origem
		aAux[2] , ; // [02] identificador (ID) do campo de destino
		aAux[3] , ; // [03] Bloco de código de validação da execução do gatilho
		aAux[4] ;	// [04] Bloco de código de execução do gatilho
	)
	aAux := FwStruTrigger(;
		'X5_DESCRI' ,;
		'X5_DESCENG' ,;
		'M->X5_DESCRI',;
		.F.,;
	)
	_oStTMP:AddTrigger(;
		aAux[1] , ; // [01] identificador (ID) do campo de origem
		aAux[2] , ; // [02] identificador (ID) do campo de destino
		aAux[3] , ; // [03] Bloco de código de validação da execução do gatilho
		aAux[4] ;	// [04] Bloco de código de execução do gatilho
	)
EndIf
//-- Objeto de modelo de dados
_oModel := MPFormModel():New("SF7321XX")
//-- A  estrutura  do  modelo  de  dados  (Model) 
_oModel:AddFields("TMPMASTER",/*cOwner*/,_oStTMP)
//-- Chave Primaria
If _nOper == 1
	_oModel:SetPrimaryKey({"ZCF_FILIAL","ZCF_PRODUT"})
Else
	_oModel:SetPrimaryKey({"X5_FILIAL","X5_TABELA","X5_CHAVE"})
	//-- Torna os campos de preenchimento OBRIGATORIO
	_oStTMP:SetProperty("X5_CHAVE"	,MODEL_FIELD_OBRIGAT	,.t.)
	_oStTMP:SetProperty("X5_DESCRI"	,MODEL_FIELD_OBRIGAT	,.t.)
	_oStTMP:SetProperty( 'X5_TABELA' , MODEL_FIELD_INIT, {|| _cTableX})
EndIf 
Return _oModel

/*
--------------------------------------------------------------------------------
{Protheus.doc} <ViewDef>
Contem a construcao e a definicao da View, ou seja, contrucao da interface
@author Antonio Dantas 
@since 01/10/2015
@return object, objeto com a definição da view
-------------------------------------------------------------------------------
*/  
Static Function ViewDef()

Local aAux		:= {}
Local cEstat 	:= U_SFGN001A(ProcName(0), "SF7321X")
Local _oModel 	:= FWLoadModel("SF7321X")
Local _oStTMPV	:= FWFormStruct(2,_cAlias) 
Local _oView   	:= FWFormView():New()
//-- Defino modelo da view
_oView:SetModel(_oModel)
//-- Mesma regra do modeldef
_oView:AddField("VW_TMP",_oStTMPV,"TMPMASTER")
//-- Crio a tela
_oView:CreateHorizontalBox("CADASTRO",100)
//-- Defino owner da tela
_oView:SetOwnerView("VW_TMP","CADASTRO")
If _nOper != 1
	//-- Modifica o Titulo "Label" e a PICT do Campo X5_CHAVE 
	_oStTMPV:SetProperty("X5_CHAVE"	,MVC_VIEW_TITULO		,_aFilOpt[_nOper,1])
	_oStTMPV:SetProperty("X5_CHAVE"	,MVC_VIEW_PICT			,_aFilOpt[_nOper,2])
	//-- Remove os campos do FormeVIEW
	_oStTMPV:RemoveField("X5_TABELA")
	_oStTMPV:RemoveField("X5_DESCSPA")
	_oStTMPV:RemoveField("X5_DESCENG") 
Endif 
Return _oView
