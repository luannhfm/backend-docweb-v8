#Include 'Protheus.ch'
#Include 'RwMake.ch'


/*/{Protheus.doc} MT340B2
@description Ajuste de inventário
@type User Function
@author Leonardo Soncin
@since 06/12/2011
@version 11.0
@see http://tdn.totvs.com/pages/releaseview.action?pageId=6087770
@history 11/02/2019, Alan Teles de Oliveira, reestruturação do fonte e aproveito a data passada no parametro MV_PAR01 para posicionar na tabela SB7.
/*/
User Function MT340B2()

	Local aArea 	:= GetArea()
	Local dDataSB7 	:= MV_PAR01

	dbSelectArea('SB7')
		SB7->(dbGoTop())
		SB7->(dbSetOrder(1)) //B7_FILIAL+DTOS(B7_DATA)+B7_COD+B7_LOCAL+B7_LOCALIZ+B7_NUMSERI+B7_LOTECTL+B7_NUMLOTE+B7_CONTAGE

	dbSelectArea('SZK')
		SZK->(dbGoTop())
		SZK->(dbSetOrder(1)) //ZK_FILIAL+ZK_DOC

	SB7->(dbSeek(xFilial('SB7') + DtoS(dDataSB7)))

	While .not. SB7->(Eof()) .and. SB7->B7_FILIAL = xFilial('SB7') .and. SB7->B7_DATA = dDataSB7

		If SB7->B7_XORIGEM		$ 'SIESTA01'	.and.;
				SB7->B7_COD 	>= MV_PAR05 	.and.;
				SB7->B7_COD 	<= MV_PAR06 	.and.;
				SB7->B7_LOCAL 	>= MV_PAR07 	.and.;
				SB7->B7_LOCAL	<= MV_PAR08 	.and.;
				SB7->B7_DOC 	>= MV_PAR11 	.and.; 
				SB7->B7_DOC 	<= MV_PAR12

			If SZK->(dbSeek(xFilial('SZK') + SB7->B7_DOC))

				If SZK->ZK_STATUS <> '3'

					RecLock('SZK', .F.)

						SZK->ZK_STATUS := '3'

					SZK->(MsUnLock())

				Endif

			Endif

		Endif 

		SB7->(dbSkip())

	EndDo

	RestArea(aArea)

Return
