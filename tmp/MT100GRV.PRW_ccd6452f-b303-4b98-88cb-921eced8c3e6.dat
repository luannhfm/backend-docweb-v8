#Include "Protheus.ch"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ SF1100I  º Autor ³  Leonardo Soncin   º Data ³  12/09/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Ponto de entrada na inclusao do documento de entrada ou    º±±
±±º          ³ classificacao.                                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ CNI                                                        º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º14/09/11  ³ Alterado por Cadu para nao validar o banco/agencia/conta.  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

User Function MT100GRV
Local oDlg 		:= Nil
Local oBanco	:= Nil
Local oAgencia	:= Nil
Local oConta	:= Nil
Local lAltBco	:= GetNewPar("SI_ALTBCO",.T.)
Local nPosTes	:= aScan(aHeader,{|x| AllTrim(x[2])=='D1_TES'})
Local nX		:= 0
Local lDup		:= .F.
Local lIsExcl	:= PARAMIXB[1]

/*------------------------------------------------------------**
**           Variaveis para Atender Bens Duraveis             **
**------------------------------------------------------------**/
Local nPosItem  := aScan(aHeader,{|x| AllTrim(x[2])=='D1_ITEM'   })
Local nPosProd  := aScan(aHeader,{|x| AllTrim(x[2])=='D1_COD'    })
Local nPosQuant := aScan(aHeader,{|x| AllTrim(x[2])=='D1_QUANT'  })
Local nPosVunit := aScan(aHeader,{|x| AllTrim(x[2])=='D1_VUNIT'  })
Local nPosAquis := aScan(aHeader,{|x| AllTrim(x[2])=='D1_EMISSAO'})
Local nY        := 0
Local nZ        := 0
Local nQtdBem   := 0    
Local cTesBem 	:= GetNewPar("MV_XTESBEN","001")
Local cDescBem  := " "
Local aCabBem   := {}

/*------------------------------------------------------------**
****************************************************************
**------------------------------------------------------------**/


// Despesa de Importacao
IF IsInCallStack("MATA119")
	Return()
ENDIF

Public __cBanco   := Criavar("E2_XBANCO")
Public __cAgencia := Criavar("E2_XAGENC")
Public __cDVAge   := Criavar("E2_XDVAGE")
Public __cConta   := Criavar("E2_XNUMCON")
Public __cDVCta   := Criavar("E2_XDVCTA")

If  l103Class
	__cBanco   := SF1->F1_XBANCO
	__cAgencia := SF1->F1_XAGENC
	__cDVAge   := SF1->F1_XDVAGE
	__cConta   := SF1->F1_XNUMCON
	__cDVCta   := SF1->F1_XDVCTA
Endif

If Empty(__cBanco)
	__cBanco   := SA2->A2_BANCO
	__cAgencia := SA2->A2_AGENCIA
	__cDVAge   := SA2->A2_XDVAGE
	__cConta   := SA2->A2_NUMCON
	__cDVCta   := SA2->A2_XDVCTA
Endif

For nX := 1 to Len(aCols)
	
	dbSelectArea("SF4")
	dbSetOrder(1)
	If dbSeek(xFilial("SF4")+aCols[nX][nPosTES])
		If SF4->F4_DUPLIC == "S"
			lDup := .T.
			Exit
		Endif
	Endif
	
Next nX

If lAltBco .and. lDup .and. inclui
	
	DEFINE MSDIALOG oDlg FROM 0,0 TO 90,670 PIXEL TITLE "Dados Bancários"
	
	@ 001,003 TO 040,330 LABEL "" OF oDlg PIXEL
	
	@ 10, 10 SAY "Banco:" SIZE 45,8 PIXEL OF oDlg
	@ 10, 30 MSGET oBanco VAR __cBanco PICTURE PesqPict("SE2","E2_XBANCO") F3 CpoRetF3('E2_XBANCO') SIZE 30,10 PIXEL OF oDlg
	
	@ 10, 70  SAY "Agencia:" SIZE 45,8 PIXEL OF oDlg
	@ 10, 95 MSGET oAgencia VAR __cAgencia PICTURE PesqPict("SE2","E2_XAGENC") F3 CpoRetF3('E2_XAGENC') SIZE 35,10 PIXEL OF oDlg

	@ 10, 140  SAY "DV Agencia:" SIZE 45,8 PIXEL OF oDlg
	@ 10, 175 MSGET oDVAge VAR __cDVAge PICTURE PesqPict("SE2","E2_XDVAGE") SIZE 15,10 PIXEL OF oDlg	
	
	@ 10, 200 SAY "Conta:" SIZE 45,8 PIXEL OF oDlg
	@ 10, 220 MSGET oConta VAR __cConta PICTURE PesqPict("SE2","E2_XNUMCON") F3 CpoRetF3('E2_XNUMCON') SIZE 35,10 PIXEL OF oDlg
	
	@ 10, 270 SAY "DV Conta:" SIZE 45,8 PIXEL OF oDlg
	@ 10, 300 MSGET oDVCta VAR __cDVCta PICTURE PesqPict("SE2","E2_XDVCTA") SIZE 15,10 PIXEL OF oDlg
    /*
	@ 10, 140 SAY "Conta:" SIZE 45,8 PIXEL OF oDlg
	@ 10, 160 MSGET oConta VAR __cConta PICTURE PesqPict("SE2","E2_XNUMCON") F3 CpoRetF3('E2_XNUMCON') SIZE 45,10 PIXEL OF oDlg
	*/
	DEFINE SBUTTON FROM 025,290 TYPE 1 OF oDlg ENABLE ONSTOP "OK" ACTION (oDlg:End())
	
	ACTIVATE MSDIALOG oDlg CENTER
	
EndIf

If .Not. lIsExcl
	//******************************************************************
	//					Criar o Vetor para Criação do BEM              * 
	//******************************************************************
	For nY := 1 to Len(aCols)
		If(cTESBem = aCols[nY][nPosTES] )
			    aCabBem := {}
			    cDescBem := Posicione("SB1",1,xFilial("SB1") + aCols[nY][nPosProd],"B1_DESC")
			    nQtdBem  := aCols[nY][nPosQuant]		    
				aAdd(aCabBem, {'ZEA_DESC'  , AllTrim(cDescBem)      })	
				aAdd(aCabBem, {'ZEA_DTAQUI', Date()                 })
				aAdd(aCabBem, {'ZEA_PRCAQS', aCols[nY][nPosVunit]   })
				aAdd(aCabBem, {'ZEA_DOC'   , cNFiscal   })
				aAdd(aCabBem, {'ZEA_SERIE' , cSerie   	})
				aAdd(aCabBem, {'ZEA_FORNEC', cA100For   })
				aAdd(aCabBem, {'ZEA_LOJA'  , cLoja    	})
				aAdd(aCabBem, {'ZEA_ITEM'  , aCols[nY][nPosItem]    })
				aAdd(aCabBem, {'ZEA_SITBEM', 'C'     				})
				aAdd(aCabBem, {'ZEA_CODPRO', aCols[nY][nPosProd]    })	
				
				For nZ := 1 to nQtdBem
				U_IMPM01S('ZEA',aCabBem,'SF0102S',3)
				Next nZ
			EndIf
	
	Next nY
EndIf


Return
