#Include 'Protheus.ch'
#Include 'FwMVCDef.ch'

/*/{Protheus.doc} CN300PCMT
@description Ponto de entrada para atuações pré-commit.
@obs Validação para o numero do contrato de venda, para não permitir ser salvo um contrato de venda com o mesmo número.
@author Alan Teles de Olvieira
@since 31/01/2018
@version 12.1.17
@return lRet, Lógico, verdadeiro se permite a gravação do contrato
/*/
User Function CN300PCMT()

	Local aArea 	:= GetArea()
	Local cAlTMP	:= GetNextAlias()
	Local oModel  	:= ParamIXB[1]
	Local oModelCN9	:= Nil
	Local lRet		:= .T.

	/* Contrato de venda  e se trata de inclusão*/
	If IsInCallStack('CN300INVEN') .and. INCLUI  

		oModelCN9 := oModel:GetModel("CN9MASTER")

		If Select(cAlTMP) > 0
			(cAlTMP)->(DbCloseArea())
		EndIf

		BeginSQL Alias cAlTMP

			SELECT 
				MAX(CN9_XNROCV) AS ULTIMO 
			FROM %Table:CN9% CN9
			WHERE 
				CN9_FILIAL = %Exp:FwXFilial('CN9')%

		EndSQL

		//MemoWrite('C:\TEMP\CN100GRM.sql', GetLastQuery()[2])

		dbSelectArea(cAlTMP)
		(cAlTMP)->(DbGoTop())
		
		If .not. (cAlTMP)->(EoF())
			oModelCN9:SetValue('CN9_XNROCV', StrZero(Val((cAlTMP)->ULTIMO) + 1, TamSx3("CN9_XNROCV")[1])) 
		EndIf

		(cAlTMP)->(DbCloseArea())

	EndIf

	RestArea(aArea)

Return lRet
