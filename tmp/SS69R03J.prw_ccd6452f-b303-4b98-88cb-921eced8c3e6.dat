#Include 'Protheus.ch'
#Include 'TopConn.ch'

/*/{Protheus.doc} SS69R03J
Relatorio Serviços x Metas (GCT/CRM)

@type 		function
@author 	Jose Leite de Barros Neto
@since 	12/04/2016
@version 	1.0
@return 	Nil, Nulo
/*/
User Function SS69R03J()
	
	Local _oReport		:= Nil
	Local _cPerg 		:= FunName()
	
	If TRepInUse()
		fCriaSx1( _cPerg )
		If Pergunte( _cPerg )
			_oReport := ReportDef( _cPerg )
			_oReport:PrintDialog()
		EndIf
	EndIf

Return


/** {Protheus.doc} ReportDef
Funcao que define as colunas

@author: 	Jose Leite de Barros Neto
@since: 	04/04/2016
@Uso: 		SFIEMT
*/
Static Function ReportDef(p_cPerg)
	
	Local oReport	:= Nil
	Local oSection	:= Nil
	Local oBreak		:= Nil
	Local cTitle 	:= "Relatorio Serviços x Metas"  
	
	oReport := TReport():New(p_cPerg,cTitle,p_cPerg,{|oReport| PrintReport(oReport)},cTitle)
	oReport:SetLandScape()
	
	oSection:= TRSection():New(oReport,OemToAnsi(""),{"CN9","ADY","ADZ","SCT"})
	
	oMes:= TRCell():New(oSection, "_cMes","TRA", "Mes")
	oMes:SetSize(12)
	
	oUndExec:= TRCell():New(oSection, "_cUndExe","TRA", "Unid. Executora")
	oUndExec:SetSize(15)
	
	oVendedor:= TRCell():New(oSection, "_cVended","TRA", "Consultor")
	oVendedor:SetSize(60)
	
	oProdut:= TRCell():New(oSection, "_cProdut","TRA", "Serviço(s)")
	oProdut:SetSize(50)
	
	oQtdVend:= TRCell():New(oSection, "_nQtdVend","TRA", "Qtde. de Serviço")
	oQtdVend:SetSize(12)
	
	oMetaV:= TRCell():New(oSection, "_nMetaV","TRA", "Meta")
	oMetaV:SetSize(12)
	
	oTipo:= TRCell():New(oSection, "_cTipo","TRA", "Tipo")
	oTipo:SetSize(12)
	
	oGrupo:= TRCell():New(oSection, "_cGrupo","TRA", "Grupo")
	oGrupo:SetSize(12)
	
	oBreak := TRBreak():New(oSection, oSection:Cell("_cMes"), "",.F.)
	
Return( oReport )


/** {Protheus.doc} ReportDef
Funcao que gera o relatorio

@author: 	Jose Leite de Barros Neto
@since: 	04/04/2016
@Uso: 		SFIEMT
*/
Static Function PrintReport(oReport)
	
	Local oSection 	:= oReport:Section(1)
	Local _cQuery 	:= ""
	Local _cUndExe	:= ""
	Local _cVended	:= ""
	Local _cProdut	:= ""
	Local _nQtdVend	:= 0
	Local _nMetaV	:= 0
	Local _cTipo		:= ""
	Local _cGrupo	:= ""
	Local _cMetaMes	:= ""
	Local _cMes		:= ""
	
	If Select("TRA") > 0
		DbSelectarea("TRA")
		DbCloseArea()
	EndIf
		
	_cQuery := " SELECT ADZ_XUNEXE, ADY_VEND, ADZ_PRODUT, SUBSTR(CN9_DTINIC,1,6) CN9_DTINIC,										" 
	_cQuery += CRLF + "	SUM(ADZ_QTDVEN) ADZ_QTDVEN, MAX(COALESCE(CT_QUANT,0)) CT_QUANT												"		
	_cQuery += CRLF + "FROM  " + RetSqlName("CN9") + " CN9 																					"
	_cQuery += CRLF + " INNER JOIN " + RetSqlName("ADY") + " ADY ON 	ADY.ADY_FILIAL = CN9.CN9_FILIAL								"
	_cQuery += CRLF + " 															AND ADY.ADY_OPORTU = CN9_XOPORT								"
	_cQuery += CRLF + "                                   				AND ADY.ADY_REVISA = CN9_XREVOP								"
	_cQuery += CRLF + "                                   				AND ADY.D_E_L_E_T_ <> '*'									"
	_cQuery += CRLF + " INNER JOIN " + RetSqlName("ADZ") + " ADZ ON 	ADZ.ADZ_FILIAL = ADY.ADY_FILIAL								"
   _cQuery += CRLF + "                                					AND ADZ.ADZ_PROPOS = ADY.ADY_PROPOS						"
   _cQuery += CRLF + "                                					AND ADZ.ADZ_REVISA = ADY.ADY_PREVIS						"
   _cQuery += CRLF + "                                					AND ADZ.D_E_L_E_T_ <> '*'									"
	_cQuery += CRLF + " LEFT JOIN " + RetSqlName("SCT") + " SCT ON SCT.CT_FILIAL = '"+ xFilial('SCT') +"' 						"
   _cQuery += CRLF + "                                					AND SUBSTR(SCT.CT_DATA,1,6) = SUBSTR(CN9_DTINIC,1,6)	"
   _cQuery += CRLF + "                                					AND SCT.CT_VEND = ADY.ADY_VEND								"
   _cQuery += CRLF + "                                					AND SCT.CT_PRODUTO = ADZ.ADZ_PRODUT						"
   _cQuery += CRLF + "                                					AND SCT.D_E_L_E_T_ <> '*'									"
	_cQuery += CRLF + "WHERE																															"
	_cQuery += CRLF + "	SUBSTR(CN9_DTINIC,1,4) = '"+ MV_PAR01 +"'																		"
	_cQuery += CRLF + "	AND SUBSTR(CN9_DTINIC,5,2) >= '"+ MV_PAR02 +"'																	"
	_cQuery += CRLF + "	AND SUBSTR(CN9_DTINIC,5,2) <= '"+ MV_PAR03 +"'																	"
	_cQuery += CRLF + "	AND CN9.D_E_L_E_T_ <> '*'																							"	
	_cQuery += CRLF + "	AND ADZ_XUNEXE 	BETWEEN '"+ MV_PAR04 +"'  AND '"+ MV_PAR05 +"'												"
	_cQuery += CRLF + "	AND ADY_VEND 	BETWEEN '"+ MV_PAR06 + "' AND '"+ MV_PAR07 +"'												"
	
	If .Not. Empty(Alltrim(MV_PAR08))
		_cQuery += CRLF + " AND CN9.CN9_SITUAC IN "+ FormatIn(AllTrim(MV_PAR08),";")
	Endif
				
	_cQuery += CRLF + "GROUP	BY	ADZ_XUNEXE, ADY_VEND, ADZ_PRODUT, SUBSTR(CN9_DTINIC,1,6) 											"
	_cQuery += CRLF + "ORDER BY ADZ_XUNEXE, SUBSTR(CN9_DTINIC,1,6), ADY_VEND, ADZ_PRODUT											"
	
	TCQUERY _cQuery NEW ALIAS "TRA"
		
	DbSelectArea("TRA")
	TRA->(DbGoTop())
		
	oReport:SetMeter(RecCount())
	oSection:Init()
		
	While .Not. TRA->( EOF() )
		
		If oReport:Cancel()
			Exit
		EndIf
		
		If _cMetaMes <> SubStr(TRA->CN9_DTINIC,5,2)
			
			_cMes 		:= ''
			_cMetaMes	:= SubStr(TRA->CN9_DTINIC,5,2)
			
			Do Case
				Case _cMetaMes == '01'
					_cMes := 'Janeiro'
				Case _cMetaMes == '02'
					_cMes := 'Fevereiro'
				Case _cMetaMes == '03'
					_cMes := 'Março'
				Case _cMetaMes == '04'
					_cMes := 'Abril'
				Case _cMetaMes == '05'
					_cMes := 'Maio'
				Case _cMetaMes == '06'
					_cMes := 'Junho'
				Case _cMetaMes == '07'
					_cMes := 'Julho'
				Case _cMetaMes == '08'
					_cMes := 'Agosto'
				Case _cMetaMes == '09'
					_cMes := 'Setembro'
				Case _cMetaMes == '10'
					_cMes := 'Outubro'
				Case _cMetaMes == '11'
					_cMes := 'Novembro'
				Case _cMetaMes == '12'
					_cMes := 'Dezembro'
			End Case
			
		EndIf
		
		_cUndExe	:= AllTrim(TRA->ADZ_XUNEXE)
		_cVended	:= AllTrim(Posicione("SA3",1,xFilial("SA3")+TRA->ADY_VEND,"A3_NOME"))
		_cProdut	:= AllTrim(Posicione("SB1",1,xFilial("SB1")+TRA->ADZ_PRODUT,"B1_DESC"))
		_nQtdVend	:= TRA->ADZ_QTDVEN
		_nMetaV	:= TRA->CT_QUANT
		_cTipo		:= AllTrim(Posicione("SB1",1,xFilial("SB1")+TRA->ADZ_PRODUT,"B1_TIPO"))
		_cGrupo	:= AllTrim(Posicione("SB1",1,xFilial("SB1")+TRA->ADZ_PRODUT,"B1_GRUPO"))
		
		oSection:Cell("_cUndExe"):SetValue(_cUndExe)
		oSection:Cell("_cVended"):SetValue(_cVended)
		oSection:Cell("_cProdut"):SetValue(_cProdut)
		oSection:Cell("_nQtdVend"):SetValue(_nQtdVend)
		oSection:Cell("_nMetaV"):SetValue(_nMetaV)
		oSection:Cell("_cTipo"):SetValue(_cTipo)
		oSection:Cell("_cGrupo"):SetValue(_cGrupo)
		oSection:Cell("_cMes"):SetValue(_cMes)
		
		oSection:PrintLine()
		oReport:IncMeter()
			
		TRA->( DbSkip() )
	End
		
	TRA->(DbCloseArea())
	oSection:Finish()
	
Return( Nil )


/** {Protheus.doc} fCriaSx1
Funcao para criacao das perguntas

@param: 	<Nil>
@author 	Jose Leite de Barros Neto
@since 	04/04/2016
@Uso: 		SFIEMT
*/
Static Function fCriaSx1( p_cPerg )
	
	Local aHelp := {}

	AAdd(aHelp, {{"Informe o Ano"								}, {""}, {""}})
	AAdd(aHelp, {{"Informe o Mes Inicial"						}, {""}, {""}})
	AAdd(aHelp, {{"Informe o Mes Final"						}, {""}, {""}})
	AAdd(aHelp, {{"Informe a Unidade Executora Inicial"	}, {""}, {""}})
	AAdd(aHelp, {{"Informe a Unidade Executora Final"		}, {""}, {""}})
	AAdd(aHelp, {{"Informe o Consultor Inicial"				}, {""}, {""}})
	AAdd(aHelp, {{"Informe o Consultor Inicial"				}, {""}, {""}})
	AAdd(aHelp, {{"Informe a Situação do Contrato"			}, {""}, {""}})
	
	u_SFPUTSX1( p_cPerg, "01","Ano?							"	,"","","mv_ch1","C",4						 		,0,0,"G",""						,""			,"","","mv_par01","","","","","","","","","","","","","","","","",aHelp[1,1],aHelp[1,2],aHelp[1,3],"")
	u_SFPUTSX1( p_cPerg, "02","Mes Inicial?				"	,"","","mv_ch2","C",2						 		,0,0,"G",""						,""			,"","","mv_par02","","","","","","","","","","","","","","","","",aHelp[2,1],aHelp[2,2],aHelp[2,3],"")
	u_SFPUTSX1( p_cPerg, "03","Mes Final?					"	,"","","mv_ch3","C",2						 		,0,0,"G",""						,""			,"","","mv_par03","","","","","","","","","","","","","","","","",aHelp[3,1],aHelp[3,2],aHelp[3,3],"")
	u_SFPUTSX1( p_cPerg, "04","Unidade Executora de?	"  ,"","","mv_ch4","C",TamSX3("ADZ_XUNEXE")[1]	,0,0,"G",""						,"SM0"		,"","","mv_par04","","","","","","","","","","","","","","","","",aHelp[4,1],aHelp[4,2],aHelp[4,3],"")
	u_SFPUTSX1( p_cPerg, "05","Unidade Executora Ate?	"  ,"","","mv_ch5","C",TamSX3("ADZ_XUNEXE")[1]	,0,0,"G",""						,"SM0"		,"","","mv_par05","","","","","","","","","","","","","","","","",aHelp[5,1],aHelp[5,2],aHelp[5,3],"")	
	u_SFPUTSX1( p_cPerg, "06","Consultor de?				"  ,"","","mv_ch6","C",TamSX3("ADY_VEND")[1]	,0,0,"G",""						,"SA3"		,"","","mv_par06","","","","","","","","","","","","","","","","",aHelp[6,1],aHelp[6,2],aHelp[6,3],"")
	u_SFPUTSX1( p_cPerg, "07","Consultor Ate?				"  ,"","","mv_ch7","C",TamSX3("ADY_VEND")[1]	,0,0,"G",""						,"SA3"		,"","","mv_par07","","","","","","","","","","","","","","","","",aHelp[7,1],aHelp[7,2],aHelp[7,3],"")
	u_SFPUTSX1( p_cPerg, "08","Situação do Contrato ?	"  ,"","","mv_ch8","C",99							,0,0,"G","u_MrkCN9()"			,""			,"","","mv_par08","","","","","","","","","","","","","","","","",aHelp[8,1],aHelp[8,2],aHelp[8,3],"")
	
Return( Nil )