#Include "Protheus.ch"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³SIFINA13  ºAutor  ³Thiago L. Dyonisio  º Data ³  18/09/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Transferencia bancária entre Empresas e Filiais            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ P11 - Sistema Indústria                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

User Function SIFINA13()
/* Variável para Montagem da tela */
Local oDlg
Local lOk := .F.
Local aButtons := {}
Private _cBkpFil := cFilAnt
Private bDados:= {|| SetKey( VK_F7, NIL ) , _FIN13Get() }

/* Variáveis para Dados de Origem */
Private _cFilialOr := CFilAnt //CFilAnt: Traz na variável a filial corrente
Private _cBancoOr  := Space(3)
Private _cAgenciOr := Space(5)
Private _cContaOr  := Space(10)
Private _cNatureOr := Space(10)
Private _cUoOr     := Space(TamSX3("E5_CCD")[1])
Private _cCrOr     := Space(TamSX3("E5_ITEMD")[1])

/* Variáveis para Dados de Destino */
Private _cFilDe    := Space(8)   // Variável de Filial Destino na segunda tela. Vai atualizar a variável _cFilialDe na primeira tela
Private _cFilialDe := Space(8)
Private _cBancoDe  := Space(3)
Private _cAgenciDe := Space(5)
Private _cContaDe  := Space(10)
Private _cNatureDe := Space(10)
Private _cUoDe     := Space(TamSX3("E5_CCD")[1])
Private _cCrDe     := Space(TamSX3("E5_ITEMD")[1])

/* Variáveis para Dados de Identificação */
Private _cNumerDoc := Space(10)
Private _nValor    := 0
Private _cHist     := Space(60)
Private _cBenefici := Space(60)

SetKey( VK_F7, bDados)

aadd(aButtons,{'NOTE',{ || _FIN13Get(), cFilAnt := _cBkpFil},'Dados de Destino'}) //Adiciona opção ao Ações Relacionadas

DEFINE MSDIALOG oDlg TITLE "Transferência Bancária Entre Empresas e Filiais " FROM 000,000 TO 350,670 PIXEL

/* Montagem da tela: Dados de Origem */
@035,003 TO 070,335 LABEL "Dados de Origem" OF oDlg PIXEL

@47,10 Say "Fil. Origem" Pixel Of oDlg
@57,10 MSGet _cFilialOr F3 "SM0" Size 40,8 Pixel Of oDlg When .F.

@47,55 Say "Banco" Pixel Of oDlg
@57,55 MSGet _cBancoOr F3 "SA6" Valid ExistCpo("SA6",_cBancoOr,).AND.!CCBlocked(_cBancoOr,_cAgenciOr,_cContaOr) Size 20,8 Pixel Of oDlg

@47,88 Say "Agencia" Pixel Of oDlg
@57,88 MSGet _cAgenciOr Size 20,8 Pixel Of oDlg When .F.

@47,121 Say "Conta" Pixel Of oDlg
@57,121 MSGet _cContaOr Size 40,8 Pixel Of oDlg When .F.

@47,164 Say "Natureza" Pixel Of oDlg
@57,164 MSGet _cNatureOr F3 "SED" Valid ExistCpo("SED",_cNatureOr) Size 40,8 Pixel Of oDlg   

@47,207 Say "UO" Pixel Of oDlg
@57,207 MSGet _cUoOr F3 "CTT" Valid ExistCpo("CTT",_cUoOr).AND.ctb105cc(_cUoOr) Size 40,8 Pixel Of oDlg

@47,250 Say "CR" Pixel Of oDlg
@57,250 MSGet _cCrOr F3 "CTD" Valid ExistCpo("CTD",_cCrOr).AND.ctb105item(_cCrOr) Size 80,8 Pixel Of oDlg

/* Montagem da tela: Dados de Destino */
@072,003 TO 107, 335 LABEL "Dados de Destino" OF oDlg PIXEL

@82,11 Say "Fil. Destino" Pixel Of oDlg
@92,11 MSGet _cFilialDe F3 "SM0EMP" Valid ExistCpo("SM0",cEmpAnt+ _cFilialDe) Size 40,8 Pixel Of oDlg When .F.

@82,55 Say "Banco" Pixel Of oDlg
@92,55 MSGet _cBancoDe Valid ExistCpo("SA6",_cBancoDe) Size 30,8 Pixel Of oDlg When .f.

@82,88 Say "Agencia" Pixel Of oDlg
@92,88 MSGet _cAgenciDe Size 20,8 Pixel Of oDlg When .f.

@82,121 Say "Conta" Pixel Of oDlg
@92,121 MSGet _cContaDe Size 40,8 Pixel Of oDlg When .f.

@82,164 Say "Natureza" Pixel Of oDlg
@92,164 MSGet _cNatureDe Valid ExistCpo("SED",_cNatureDe) Size 40,8 Pixel Of oDlg When .f.        

@82,207 Say "UO" Pixel Of oDlg
@92,207 MSGet _cUoDe Valid ExistCpo("CTT",_cUoDe) Size 40,8 Pixel Of oDlg When .F.

@82,250 Say "CR" Pixel Of oDlg
@92,250 MSGet _cCrDe Valid ExistCpo("CTD",_cCrDe) Size 80,8 Pixel Of oDlg When .F.

/* Montagem da tela: Dados de Identificação */
@110,003 TO 170, 335 LABEL "Dados de Identificação" OF oDlg PIXEL

@122,10 Say "Numero Doc" Pixel Of oDlg
@132,10 MSGet _cNumerDoc Size 60,8 Pixel Of oDlg

@122,73 Say "Valor" Pixel Of oDlg                                                                           
@132,73 MSGet _nValor Valid(positivo().and.naovazio()) Size 60,8 Picture PesqPict("SE5","E5_VALOR")Pixel Of oDlg

@122,137 Say "Beneficiário" Pixel Of oDlg
@132,137 MSGet _cBenefici Size 193,8 Pixel Of oDlg

@145,10 Say "Histórico" Pixel Of oDlg
@155,10 MSGet _cHist Size 320,8 Pixel Of oDlg

ACTIVATE MSDIALOG oDlg CENTERED ON INIT EnchoiceBar(oDlg,{|| IIF(_FIN13Vld1(),Eval({|| lOk := .T.,oDlg:End()}),lOk := .F.) },{|| oDlg:End()},,aButtons)

IF lOk // Confirmou
	MsgRun("Transferindo...",,{|| _FIN13Proc() })
ENDIF

SetKey(VK_F7, {|| NIL })

Return()

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³_FIN13Proc  ºThiago L. Dyonisio  º Data ³  22/09/2012       º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Processamento: Gera título a pagar na Empresa origem e     º±±
±±º          ³ título a receber na Empresa destino                        º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ CNI - SISTEMA INDÚSTRIA                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function _FIN13Proc()
Local lMsErroAuto := .f.
Local _aMovPag := {}
Local _aMovRec := {}

Begin Transaction

// Array com dados da baixa a pagar
Aadd(_aMovPag,{"E5_DATA",dDataBase,Nil})
Aadd(_aMovPag,{"E5_MOEDA","M1",Nil})
Aadd(_aMovPag,{"E5_VALOR",_nValor,Nil})
Aadd(_aMovPag,{"E5_NATUREZ",_cNatureOr,Nil})
Aadd(_aMovPag,{"E5_BANCO",_cBancoOr,Nil})
Aadd(_aMovPag,{"E5_AGENCIA",_cAgenciOr,Nil})
Aadd(_aMovPag,{"E5_CONTA",_cContaOr,Nil})
Aadd(_aMovPag,{"E5_CCD",_cUoOr,Nil})
Aadd(_aMovPag,{"E5_ITEMD",_cCrOr,Nil})
Aadd(_aMovPag,{"E5_BENEF",_cBenefici,Nil})
Aadd(_aMovPag,{"E5_HISTOR",_cHist,Nil})
Aadd(_aMovPag,{"E5_DOCUMEN",_cNumerDoc,Nil})

MSExecAuto({|x,y,z| FinA100(x,y,z)},0,_aMovPag,3)

If lMsErroAuto
	If __lSX8
		RollBackSX8()
	Endif
	
	MostraErro()
	
	DisarmTransaction()
	Break
ELSE
	If __lSX8
		ConfirmSX8()
	Endif
EndIf

cFilAnt := _cFilialDe

// Array com dados da baixa a receber
Aadd(_aMovRec,{"E5_DATA",dDataBase,Nil})
Aadd(_aMovRec,{"E5_MOEDA","M1",Nil})
Aadd(_aMovRec,{"E5_VALOR",_nValor,Nil})
Aadd(_aMovRec,{"E5_NATUREZ",_cNatureDe,Nil})
Aadd(_aMovRec,{"E5_BANCO",_cBancoDe,Nil})
Aadd(_aMovRec,{"E5_AGENCIA",_cAgenciDe,Nil})
Aadd(_aMovRec,{"E5_CONTA",_cContaDe,Nil})
Aadd(_aMovRec,{"E5_CCC",_cUoDe,Nil})
Aadd(_aMovRec,{"E5_ITEMC",_cCrDe,Nil})
Aadd(_aMovRec,{"E5_BENEF",_cBenefici,Nil})
Aadd(_aMovRec,{"E5_HISTOR",_cHist,Nil})
Aadd(_aMovRec,{"E5_DOCUMEN",_cNumerDoc,Nil})

MSExecAuto({|x,y,z| FinA100(x,y,z)},0,_aMovRec,4)

If lMsErroAuto
	If __lSX8
		RollBackSX8()
	Endif
	
	MostraErro()
	
	DisarmTransaction()
	Break
Else
	If __lSX8
		ConfirmSX8()
	Endif
ENDIF

End Transaction

Aviso("Atenção","Transferência realizada com sucesso!",{"Ok"})

// Restaura filial
cFilAnt := _cBkpFil

Return()

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³_FIN13Vld1  ºAutor  ³Thiago L. Dyonisio  º Data ³ 22/09/2012º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Funçao de Validação dos campos da primeira tela            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ CNI - SISTEMA INDÚSTRIA                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

//Validacao dos campos obrigatórios tela Dados de Origem

Static Function _FIN13Vld1()
Private _lVldRet1 := .t.

IF Empty(_cBancoOr) 
	Aviso("Atenção","Informe o Banco de Origem.",{"Ok"})
	_lVldRet1 := .f.
	Return(_lVldRet1)
EndIf	          

IF Empty(_cNatureOr)
	Alert("Informe a Natureza de Origem.")
	_lVldRet1 := .f.
	Return(_lVldRet1)
EndIf          

IF Empty(_cUoOr)                     
	Aviso("Atenção","Informe a UO de Origem.",{"Ok"})
	_lVldRet1 := .f.
	Return(_lVldRet1)
EndIf

IF Empty(_cCrOr)
	Aviso("Atenção","Informe a CR de Origem.",{"Ok"})
	_lVldRet1 := .f.
	Return(_lVldRet1)
EndIf 

//Valida dados de Destino na primeira tela
IF Empty(_cFilialDe)
	Aviso("Atenção","Informe a Filial de Destino.",{"Ok"})
	_lVldRet1 := .f.
	Return(_lVldRet1)
EndIf

IF Empty(_cBancoDe)
	Aviso("Atenção","Informe o Banco de Destino.",{"Ok"})
	_lVldRet1 := .f.
	Return(_lVldRet1)
EndIf               

IF Empty(_cNatureDe)
	Aviso("Atenção","Informe a Natureza de Destino.",{"Ok"})
	_lVldRet1 := .f.
	Return(_lVldRet1)
EndIf

IF Empty(_cUoDe)
	Aviso("Atenção","Informe a UO de Destino.",{"Ok"})
	_lVldRet1 := .f.
	Return(_lVldRet1)
EndIf

IF Empty(_cCrDe)
	Aviso("Atenção","Informe a CR de Destino.",{"Ok"})
	_lVldRet1 := .f.
	Return(_lVldRet1)
EndIf   
// fim validação de dados destino na primeira tela
IF Empty(_nValor)
	Aviso("Atenção","Informe a o valor da transferencia!",{"Ok"})
	_lVldRet1 := .f.
	Return(_lVldRet1)
EndIf

IF Aviso("Atenção","Confirma a Transferência?",{"Sim","Não"})<>1
	_lVldRet1 := .f.
	Return(_lVldRet1)
EndIf    

Return(_lVldRet1) 

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³_FIN13Vld2  ºAutor  ³Thiago L. Dyonisioº Data ³  24/09/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Validação dos campos da segunda tela com dados de destino  º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ CNI - SISTEMA INDÚSTRIA                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

//Validacao dos campos obrigatórios tela Dados de Destino

Static Function _FIN13Vld2()
Private _lVldRet2 := .t.

IF Empty(_cFilDe)
	Aviso("Atenção","Informe a Filial de Destino.",{"Ok"})
	_lVldRet2 := .f.
	Return(_lVldRet2)
EndIf

IF Empty(_cBancoDe)
	Aviso("Atenção","Informe o Banco de Destino.",{"Ok"})
	_lVldRet2 := .f.
	Return(_lVldRet2)
EndIf               

IF Empty(_cNatureDe)
	Aviso("Atenção","Informe a Natureza de Destino.",{"Ok"})
	_lVldRet2 := .f.
	Return(_lVldRet2)
EndIf

IF Empty(_cUoDe)
	Aviso("Atenção","Informe a UO de Destino.",{"Ok"})
	_lVldRet2 := .f.
	Return(_lVldRet2)
EndIf

IF Empty(_cCrDe)
	Aviso("Atenção","Informe a CR de Destino.",{"Ok"})
	_lVldRet2 := .f.
	Return(_lVldRet2)
EndIf      

Return(_lVldRet2) 
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³_FIN13Get ºAutor  ³Thiago L. Dyonisio  º Data ³  24/09/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Tela com dados de Destino                                  º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ CNI - SISTEMA INDÚSTRIA                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function _FIN13Get()      

	Local oDlgGet
	Local nLinha := 32
						
	_cFilDe    := Space(8)   // Variável de Filial Destino na segunda tela. Vai atualizar a variável _cFilialDe na primeira tela
	_cFilialDe := Space(8)
	_cBancoDe  := Space(3)
	_cAgenciDe := Space(5)
	_cContaDe  := Space(10)
	_cNatureDe := Space(10)
	_cUoDe     := Space(TamSX3("E5_CCD")[1])
	_cCrDe     := Space(TamSX3("E5_ITEMD")[1])

	DEFINE MSDIALOG oDlgGet TITLE "Transferência - Dados de Destino" FROM 000,000 TO 155,670 PIXEL
	oDlgGet:lEscClose := .F.

	/* Montagem da tela: Dados de Destino */
	@nLinha, 003 TO 070, 335 LABEL "Dados de Destino" OF oDlgGet PIXEL   
	
	nLinha += 10
	
	@nLinha, 010 Say "Fil. Destino"	Pixel Of oDlgGet
	@nLinha, 055 Say "Banco" 		Pixel Of oDlgGet
	@nLinha, 088 Say "Agencia" 		Pixel Of oDlgGet
	@nLinha, 121 Say "Conta" 		Pixel Of oDlgGet
	@nLinha, 164 Say "Natureza" 	Pixel Of oDlgGet
	@nLinha, 207 Say "UO" 			Pixel Of oDlgGet
	@nLinha, 250 Say "CR" 			Pixel Of oDlgGet

	nLinha += 10

	@nLinha, 010 MSGet _cFilDe 		F3 "SM0EMP" Valid ExistCpo("SM0",cEmpAnt+ _cFilDe).AND._FIN13Fil() 							Size 40,8 Pixel Of oDlgGet  
	@nLinha, 055 MSGet _cBancoDe 	F3 "SA6" 	Valid ExistCpo("SA6",_cBancoDe).AND.!CCBlocked(_cBancoDe,_cAgenciDe,_cContaDe)	Size 20,8 Pixel Of oDlgGet  
	@nLinha, 088 MSGet _cAgenciDe																								Size 20,8 Pixel Of oDlgGet When .F.
	@nLinha, 121 MSGet _cContaDe 																								Size 40,8 Pixel Of oDlgGet When .F. 
	@nLinha, 164 MSGet _cNatureDe 	F3 "SED" 	Valid ExistCpo("SED",_cNatureDe) 												Size 40,8 Pixel Of oDlgGet    
	@nLinha, 207 MSGet _cUoDe 		F3 "CTT" 	Valid ExistCpo("CTT",_cUoDe).AND.ctb105cc(_cUoDe) 								Size 40,8 Pixel Of oDlgGet 
	@nLinha, 250 MSGet _cCrDe 		F3 "CTD" 	Valid ExistCpo("CTD",_cCrDe).AND.ctb105item(_cCrDe) 							Size 80,8 Pixel Of oDlgGet                                       

	ACTIVATE MSDIALOG oDlgGet CENTERED ON INIT EnchoiceBar(oDlgGet,{|| IIF(_FIN13Vld2(),Eval({|| oDlgGet:End()}),lOk := .F.)  },{|| oDlgGet:End()},,aButtons)

	_cFilialDe := _cFilDe
	cFilAnt    := _cBkpFil  
	SetKey( VK_F7, bDados)

Return()

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³_FIN13Fil  ºAutor  ³Thiago L. Dyonisio º Data ³  24/09/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Troca a Filial Corrente na tela de dados de destino         º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ CNI - SISTEMA INDÚSTRIA                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function _FIN13Fil()
Local lRet := .T.

If (SubStr(_cFilDe, 1, 2) != SubStr(cFilAnt, 1, 2))
	Aviso("Aviso", "Não é possível transferir movimentos bancários entre empresas.", {"Ok"}, 1)
	lRet := .F.
Endif

If (lRet)
	cFilAnt := _cFilDe	
Endif

Return(lRet)