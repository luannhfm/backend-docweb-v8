#include 'protheus.ch'
#include 'parmtype.ch'

/*/
@author: j2a.caiolima
@data: 10/12/2019
@descricao: Funcao para filtros adcionais
/*/
User Function SF06A37X()
	Local _aPergBkp := fBkpPerg()
	Local _cPerg := "SF06A37X"
	Local _cFiltro := ""
	Local _cFiltroSQL := ""
	Local _oBrowse := GetMBrowse()
	
	//Aviso( "VarInfo", VarInfo("Method _oBrowse", ClassMethArr(_oBrowse) , , .F. ) ,{"ok"} ,4,,,, .T.)
	//Aviso( "VarInfo", VarInfo("_oBrowse", _oBrowse , , .F. ) ,{"ok"} ,4,,,, .T.)
	
	fCriaSX1(_cPerg)
	If Pergunte(_cPerg, .T.)
		If !Empty(MV_PAR01)
			_cFiltro += " E1_XDGCART='"+Alltrim(MV_PAR01)+"' "
		EndIf
		
		If !Empty(MV_PAR02)
			If !Empty(_cFiltro)
				_cFiltro += " .AND. "
			EndIf
			_cFiltro += " E1_NUMBCO='"+Alltrim(MV_PAR02)+"' "
		EndIf
		
		If !Empty(MV_PAR03)
			If !Empty(_cFiltro)
				_cFiltro += " .AND. "
			EndIf
			_cFiltro += " E1_CLIENTE='"+Alltrim(MV_PAR03)+"' "
		EndIf
		
		//aLERT(_cFiltro)
		
		If !Empty(_cFiltro)
			_cFiltroSQL := StrTran(_cFiltro, ".AND.", "AND" )
			DbSelectArea("SE1")
			_nPos := AScan(_oBrowse:oFwFilter:aFilter , {|x| x[1] = "SF06A37X"  }  )
			If _nPos = 0
				_oBrowse:oFwFilter:AddLevel("SF06A37X", _cFiltro, _cFiltroSQL,, .F.,.F.,.T.)
				_nPos := Len(_oBrowse:oFwFilter:aFilter)
			Else
				_oBrowse:oFwFilter:aFilter[_nPos,2] := _cFiltro
				_oBrowse:oFwFilter:aFilter[_nPos,3] := _cFiltroSQL
			EndIf
			_oBrowse:oFwFilter:aCheckFil[_nPos] := .T.
			_oBrowse:oFwFilter:aFilter[_nPos,6] := .T.
			
			FWMsgRun( /* Obj_tela */ , {|oSay|  _oBrowse:oData:ExecuteFilter(_cFiltro,2), _oBrowse:ExecuteFilter() } , "Executando filtro" , _cFiltro )
			SE1->(dbGoTop())
		Else
			_nPos := AScan(_oBrowse:oFwFilter:aFilter , {|x| x[1] = "SF06A37X"  }  )
			If _nPos > 0
				_oBrowse:oFwFilter:aFilter[_nPos,6] := .F.
				_oBrowse:oFwFilter:aCheckFil[_nPos] := .F.
				DbSelectArea("SE1")
				SE1->(dbClearFilter())
				_oBrowse:CleanFilter()
				SE1->(dbGoTop())
			EndIf
		EndIf
	Else
		_nPos := AScan(_oBrowse:oFwFilter:aFilter , {|x| x[1] = "SF06A37X"  }  )
		If _nPos > 0
			_oBrowse:oFwFilter:aFilter[_nPos,6] := .F.
			_oBrowse:oFwFilter:aCheckFil[_nPos] := .F.
			DbSelectArea("SE1")
			SE1->(dbClearFilter())
			_oBrowse:CleanFilter()
			SE1->(dbGoTop())
		EndIf
	EndIf
	
	fRestPerg(_aPergBkp)
Return

/*/
@author: j2a.caiolima
@data: 10/12/2019
@descricao: função auxiliar para criar as perguntas utilizada por esse fonte
/*/
Static Function fCriaSX1(_cPerg)
	u_SFPutSx1( _cPerg, "01","4 Digitos?", "", "", "mv_ch1", "C",4, 0, 0, "G","", "", "", "", "mv_par01",;
	"", "", "", "", "", "", "", "", "", "", "", "", "", "", "", ""                      , , , , "")
	u_SFPutSx1( _cPerg, "02","Nosso num?", "", "", "mv_ch2", "C",TamSX3("E1_NUMBCO")[1], 0, 0, "G","", "", "", "", "mv_par02",;
	"", "", "", "", "", "", "", "", "", "", "", "", "", "", "", ""                      , , , , "")
	u_SFPutSx1( _cPerg, "03","Cliente?", "", "", "mv_ch3", "C",TamSX3("E1_CLIENTE")[1], 0, 0, "G","", "CLI", "", "", "mv_par03",;
	"", "", "", "", "", "", "", "", "", "", "", "", "", "", "", ""                      , , , , "")
Return nil

/*/
@author: j2a.caiolima
@data: 10/12/2019
@descricao: salva um backup das perguntas atuais em um array
/*/
Static Function fBkpPerg()
	Local _aRet := {}
	Local _nx := 0
	
	For _nx:= 1 to 60
		If Type("MV_PAR"+StrZero(_nx,2)) != 'U'
			Aadd(_aRet, &("MV_PAR"+StrZero(_nx,2)) )
		EndIf
	Next
Return(_aRet)

/*/
@author: j2a.caiolima
@data: 10/12/2019
@descricao: restaura as perguntas conforme backup feito anteriormente
/*/
Static Function fRestPerg(_aPergunta)
	Local _nx := 0
	For _nx:= 1 to Len(_aPergunta)
		&("MV_PAR"+StrZero(_nx,2)) := _aPergunta[_nx]
	Next
Return
