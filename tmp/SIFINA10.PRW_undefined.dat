#INCLUDE "protheus.ch"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ SIFINA10 º Autor ³ Leonardo Soncin    º Data ³  28/09/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Cadastro de Emprestimos                                    º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ CNI                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

User Function SIFINA10
Private cCadastro := "Empréstimos"
Private aRotina := { {"Pesquisar","AxPesqui",0,1} ,;
{"Visualizar","AxVisual",0,2} ,;
{"Incluir","U_SIFA10I",0,3} ,;
{"Consultar","U_SIFA10C",0,2} ,;
{"Reajustar","U_SIFA10R",0,4} ,;
{"Excluir","U_SIFA10E",0,5} }

Private cString := "SZG"

dbSelectArea("SZG")
dbSetOrder(1)

dbSelectArea(cString)
mBrowse( 6,1,22,75,cString)

Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ SIFA10I  ³ Autor ³ Leonardo Soncin       ³ Data ³ 28/09/11 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa para inclusao de emprestimos                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ SIFA10I(ExpC1,ExpN1,ExpN2)                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Numero da opcao selecionada                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIFINA10                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
User Function SIFA10I(cAlias,nReg,nOpc)
Local nOpca
Local cTudoOk   := "U_SIF02TOK()"
Local cTransact := Nil

cTransact := "U_SIGRVTIT()"

nOpca := AxInclui(cAlias,nReg,nOpc,,,,cTudoOk,,cTransact)

Return nOpca

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ SIFA10I  ³ Autor ³ Leonardo Soncin       ³ Data ³ 28/09/11 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa para inclusao de emprestimos                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ SIFA10I(ExpC1,ExpN1,ExpN2)                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Numero da opcao selecionada                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIFINA10                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

User Function SIGRVTIT()
Local lErro := .F.
Local aTits := {}
Local nOpc 	:= 3
Local nX 	:= 0
Private aTamParc := TamSX3("E1_PARCELA")
Private cParc     := "123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ0" // sequencia de parcelas a serem geradas
Private aParc := {}

Begin Transaction

lErro  := SIGRVCP(aTits,nOpc)  // Titulo a Pagar

// Calcular database para quando tiver carencia
dDataVenc := MsSomaMes(SZG->ZG_EMISSAO,SZG->ZG_CARENC)

aParc := Condicao(SZG->ZG_VALOR,SZG->ZG_COND,,dDataVenc)

// Calcula os Juros
xCalcJur()

If !lErro

	// ordena vencimentos
	aSort(aParc,,,{| x,y | x[1] < y[1]})
	
	cParc := If(aTamParc[1]>1,StrZero(0,aTamParc[1]),cParc)
	
	For nX := 1 to Len(aParc)
		
		cParc := If(aTamParc[1]>1,Soma1(cParc,aTamParc[1]),cParc)
		
		lErro := SIGRVCR(aTits,nOpc,nX)
		
		If lErro
			Exit
		Endif
	Next nX
	
Endif

If lErro
	DisarmTransaction()
Else
	EvalTrigger()
	Commit
EndIf

End Transaction


Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ SIFA10I  ³ Autor ³ Leonardo Soncin       ³ Data ³ 28/09/11 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa para inclusao de emprestimos                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ SIFA10I(ExpC1,ExpN1,ExpN2)                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Numero da opcao selecionada                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIFINA10                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function SIGRVCP(aTits,nOpc)

Local aVetSE2	:= {}
Local nI		:= 0
Private lMsErroAuto := .F.

If Len(aTits) = 0
	AADD( aVetSE2 , { "E2_PREFIXO"	, "EMP" 			, Nil })
	AADD( aVetSE2 , { "E2_NUM"		, SZG->ZG_NUMERO	, Nil })
	AADD( aVetSE2 , { "E2_TIPO"		, SZG->ZG_TIPOSE2	, Nil })
	AADD( aVetSE2 , { "E2_NATUREZ"	, SZG->ZG_NATSE2	, Nil })
	AADD( aVetSE2 , { "E2_FORNECE"	, SZG->ZG_TOMADOR	, Nil })
	AADD( aVetSE2 , { "E2_LOJA"		, SZG->ZG_TOMLOJA	, Nil })
	AADD( aVetSE2 , { "E2_EMISSAO"	, SZG->ZG_EMISSAO	, Nil })
	AADD( aVetSE2 , { "E2_VENCTO"	, SZG->ZG_EMISSAO	, Nil })
	AADD( aVetSE2 , { "E2_VALOR"	, SZG->ZG_VALOR		, Nil })
	AADD( aVetSE2 , { "E2_HIST"		, "Empréstimo Nr. "+SZG->ZG_NUMERO		, Nil })
	AADD( aVetSE2 , { "E2_CONTAD"	, SZG->ZG_CONTA		, Nil })
	AADD( aVetSE2 , { "E2_CCD"		, SZG->ZG_CC		, Nil })
	AADD( aVetSE2 , { "E2_ITEMD"	, SZG->ZG_ITEM		, Nil })
	AADD( aVetSE2 , { "E2_CLVLDB"	, SZG->ZG_CLVL		, Nil })
	AADD( aVetSE2 , { "E2_EC05DB"	, SZG->ZG_EC05DB	, Nil })
	AADD( aVetSE2 , { "E2_EC06DB"	, SZG->ZG_EC06DB	, Nil })
	AADD( aVetSE2 , { "E2_EC07DB"	, SZG->ZG_EC07DB	, Nil })
	AADD( aVetSE2 , { "E2_EC08DB"	, SZG->ZG_EC08DB	, Nil })
	AADD( aVetSE2 , { "E2_EC09DB"	, SZG->ZG_EC09DB	, Nil })
	AADD( aVetSE2 , { "E2_XEMP"		, SZG->ZG_NUMERO	, Nil })
	AADD( aVetSE2 , { "E2_ORIGEM"	, "SIFINA10"		, Nil })
	
Else
	
	AADD( aVetSE2 , { "E2_PREFIXO"	, aTits[1] 			, Nil })
	AADD( aVetSE2 , { "E2_NUM"		, aTits[2]			, Nil })
	AADD( aVetSE2 , { "E2_PARCELA"	, aTits[3]			, Nil })
	AADD( aVetSE2 , { "E2_TIPO"		, aTits[4]			, Nil })
	AADD( aVetSE2 , { "E2_FORNECE"	, aTits[5]			, Nil })
	AADD( aVetSE2 , { "E2_LOJA"		, aTits[6]			, Nil })
	
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Chamada da execauto FINA050                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Begin Transaction

MSExecAuto({|x,y,z| Fina050(x,y,z)},aVetSE2,,nOpc)

If lMsErroAuto
	DisarmTransaction()
	MostraErro()
Else
	EvalTrigger()
	Commit
EndIf

End Transaction

Return lMsErroAuto

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ SIFA10I  ³ Autor ³ Leonardo Soncin       ³ Data ³ 28/09/11 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa para inclusao de emprestimos                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ SIFA10I(ExpC1,ExpN1,ExpN2)                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Numero da opcao selecionada                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIFINA10                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function SIGRVCR(aTits,nOpc,nX)

Local aVetSE1	:= {}
Local nI		:= 0
Private lMsErroAuto := .F.

If Len(aTits) = 0
	AADD( aVetSE1 , { "E1_PREFIXO"	, "EMP" 			, Nil })
	AADD( aVetSE1 , { "E1_NUM"		, SZG->ZG_NUMERO	, Nil })
	AADD( aVetSE1 , { "E1_TIPO"		, SZG->ZG_TIPOSE1	, Nil })
	AADD( aVetSE1 , { "E1_PARCELA"	, If(aTamParc[1]>1,cParc,Substr(cParc,nX,1))				, Nil })
	AADD( aVetSE1 , { "E1_NATUREZ"	, SZG->ZG_NATSE1	, Nil })
	AADD( aVetSE1 , { "E1_CLIENTE"	, SZG->ZG_PAGADOR	, Nil })
	AADD( aVetSE1 , { "E1_LOJA"		, SZG->ZG_PAGLOJA	, Nil })
	AADD( aVetSE1 , { "E1_EMISSAO"	, SZG->ZG_EMISSAO	, Nil })
	AADD( aVetSE1 , { "E1_VENCTO"	, aParc[nX][1]		, Nil })
	AADD( aVetSE1 , { "E1_VALOR"	, aParc[nX][2]		, Nil })
	AADD( aVetSE1 , { "E1_HIST"		, "Empréstimo Nr. "+SZG->ZG_NUMERO	, Nil })
	AADD( aVetSE1 , { "E1_CREDIT"	, SZG->ZG_CONTA		, Nil })
	AADD( aVetSE1 , { "E1_CCC"		, SZG->ZG_CC		, Nil })
	AADD( aVetSE1 , { "E1_ITEMC"	, SZG->ZG_ITEM		, Nil })
	AADD( aVetSE1 , { "E1_CLVLCR"	, SZG->ZG_CLVL		, Nil })
	AADD( aVetSE1 , { "E1_EC05CR"	, SZG->ZG_EC05DB	, Nil })
	AADD( aVetSE1 , { "E1_EC06CR"	, SZG->ZG_EC06DB	, Nil })
	AADD( aVetSE1 , { "E1_EC07CR"	, SZG->ZG_EC07DB	, Nil })
	AADD( aVetSE1 , { "E1_EC08CR"	, SZG->ZG_EC08DB	, Nil })
	AADD( aVetSE1 , { "E1_EC09CR"	, SZG->ZG_EC09DB	, Nil })
	AADD( aVetSE1 , { "E1_XEMP"		, SZG->ZG_NUMERO	, Nil })
	AADD( aVetSE1 , { "E1_ORIGEM"	, "SIFINA10"		, Nil })
Else
	
	AADD( aVetSE1 , { "E1_PREFIXO"	, aTits[1]			, Nil })
	AADD( aVetSE1 , { "E1_NUM"		, aTits[2]			, Nil })
	AADD( aVetSE1 , { "E1_PARCELA"	, aTits[3]			, Nil })
	
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Chamada da execauto FINA040                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Begin Transaction

MSExecAuto({|x,y| Fina040(x,y)},aVetSE1,nOpc)

If lMsErroAuto
	DisarmTransaction()
	MostraErro()
Else
	EvalTrigger()
	Commit
EndIf

End Transaction

Return lMsErroAuto

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ SIFA10C  ³ Autor ³ Leonardo Soncin       ³ Data ³ 28/09/11 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa para consulta de emprestimos                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ SIFA10C(ExpC1,ExpN1,ExpN2)                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Numero da opcao selecionada                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIFINA10                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
User Function SIFA10C(cAlias,nReg,nOpc)

Local nX 	 := 0
Local nUsado 	 := 0

Local aButtons	:= {}
Local aCpoEnch 	:= {}
Local cAliasE 		:= cAlias
Local aAlterEnch	:= {}
Local aPos		:= {000,000,080,400}
Local nModelo	:= 3
Local lF3 		:= .F.
Local lMemoria	:= .T.
Local lColumn		:= .F.
Local caTela 		:= ""
Local lNoFolder	:= .F.
Local lProperty	:= .F.
Local aCpoGDa1       	:= {}
Local aCpoGDa2       	:= {}
Local aCpoGDa3       	:= {}
Local nSuperior    	:= 081
Local nEsquerda    	:= 000
Local nInferior    	:= 250
Local nDireita     	:= 400
Local cLinOk       	:= "AllwaysTrue"
Local cTudoOk      	:= "AllwaysTrue"
Local cIniCpos     	:= "ZH_DATA"
Local nFreeze      	:= 000
Local nMax         	:= 999
Local cFieldOk     	:= "AllwaysTrue"
Local cSuperDel     	:= ""
Local cDelOk        	:= "AllwaysFalse"
Local aHeader       	:= {}
Local aCols1         	:= {}
Local aHeader2       	:= {}
Local aCols2         	:= {}
Local aHeader3       	:= {}
Local aCols3         	:= {}
Local aAlterGDa1	:= {}
Local aAlterGDa2	:= {}
Local aAlterGDa3	:= {}

Local aTitles	 := {"A Pagar",;
"A Receber",;
"Correções"}

Private oDlg
Private oBrw1
Private oBrw2
Private oBrw3
Private oEnch
Private oFolder
Private aTELA[0][0]
Private aGETS[0]

DbSelectArea("SX3")
DbSetOrder(1)
DbSeek(cAliasE)

While !Eof() .And. SX3->X3_ARQUIVO == cAliasE
	If !(SX3->X3_CAMPO $ "ZG_FILIAL") .And. cNivel >= SX3->X3_NIVEL .And.;
		X3Uso(SX3->X3_USADO)
		AAdd(aCpoEnch,SX3->X3_CAMPO)
	EndIf
	DbSkip()
End

aAlterEnch := aClone(aCpoEnch)


// Monta GetDados Correcoes
DbSelectArea("SX3")
DbSetOrder(1)
MsSeek("SZH")

While !Eof() .And. SX3->X3_ARQUIVO == "SZH"
	If	!(AllTrim(SX3->X3_CAMPO) $ "ZH_FILIAL") .And. cNivel >= SX3->X3_NIVEL .And. X3Uso(SX3->X3_USADO)
		AAdd(aCpoGDa1,SX3->X3_CAMPO)
	EndIf
	DbSkip()
End

aAlterGDa1 := aClone(aCpoGDa1)

nUsado:=0
dbSelectArea("SX3")
dbSeek("SZH")
aHeader1:={}
While !Eof().And.(x3_arquivo=="SZH")
	If X3USO(x3_usado).And.cNivel>=x3_nivel .and. !(x3_campo $ "ZH_NUMERO ")
		nUsado:=nUsado+1
		Aadd(aHeader1,{ TRIM(x3_titulo), x3_campo, x3_picture,;
		x3_tamanho, x3_decimal,"AllwaysTrue()",;
		x3_usado, x3_tipo, x3_arquivo, x3_context } )
	Endif
	dbSkip()
End

aCols1:={}
dbSelectArea("SZH")
dbSetOrder(1)
dbSeek(xFilial("SZH")+SZG->ZG_NUMERO)
While !eof().and. ZH_FILIAL == xFilial("SZH") .and. ZH_NUMERO==SZG->ZG_NUMERO
	AADD(aCols1,Array(nUsado+1))
	For nX:=1 to nUsado
		aCols1[Len(aCols1),nX]:=FieldGet(FieldPos(aHeader1[nX,2]))
	Next
	aCols1[Len(aCols1),nUsado+1]:=.F.
	dbSkip()
End

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Montagem GetDados Contas a Pagar                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

DbSelectArea("SX3")
DbSetOrder(1)
MsSeek("SE2")

While !Eof() .And. SX3->X3_ARQUIVO == "SE2"
	If	!(AllTrim(SX3->X3_CAMPO) $ "E2_FILIAL") .And. cNivel >= SX3->X3_NIVEL .And. X3Uso(SX3->X3_USADO)
		AAdd(aCpoGDa2,SX3->X3_CAMPO)
	EndIf
	DbSkip()
End

aAlterGDa2 := aClone(aCpoGDa2)

nUsado:=0
dbSelectArea("SX3")
dbSeek("SE2")
aHeader2:={}
While !Eof().And.(x3_arquivo=="SE2")
	If X3USO(x3_usado).And.cNivel>=x3_nivel .or. x3_campo = "E2_SALDO"
		nUsado:=nUsado+1
		Aadd(aHeader2,{ TRIM(x3_titulo), x3_campo, x3_picture, x3_tamanho } )
	Endif
	dbSkip()
End

aCols2:={}
dbSelectArea("SE2")
dbOrderNickName("SISE202")
dbSeek(xFilial("SE2")+SZG->ZG_NUMERO)
While !eof().and.E2_FILIAL == xFilial("SE2") .and. E2_XEMP==SZG->ZG_NUMERO
	AADD(aCols2,Array(nUsado+1))
	For nX:=1 to nUsado
		aCols2[Len(aCols2),nX]:=FieldGet(FieldPos(aHeader2[nX,2]))
	Next
	aCols2[Len(aCols2),nUsado+1]:=.F.
	dbSkip()
End


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Montagem GetDados Contas a Receber                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

DbSelectArea("SX3")
DbSetOrder(1)
MsSeek("SE1")

While !Eof() .And. SX3->X3_ARQUIVO == "SE1"
	If	!(AllTrim(SX3->X3_CAMPO) $ "E1_FILIAL") .And. cNivel >= SX3->X3_NIVEL .And. X3Uso(SX3->X3_USADO)
		AAdd(aCpoGDa3,SX3->X3_CAMPO)
	EndIf
	DbSkip()
End

aAlterGDa3 := aClone(aCpoGDa3)

nUsado:=0
dbSelectArea("SX3")
dbSeek("SE1")
aHeader3:={}
While !Eof().And.(x3_arquivo=="SE1")
	If X3USO(x3_usado).And.cNivel>=x3_nivel .or. x3_campo = "E1_SALDO"
		nUsado:=nUsado+1
		Aadd(aHeader3,{ TRIM(x3_titulo), x3_campo, x3_picture, x3_tamanho } )
	Endif
	dbSkip()
End

aCols3:={}
dbSelectArea("SE1")
dbOrderNickName("SISE101")
dbSeek(xFilial("SE1")+SZG->ZG_NUMERO)
While !eof().and. E1_FILIAL == xFilial("SE1") .AND. E1_XEMP==SZG->ZG_NUMERO
	AADD(aCols3,Array(nUsado+1))
	For nX:=1 to nUsado
		aCols3[Len(aCols3),nX]:=FieldGet(FieldPos(aHeader3[nX,2]))
	Next
	aCols3[Len(aCols3),nUsado+1]:=.F.
	dbSkip()
End

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Montagem da Tela de Consulta                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

aObjects 	:= {}
aSizeAut	:= MsAdvSize()
Aadd( aObjects, { 100, 100, .T., .T. } )
Aadd( aObjects, { 100, 100, .T., .T. } )
Aadd( aObjects, { 100, 015, .T., .F. } )
aInfo		:= { aSizeAut[ 1 ], aSizeAut[ 2 ], aSizeAut[ 3 ], aSizeAut[ 4 ], 3, 3 }
aPosObj		:= MsObjSize( aInfo, aObjects )
aPosGet		:= MsObjGetPos(aSizeAut[ 3 ]-aSizeAut[ 1 ], 315, {{003, 033, 160, 200, 240, 265}} )

oDlg 	:= MSDIALOG():New(aSizeAut[7],000, aSizeAut[6],aSizeAut[5], cCadastro,,,,,,,,,.T.)

RegToMemory("SZG", If(nOpc==3,.T.,.F.))

oEnch := MsMGet():New(cAliasE, nReg,nOpc, /*aCRA*/, /*cLetra*/, /*cTexto*/,;
aCpoEnch,aPosObj[1],	aAlterEnch, nModelo, /*nColMens*/, /*cMensagem*/,;
/*cTudoOk*/, oDlg,lF3, lMemoria,lColumn,caTela,lNoFolder,lProperty)

oFolder := TFolder():New(aPosObj[2,1],aPosObj[2,2],aTitles,{"AHEADER"},oDlg,,,,.T.,.F.,aPosObj[2,4]-aPosObj[2,2],aPosObj[2,3]-aPosObj[2,1])

// Browse 1 // A Pagar
oBrw1:= TcBrowse():New(1,1,602,115, ,,,oFolder:aDialogs[1],,,,,{||},,,,,,,.F.,,.T.,,.F.,,, )
oBrw1:nScrollType := 1 // Scroll VCR

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carrega os dados																			³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oBrw1:SetArray(aCols2)

oBrw1:AddColumn(TcColumn():New(" ",{|| LoadBitmap(GetResources(), Iif(aCols2[oBrw1:nAt, aScan( aHeader2, { |x| Alltrim(x[2])=="E2_SALDO" } )]=0, "BR_VERMELHO",IIf(aCols2[oBrw1:nAt, aScan( aHeader2, { |x| Alltrim(x[2])=="E2_SALDO" } )]#aCols2[oBrw1:nAt, aScan( aHeader2, { |x| Alltrim(x[2])=="E2_VALOR" } )],"BR_AZUL","BR_VERDE")))},"",Nil,Nil,Nil,010,.T.,.T.,Nil,Nil,Nil,.T.,Nil))

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carrega o cabecalho																			³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nAlias := Select("SE2")
For nFor := 1 to Len(aHeader2)
	bBlock := "{ || aCols2[oBrw1:nAt, "+Str(nFor,4)+"] }"
	bBlock := &bBlock
	oBrw1:AddColumn(TcColumn():New(aHeader2[nFor,1],bBlock,aHeader2[nFor,3],Nil,Nil,Nil,IiF(Len(aHeader2[nFor,1]) > aHeader2[nFor,4],Len(aHeader2[nFor,1])*3.6,aHeader2[nFor,4]*3.6),.F.,.F.,Nil,Nil,Nil,.F.,Nil))
Next

// Browse 2 // Receber
oBrw2:= TcBrowse():New(1,1,602,115, ,,,oFolder:aDialogs[2],,,,,{||},,,,,,,.F.,,.T.,,.F.,,, )
oBrw2:nScrollType := 1 // Scroll VCR

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carrega os dados																			³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oBrw2:SetArray(aCols3)

oBrw2:AddColumn(TcColumn():New(" ",{|| LoadBitmap(GetResources(),  Iif(aCols3[oBrw2:nAt, aScan( aHeader3, { |x| Alltrim(x[2])=="E1_SALDO" } )]=0, "BR_VERMELHO",IIf(aCols3[oBrw2:nAt, aScan( aHeader3, { |x| Alltrim(x[2])=="E1_SALDO" } )]#aCols3[oBrw2:nAt, aScan( aHeader3, { |x| Alltrim(x[2])=="E1_VALOR" } )],"BR_AZUL","BR_VERDE")))},"",Nil,Nil,Nil,010,.T.,.T.,Nil,Nil,Nil,.T.,Nil))

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carrega o cabecalho																			³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nAlias := Select("SE1")
For nFor := 1 to Len(aHeader2)
	bBlock := "{ || aCols3[oBrw2:nAt, "+Str(nFor,4)+"] }"
	bBlock := &bBlock
	oBrw2:AddColumn(TcColumn():New(aHeader3[nFor,1],bBlock,aHeader3[nFor,3],Nil,Nil,Nil,IiF(Len(aHeader3[nFor,1]) > aHeader3[nFor,4],Len(aHeader3[nFor,1])*3.6,aHeader3[nFor,4]*3.6),.F.,.F.,Nil,Nil,Nil,.F.,Nil))
Next

// Browse 3 // Correcoes
oBrw3:= MsNewGetDados():New(1,1,aPosObj[2,3]-162,aPosObj[2,4]-5, nOpc,cLinOk, cTudoOk, cIniCpos, aAlterGDa1, nFreeze, nMax, cFieldOk, cSuperDel,cDelOk, oFolder:aDialogs[3], aHeader1, aCols1)

oDlg:bInit	:= {|| EnchoiceBar(oDlg, {||oDlg:End()}, {||oDlg:End()},,aButtons)}
oDlg:lCentered := .T.
oDlg:Activate()

Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ SIFA10R  ³ Autor ³ Leonardo Soncin       ³ Data ³ 28/09/11 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa para reajustar emprestimos                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ SIFA10R(ExpC1,ExpN1,ExpN2)                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Numero da opcao selecionada                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIFINA10                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
User Function SIFA10R(cAlias,nReg,nOpc)

//--- Ambiente
Local aArea	:= GetArea()

//--- Objetos da Dialog
Local oDlg
Local oChk
Local oInv
Local oDtDe
Local oDtAte
Local oMark

Local aCampos := {}
Local _cDataDe := Ctod("  /  /  ")
Local _cDataAte := Ctod("  /  /  ")
Local _cVencto  := Ctod("  /  /  ")
Local _nMoeda := 1

//--- MarkBrowse
Private oTax
Private _nTaxa   := 0
Private lInverte := .F.
Private cMarca   := GetMark()
Private lTodos   := .T.
Private lChang   := .T.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Campos visualizados no MarkBrowse ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aAdd( aCampos, { "E1_OK"		 ,, "" } )
aAdd( aCampos, { "E1_FILIAL"	 ,, "Filial" } )
aAdd( aCampos, { "E1_PREFIXO"	 ,, "Prefixo" } )
aAdd( aCampos, { "E1_NUM"	 	 ,, "Numero" } )
aAdd( aCampos, { "E1_PARCELA"	,, "Parcela" } )
aAdd( aCampos, { "E1_TIPO"		,, "Tipo" } )
aAdd( aCampos, { "E1_NATUREZ"	,, "Natureza" } )
aAdd( aCampos, { "E1_CLIENTE"	,, "Cliente" } )
aAdd( aCampos, { "E1_NOMCLI"	,, "Descrição" } )
aAdd( aCampos, { "E1_LOJA"		,, "Loja" } )
aAdd( aCampos, { "E1_VALOR"		,, "Valor" } )
aAdd( aCampos, { "E1_VENCTO"	,, "Vencimento" } )
aAdd( aCampos, { "E1_VENCREA"	,, "Vencimento Real" } )
aAdd( aCampos, { "E1_ACRESC"	,, "Acrescimo" } )

dbSelectArea("SE1")
SE1->(msFilter( "!Empty(E1_XEMP) .and. E1_SALDO > 0 "))

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta a Tela com MsSelect e Objetos de Check Box ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DEFINE FONT oFont NAME "Mono AS" SIZE 8,15 BOLD

DEFINE MSDIALOG oDlg TITLE "Atualização de Empréstimos" From 7,0 To 28,80

oPanel := TPanel():New(0,0,'',oDlg,, .T., .T.,, ,35,35,.T.,.T. )
oPanel:Align := CONTROL_ALIGN_TOP

@ C(007),C(005) Say "Data de: "  Size C(060),C(010) COLOR CLR_BLACK PIXEL OF oPanel
@ C(005),C(025) MsGet oDtDe  	   Var _cDataDe  Size C(039),C(007) COLOR CLR_BLACK Picture "@!"  PIXEL OF oPanel

@ C(007),C(070) Say "Data até: "  Size C(060),C(010) COLOR CLR_BLACK PIXEL OF oPanel
@ C(005),C(090) MsGet oDtAte  	  Var _cDataAte  Size C(039),C(007) COLOR CLR_BLACK Picture "@!"  PIXEL OF oPanel

@ C(017),C(005) Say "Moeda: "  Size C(020),C(010) COLOR CLR_BLACK PIXEL OF oPanel
@ C(015),C(025) MsGet oMoeda   Var _nMoeda  Size C(010),C(007) COLOR CLR_BLACK Valid SIF02Tx(_nMoeda,_cDataDe,_cDataAte) Picture X3Picture("E1_MOEDA")  PIXEL OF oPanel

@ C(017),C(040) Say "Taxa: "  Size C(060),C(010) COLOR CLR_BLACK PIXEL OF oPanel
@ C(015),C(055) MsGet oTax  Var _nTaxa  Size C(030),C(007) COLOR CLR_BLACK Picture X3Picture("E1_TXMOEDA")  PIXEL OF oPanel

@ C(017),C(090) Say "Novo Vencto: "  Size C(060),C(010) COLOR CLR_BLACK PIXEL OF oPanel
@ C(015),C(120) MsGet oVencto  	     Var _cVencto  Size C(039),C(007) COLOR CLR_BLACK Picture "@!"  PIXEL OF oPanel
oVencto:cTooltip := "Informe a nova data de vencimento dos títulos selecionados."

@ C(005),C(200) BUTTON "Atualizar" SIZE 50,24 ACTION (OKAtualiz(oMark,_cDatade,_cDataAte)) PIXEL OF oPanel

oMark := MsSelect():New( "SE1","E1_OK",,aCampos, @lInverte, @cMarca, { 48, 0, 150, 318 } )

oMark:oBrowse:lhasMark := .T.
oMark:oBrowse:bAllMark := {|| U_SIF02Inv( oMark ) }  // Na verdade, esse comando esta desabilitado pela instrucao de cima
oMark:oBrowse:Align := CONTROL_ALIGN_ALLCLIENT

ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar( oDlg, {||( nOpc := 1, oDlg:End() )}, {||(nOpc := 0, oDlg:End())},,) CENTERED

DeleteObject( oMark )
DeleteObject( oDlg )
DeleteObject( oChk )
DeleteObject( oInv )

If nOpc == 1
	
	dbSelectArea( "SE1" )
	dbGoTop()
	
	cEmprest := SE1->E1_XEMP
	_aEmps   := {}
	
	While !EOF()
		
		If SE1->E1_OK == cMarca
			nAcresc := SE1->E1_ACRESC + ((SE1->E1_SALDO * _nTaxa)/100)
			
			RecLock("SE1",.F.)
			SE1->E1_ACRESC  := nAcresc
			SE1->E1_SDACRES := nAcresc
			IF !Empty(_cVencto) // atualiza vencimento
				SE1->E1_VENCTO  := _cVencto
				SE1->E1_VENCREA := DataValida(_cVencto)
			ENDIF
			MsUnlock()
			
			dbSelectArea("SE1")
			dbSkip()
			
			If Ascan(_aEmps,cEmprest) == 0
				//Grava SZH
				dbSelectArea("SZH")
				RecLock("SZH",.T.)
				SZH->ZH_FILIAL := xFilial("SZH")
				SZH->ZH_NUMERO := cEmprest
				SZH->ZH_DATA   := dDatabase
				SZH->ZH_TAXA   := _nTaxa
				MsUnlock()
				Aadd(_aEmps,cEmprest)
			Endif
			
			cEmprest := SE1->E1_XEMP
			
		Else
			dbSelectArea("SE1")
			dbSkip()
			cEmprest := SE1->E1_XEMP
		EndIf
		
	Enddo
	
EndIf

dbSelectArea("SE1")
Set Filter To

RestArea(aArea)
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³SIFINA10  ºAutor  ³Microsiga           º Data ³  11/11/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Refresh de tela                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ P11 - Uso SIFINA10                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function OKAtualiz(oMark,_cDataDe,_cDataAte)

lInverte := .F.

If !Empty(_cDataDe) .and. !Empty(_cDataAte)
	SE1->(msFilter( "!Empty(E1_XEMP) .and. E1_SALDO > 0 .AND. DTOS(E1_VENCTO) >= '"+DtoS(_cDataDe)+"' .and. DTOS(E1_VENCTO) <= '"+dToS(_cDataAte)+"'" ) )
	oMark:oBrowse:Refresh()
Else
	MsgStop("Favor preencher os campos: Data de e Data até para realizar esta operação.")
Endif

Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ SIFA10R  ³ Autor ³ Leonardo Soncin       ³ Data ³ 28/09/11 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa para reajustar emprestimos                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ SIFA10R(ExpC1,ExpN1,ExpN2)                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Numero da opcao selecionada                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIFINA10                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
User Function SIFA10E(cAlias,nReg,nOpc)
Local nOpca
Local cTransact := Nil
Private cDelFunc  := Nil

cDelFunc := "U_SIDELTIT()"

nOpca := AxDeleta(cAlias,nReg,nOpc,cTransact)

Return nOpca

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ SIFA10R  ³ Autor ³ Leonardo Soncin       ³ Data ³ 28/09/11 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa para reajustar emprestimos                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ SIFA10R(ExpC1,ExpN1,ExpN2)                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Numero da opcao selecionada                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIFINA10                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

User Function SIDELTIT()

Local aTits:= {}
Local aItens:= {}
Local lErro := .F.

// Deleta Titulos a Pagar
dbSelectArea("SE2")
dbOrderNickName("SISE202")
dbSeek(xFilial("SE2")+SZG->ZG_NUMERO)

Begin Transaction

While !Eof() .and. SE2->E2_FILIAL == xFilial("SE2") .AND. SE2->E2_XEMP == SZG->ZG_NUMERO
	
	AADD( aTits , SE2->E2_PREFIXO	)
	AADD( aTits , SE2->E2_NUM	)
	AADD( aTits , SE2->E2_PARCELA)
	AADD( aTits , SE2->E2_TIPO	)
	AADD( aTits , SE2->E2_FORNECE	)
	AADD( aTits , SE2->E2_LOJA )
	AADD( aTits , SE2->E2_XEMP )
	
	lErro := SIGRVCP(aTits,5)
	
	If lErro
		Skip
	Endif
	
	aTits := {}
	
	dbSelectArea("SE2")
	dbSkip()
Enddo

aTits := {}

If !lErro
	// Deleta Titulos a Receber
	dbSelectArea("SE1")
	dbOrderNickName("SISE101")
	dbSeek(xFilial("SE1")+SZG->ZG_NUMERO)
	
	While !Eof() .and. SE1->E1_FILIAL == xFilial("SE1") .AND. SE1->E1_XEMP == SZG->ZG_NUMERO
		
		AADD( aItens , SE1->E1_PREFIXO	)
		AADD( aItens , SE1->E1_NUM	)
		AADD( aItens , SE1->E1_PARCELA)
		AADD( aItens , SE1->E1_TIPO	)
		
		AADD( aTits , aItens	)
		aItens := {}
		
		dbSelectArea("SE1")
		dbSkip()
	Enddo
Endif


// Deleta SE1
For nX := 1 to Len(aTits)
	lErro := SIGRVCR(aTits[nX],5,0)
	If lErro
		Exit
	Endif
Next nX

// Deleta SZH
If !lErro
	dbSelectArea("SZH")
	dbSetorder(1)
	dbSeek(xFilial("SZH")+SZG->ZG_NUMERO)
	
	While !Eof() .and. SZH->ZH_FILIAL == xFilial("SZH") .AND. SZH->ZH_NUMERO == SZG->ZG_NUMERO
		
		RecLock("SZH",.F.)
		dbDelete()
		MsUnlock()
		
		dbSkip()
	EndDo
	
Endif

If lErro
	DisarmTransaction()
Else
	EvalTrigger()
	Commit
EndIf

End Transaction

Return !lErro

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³ xCalcJur  ³ Autor ³ Leonardo Soncin      ³ Data ³ 10/10/11 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³ Calculo os Juros                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ MP11 - Especifico CNI                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function xCalcJur()

Local nY := 0
Local nX := 1
Local nZ := 1
Local nSldIni := 0
Local nCorrec := 0
Local nAmortz := 0
Local nVParce := 0
Local nTParc  := 0
Local nParcs  := 0
Local nJurCar := 0

nTParc := Len(aParc)
nParcs := nTParc

// Cacula Carencia
IF SZG->ZG_CARENC > 0
	nJurCar := xCalcCar()
	If SZG->ZG_TPJUROS == "2"
		nX := SZG->ZG_CARENC + 1
	Endif
Endif

For nY := nX to Len(aParc)
	
	If nZ == 1
		nSldIni := SZG->ZG_VALOR + nJurCar
	Else
		nSldIni := nAmortz - nVParce
	Endif
	
	nCorrec := Round((nSldIni * SZG->ZG_TAXA)/100,2)
	nAmortz := nSldIni + nCorrec
	nVParce := Round(nAmortz / nParcs,2)
	
	aParc[nY][2] := nVParce // Grava Valor da Parcela no array
	
	nParcs := nTparc - nZ
	nZ ++
	
Next nY

Return


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³ xCalcJur  ³ Autor ³ Leonardo Soncin      ³ Data ³ 10/10/11 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³ Calculo os Juros                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ MP11 - Especifico CNI                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function xCalcCar()
Local nY := 0
Local nSldIni := 0
Local nCorrec := 0
Local nAmortz := 0
Local nCaren  := SZG->ZG_CARENC
Local nJuros  := 0
Local dDataVenc := SZG->ZG_EMISSAO

For nY := 1 to nCaren
	
	If nY == 1
		nSldIni := SZG->ZG_VALOR
	Else
		nSldIni := nAmortz
	Endif
	
	nCorrec := Round((nSldIni * SZG->ZG_TAXA)/100,2)
	nAmortz := nSldIni + nCorrec
	
	If SZG->ZG_TPJUROS == "1"
		nJuros += nCorrec
	Else
		// Adicionar Parcelas no APARC
		AAdd(aParc, aParc[nY] )
		aParc[nY] := {dDataVenc,nCorrec,SZG->ZG_COND}
		dDataVenc := dDataVenc + 30
	Endif
	
Next nY

Return nJuros

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³ xCalcJur  ³ Autor ³ Leonardo Soncin      ³ Data ³ 10/10/11 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³ Calculo os Juros                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ MP11 - Especifico CNI                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

User Function XSIVLCAR()
Local lRet := .T.
Local aParcs := {}

If M->ZG_CARENC > 0 .and. !Empty(M->ZG_COND)
	
	aParcs := Condicao(100,M->ZG_COND)
	
	If M->ZG_CARENC >= Len(aParcs)
		Help(" ",1, "CARENCIA","Carência inválida","A carência deve ser inferior a quantidade de parcelas.",1,0 )
		lRet := .F.
	Endif
	
Endif

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³ SIF02Inv  ³ Autor ³ Stanko               ³ Data ³ 04/12/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³ Marca / Desmarca todas as Filiais                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ MP10 - Especifico OAS                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
User Function SIF02Inv( oMark )

Local nRec := SE1->( Recno() )

dbSelectArea("SE1")
dbGoTop()

While !EOF()
	
	RecLock( "SE1" , .F. )
	SE1->E1_OK := If( SE1->E1_OK == cMarca, "", cMarca )
	MsUnlock()
	
	dbSkip()
EndDo

SE1->( DbGoTo( nRec ) )

lInverte := !lInverte

oMark:oBrowse:Refresh()

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³SIFINA10  ºAutor  ³Microsiga           º Data ³  11/11/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Calcula taxa media                                         º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±³Uso       ³ MP11 - Especifico CNI                                      ³±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function SIF02Tx(_nMoeda,_cDataDe,_cDataAte)
Local _nRet  := 0
Local _cArea := GetArea()

IF _nMoeda > 1
	IF SM2->(FieldPos("M2_MOEDA"+Alltrim(Str(_nMoeda)))) == 0
		Return .f.
	ENDIF
	_cArqTRB := CriaTrab(nil,.f.)
	_cQuery  := "SELECT AVG(M2_MOEDA"+Alltrim(Str(_nMoeda))+") MEDIA FROM "+RetSqlName("SM2")+" WHERE D_E_L_E_T_ = ' ' "
	_cQuery  += "AND M2_DATA BETWEEN '"+Dtos(_cDataDe)+"' AND '"+Dtos(_cDataAte)+"'"
	_cQuery  := ChangeQuery(_cQuery)
	
	dbUseArea(.T.,"TOPCONN",TCGenQry(,,_cQuery),_cArqTRB,.t.,.t.)
	
	_nRet := (_cArqTRB)->MEDIA
	
	(_cArqTRB)->(dbCloseArea())
	fErase(_cArqTRB+OrdBagExt())
ENDIF

_nTaxa := _nRet
oTax:Refresh()

RestArea(_cArea)
Return(.t.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³SIFINA10  ºAutor  ³Microsiga           º Data ³  12/01/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Validacao TudoOk                                           º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±³Uso       ³ MP11 - Especifico CNI                                      ³±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

User Function SIF02TOK()
Local _lRet := .t.

IF M->ZG_TAXA <= 0 .and. M->ZG_TPJUROS <> "1"
	MsgStop("Para empréstimos sem taxa deve ser utilizado tipo de juros igual a NORMAL.")
	_lRet := .f.
ENDIF

Return(_lRet)
