#Include 'Protheus.ch'
#Include 'FWMVCDEF.ch'

/*/{Protheus.doc} SF7304J
Cadastro Blocos x Cargos x Entidades

@author 	Jose Leite de Barros Neto	
@since 	22/06/2016
@version 	1.0

@return Nil, Nulo
/*/
User Function SF7304J()

	Local oBrowse

	oBrowse := FWMBrowse():New()
	oBrowse:SetAlias('ZC5')
	oBrowse:SetDescription('Blocos x Cargos x Entidades')
	oBrowse:Activate()

Return( Nil )


/** {Protheus.doc} MenuDef
Funcao para Montar o Menu

@author: 	Jose Leite de Barros Neto
@since: 	22/06/2016
@Uso: 		SFIEMT
*/
Static Function MenuDef()

	Local aRotina := {}

	ADD OPTION aRotina Title 'Visualizar' Action 'VIEWDEF.SF7304J' OPERATION 2 ACCESS 0
	ADD OPTION aRotina Title 'Incluir'    Action 'VIEWDEF.SF7304J' OPERATION 3 ACCESS 0
	ADD OPTION aRotina Title 'Alterar'    Action 'VIEWDEF.SF7304J' OPERATION 4 ACCESS 0
	ADD OPTION aRotina Title 'Excluir'    Action 'VIEWDEF.SF7304J' OPERATION 5 ACCESS 0
	ADD OPTION aRotina Title 'Imprimir'   Action 'VIEWDEF.SF7304J' OPERATION 8 ACCESS 0
	ADD OPTION aRotina Title 'Copiar'     Action 'VIEWDEF.SF7304J' OPERATION 9 ACCESS 0

Return ( aRotina )


/** {Protheus.doc} ModelDef
Funcao para Montar o Modelo de Dados

@author: 	Jose Leite de Barros Neto
@since: 	22/06/2016
@Uso: 		SFIEMT
*/
Static Function ModelDef()

	Local oStruZC5 := FWFormStruct(1, 'ZC5')
	Local oStruZC9 := FWFormStruct(1, 'ZC9')
	Local oStruZC8 := FWFormStruct(1, 'ZC8')
	Local oModel

	oModel := MPFormModel():New('SF7304JM')
	
	//Blocos
	oModel:AddFields('ZC5MASTER',/*cOwner*/,oStruZC5)
	
	//Cargos
	oModel:AddGrid('ZC9DETAIL','ZC5MASTER',oStruZC9)
	
	//Entidades
	oModel:AddGrid('ZC8DETAIL','ZC9DETAIL',oStruZC8)
	oModel:GetModel('ZC8DETAIL'):SetOptional(.T.)
	
	//Relacao Bloco x Cargos
	oModel:SetRelation('ZC9DETAIL',{{'ZC9_FILIAL','xFilial("ZC9")'}, {'ZC9_CODBLC','ZC5_CODIGO'}},ZC9->(IndexKey(3)))
	
	//Relacao Entidade x Cargos
	oModel:SetRelation('ZC8DETAIL',{{'ZC8_FILIAL','xFilial("ZC8")'}, {'ZC8_CODBLC','ZC5_CODIGO'} ,{'ZC8_CODCAR','ZC9_CODIGO'}},ZC8->(IndexKey(3)))
	
	oModel:SetPrimaryKey({"ZC5_FILIAL","ZC5_CODIGO"})
	
	oModel:SetDescription('Blocos')
	oModel:GetModel('ZC5MASTER'):SetDescription('Blocos')
	oModel:GetModel('ZC9DETAIL'):SetDescription('Cargos')
	oModel:GetModel('ZC8DETAIL'):SetDescription('Entidades')

Return ( oModel )


/** {Protheus.doc} ViewDef
Funcao para Montar a Interface

@author: 	Jose Leite de Barros Neto
@since: 	22/06/2016
@Uso: 		SFIEMT
*/
Static Function ViewDef()

	Local oModel   := FWLoadModel('SF7304J')
	Local oStruZC5 := FWFormStruct(2, 'ZC5')
	Local oStruZC9 := FWFormStruct(2, 'ZC9')
	Local oStruZC8 := FWFormStruct(2, 'ZC8')
	Local oView

	oView := FWFormView():New()
	oView:SetModel(oModel)
	
	//Blocos
	oView:AddField('VIEW_ZC5', oStruZC5, 'ZC5MASTER')
	
	//Cargos
	oView:AddGrid ('VIEW_ZC9', oStruZC9, 'ZC9DETAIL')
	oView:AddIncrementField( 'VIEW_ZC9', 'ZC9_ITEM' )
	oStruZC9:RemoveField('ZC9_CODBLC')
	
	//Entidades
	oView:AddGrid ('VIEW_ZC8', oStruZC8, 'ZC8DETAIL')
	oView:AddIncrementField( 'VIEW_ZC8', 'ZC8_ITEM' )
	oStruZC8:RemoveField('ZC8_CODBLC')
	oStruZC8:RemoveField('ZC8_CODCAR')
	
	//Cria os Box
	oView:CreateHorizontalBox( 'SUPERIOR' , 20 )
	oView:CreateHorizontalBox( 'INFERIOR', 80 )
	
	oView:CreateVerticalBox( 'INFERIORE', 50, 'INFERIOR' )
	oView:CreateVerticalBox( 'INFERIORD', 50, 'INFERIOR' )
	
	oView:SetOwnerView ('VIEW_ZC5', 'SUPERIOR')
	oView:SetOwnerView ('VIEW_ZC9', 'INFERIORE')
	oView:SetOwnerView ('VIEW_ZC8', 'INFERIORD')
	
	oView:EnableTitleView('VIEW_ZC5')
	oView:EnableTitleView('VIEW_ZC9')
	oView:EnableTitleView('VIEW_ZC8')

Return ( oView )