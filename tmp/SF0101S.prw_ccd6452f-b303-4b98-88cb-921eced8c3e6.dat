#include 'protheus.ch'
#Include 'FWMVCDEF.CH'

/*/{Protheus.doc} SF0101S
	Browse MVC da rotina de histórico de bens duráveis
	
@author Sergio Ricardo Leite Salustiano 
@since 14/09/2016

@type function
/*/
user function SF0101S()
	Local oBrowse
	Private _cFilCod :=  FWCODFIL() 
	oBrowse := FWMBrowse():New()
	oBrowse:SetAlias('ZEA')
	oBrowse:SetDescription('Histórico de Bens Duraveis')
	oBrowse:AddLegend("ZEA_SITBEM =='A'","GREEN"       ,"Ativo")
	oBrowse:AddLegend("ZEA_SITBEM =='B'","BR_CANCEL"   ,"Baixado")
	oBrowse:AddLegend("ZEA_SITBEM =='E'","BLUE"        ,"Emprestado")
	oBrowse:AddLegend("ZEA_SITBEM =='T'","BR_AMARELO"  ,"Transferido")
	oBrowse:AddLegend("ZEA_SITBEM =='C'","BR_PRETO"    ,"A Classificar")
	oBrowse:SetFilterDefault("ZEA_FILORI == _cFilCod .OR. ZEA_FILATU == _cFilCod ")
	
	oBrowse:Activate()

Return ( Nil )

/*/{Protheus.doc} MenuDef
	Menu MVC da rotina de histórico de bens duráveis
	
@author Sergio Ricardo Leite Salustiano
@since 14/09/2016

@type function
/*/
Static Function MenuDef()

	Local aRotina := {}

	ADD OPTION aRotina Title 'Visualizar' Action 'VIEWDEF.SF0101S' OPERATION 2 ACCESS 0
	//ADD OPTION aRotina Title 'Incluir'    Action 'VIEWDEF.SF0101S' OPERATION 3 ACCESS 0
	//ADD OPTION aRotina Title 'Alterar'    Action 'VIEWDEF.SF0101S' OPERATION 4 ACCESS 0
	//ADD OPTION aRotina Title 'Excluir'    Action 'VIEWDEF.SF0101S' OPERATION 5 ACCESS 0
	//ADD OPTION aRotina Title 'Imprimir'   Action 'VIEWDEF.SF0101S' OPERATION 8 ACCESS 0
	//ADD OPTION aRotina Title 'Copiar'     Action 'VIEWDEF.SF0101S' OPERATION 9 ACCESS 0

Return ( aRotina )

/*/{Protheus.doc} ModelDef
	Definição do modedo de dados da rotina de histórico de bens duráveis 
	
@author Sergio Ricardo Leite Salustiano 
@since 14/09/2016

@type function
/*/
Static Function ModelDef()

	Local oStruZEA := FWFormStruct(1, 'ZEA')
	Local oStruZEB := FWFormStruct(1, 'ZEB')
	Local oModel

	oModel:= MPFormModel():New('SF0101SM')
	oModel:AddFields('ZEAMASTER',/*cOwner*/, oStruZEA)
	oModel:AddGrid('ZEBDETAIL','ZEAMASTER',oStruZEB)
	oModel:SetRelation('ZEBDETAIL',{{'ZEB_FILIAL','xFilial("ZEB")'}, {'ZEB_CODBEM','ZEA_CODIGO'}},ZEB->(IndexKey(1)))
	oModel:SetPrimaryKey({"ZEA_FILIAL","ZEA_CODIGO"})
	oModel:SetDescription('Bens Duraveis')
	oModel:GetModel('ZEAMASTER'):SetDescription('Bens Duraveis')
	oModel:GetModel('ZEBDETAIL'):SetDescription('Movimentacao Bens Duraveis')

Return ( oModel )

/*/{Protheus.doc} ViewDef
	Definição da visualização de dados da rotina de histórico de bens duráveis

@author Sergio Ricardo Leite Salustiano 
@since 14/09/2016

@type function
/*/
Static Function ViewDef()

	Local oModel   := FWLoadModel('SF0101S')
	Local oStruZEA := FWFormStruct(2, 'ZEA')
	Local oStruZEB := FWFormStruct(2, 'ZEB')
	Local oView

	oView:=FWFormView():New()
	oView:SetModel(oModel)
	oView:AddField('VIEW_ZEA', oStruZEA, 'ZEAMASTER')
	oView:AddGrid ('VIEW_ZEB', oStruZEB, 'ZEBDETAIL')
	oView:AddIncrementField( 'VIEW_ZEB', 'ZEB_ITEM' )
	oView:CreateHorizontalBox ('SUPERIOR', 47)
	oView:CreateHorizontalBox ('INFERIOR', 52)
	oView:SetOwnerView ('VIEW_ZEA', 'SUPERIOR')
	oView:SetOwnerView ('VIEW_ZEB', 'INFERIOR')

Return ( oView )
	
return