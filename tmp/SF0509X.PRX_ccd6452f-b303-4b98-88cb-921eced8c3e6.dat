#INCLUDE 'PROTHEUS.CH'
#INCLUDE 'FWMVCDEF.CH' 
#INCLUDE 'TOPCONN.CH' 

/*/{Protheus.doc} SF0509X
(long_description)
@author j2a.luizjunior
@since 21/07/2017
@version 1.0
@example
(examples)
@see (links_or_references)
/*/

User Function SF0509X

	Local cEstat := U_SFGN001A(ProcName(0), "SF0509X")
	Local   oBrowse := Nil
	Local   aRotina := {}
	Private cCart   := ""	
	  
	//Private cCodigo := ""                                        
	//-> Instanciamento da Classe de Browse
	oBrowse := FWMBrowse():New()
	        
	//-> Definição da tabela do Browse
	oBrowse:SetAlias("ZH5")
	oBrowse:SetMenuDef("SF0509X")
	        
	// Titulo da Browse
	oBrowse:SetDescription("Rotina para Cadastro de Locação")
	
	aRotina := MenuDef()
	        
	// Ativação da Classe
	oBrowse:Activate()

Return

/*/{Protheus.doc} MenuDef
(long_description)
@author j2a.luizjunior
@since 21/07/2017
@version 1.0
@example
(examples)
@see (links_or_references)
/*/

Static Function MenuDef()

	Local cEstat := U_SFGN001A(ProcName(0), "SF0509X")
	Local aRotina := {}
	
	ADD OPTION aRotina Title "Pesquisar"  Action "PesqBrw"         OPERATION 1 ACCESS 0
	ADD OPTION aRotina Title "Visualizar" Action "ViewDef.SF0509X" OPERATION 2 ACCESS 0
	ADD OPTION aRotina Title "Incluir"    Action "ViewDef.SF0509X" OPERATION 3 ACCESS 0
	ADD OPTION aRotina Title "Alterar"    Action "ViewDef.SF0509X" OPERATION 4 ACCESS 0
	ADD OPTION aRotina Title "Excluir"    Action "ViewDef.SF0509X" OPERATION 5 ACCESS 0 
	ADD OPTION aRotina Title "Imprimir"   Action "U_SF0511X()"     OPERATION 6 ACCESS 0
	ADD OPTION aRotina Title "Imp Rec"    Action "U_SF0522X()"     OPERATION 7 ACCESS 0
 
Return(aRotina)

/*/{Protheus.doc} ModelDef
(long_description)
@author j2a.luizjunior
@since 21/07/2017
@version 1.0
@return ${return}, ${return_description}
@example
(examples)
@see (links_or_references)
/*/

Static Function ModelDef()

	Local cEstat := U_SFGN001A(ProcName(0), "SF0509X")                                        
	Local   oStruZH5 := FWFormStruct(1,"ZH5")	
	Local   oModel   := MPFormModel():New("F0509X")
	
	oStruZH5:SetProperty("ZH5_COD"    , MODEL_FIELD_INIT   , {|x| x := GETCOD("1",x)  })	
	oStruZH5:SetProperty("ZH5_COD"    , MODEL_FIELD_WHEN   , {||.F.                   })
	
	oStruZH5:SetProperty("ZH5_DTCAD"  , MODEL_FIELD_INIT   , {|x| x := dDataBase      })
	
	oStruZH5:AddTrigger ("ZH5_NUMERO" , "ZH5_NOME",{|| .T.}, {|x| x := 	GATNOME(x)    })
	
	oStruZH5:AddTrigger ("ZH5_PROD"   , "ZH5_DESC",{|| .T.}, {|x| x := 	GATDESC(x)    })
		
	oModel:AddFields("ZH5UNICO", Nil  , oStruZH5)
	oModel:SetDescription("Cupons Locacoes")
	oModel:GetModel("ZH5UNICO"):SetDescription("Cupons Locacoes")      
	oModel:SetPrimaryKey({"ZH5_FILIAL","ZH5_COD","ZH5_NUMERO"})

Return(oModel) 

/*/{Protheus.doc} ViewDef
(long_description)
@author j2a.luizjunior
@since 21/07/2017
@version 1.0
@return ${return}, ${return_description}
@example
(examples)
@see (links_or_references)
/*/

Static Function ViewDef()

	Local cEstat := U_SFGN001A(ProcName(0), "SF0509X")
	Local oModel   := FWLoadModel("SF0509X")
	Local oStruZH5 := FWFormStruct(2,"ZH5")
	Local oView	   := FWFormView():New()
	
	oStruZH5:RemoveField("ZH5_USER")
	oView:SetModel(oModel)
	oView:AddField("VIEW_ZH5", oStruZH5, "ZH5UNICO")
	oView:CreateHorizontalBox("UM", 100)
	oView:SetOwnerView("VIEW_ZH5" , "UM")

Return(oView)

/*/{Protheus.doc} GETCOD
(long_description)
@author j2a.luizjunior
@since 21/07/2017
@version 1.0
@example
(examples)
@see (links_or_references)
/*/

Static Function GETCOD

	Local cEstat := U_SFGN001A(ProcName(0), "SF0509X")
	Local cQuery  := ""
	Local cAlCOD  := GetNextAlias()
	Local cCodigo := ""
	
	If Select("cAlCOD") > 0
		("cAlCOD")->(DbCloseArea())
	EndIf
	
	cQuery := " SELECT MAX(ZH5_COD) AS ZH5_COD FROM "+RETSQLNAME("ZH5") 
	
	TcQuery cQuery New Alias cAlCOD
	
	cCodigo := Iif(Empty(("cAlCOD")->ZH5_COD),"000000001", Soma1(("cAlCOD")->ZH5_COD))

Return(cCodigo)

/*/{Protheus.doc} GATNOME
(long_description)
@author j2a.luizjunior
@since 21/07/2017
@version 1.0
@example
(examples)
@see (links_or_references)
/*/

Static Function GATNOME(oField)

	Local cEstat := U_SFGN001A(ProcName(0), "SF0509X")
	Local cNumero := oField:GetValue("ZH5_NUMERO")
	Local cRevisa := oField:GetValue("ZH5_REVISA")
	Local cProd   := ""
	Local cDesc   := ""
	Local aArea   := GetArea()
	Local cRet    := ""
	Local nQtde   := 0
	
	DbSelectArea("CN9")
	DbSetOrder(1)
	If DbSeek(xFilial("CN9") + cNumero + cRevisa)
			
		oField:SetValue("ZH5_VALID" ,CN9->CN9_XDTFIM)		
		
		//->CNB_FILIAL, CNB_CONTRA, CNB_REVISA, CNB_NUMERO		
		cProd := Posicione("CNB",1,xFilial("CNB") + CN9->CN9_NUMERO + CN9->CN9_REVISA,"CNB_PRODUT")
		cDesc := Posicione("CNB",1,xFilial("CNB") + CN9->CN9_NUMERO + CN9->CN9_REVISA,"CNB_DESCRI")
		nQtde := Posicione("CNB",1,xFilial("CNB") + CN9->CN9_NUMERO + CN9->CN9_REVISA,"CNB_QUANT" )
		
		oField:SetValue("ZH5_QUANT" ,nQtde )		
		oField:SetValue("ZH5_PROD"  ,cProd )
		oField:SetValue("ZH5_DESC"  ,cDesc )
		
		DbSelectArea("SA1")
		DbSetOrder(1)
		If DbSeek(xFilial("SA1") + CN9->CN9_CLIENT + CN9->CN9_LOJACL)

			oField:SetValue("ZH5_END"   ,SA1->A1_END     )
			oField:SetValue("ZH5_MUN"   ,SA1->A1_MUN     )
			oField:SetValue("ZH5_BAIRRO",SA1->A1_BAIRRO  )
			oField:SetValue("ZH5_EST"   ,SA1->A1_EST     )			
			oField:SetValue("ZH5_CGC"   ,SA1->A1_CGC     )		
			oField:SetValue("ZH5_CEP"   ,SA1->A1_CEP     )
			oField:SetValue("ZH5_FONRES",SA1->A1_TEL     )
			oField:SetValue("ZH5_EMAIL" ,SA1->A1_EMAIL   )
			oField:SetValue("ZH5_TIPO"  ,SA1->A1_PESSOA  )			
			
			cRet := SA1->A1_NOME
		
		EndIf

	EndIf
	
	RestArea(aArea)
	 
Return cRet

/*/{Protheus.doc} GATDESC
(long_description)
@author j2a.luizjunior
@since 21/07/2017
@version 1.0
@example
(examples)
@see (links_or_references)
/*/

Static Function GATDESC(oField)

	Local cEstat := U_SFGN001A(ProcName(0), "SF0509X")
	Local cCod := oField:GetValue("ZH5_PROD")
	Local cRet := Posicione("SB1",1,xFilial("SB1") + cCod,"B1_DESC")

Return cRet

/*/{Protheus.doc} F0509X
(long_description)
@author j2a.luizjunior
@since 14/08/2017
@version 1.0
@example
(examples)
@see (links_or_references)
/*/

User Function F0509X()

	Local cEstat := U_SFGN001A(ProcName(0), "SF0509X")
	Local aParam   := PARAMIXB
	Local cIdPonto := aParam[2]
	Local oObj     := aParam[1]
	Local cNumero  := ""
	Local cRevisa  := ""
	Local lRet     := .T.  
	Local nQtde    := 0

	If cIdPonto == "MODELPOS"

		If oObj:GetOperation() == 3
			
			oModel  := FWModelActive()
			oStrZH5 := oModel:GetModel("ZH5UNICO")
			
			oStrZH5:SetValue("ZH5_USER",cUserName)

			cNumero  := oStrZH5:GetValue("ZH5_NUMERO")
			cRevisa  := oStrZH5:GetValue("ZH5_REVISA")  
			
			If oStrZH5:GetValue("ZH5_DATA") > CN9->CN9_DTFIM
				lRet := .F.
				Return lRet
			EndIf
			
			//-Valida quantidade informada em tela
			nQtde := Posicione("CNB",1,xFilial("CNB") + cNumero + cRevisa,"CNB_QUANT" )
			If oStrZH5:GetValue("ZH5_QUANT") > nQtde 
				lRet := .F.
				Return lRet
			EndIf
			
			DbSelectArea("CN9")
			DbSetOrder(1)
			If DbSeek(xFilial("CN9") + cNumero + cRevisa )
				
				While !CN9->(Eof()) .And. xFilial("CN9") + cNumero + cRevisa == CN9->CN9_FILIAL + CN9->CN9_NUMERO + CN9->CN9_REVISA				
					If Reclock("CN9",.F.)					
						CN9->CN9_XUSADO := "S"					
						CN9->(MsUnlock())					
					EndIf 				
					CN9->(DbSkip())
				EndDo
				
			EndIf		

		ElseIf oObj:GetOperation() == 4

			oModel  := FWModelActive()
			oStrZH5 := oModel:GetModel("ZH5UNICO")

			//-Valida quantidade informada em tela
			nQtde := Posicione("CNB",1,xFilial("CNB") + ZH5->ZH5_NUMERO + ZH5->ZH5_REVISA,"CNB_QUANT" )
			If oStrZH5:GetValue("ZH5_QUANT") > nQtde 
				lRet := .F.
				Return lRet
			EndIf

			DbSelectArea("CN9")
			DbSetOrder(1)
			If DbSeek(xFilial("CN9") + ZH5->ZH5_NUMERO + ZH5->ZH5_REVISA )		
				If oStrZH5:GetValue("ZH5_DATA") > CN9->CN9_DTFIM
					lRet := .F.
					Return lRet
				EndIf							
			EndIf	
		
		ElseIf oObj:GetOperation() == 5
	
			DbSelectArea("CN9")
			DbSetOrder(1)
			If DbSeek(xFilial("CN9") + ZH5->ZH5_NUMERO + ZH5->ZH5_REVISA )
			
				While !CN9->(Eof()) .And. xFilial("CN9") + ZH5->ZH5_NUMERO + ZH5->ZH5_REVISA == CN9->CN9_FILIAL + CN9->CN9_NUMERO + CN9->CN9_REVISA				
					If Reclock("CN9",.F.)						
						CN9->CN9_XUSADO := "N"						
						CN9->(MsUnlock())					
					EndIf				
					CN9->(DbSkip())
				EndDo	
			
			EndIf
		
		EndIf
		
	EndIf	

Return lRet