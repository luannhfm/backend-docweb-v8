#Include 'Protheus.ch'
#Include 'TopConn.ch'

/*/{Protheus.doc} SS69R01J
Relatorio Geral de Contratos Fechados (GCT/CRM)

@type 		function
@author 	Jose Leite de Barros Neto
@since 	15/03/2016
@version 	1.0
@return 	Nil, Nulo
@history 14/01/2019, Franklin de Brito de Oliveira, Adicionado a coluna do percentual de desconto (CNB_DESC -> ADZ_DESCON)
/*/
User Function SS69R01J()

	Local _oReport		:= Nil
	Local _cPerg 		:= FunName()
	
	If TRepInUse()
		fCriaSx1( _cPerg )
		If Pergunte( _cPerg )
			_oReport := ReportDef( _cPerg )
			_oReport:PrintDialog()
		EndIf
	EndIf
	
Return( Nil )

/** {Protheus.doc} ReportDef
Funcao que define as colunas

@author: 	Jose Leite de Barros Neto
@since: 	15/03/2016
@Uso: 		SFIEMT
*/

Static Function ReportDef(p_cPerg)
	
	Local oReport	:= Nil
	Local oSection	:= Nil
	Local oBreak	:= Nil
	Local cTitle 	:= "Relatorio Geral de Contratos Fechados"  
	
	oReport  := TReport():New(p_cPerg,cTitle,p_cPerg,{|oReport| PrintReport(oReport)},cTitle)
	oReport:lHeaderVisible := .T.
	oReport:SetLandScape() 
	//oReport:SetTotalInLine(.F.) 
	oReport:Page()
	
	oCliente := TRSection():New(oReport,OemToAnsi(""),{"SA1"})
	//oCliente:SetHeaderBreak(.T.)
	oCliente:SetHeaderBreak(.T.)
	
	oNomCli  := TRCell():New(oCliente, "_cNomCli","TRA", "Cliente")
	oNomCli:SetSize(60)

	oCGC     := TRCell():New(oCliente, "_cCGC","TRA", "CNPJ")
	oCGC:SetSize(40)

	// Luiz Junior
	// Campos inseridos para atender ao documento de origem Rel. Geral de Contratos Fechados
	//---------------------------------------------------------------------------------------
	oEmail := TRCell():New(oCliente,"A1_XEMLCOB","SA1")
	oEmail:SetSize(80)
	
	oTel   := TRCell():New(oCliente,"TEL","TRA","Telefone")	
	oTel:SetSize(70)	
	//---------------------------------------------------------------------------------------
	
	oSection := TRSection():New(oReport,OemToAnsi(""),{"CN9","ADY","ADZ"})
	//oSection:SetHeaderBreak(.T.)
	//oSection:SetHeaderPage(.T.)
	oSection:SetHeaderBreak(.T.)
		
	oCtrVend := TRCell():New(oSection, "_cCtrVen","TRA", "Nº Ctr. Vend.")
	oCtrVend:SetSize(35)
	
	oProdut  := TRCell():New(oSection, "_cProdut","TRA", "Serviço(s)")
	oProdut:SetSize(50)
	
	oUndExec := TRCell():New(oSection, "_cUndExe","TRA", "Unid. Executora")
	oUndExec:SetSize(15)
	
	oConsult := TRCell():New(oSection, "_cNomVend","TRA", "Consultor(a)")
	oConsult:SetSize(35)
	
	oVlrProp := TRCell():New(oSection, "_nVlrProp","TRA", "Valor", "@E 99,999,999,999.99")
	oVlrProp:SetSize(25)
	
	oPercDes := TRCell():New(oSection, "ADZ_DESCON","ADZ")
		
	oCliInd  := TRCell():New(oSection, "_cCliInd","TRA", "Cli. Ind?")
	oCliInd:SetSize(10)
	
	oTopUf   := TRCell():New(oSection, "_cTopUf","TRA", "TOP UF?")
	oTopUf:SetSize(5)
	
	oDtInEx  := TRCell():New(oSection, "_cDtInEx","TRA", "Dt. Inc. Exec.")
	oDtInEx:SetSize(23)
	
	oDtFiEx  := TRCell():New(oSection, "_cDtFiEx","TRA", "Dt. Fin. Exec.")
	oDtFiEx:SetSize(23)
	
	oDtInVig := TRCell():New(oSection, "_cDtInVi","TRA", "Dt. Inc. Vig.")
	oDtInVig:SetSize(23)
	
	oDtFiVig := TRCell():New(oSection, "_cDtFiVi","TRA", "Dt. Fin. Vig.")
	oDtFiVig:SetSize(23)
	
	oSitCtr := TRCell():New(oSection, "_cSituac","TRA", "Situação CTR")
	oSitCtr:SetSize(12)
	
	oFilial := TRCell():New(oSection, "_cFilial","TRA", "Filial")
	oFilial:SetSize(18)
	
	oDtAssi := TRCell():New(oSection, "_cDtAssi","TRA", "Dt. Assin.")
	oDtAssi:SetSize(23)
	
	oTipoPr := TRCell():New(oSection, "_cTpPrd","TRA", "Tipo Prd.")
	oTipoPr:SetSize(20)
	
	oGrpPr  := TRCell():New(oSection, "_cGrpPrd","TRA", "Grupo Prd.")
	oGrpPr:SetSize(20)
	
	oBreak := TRBreak():New(oCliente, oCliente:Cell("_cCGC"), "Valor Total da Proposta/Contrato:",.F.)
	TRFunction():New(oSection:Cell("_nVlrProp"),,"SUM",oBreak,,"@E 99,999,999,999.99",,.F.,.F.,.F.)
	
Return( oReport )


/** {Protheus.doc} ReportDef
Funcao que gera o relatorio

@author: 	Jose Leite de Barros Neto
@since: 	15/03/2016
@Uso: 		SFIEMT
*/

Static Function PrintReport(oReport)
	
	Local oSec2  	:= oReport:Section(1)
	Local oSection 	:= oReport:Section(2)
	Local _cQuery 	:= ""
	Local _cCtrVen	:= ""
	Local _cNomCli	:= ""
	Local _cCGC		:= ""
	Local _cProdut	:= ""
	Local _cUndExe	:= ""
	Local _cNomVend	:= ""
	Local _nVlrProp	:= 0
	Local _cCliInd	:= ""
	Local _cTopUf	:= ""
	Local _cDtInEx	:= ""
	Local _cDtFiEx	:= ""
	Local _cDtInVi	:= ""
	Local _cDtFiVi	:= ""
	Local _cSituac	:= ""
	Local _cFilial	:= ""
	Local _cDtAssi	:= ""
	Local _cTpPrd	:= ""
	Local _cGrpPrd	:= ""
	
	If Select("TRA") > 0
		DbSelectarea("TRA")
		DbCloseArea()
	EndIf
	
	_cQuery := "SELECT	CN9_FILIAL, CN9_XNROCV, A1_NOME, A1_CGC,	"
	// Luiz Junior
	// Campos inseridos para atender ao documento de origem Rel. Geral de Contratos Fechados
	_cQuery += CRLF + " A1_XEMLCOB, A1_DDD, A1_TEL, "	
	//--------------------------------------------------------------------------------------
	_cQuery += CRLF + "	ADZ_PRODUT, ADZ_XUNEXE, ADZ_TOTAL, ADZ_TES, ADY_VEND, ADZ_DESCON,	"
	_cQuery += CRLF + "	CASE A1_XTRATIN				"
	_cQuery += CRLF + "		WHEN 'S' THEN 'SIM'	"
	_cQuery += CRLF + "		WHEN 'N' THEN 'NAO'	"
	_cQuery += CRLF + "		ELSE ' '					"
	_cQuery += CRLF + "	END AS A1_XTRATIN,			"
	_cQuery += CRLF + "	CASE A1_XTOPUF				"
	_cQuery += CRLF + "		WHEN 'S' THEN 'SIM'	"
	_cQuery += CRLF + "		WHEN 'N' THEN 'NAO'	"
	_cQuery += CRLF + "		ELSE ' '					"
	_cQuery += CRLF + "	END AS A1_XTOPUF,			"
	_cQuery += CRLF + "	CN9_DTASSI, CN9_XDTINI, CN9_XDTFIM, CN9_DTINIC,CN9_DTFIM,	"
	_cQuery += CRLF + "	CASE CN9_SITUAC				"
	_cQuery += CRLF + "		WHEN '01' THEN 'Cancelado'			"
	_cQuery += CRLF + "		WHEN '02' THEN 'Em Elaboração'		"
	_cQuery += CRLF + "		WHEN '03' THEN 'Emitido'				"
	_cQuery += CRLF + "		WHEN '04' THEN 'Em Aprovação'		"
	_cQuery += CRLF + "		WHEN '05' THEN 'Vigente'				"
	_cQuery += CRLF + "		WHEN '06' THEN 'Paralisado'			"
	_cQuery += CRLF + "		WHEN '07' THEN 'Sol. Finalização'	"
	_cQuery += CRLF + "		WHEN '08' THEN 'Finalizado'			"
	_cQuery += CRLF + "		WHEN '09' THEN 'Revisão'				"
	_cQuery += CRLF + "		WHEN '10' THEN 'Revisado'			"
	_cQuery += CRLF + "		WHEN '11' THEN 'Aguardando Finalização'	"
	_cQuery += CRLF + "		ELSE ' '	"
	_cQuery += CRLF + "	END AS CN9_SITUAC,	"
	_cQuery += CRLF + "	CN9_XOPORT,			"
	_cQuery += CRLF + "	CN9_XREVOP			"
	_cQuery += CRLF + "FROM " + RetSqlName("CN9") + " CN9 "
	_cQuery += CRLF + "INNER JOIN " + RetSqlName("SA1") + " SA1 ON SA1.A1_FILIAL = '"+xFilial("SA1")+"' 	"
	_cQuery += CRLF + "								AND SA1.A1_COD = CN9.CN9_CLIENT							"
	_cQuery += CRLF + "								AND SA1.A1_LOJA = CN9.CN9_LOJACL						"	
	_cQuery += CRLF + "								AND SA1.D_E_L_E_T_ <> '*'								"
	_cQuery += CRLF + "LEFT JOIN " + RetSqlName("ADY") + " ADY ON ADY.ADY_FILIAL = CN9.CN9_FILIAL	"
	_cQuery += CRLF + "								AND ADY.ADY_OPORTU = CN9_XOPORT							"
	_cQuery += CRLF + "								AND ADY.ADY_REVISA = CN9_XREVOP							"
	_cQuery += CRLF + "								AND ADY.D_E_L_E_T_ <> '*'								"
	_cQuery += CRLF + "LEFT JOIN " + RetSqlName("ADZ") + " ADZ ON ADZ.ADZ_FILIAL = ADY.ADY_FILIAL	"
	_cQuery += CRLF + "								AND ADZ.ADZ_PROPOS = ADY.ADY_PROPOS					"
	_cQuery += CRLF + "								AND ADZ.ADZ_REVISA = ADY.ADY_PREVIS					"
	_cQuery += CRLF + "								AND ADZ.D_E_L_E_T_ <> '*'								"
	_cQuery += CRLF + "WHERE	CN9_FILIAL BETWEEN '" + MV_PAR01 + "' AND '" + MV_PAR02 + "'			"
	_cQuery += CRLF + "		AND CN9.D_E_L_E_T_ <> '*'														"
	_cQuery += CRLF + "		AND ADY_VEND BETWEEN '" + MV_PAR03 + "' AND '" + MV_PAR04 + "'			"
	_cQuery += CRLF + "		AND ADY_STATUS = 'B'																"
	_cQuery += CRLF + "		AND ADZ_XUNEXE BETWEEN '" + MV_PAR05 + "' AND '" + MV_PAR06 + "'		"
	_cQuery += CRLF + "		AND CN9_DTINIC  >= '" + DtoS(MV_PAR07) + "'									"
	_cQuery += CRLF + "		AND CN9_DTFIM   <= '" + DtoS(MV_PAR08) + "'									"
	_cQuery += CRLF + "ORDER BY CN9.CN9_FILIAL, CN9.CN9_XNROCV												"
		
	TCQUERY _cQuery NEW ALIAS "TRA"
		
	DbSelectArea("TRA")
	TRA->(DbGoTop())
		
	oReport:SetMeter(RecCount())
	oSection:Init()
	oSec2:Init()
		
	While .Not. TRA->( EOF() )
		
		If oReport:Cancel()
			Exit
		EndIf
		
		_cCtrVen	:= AllTrim(TRA->CN9_XNROCV)
		_cNomCli	:= AllTrim(TRA->A1_NOME)
		_cCGC		:= AllTrim(Transform(TRA->A1_CGC,Iif(Len(AllTrim(TRA->A1_CGC))==11,"@R 999.999.999-99","@R 99.999.999/9999-99")))
		_cProdut	:= AllTrim(Posicione("SB1",1,xFilial("SB1")+TRA->ADZ_PRODUT,"B1_DESC"))
		_cUndExe	:= AllTrim(TRA->ADZ_XUNEXE)
		_cNomVend	:= AllTrim(Posicione("SA3",1,xFilial("SA3")+TRA->ADY_VEND,"A3_NOME"))
		_cGratis 	:= AllTrim(Posicione("SF4",1,xFilial("SF4")+TRA->ADZ_TES,"F4_DUPLIC"))
		
		//Tratamento para gratuidades/bonificacao
		If _cGratis == "N"
			_nVlrProp	:= 0
		Else
			_nVlrProp	:= TRA->ADZ_TOTAL
		EndIf
		
		nPerDesc := TRA->ADZ_DESCON
		_cCliInd := AllTrim(TRA->A1_XTRATIN)
		_cTopUf	 := AllTrim(TRA->A1_XTOPUF)
		_cDtInEx := DtoC(StoD(TRA->CN9_XDTINI))
		_cDtFiEx := DtoC(StoD(TRA->CN9_XDTFIM))
		_cDtInVi := DtoC(StoD(TRA->CN9_DTINIC))
		_cDtFiVi := DtoC(StoD(TRA->CN9_DTFIM))
		_cSituac := AllTrim(TRA->CN9_SITUAC)
		
		_cFilial := AllTrim(TRA->CN9_FILIAL)
		_cDtAssi := DtoC(StoD(TRA->CN9_DTASSI))
		_cTpPrd  := AllTrim(Posicione("SB1",1,xFilial("SB1")+TRA->ADZ_PRODUT,"B1_TIPO"))
		_cGrpPrd := AllTrim(Posicione("SB1",1,xFilial("SB1")+TRA->ADZ_PRODUT,"B1_GRUPO"))
		
		oSection:Cell("_cFilial") :SetValue(_cFilial)
		oSection:Cell("_cDtAssi") :SetValue(_cDtAssi)
		oSection:Cell("_cCtrVen") :SetValue(_cCtrVen)
		oSection:Cell("_cProdut") :SetValue(_cProdut)
		oSection:Cell("_cTpPrd")  :SetValue(_cTpPrd)
		oSection:Cell("_cGrpPrd") :SetValue(_cGrpPrd)
		oSection:Cell("_cUndExe") :SetValue(_cUndExe)
		oSection:Cell("_cNomVend"):SetValue(_cNomVend)
		oSection:Cell("ADZ_DESCON"):SetValue(nPerDesc)
		oSection:Cell("_nVlrProp"):SetValue(_nVlrProp)
		oSection:Cell("_cCliInd") :SetValue(_cCliInd)
		oSection:Cell("_cTopUf")  :SetValue(_cTopUf)
		oSection:Cell("_cDtInEx") :SetValue(_cDtInEx)
		oSection:Cell("_cDtFiEx") :SetValue(_cDtFiEx)
		oSection:Cell("_cDtInVi") :SetValue(_cDtInVi)
		oSection:Cell("_cDtFiVi") :SetValue(_cDtFiVi)
		oSection:Cell("_cSituac") :SetValue(_cSituac)
		
		// Luiz Junior
		// Campos inseridos para atender ao documento de origem Rel. Geral de Contratos Fechados
		oSec2:Cell("_cNomCli") :SetValue(_cNomCli)
		oSec2:Cell("_cCGC")    :SetValue(_cCGC)
					
		oSec2:Cell("A1_XEMLCOB"):SetValue(AllTrim(TRA->A1_XEMLCOB))
		oSec2:Cell("TEL"):SetValue(AllTrim(TRA->A1_DDD) + AllTrim(TRA->A1_TEL))		 
		//--------------------------------------------------------------------------------------	
		
		oSec2:PrintLine()	
		oSection:PrintLine()
		oReport:IncMeter()
			
		TRA->( DbSkip() )
	End
		
	TRA->(DbCloseArea())
	oSec2:Finish()
	oSection:Finish()
	
	
Return( Nil )


/** {Protheus.doc} fCriaSx1
Funcao para criacao das perguntas

@param: 	<Nil>
@author 	Jose Leite de Barros Neto
@since 	15/03/2016
@Uso: 		SFIEMT
*/
Static Function fCriaSx1( p_cPerg )
	
	Local aHelp := {}

	AAdd(aHelp, {{"Informe a Filial Inicial"			}, {""}, {""}})
	AAdd(aHelp, {{"Informe a Filial Final"				}, {""}, {""}})
	AAdd(aHelp, {{"Informe o Consultor Inicial"			}, {""}, {""}})
	AAdd(aHelp, {{"Informe o Consultor Inicial"			}, {""}, {""}})
	AAdd(aHelp, {{"Informe a Unidade Executora Inicial"	}, {""}, {""}})
	AAdd(aHelp, {{"Informe a Unidade Executora Final"	}, {""}, {""}})
	AAdd(aHelp, {{"Informe a Data Vigencia Inicial"		}, {""}, {""}})
	AAdd(aHelp, {{"Informe a Data Vigencia Final"		}, {""}, {""}})
	

	u_SFPUTSX1( p_cPerg, "01","Filial de	?  			"  ,"","","mv_ch1","C",TamSX3("CN9_FILIAL")[1]	,0,0,"G","","SM0"	,"","","mv_par01","","","","","","","","","","","","","","","","",aHelp[1,1],aHelp[1,2],aHelp[1,3],"")
	u_SFPUTSX1( p_cPerg, "02","Filial Ate	?			"  ,"","","mv_ch2","C",TamSX3("CN9_FILIAL")[1]	,0,0,"G","","SM0"	,"","","mv_par02","","","","","","","","","","","","","","","","",aHelp[2,1],aHelp[2,2],aHelp[2,3],"")
	u_SFPUTSX1( p_cPerg, "03","Consultor de?			"  ,"","","mv_ch3","C",TamSX3("ADY_VEND")[1]	,0,0,"G","","SA3"	,"","","mv_par03","","","","","","","","","","","","","","","","",aHelp[3,1],aHelp[3,2],aHelp[3,3],"")
	u_SFPUTSX1( p_cPerg, "04","Consultor Ate?			"  ,"","","mv_ch4","C",TamSX3("ADY_VEND")[1]	,0,0,"G","","SA3"	,"","","mv_par04","","","","","","","","","","","","","","","","",aHelp[4,1],aHelp[4,2],aHelp[4,3],"")
	u_SFPUTSX1( p_cPerg, "05","Unidade Executora de?	"  ,"","","mv_ch5","C",TamSX3("ADZ_XUNEXE")[1]	,0,0,"G","","SM0"	,"","","mv_par05","","","","","","","","","","","","","","","","",aHelp[5,1],aHelp[5,2],aHelp[5,3],"")
	u_SFPUTSX1( p_cPerg, "06","Unidade Executora Ate?	"  ,"","","mv_ch6","C",TamSX3("ADZ_XUNEXE")[1]	,0,0,"G","","SM0"	,"","","mv_par06","","","","","","","","","","","","","","","","",aHelp[6,1],aHelp[6,2],aHelp[6,3],"")
	u_SFPUTSX1( p_cPerg, "07","Data Vigencia Inicial?	"  ,"","","mv_ch7","D",8						 		,0,0,"G","",""		,"","","mv_par07","","","","","","","","","","","","","","","","",aHelp[7,1],aHelp[7,2],aHelp[7,3],"")
	u_SFPUTSX1( p_cPerg, "08","Data Vigencia Final?		"  ,"","","mv_ch8","D",8						 		,0,0,"G","",""		,"","","mv_par08","","","","","","","","","","","","","","","","",aHelp[8,1],aHelp[8,2],aHelp[8,3],"")
	
Return( Nil )