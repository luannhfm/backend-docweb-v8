#INCLUDE "TOTVS.CH"

/*/{Protheus.doc} SF06R23X
    @description Imprime relatório indicador de boleto
    @type User Function
    @author Rafael Karczevski
    @since 24/07/2019
    @version 1.0
    /*/
User Function SF06R23X()

	Local oReport
	Local cPerg  := 'SF06R23X'
    Local lTdasFil := .F.
    Private cFilSql := " "
	Private cAliasTMP := GetNextAlias()
    Private nQtdAtr := 0
    Private nQtdTtl := 0

	CriaSX1(cPerg)
	If !Pergunte(cPerg)
		Return
	EndIf

    If MV_PAR01 = 1	
        aSelFil := AdmGetFil(@lTdasFil,.F.,"SE1")
        While Len(aSelFil) = 0
            aSelFil := AdmGetFil(@lTdasFil,.F.,"SE1")
        EndDo		
        cParam  := GetRngFil( aSelFil, "SE1" )
        cFilSql := " E1_FILIAL " +	cParam +" AND "
    EndIf	
    cFilSql := "%" + cFilSql + "%"

	oReport := ReportDef(cPerg)
	oReport:PrintDialog()

	Iif(Select(cAliasTMP) > 0, (cAliasTMP)->(DbCloseArea()), Nil)

Return
        
/*/{Protheus.doc} ReportDef 
    @description Inclusao de colunas. 
    @type Static Function
    @author Rafael Karczevski
    @since 24/07/2019
    @version 1.0
/*/
Static Function ReportDef(p_cPerg)

	Local cTitle  := "Indicador de Boletos"
	Local cHelp   := "Permite gerar relatario de indicador de boleto."
	Local oReport
	Local oSection1
	
	oReport := TReport():New('SF06R23X', cTitle, p_cPerg,{|oReport| ReportPrint(oReport)}, cHelp)
	oReport:ParamReadOnly(.T.)
	oReport:lParamPage 	:= .F.	

	oSection1 := TRSection():New(oReport)

    TRCell():New(oSection1,"FILIAL"     ,, "Filial"      ,,   TamSx3("E1_FILIAL" )[01])
    TRCell():New(oSection1,"PREFIXO"    ,, "Prefixo"     ,,   TamSx3("E1_PREFIXO")[01])
	TRCell():New(oSection1,"NUMERO"     ,, "Titulo"      ,,   TamSx3("E1_NUM"	 )[01])
    TRCell():New(oSection1,"PARCELA"    ,, "Parcela"     ,,   TamSx3("E1_PARCELA")[01])
	TRCell():New(oSection1,"CODCLI"     ,, "Cod Cliente" ,,   TamSx3("E1_CLIENTE")[01])
	TRCell():New(oSection1,"NOMCLI"     ,, "Nom Cliente" ,,   TamSx3("E1_NOMCLI" )[01])
	TRCell():New(oSection1,"VALOR"      ,, "Valor"       ,"@R 99,999.99",   TamSx3("E1_VALOR"  )[01])
    TRCell():New(oSection1,"EMISSAO"    ,, "Emissao"     ,,   13                      )
    TRCell():New(oSection1,"VENCTO"     ,, "Vencimento"  ,,   13                      )
    TRCell():New(oSection1,"DTSOL"      ,, "Dt Sol Emiss",,   13                      )
    TRCell():New(oSection1,"DTGER"      ,, "Dt Get Bolet",,   13                      )
	TRCell():New(oSection1,"DIFERENÇA"  ,, "Dif. (Dias Uteis)"   ,,   05                      )
	
    TRFunction():New(oSection1:Cell("DIFERENÇA"),"", "ONPRINT",,"Numero Atrasados " ,,"nQtdAtr",.t.,.f.,.t.,oSection1,,,)
    TRFunction():New(oSection1:Cell("DIFERENÇA"),"", "ONPRINT",,"Numero No Prazo  " ,,"nQtdTtl-nQtdAtr",.t.,.f.,.t.,oSection1,,,)
    TRFunction():New(oSection1:Cell("DIFERENÇA"),"", "ONPRINT",,"% Atrasado  "      ,,"Round((nQtdAtr/nQtdTtl)*100,2)",.t.,.f.,.t.,oSection1,,,)
    TRFunction():New(oSection1:Cell("DIFERENÇA"),"", "ONPRINT",,"% No Prazo  "      ,,"Round(((nQtdTtl-nQtdAtr)/nQtdTtl)*100,2)",.t.,.f.,.t.,oSection1,,,)

Return oReport

/*/{Protheus.doc} ReportPrint
@description Rotina para montagem dos dados do relatÃ³rio.

@author Rafael Karczevski
@since 09/05/2018
@version 1.0
/*/
Static Function ReportPrint(oReport)
              
	Local oSecao1 := oReport:Section(1)
    Local oSecao2 := oReport:Section(2)
    Local dDtAux  := Date()
    Local nDifBol := 0

    oSecao1:Init()

	BeginSQL Alias cAliasTMP
 	     
        SELECT
            E1_FILIAL,
            E1_PREFIXO,
            E1_NUM,
            E1_PARCELA,
            E1_CLIENTE,
            E1_LOJA,
            E1_NOMCLI,
            E1_VALOR,
            E1_EMISSAO,
            E1_VENCTO,
            ZEF_DTSLEM, 
            ZEF_DTGRBL
        FROM %Table:SE1% SE1
        INNER JOIN %Table:ZEF% ZEF
            ON ZEF_FILIAL = E1_FILIAL
            AND ZEF_PREFIX = E1_PREFIXO
            AND ZEF_NUM = E1_NUM
            AND ZEF_PARCEL = E1_PARCELA
            AND ZEF_TIPO = E1_TIPO
            AND ZEF_EMISSA = E1_EMISSAO
            AND ZEF_CLIENT = E1_CLIENTE
            AND ZEF_LOJA = E1_LOJA
            AND ZEF_DTSLEM <> ' '
            AND ZEF.%NotDel%
        WHERE
            %Exp:cFilSql%
            E1_EMISSAO BETWEEN %Exp:MV_PAR06% AND %Exp:MV_PAR07%
            AND E1_PREFIXO BETWEEN %Exp:MV_PAR02% AND %Exp:MV_PAR03%
            AND E1_NUM BETWEEN %Exp:MV_PAR04% AND %Exp:MV_PAR05%
            AND ((SUBSTR(E1_FILIAL,3,6) = 'MT0001' AND E1_TIPO = 'BOL') OR (SUBSTR(E1_FILIAL,3,6) != 'MT0001' AND ZEF_DTSLEM != ' '))
            AND SE1.%NotDel%
	EndSQL
	MemoWrite('C:\TEMP\SF06R23X.sql', GetLastQuery()[2])
    (cAliasTMP)->(dbGoTop())

    While !(cAliasTMP)->(eof())
        
        oSecao1:Cell("FILIAL"   ):SetValue( (cAliasTMP)->(E1_FILIAL)   )
        oSecao1:Cell("PREFIXO"  ):SetValue( (cAliasTMP)->(E1_PREFIXO)  )
        oSecao1:Cell("NUMERO"   ):SetValue( (cAliasTMP)->(E1_NUM)      )
        oSecao1:Cell("PARCELA"  ):SetValue( (cAliasTMP)->(E1_PARCELA)  )
        oSecao1:Cell("CODCLI"   ):SetValue( (cAliasTMP)->(E1_CLIENTE)  )
        oSecao1:Cell("NOMCLI"   ):SetValue( (cAliasTMP)->(E1_NOMCLI)   )
        oSecao1:Cell("VALOR"    ):SetValue( (cAliasTMP)->(E1_VALOR)    )
        oSecao1:Cell("EMISSAO"  ):SetValue( dToC(sToD((cAliasTMP)->(E1_EMISSAO)))  )
        oSecao1:Cell("VENCTO"   ):SetValue( dToC(sToD((cAliasTMP)->(E1_VENCTO) ))  )
        oSecao1:Cell("DTSOL"    ):SetValue( dToC(sToD((cAliasTMP)->(ZEF_DTSLEM)))  )
        oSecao1:Cell("DTGER"    ):SetValue( dToC(sToD((cAliasTMP)->(ZEF_DTGRBL)))  )
        
        dDtAux := sToD((cAliasTMP)->(ZEF_DTSLEM))
        nDifBol := 0
        While ( dDtAux <> sToD((cAliasTMP)->(ZEF_DTGRBL)) .and. dDtAux <> dDataBase )
            dDtAux := DaySum(dDtAux,1)
            If !(Dow(dDtAux) = 7 .or. Dow(dDtAux) = 1)
                nDifBol++
            EndIf
        End
        oSecao1:Cell("DIFERENÇA"):SetValue( cValToChar(nDifBol) )
        If nDifBol > GetMV("MV_XPRZBOL")
            nQtdAtr++
        EndIf
        nQtdTtl++
        oSecao1:PrintLine()
        (cAliasTMP)->(dbSkip())
    End
    
    (cAliasTMP)->(DbCloseArea())
	oSecao1:Finish()

Return

/*/{Protheus.doc} CriaSX1
@description Funcaoo para criacao das perguntas (se nao existirem)

@author Rafael Karczevski
@since 09/05/2018
@version 1.0
/*/
Static Function CriaSX1(p_cPerg)

	Local aHelp	:= {}
	
	AAdd(aHelp, {{"Seleciona Filiais?, Se selecionar não, o relatório ira considerar todas as filiais."				}, {""}, {""}})
	AAdd(aHelp, {{"Informe o prefixo inicial."              }, {""}, {""}})
	AAdd(aHelp, {{"Informe o prefixo final."                }, {""}, {""}})
	AAdd(aHelp, {{"Informe o numero do titulo inicial"  	}, {""}, {""}})
	AAdd(aHelp, {{"Informe o numero do titulo final"      	}, {""}, {""}})
	AAdd(aHelp, {{"Emissão De"	        				    }, {""}, {""}})
	AAdd(aHelp, {{"Emissão Ate"			        		    }, {""}, {""}})

	u_SFPutSx1( p_cPerg, "01","Seleciona Filial?    		"    , "", "", "mv_ch1", "N",1                          , 0, 0, "C","", ""	    , "", "", "mv_par01","Sim","Si","Yes","","Nao","No","No","","","","","","","","",""                       , aHelp[1,1], aHelp[1,2], aHelp[1,3], "")
	u_SFPutSx1( p_cPerg, "02","Prefixo de?					"    , "", "", "mv_ch2", "C",3							, 0, 0, "G","", ""		, "", "", "mv_par02", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", ""                      , aHelp[2,1], aHelp[2,2], aHelp[2,3], "")
	u_SFPutSx1( p_cPerg, "03","Prefixo Até?					"    , "", "", "mv_ch3", "C",3							, 0, 0, "G","", ""		, "", "", "mv_par03", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", ""                      , aHelp[3,1], aHelp[3,2], aHelp[3,3], "")
	u_SFPutSx1( p_cPerg, "04","Titulo De?					"    , "", "", "mv_ch4", "C",TamSX3("E1_NUM")[1]    	, 0, 0, "G","", ""	    , "", "", "mv_par04", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", ""                      , aHelp[4,1], aHelp[4,2], aHelp[4,3], "")
	u_SFPutSx1( p_cPerg, "05","Titulo Até?					"    , "", "", "mv_ch5", "C",TamSX3("E1_NUM")[1]    	, 0, 0, "G","", ""	    , "", "", "mv_par05", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", ""                      , aHelp[5,1], aHelp[5,2], aHelp[5,3], "")
	u_SFPutSx1( p_cPerg, "06","Emissao de?	    			"    , "", "", "mv_ch6", "D",8							, 0, 0, "G","", ""		, "", "", "mv_par06", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", ""                      , aHelp[6,1], aHelp[6,2], aHelp[6,3], "")
	u_SFPutSx1( p_cPerg, "07","Emissao Até?	    			"    , "", "", "mv_ch7", "D",8							, 0, 0, "G","", ""		, "", "", "mv_par07", "", "", "", "", "", "", "", "", "", "", "", "", "", "", "", ""                      , aHelp[7,1], aHelp[7,2], aHelp[7,3], "")

Return