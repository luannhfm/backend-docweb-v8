#Include 'Protheus.ch'

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³F150ARQ   ºAutor  ³Walmir Junior       º Data ³  27/01/17   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Ponto de Entrada para alterar nome do arquivo na geração   º±±
±±º          ³ de CNAB.                                                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ CNI                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

User Function F150ARQ()
Local _cRet		:= ParamIXB
Local _cdSPC	:= ""
Local _nCont	:= GetMV("MV_XNRMSPC")

//Layout SPC - AAANN0000AAAAMMDDNNNNNNNN.txt
If (AllTrim(MV_PAR03) == "SPC.2RE")
	_cRet		:= SubStr(_cRet,1, Rat("\", _cRet))
	If !Empty(_nCont)
		DBSelectArea("ZF3")
		ZF3->(DbSetOrder(1))
		If 	ZF3->(DbSeek(cFilAnt)) // Walmir Junior 21/03/2017 -  [xFilial("ZF3") +] Alterado o Seek, pois o indice de desenvolvimento estava diferente do indice de Homologação. 
			If !Empty(ZF3->(ZF3_CDARQ))
				_cdSPC := ZF3->(ZF3_CDARQ)
			Else
				Aviso("Atenção","Você precisa informar o campo 'Cod Arq' na rotina 'Filial X Entidade X Assoc'.", {"Ok"})
			EndIf
		Else
			Aviso("Atenção","Configure o relacionamento 'Filial X Entidade X Assoc' para a Filial logada ["+cFilAnt+"].", {"Ok"})
		EndIf 
			
		_cRet += _cdSPC + "0000" + AllTrim(Str(Year(Date()))) + AllTrim(StrZero(Month(Date()),2)) +;
									AllTrim(Str(Day(Date()))) + AllTrim(StrZero(_nCont, 8))+".TXT"
		
		_nCont++
		
		PutMV("MV_XNRMSPC",_nCont)
		
		Aviso("Atenção","Atenção! O arquivo CNAB SPC foi gerado com o nome ["+_cRet+"].", {"Ok"})
	Else
		Aviso("Atenção","Para geração de arquivos CNAB SPC, você deve informar o parâmetro [MV_XNRMSPC] por filial.", {"Ok"})
	EndIf	
EndIf

Return Upper(_cRet)