#INCLUDE "PROTHEUS.CH"

//------------------------------------------------------
/*/{Protheus.doc} F060SEA2
Grava na geração de Borderô.

@author Allan da Silva Faria
@since 25/05/2016
@version 1.0
/*/
//------------------------------------------------------
User Function F060SEA2()

	Local  _aHistCob := Array(9)
	Local  cSitDR    := GetNewPar("MV_XPCSDR")
	Local  lCobTer   := .F.
	Local  cOrig     := ""
	Public _cNumDR   := ""
	Public _cSituaca := ""

	If !IsBlind()

		If (SEA->EA_SITUACA == cSitDR) .Or. (SEA->EA_SITUACA == "8")
			_cSituaca := SEA->EA_SITUACA
			_cNumDR   := SEA->EA_NUMBOR
		EndIf

		If Inclui
			If nOpca = 1 .And. !Empty(_cNumDR)
				lCobTer := .T.
			EndIf
		EndIf

		Iif(!Empty(_cNumDr),cOrig := "SF06A07X",cOrig := "FINA060B")

			//--------------------------------------------------
			//-- Grava Historico de Cobranla - Emissão Borderô
			///-------------------------------------------------
			_aHistCob[1] := SE1->E1_CLIENTE
			_aHistCob[2] := SE1->E1_LOJA
			_aHistCob[3] := SE1->E1_PREFIXO
			_aHistCob[4] := SE1->E1_NUM
			_aHistCob[5] := SE1->E1_PARCELA
			_aHistCob[6] := SE1->E1_TIPO
			_aHistCob[7] := StoD("//")

			If lCobTer

				_aHistCob[8] := "TÍTULO ENVIADO PARA COBRANÇA TERCEIRIZADA " + CRLF
				_aHistCob[8] += "Data do Envio: " + DToC(dDataBase)          + CRLF
				_aHistCob[9] := "01"

			Else

				_aHistCob[8] := "EMISSÃO BORDERÔ"     + CRLF
				_aHistCob[8] += "Numero: "            + SEA->EA_NUMBOR + CRLF
				_aHistCob[8] += "Situação Atual: "    + SEA->EA_SITUACA + " " + FWGetSX5("07", PADR(SEA->EA_SITUACA,6))[1, 4]+ CRLF
				_aHistCob[8] += "Situação Anterior: " + SEA->EA_SITUANT + " " + FWGetSX5("07", PADR(SEA->EA_SITUANT,6))[1, 4]+ CRLF
				
				If .Not. Empty(cPort060)
					_aHistCob[8] += "Portador: "      + SEA->EA_PORTADO + CRLF
					_aHistCob[8] += "Agência: "       + SEA->EA_AGEDEP + CRLF
					_aHistCob[8] += "Conta: "         + SEA->EA_NUMCON + CRLF
				EndIf

				//_aHistCob[9]:= StaticCall(FA60CAN2,GetNextSeq,"FINA060B")

			EndIf

			//---------------------------------------
			//-- Chama Função que grava historico
			//---------------------------------------
			U_SF06A74X(_aHistCob,cOrig)

	EndIf

Return Nil
