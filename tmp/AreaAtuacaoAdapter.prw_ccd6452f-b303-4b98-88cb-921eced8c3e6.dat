#include 'totvs.ch'
#include 'parmtype.ch'

/*/{Protheus.doc} AreaAtuacaoAdapter
Classe Adapter para o serviço
@author  Sergio Salustiano
@since   29/04/2021
@version 1.0
/*/

CLASS areaatuacaoAdapter FROM FWAdapterBaseV2
	METHOD New()
	METHOD getListareaatuacao()
EndClass


/*/{Protheus.doc} New
Método construtor
@param cVerb, verbo HTTP utilizado
@author  Sergio Salustiano
@since   29/04/2020
@version 1.0
/*/

Method New( cVerb ) CLASS areaatuacaoAdapter
	_Super:New( cVerb, .T. )
return


/*/{Protheus.doc} GetListProd
Método que retorna uma lista de produtos 
@author  Sergio Salustiano
@since   01/09/2020
@version 1.0
/*/

Method getListareaatuacao( ) CLASS areaatuacaoAdapter
	Local aArea 	AS ARRAY
	Local cWhere	AS CHAR
	aArea   := FwGetArea()
	AddMapFields( self )
	::SetQuery( GetQuery() )
	cWhere := " ZDG_FILIAL = '"+ FWxFilial('ZDG') +"' AND ZDG.D_E_L_E_T_ = ' '"
	::SetWhere( cWhere )
	::SetOrder( "ZDG_CODIGO" )
	//Executa a consulta, se retornar .T. tudo ocorreu conforme esperado
	If ::Execute() 
		// Gera o arquivo Json com o retorno da Query
		::FillGetResponse()
	EndIf
	FwrestArea(aArea)
RETURN

/*/{Protheus.doc} AddMapFields
Função para geração do mapa de campos
@param oSelf, object, Objeto da prórpia classe
@author  Sergio Salustiano
@since   29/04/2021
@version 1.0
/*/
//-------------------------------------------------------------------
Static Function AddMapFields( oSelf )
		
	oSelf:AddMapFields( 'AREADEATUACAO'	    , 'ZDG_CODIGO'    , .T., .F., { 'ZDG_CODIGO', 'C', TamSX3( 'ZDG_CODIGO' )[1], 0 } )
	oSelf:AddMapFields( 'DESCAREASITE'      , 'ZDG_AREAAT'    , .T., .F., { 'ZDG_AREAAT', 'M', TamSX3( 'ZDG_AREAAT' )[1], 0 } )
	oSelf:AddMapFields( 'DESCARAEATUACAO'   , 'ZDG_AREA'      , .T., .T., { 'ZDG_AREA'  , 'C', TamSX3( 'ZDG_AREA' )  [1], 0 } )
	oSelf:AddMapFields( 'ATIVO'	            , 'ZDG_ATIVO'     , .T., .F., { 'ZDG_ATIVO' , 'C', TamSX3( 'ZDG_ATIVO' ) [1], 0 } )	
	
	
Return 

/*/{Protheus.doc} GetQuery
Retorna a query usada no serviço
@param oSelf, object, Objeto da prórpia classe
@author  Sergio Salustiano
@since   29/04/2021
@version 1.0
/*/
//-------------------------------------------------------------------
Static Function GetQuery()
	Local cQuery AS CHARACTER
	
	cQuery := " SELECT #QueryFields#"
    cQuery +=   " FROM " + RetSqlName( 'ZDG' ) + " ZDG "
    cQuery += " WHERE #QueryWhere#"
	
Return cQuery
