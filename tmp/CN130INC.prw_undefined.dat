#INCLUDE "RWMAKE.CH"
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³ CN130INC ³ Autor ³  Cadubitski           ³ Data ³ Mai/2010 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Ponto de entrada executado no momeno da inclusao das        ³±±
±±³          ³medicoes de contrato.                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

User Function CN130Inc()

Local nx
Local aHeader 	:= ParamIXB[1]
Local aCols 	:= ParamIXB[2]
Local nPosDtEnt	:= aScan(aHeader,{|x| AllTrim(x[2]) == 'CNE_DTENT'})
Local nPosItm   := aScan(aHeader,{|x| AllTrim(x[2]) == 'CNE_ITEM'})
Local cQuery    := ''
Local cAlias    := GetNextAlias()
Local nPos

//Filtra os itens da planilha
cQuery := "SELECT CNB.CNB_ITEM,CNB.CNB_XPRZ "
cQuery += "FROM "+RetSQLName("CNB")+" CNB WHERE "
cQuery += "CNB.CNB_FILIAL = '"+xFilial('CNB')+"' AND "
cQuery += "CNB.CNB_CONTRA = '"+M->CND_CONTRA+"' AND "
cQuery += "CNB.CNB_REVISA = '"+M->CND_REVISA+"' AND "
cQuery += "CNB.CNB_NUMERO = '"+M->CND_NUMERO+"' AND "
cQuery += "CNB.D_E_L_E_T_ = ' '"

//Executa query
cQuery := ChangeQuery(cQuery)
dbUseArea( .T., 'TOPCONN', TcGenQry( ,, cQuery ), cAlias, .F., .T. )

//Replica informacao de campos criados na CNB para os campos criados na CNE
While !(cAlias)->(Eof())
	If (nPos := aScan(aCols,{|x| x[nPosItm] == (cAlias)->CNB_ITEM})) > 0
  		aCols[nPos,nPosDtEnt] := dDataBase+(cAlias)->CNB_XPRZ
 	EndIf

 	(cAlias)->(dbSkip())
EndDo

Return {aHeader,aCols}

