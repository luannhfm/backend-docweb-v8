#INCLUDE "PROTHEUS.CH"
#INCLUDE "RWMAKE.CH"
#INCLUDE "FONT.CH"
#INCLUDE "COLORS.CH"
#INCLUDE "APWIZARD.CH"
#INCLUDE "TBICONN.CH"
#INCLUDE "TOPCONN.CH"
/*
+--------------------------------------------------------------------------+
|{Protheus.doc}<SN0632X>                                                   |
| 302 - Relatório de Pagamentos - Assistência PRONATEC - Geral             |
|                                                                          |
|@Author<Antonio Dantas>                                                   |
|@since<23/06/2014>                                                        |
|@version<Nil>                                                             |
|                                                                          |
|@parameters<Nil>                                                          |
|@return<Nil>                                                              |
+--------------------------------------------------------------------------+
|Uso        |FIEMT - Federacao das Industrias no Estado de Mato Grosso     |
+-----------+--------------------------------------------------------------+
*/
User Function SN0632X()
Local _oReport    
Private _lCancel	:= .f.  
Private _cPerg 		:= FunName()		//-- Define o Grupo de Perguntas   
Private _cAlias		:= GetNextAlias() 
 
//-- **************************************************************
//--   Interface de impressao
//-- **************************************************************
_oReport := ReportDef()
If !_lCancel
	_oReport:PrintDialog()
Endif	    
Return


/*
+--------------------------------------------------------------------------+
|{Protheus.doc}<ReportDef>                                                 |
|302 - Relatório de Pagamentos - Assistência PRONATEC - Geral			   |
|                                                                          |
|@Author<Antonio Dsantas>                                                  |
|@since<28/03/2014>                                                        |
|@version<Nil>                                                             |
|                                                                          |
|@parameters<Nil>                                                          |
|@return<Nil>                                                              |
+--------------------------------------------------------------------------+
|Uso        |SENAI										     			   |
+-----------+--------------------------------------------------------------+
*/
Static Function ReportDef()
Local _cTitulo		:= "302 - Relatório de Pagamentos - Assistência PRONATEC - Geral"
Local _cSubTit		:= "Detalhes dos Pagamentos Pronatec"  
Local _oReport		:= Nil
Local _oAlunos		:= Nil
Local _oSubTot		:= Nil
Local _oTotStat		:= Nil
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//| Carrega as perguntas relativas aos parametros de Impressao          |
//|   Mv_par01 - Periodo DE?                                           |
//|   Mv_par02 - Periodo ATE?                                           |
//|   Mv_par03 - Data de Pgto DE ?                                      |
//|   Mv_par04 - Data de Pgto ATE ?                                     |
//|   Mv_par05 - Codigo da Matricula DE ?                               |
//|   Mv_par06 - Codigo da Matricula ATE ?                              |
//|   Mv_par07 - Tipo(s) de Pagamento ?                                 |
//|   Mv_par08 - Codigo de Evento DE ?                                  |
//|   Mv_par09 - Codigo de Evento ATE ?                                 |
//|   Mv_par10 - Codigo da Modalidade DE ?                              |
//|   Mv_par11 - Codigo da Modalidade ATE ?                             |
//|   Mv_par12 - Codigo da Area Tecnolog DE ?                           |
//|   Mv_par13 - Codigo da Area Tecnolog ATE ?                          |
//|   Mv_par14 - Codigo do Curso DE ?                                   |
//|   Mv_par15 - Codigo do Curso ATE ?                                  |
//|   Mv_par16 - Com Seguinte(s) Status ?                               |
//|   Mv_par17 - Do(s) Turno(s): (Matutino,Verpertino,Diurno,Noturno)   |  
//|   Mv_par18 - Sessao De ?                                            |
//|   Mv_par19 - Sessao Ate ?                                           |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
xParPerg(_cPerg)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//|  Monta tela de paramentos para usuario, se cancelar sair                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Pergunte(_cPerg,.t.)  
	_lCancel := .T. 
   Return
Endif 
//-- *********************************************************************
//--   Seleciona a tabela temporaria que sera referencia 
//-- *********************************************************************
FWMsgRun(, {|| u_fLerZP7() }, "Processando a Consulta das Matriculas!", "Aguarde...")
//-- ************************************************************************
//--    Construcao do Objeto de Impressao do Relatorio
//-- ************************************************************************
//                    New(cReport,cTitle,uParam,bAction,cDescription,lLandscape,uTotalText,lTotalInLine,cPageTText,lPageTInLine,lTPageBreak,nColSpace)
_oReport := TReport():New(_cPerg,_cTitulo,_cPerg,{|_oReport|  ReportPrint(_oReport) },"",.t.)
_oReport:SetLandScape() 			//-- Modo Paisagem 
//-- *********************************************************************************************************
//--    *****      S e c a o  -  1      *****
//-- *********************************************************************************************************
_oAlunos := TRSection():New(_oReport,"Matriculas",{"TRBZP7"} )
TRCell():New(_oAlunos,"ZP7_XMATRI"	,"ZP7" 	,"Matric"  		,"@!"	,7							,.F.)
TRCell():New(_oAlunos,"ZP7_XNOME"	,"ZP7" 	,"Nome"			,"@!"   ,TamSX3("ZP7_XNOME")[1]		,.F.)
TRCell():New(_oAlunos,"ZP7_XCPF"	,"ZP7" 	,"CPF"			,"@R 999.999.999-99"		,20		,.F.)
TRCell():New(_oAlunos,"ZP7_XDESCS"	,"ZP7" 	,"Descr Curso"  ,"@!"   ,40							,.F.)
TRCell():New(_oAlunos,"ZP7_XDESMO"	,"ZP7" 	,"Descr Modali"	,"@!"   ,25							,.F.)
TRCell():New(_oAlunos,"ZP7_XTURNO"	,"ZP7" 	,"Turno"	   	,"@!"   ,TamSX3("ZP7_XTURNO")[1]	,.F.)
//--
TRCell():New(_oAlunos,"ZP7_XQTDE"	,"ZP7" 	,"Quantidade"	,"@E 999,999,999.99"   	,15			,.F.)
TRCell():New(_oAlunos,"ZP7_XVALOR"	,"ZP7" 	,"Vlr Benefic"	,"@E 999,999,999.99"	,15			,.F.)
TRCell():New(_oAlunos,"ZP7_XPERIO"	,"ZP7" 	,"Per/Seq/Ses"	,"@!"	,30							,.F.)
//--
TRCell():New(_oAlunos,"ZTIPO"		,"" 	,"Tp"  			,"@!"   ,1							,.F.)
TRCell():New(_oAlunos,"ZP7_XDATAG"	,"ZP7" 	,"Data Geracao"	,"@!"   ,TamSX3("ZP7_XDATAG")[1]	,.F.)
TRCell():New(_oAlunos,"ZP7_XDTLIB"	,"ZP7" 	,"Data Liberac"	,"@!"   ,TamSX3("ZP7_XDTLIB")[1]	,.F.)
TRCell():New(_oAlunos,"ZPA_XDATA"	,"ZPA" 	,"Data Remessa"	,"@!"   ,TamSX3("ZPA_XDATA")[1]		,.F.)
TRCell():New(_oAlunos,"ZP7_XDTPG"	,"ZP7" 	,"Dt.Pagamento"	,"@!"   ,TamSX3("ZP7_XDTPG")[1]		,.F.)
TRCell():New(_oAlunos,"ZSTATU"		,"" 	,"Sit"			,"@!"   ,1							,.F.)  
//-- *********************************************************************************************************
//--    *****      S e c a o  -  2      *****
//-- *********************************************************************************************************
_oSubTot := TRSection():New(_oReport,"Total por Status",{"TRBZP7"} )
TRCell():New(_oSubTot,"ZP7_XMATRI"	,"ZP7" 	,"Matric" 		,"@!"	,7							,.F.)
TRCell():New(_oSubTot,"ZP7_XNOME"	,"ZP7" 	,"Nome"			,"@!"   ,TamSX3("ZP7_XNOME")[1]		,.F.)
TRCell():New(_oSubTot,"ZP7_XCPF"	,"ZP7" 	,"CPF"			,"@!" 	,20	  		,.F.)
TRCell():New(_oSubTot,"ZP7_XDESCS"	,"ZP7" 	,"Descr Curso"  ,"@!"   ,40							,.F.)
TRCell():New(_oSubTot,"ZP7_XDESMO"	,"ZP7" 	,"Descr Modali"	,"@!"   ,25							,.F.)
TRCell():New(_oSubTot,"ZP7_XTURNO"	,"ZP7" 	,"Turno"	   	,"@!"   ,TamSX3("ZP7_XTURNO")[1]	,.F.)
//--
TRCell():New(_oSubTot,"ZP7_XQTDE"	,"ZP7" 	,"Quantidade"	,"@E 999,999,999.99"   	,15			,.F.)
TRCell():New(_oSubTot,"ZP7_XVALOR"	,"ZP7" 	,"Vlr Benefic"	,"@E 999,999,999.99"	,15			,.F.)
TRCell():New(_oSubTot,"ZP7_XPERIO"	,"ZP7" 	,"Per/Seq/Ses"	,"@!"	,30							,.F.)
//--
TRCell():New(_oSubTot,"ZTIPO"		,"" 	,"Tp"			,"@!"   ,1							,.F.)
TRCell():New(_oSubTot,"ZP7_XDATAG"	,"ZP7" 	,"Data Geracao"	,"@!"   ,TamSX3("ZP7_XDATAG")[1]	,.F.)
TRCell():New(_oSubTot,"ZP7_XDTLIB"	,"ZP7" 	,"Data Liberac"	,"@!"   ,TamSX3("ZP7_XDTLIB")[1]	,.F.)
TRCell():New(_oSubTot,"ZPA_XDATA"	,"ZPA" 	,"Data Remessa"	,"@!"   ,TamSX3("ZPA_XDATA")[1]		,.F.)
TRCell():New(_oSubTot,"ZP7_XDTPG"	,"ZP7" 	,"Dt.Pagamento"	,"@!"   ,TamSX3("ZP7_XDTPG")[1]		,.F.)
TRCell():New(_oSubTot,"ZSTATU"		,"" 	,"Sit"			,"@!"   ,1							,.F.)  
_oSubTot:lHeaderSection := .f.  
_oSubTot:SetHeaderSection(.f.)
//-- *********************************************************************************************************
//--    *****      S e c a o  -  3      *****
//-- *********************************************************************************************************
_oTotStat := TRSection():New(_oReport,"Total GERAL",{"TRBZP7"} )
TRCell():New(_oTotStat,"ZP7_XMATRI"	,"ZP7" 	,"Matric"  		,"@!"	,7							,.F.)
TRCell():New(_oTotStat,"ZP7_XNOME"	,"ZP7" 	,"Nome"			,"@!"   ,TamSX3("ZP7_XNOME")[1]		,.F.)
TRCell():New(_oTotStat,"ZP7_XCPF"	,"ZP7" 	,"CPF"			,"@!" 	,20	  		,.F.)
TRCell():New(_oTotStat,"ZP7_XDESCS"	,"ZP7" 	,"Descr Curso"  ,"@!"   ,40							,.F.)
TRCell():New(_oTotStat,"ZP7_XDESMO"	,"ZP7" 	,"Descr Modali"	,"@!"   ,25							,.F.)
TRCell():New(_oTotStat,"ZP7_XTURNO"	,"ZP7" 	,"Turno"	   	,"@!"   ,TamSX3("ZP7_XTURNO")[1]	,.F.)
//--
TRCell():New(_oTotStat,"ZP7_XQTDE"	,"ZP7" 	,"Quantidade"	,"@E 999,999,999.99"   	,15			,.F.)
TRCell():New(_oTotStat,"ZP7_XVALOR"	,"ZP7" 	,"Vlr Benefic"	,"@E 999,999,999.99"	,15			,.F.)
TRCell():New(_oTotStat,"ZP7_XPERIO"	,"ZP7" 	,"Per/Seq/Ses"	,"@!"	,30							,.F.)
//--
TRCell():New(_oTotStat,"ZTIPO"		,"" 	,"Tp"			,"@!"   ,1							,.F.)
TRCell():New(_oTotStat,"ZP7_XDATAG"	,"ZP7" 	,"Data Geracao"	,"@!"   ,TamSX3("ZP7_XDATAG")[1]	,.F.)
TRCell():New(_oTotStat,"ZP7_XDTLIB"	,"ZP7" 	,"Data Liberac"	,"@!"   ,TamSX3("ZP7_XDTLIB")[1]	,.F.)
TRCell():New(_oTotStat,"ZPA_XDATA"	,"ZPA" 	,"Data Remessa"	,"@!"   ,TamSX3("ZPA_XDATA")[1]		,.F.)
TRCell():New(_oTotStat,"ZP7_XDTPG"	,"ZP7" 	,"Dt.Pagamento"	,"@!"   ,TamSX3("ZP7_XDTPG")[1]		,.F.)
TRCell():New(_oTotStat,"ZSTATU"		,"" 	,"Sit"			,"@!"   ,1							,.F.)  
_oTotStat:lHeaderSection := .f.  
_oTotStat:SetHeaderSection(.f.)
//-- *********************************************************************************************************
//--    Centralizacao a Direita Variaveis do tipo Valor dentro do objeto (container).
//-- *********************************************************************************************************
_oAlunos:Cell("ZP7_XQTDE"):SetHeaderAlign("RIGHT")
_oAlunos:Cell("ZP7_XVALOR"):SetHeaderAlign("RIGHT")
//--
_oSubTot:Cell("ZP7_XQTDE"):SetHeaderAlign("RIGHT")
_oSubTot:Cell("ZP7_XVALOR"):SetHeaderAlign("RIGHT")
//--
_oTotStat:Cell("ZP7_XQTDE"):SetHeaderAlign("RIGHT")
_oTotStat:Cell("ZP7_XVALOR"):SetHeaderAlign("RIGHT")
//--
_oAlunos:SetLineBreak(.f.)		//-- Quebra de linha automatica
Return _oReport


/*
+--------------------------------------------------------------------------+
|{Protheus.doc}<ReportPrint>                                               |
|Relatorio de Cadastro de Cartao Beneficio.    							   |
|                                                                          |
|@Author<Antonio Dsantas>                                                  |
|@since<28/03/2014>                                                        |
|@version<Nil>                                                             |
|                                                                          |
|@parameters<Nil>                                                          |
|@return<Nil>                                                              |
+--------------------------------------------------------------------------+
|Uso        |SENAI										     			   |
+-----------+--------------------------------------------------------------+
*/
Static Function ReportPrint(_oReport)
Local _oAlunos		:= _oReport:Section(1)
Local _oSubTot		:= _oReport:Section(2)
Local _oTotStat		:= _oReport:Section(3)  
Local _nTipo  		:= 0              
Local _nP			:= 0   
Local _cTipo		:= ""
Local _cStatus		:= ""         
Local _nCtaA		:= 0
Local _nCtaB 		:= 0    
Local _nCtaR		:= 0
Local _nCtaM		:= 0
Local _nRecs		:= 0 
Local _cCtaAlu		:= 0    
Local _cChvZPA		:= ""
//-- Variaveis de controle dos totais por TIPOS
Local _PTpS  	:= 0  		//-- Posicao do TIPO Sub TotaL
Local _aTipSub	:= {}		//-- Array com Sub Totais por TIPO   
Local _PTpT 	:= 0		//-- Posicao do TIPO Total 
Local _aTipTot	:= {} 		//-- Array com Totais por TIPO   
//--
Local _PStS 	:= 0		//-- Posicao do STATUS Sub Total  
Local _aStaSub	:= {}  		//-- Array com Sub Totais por STATUS 
Local _PStT 	:= 0		//-- Posicao do STATAS Total 
Local _aStaTot	:= {}		//-- Array com Totais por STATUS 
//--
Local _aSubTots	:= {}		//-- Array com Sub Totais Para Impressao 
Local _aTotais	:= {}		//-- Array com Totais Para Impressao 
Local _aTiposPG 	:={"Ordem de Pagamento","Pagamento Manual","Nao gera Pagamento"}
Local _cRangeST 	:= "BTCLxEORPND"
Local _aStatus 		:={	{ "B", "Bloqueado p/Remessa"	},;
						{ "T", "Transf. p/Prox Sessao"	},;
						{ "C", "Corrigido"				},;
						{ "L", "Liberado p/Remessa"		},;
						{ "x", "Não Liberado p/Remessa"	},;
						{ "E", "Remessa Gerada"			},;
						{ "O", "Ordem de Pgto Agendada"	},;
						{ "R", "Rejeitado"				},;
						{ "P", "Pago"					},;
						{ "N", "Não Pago"				},;
						{ "D", "Devolvido"				} }
//****************************************
//-- Inicializa as sessoes 
//****************************************
_oAlunos:Init()
_oSubTot:Init()
_oTotStat:Init()      
//**************************************************************************
//-- Posiciona no inicio o Arquivo Resultado da Consulta Sql 
//**************************************************************************
dbSelectArea(_cAlias)  
(_cAlias)->(dbgotop())    
//-- Conta o numero do Registro da Tabela Temporaria
(_cAlias)->(dbEval({|| _nRecs++ }))
_oReport:SetMeter(_nRecs)
//-- 
dbSelectArea(_cAlias)  
(_cAlias)->(dbgotop())    
_cMatric	:= (_cAlias)->ZP7_XMATRI
_lImpSTot 	:= .f.  
//**************************************************************************
//-- Inicia Laco para impressao do Relatorio
//**************************************************************************
Do While (_cAlias)->(!eof())   
	_oReport:IncMeter()    
	_nCtaR++
	_nCtaM++
	//-- *********************************************************************************************************
	//--    *****      S e c a o  -  1: Contratos PAI - ZG1     *****
	//-- *********************************************************************************************************
	//-- Define a possicao para descricao do Tipo            
	_nTipo		:= Val(Posicione("ZP1",1,FwxFilial("ZP1")+(_cAlias)->ZP7_XCONVE,"ZP1_XTIPO"))
	//-- Define a possicao para descricao do Status "Situacao"
	_nP			:= At((_cAlias)->ZP7_XSTATU,_cRangeST)  
	//-- 	
	_cTipo		:= Alltrim(Str(_nTipo))+"-"+_aTiposPG[_nTipo]
	_cStatus	:= If(_nP>0,_aStatus[_nP,1]+"-"+_aStatus[_nP,2],"DESCONHECIDO")  
	_cChvZPA	:= (_cAlias)->ZP7_FILIAL+(_cAlias)->ZP7_XPERIO+(_cAlias)->ZP7_XSEG+(_cAlias)->ZP7_XSESSA+(_cAlias)->ZP7_XCONVE+"PG"	
		
	dbSelectArea("ZPA")
	ZPA->(dbSetOrder(2))		//-- Cod Periodo+Seg Calendar+Cod Sessao+Convenio+Tp Remessa
	ZPA->(dbSeek(_cChvZPA))
	
	//-- ********************************************************************
 	//--   Imprime detalhe
	//-- ********************************************************************
	_oAlunos:Cell( "ZP7_XMATRI"	):SetBlock({|| Alltrim((_cAlias)->ZP7_XMATRI)  })
	_oAlunos:Cell( "ZP7_XNOME"	):SetBlock({|| (_cAlias)->ZP7_XNOME   	  	})
	_oAlunos:Cell( "ZP7_XCPF"	):SetBlock({|| 	(_cAlias)->ZP7_XCPF			})
	_oAlunos:Cell( "ZP7_XDESCS"	):SetBlock({|| (_cAlias)->ZP7_XEVENT+"-"+Substr((_cAlias)->ZP7_XDESCS,1,40)	})     
	_oAlunos:Cell( "ZP7_XDESMO"	):SetBlock({|| (_cAlias)->ZP7_XMODAL+"-"+Substr((_cAlias)->ZP7_XDESMO,1,40)  	})
	_oAlunos:Cell( "ZP7_XTURNO"	):SetBlock({|| (_cAlias)->ZP7_XTURNO   	})
	//--
	_oAlunos:Cell( "ZP7_XQTDE"	):SetBlock({|| (_cAlias)->ZP7_XQTDE 		})
	_oAlunos:Cell( "ZP7_XVALOR"	):SetBlock({|| (_cAlias)->ZP7_XVALOR		})
	_oAlunos:Cell( "ZP7_XPERIO"	):SetBlock({|| (_cAlias)->ZP7_XPERIO+"/"+(_cAlias)->ZP7_XSEG+"/"+(_cAlias)->ZP7_XSESSA	})
	//--
	_oAlunos:Cell( "ZTIPO"	):SetBlock({|| (_cAlias)->ZP7_XTIPO	  		})
	_oAlunos:Cell( "ZP7_XDATAG"	):SetBlock({|| DTOC((_cAlias)->ZP7_XDATAG)	})
	_oAlunos:Cell( "ZP7_XDTLIB"	):SetBlock({|| DTOC((_cAlias)->ZP7_XDTLIB)	})
	_oAlunos:Cell( "ZPA_XDATA"	):SetBlock({|| DTOC(ZPA->ZPA_XDATA)		})
	_oAlunos:Cell( "ZP7_XDTPG"	):SetBlock({|| DTOC((_cAlias)->ZP7_XDTPG)	})
	_oAlunos:Cell( "ZSTATU"	):SetBlock({|| (_cAlias)->ZP7_XSTATU	  		})
	_oAlunos:PrintLine()  
	
	//-- ********************************************************************
	//--    Conta o Numero de Lancamentos da Matricula para Impressao 
	//--    dos Sub Totais
	//-- ********************************************************************
	_cCtaAlu++
	//-- ********************************************************************
 	//--   TOTLIZACOES POR TIPO
	//-- ********************************************************************
	//-- Sub- Total por TIPO
	_PTpS := aScan(_aTipSub,{|x| x[1]==_cTipo})     
	If 	_PTpS > 0 
		_aTipSub[_PTpS,2] := _aTipSub[_PTpS,2]+(_cAlias)->ZP7_XQTDE
		_aTipSub[_PTpS,3] := _aTipSub[_PTpS,3]+(_cAlias)->ZP7_XVALOR
	Else
		aAdd(_aTipSub, {_cTipo,(_cAlias)->ZP7_XQTDE,(_cAlias)->ZP7_XVALOR})
	Endif 
	//-- Total por TIPO 
	_PTpT := aScan(_aTipTot,{|x| x[1]==_cTipo})     
	If 	_PTpT > 0 
		_aTipTot[_PTpT,2] := _aTipTot[_PTpT,2]+(_cAlias)->ZP7_XQTDE
		_aTipTot[_PTpT,3] := _aTipTot[_PTpT,3]+(_cAlias)->ZP7_XVALOR
	Else
		aAdd(_aTipTot, {_cTipo,(_cAlias)->ZP7_XQTDE,(_cAlias)->ZP7_XVALOR})
	Endif 
	//-- ********************************************************************
 	//--   TOTLIZACOES POR STATUS "SITUACAO"
	//-- ********************************************************************
	//-- Sub Total por STATUS "Situacao" 
	_PStS := aScan(_aStaSub,{|x| x[1]==_cStatus})     
	If 	_PStS > 0 
		_aStaSub[_PStS,2] := _aStaSub[_PStS,2]+(_cAlias)->ZP7_XQTDE
		_aStaSub[_PStS,3] := _aStaSub[_PStS,3]+(_cAlias)->ZP7_XVALOR
	Else
		aAdd(_aStaSub, {_cStatus,(_cAlias)->ZP7_XQTDE,(_cAlias)->ZP7_XVALOR})
	Endif 
	//-- Total por STATUS "Situacao" 
	_PStT := aScan(_aStaTot,{|x| x[1]==_cStatus})     
	If _PStT > 0 
		_aStaTot[_PStT,2] := _aStaTot[_PStT,2]+(_cAlias)->ZP7_XQTDE
		_aStaTot[_PStT,3] := _aStaTot[_PStT,3]+(_cAlias)->ZP7_XVALOR
	Else
		aAdd(_aStaTot, {_cStatus,(_cAlias)->ZP7_XQTDE,(_cAlias)->ZP7_XVALOR})
	Endif 
	//-- ********************************************************************
	//--    Vai para o Proximo Registro da ZP7 
	//-- ********************************************************************
	(_cAlias)->(dbskip()) 	
	//--  Controle de Troca de Matricula 
	If (_cAlias)->ZP7_XMATRI != _cMatric
		_cMatric	:= (_cAlias)->ZP7_XMATRI
		_lImpSTot	:= .t.  
	Endif  
	If _lImpSTot   
		//-- *********************************************************************************************************
		//--    *****      S e c a o  -  2: Sub-Total da Matricula     *****
		//-- *********************************************************************************************************
		_aSubTots := {_aTipSub,_aStaSub}
		_nCtaA := 0
		//-- ********************************************************************
		//--    So imprime os Sub Totais se houver mais de um lancamento para 
		//--    mesma Matricula
		//-- ********************************************************************
		If 	_cCtaAlu > 1
			//-- ********************************************************************
			//--    Monta a quantidade de Matriculas para Impressao
			//-- ********************************************************************
			aAdd(_aSubTots, {{"TOTAL de Matriculas",_nCtaM,0.00}})
			_nCtaM := 0
			For _nCtaA := 1 to Len(_aSubTots)	
				_nCtaB := 0
				For _nCtaB := 1 to Len(_aSubTots[_nCtaA])	
					//-- ********************************************************************
				 	//--   Imprime detalhe
					//-- ********************************************************************
					_oSubTot:Cell( "ZP7_XMATRI"	):SetBlock({|| ""	})
					_oSubTot:Cell( "ZP7_XNOME"	):SetBlock({|| ""	})
					_oSubTot:Cell( "ZP7_XCPF"	):SetBlock({|| ""	})
					_oSubTot:Cell( "ZP7_XDESCS"	):SetBlock({|| ""	})
					_oSubTot:Cell( "ZP7_XDESMO"	):SetBlock({|| _aSubTots[_nCtaA,_nCtaB,1]	})
					_oSubTot:Cell( "ZP7_XTURNO"	):SetBlock({|| ""	})
					//--
					_oSubTot:Cell( "ZP7_XQTDE"	):SetBlock({|| _aSubTots[_nCtaA,_nCtaB,2]	})
					_oSubTot:Cell( "ZP7_XVALOR"	):SetBlock({|| _aSubTots[_nCtaA,_nCtaB,3]	})
					_oSubTot:Cell( "ZP7_XPERIO"	):SetBlock({|| ""	})
					//--
					_oSubTot:Cell( "ZTIPO"	):SetBlock({|| ""		})
					_oSubTot:Cell( "ZP7_XDATAG"	):SetBlock({|| ""	})
					_oSubTot:Cell( "ZP7_XDTLIB"	):SetBlock({|| ""	})
					_oSubTot:Cell( "ZPA_XDATA"	):SetBlock({|| ""	})
					_oSubTot:Cell( "ZP7_XDTPG"	):SetBlock({|| ""	})
					_oSubTot:Cell( "ZSTATU"	):SetBlock({|| ""		})
					_oSubTot:PrintLine()
				Next _nCtaB 
		   		_oReport:SkipLine()
			Next _nCtaA        
			//-- ********************************************************************
		 	//--    Salta duas Linhas apos imprimir sub-total
			//-- ********************************************************************
			_oReport:SkipLine()
			_oReport:SkipLine()   
		Endif 
		_lImpSTot := .f.
		//-- ZERA os sub-totais   
		_aTipSub	:= {}
		_aStaSub	:= {}
		_aSubTots	:= {}   
		_cCtaAlu	:= 0
	Endif 	  
Enddo    
//-- *********************************************************************************************************
//--    *****      S e c a o  -  3: TOTAL GERAL do Relatorio*****
//-- *********************************************************************************************************  
_aTotais := {_aTipTot,_aStaTot,{{"TOTAL GERAL de Matriculas",_nCtaR,0.00}}}   
_nCtaA := 0
For _nCtaA := 1 to Len(_aTotais)	
	_nCtaB := 0
	For _nCtaB := 1 to Len(_aTotais[_nCtaA])	
		//-- ********************************************************************
	 	//--   Imprime detalhe
		//-- ********************************************************************
		_oTotStat:Cell( "ZP7_XMATRI"	):SetBlock({|| ""	})
		_oTotStat:Cell( "ZP7_XNOME"		):SetBlock({|| ""	})
		_oTotStat:Cell( "ZP7_XCPF" 		):SetBlock({|| ""	})
		_oTotStat:Cell( "ZP7_XDESCS"	):SetBlock({|| ""	})
		_oTotStat:Cell( "ZP7_XDESMO"	):SetBlock({|| _aTotais[_nCtaA,_nCtaB,1]	})
		_oTotStat:Cell( "ZP7_XTURNO"	):SetBlock({|| ""	})
		//--
		_oTotStat:Cell( "ZP7_XQTDE"		):SetBlock({|| _aTotais[_nCtaA,_nCtaB,2]	})
		_oTotStat:Cell( "ZP7_XVALOR"	):SetBlock({|| _aTotais[_nCtaA,_nCtaB,3]	})
		_oTotStat:Cell( "ZP7_XPERIO"	):SetBlock({|| ""	})
		//--
		_oTotStat:Cell( "ZTIPO"			):SetBlock({|| ""	})
		_oTotStat:Cell( "ZP7_XDATAG"	):SetBlock({|| ""	})
		_oTotStat:Cell( "ZP7_XDTLIB"	):SetBlock({|| ""	})
		_oTotStat:Cell( "ZPA_XDATA"		):SetBlock({|| ""	})
		_oTotStat:Cell( "ZP7_XDTPG"		):SetBlock({|| ""	})
		_oTotStat:Cell( "ZSTATU"   		):SetBlock({|| ""	})
		_oTotStat:PrintLine()
	Next _nCtaB 
   		_oReport:SkipLine()
Next _nCtaA 
//-- ********************************************************************
 	//--    Salta duas Linhas apos imprimir sub-total
//-- ********************************************************************
_oReport:SkipLine()
_oReport:SkipLine()   
//****************************************
//-- Fecha o Arquivo de Trabalho
//****************************************
(_cAlias)->(dbCloseArea())
//****************************************
//-- Finaliza as sessoes 
//****************************************
_oAlunos:Finish()
_oSubTot:Finish()
_oTotStat:Finish()
Return


/*
+--------------------------------------------------------------------------+
|{Protheus.doc}<fLerZP7>                                                   |
|Monta query que busca as matriculas. 	    							   |
|                                                                          |
|@Author<Antonio Dsantas>                                                  |
|@since<24/06/2014>                                                        |
|@version<Nil>                                                             |
|                                                                          |
|@parameters<Nil>                                                          |
|@return<Nil>                                                              |
+--------------------------------------------------------------------------+
|Uso        |SENAI										     			   |
+-----------+--------------------------------------------------------------+
*/
User Function fLerZP7()
Local _ChrBreak		:= Chr(13)+Chr(10)   
Local _cQuery		:= "" 
Local _aTurnos		:= {"MATUTINO","VERPERTINO","DIURNO","NOTURNO","TODOS"}
_cQuery := ""
_cQuery += " Select *From "+RetSqlName("ZP7")+" "+_ChrBreak
_cQuery += " where D_E_L_E_T_ <> '*' "+_ChrBreak
_cQuery += " and ZP7_FILIAL = '"+FwxFilial("ZP7")+"' "+_ChrBreak
//Wjr Alteração
//-- Mv_par01 - Periodo DE?
//-- Mv_par02 - Periodo ATE?
_cQuery += " and ZP7_XPERIO between '"+Mv_par01+"' and '"+Mv_par02+"' "+ CRLF
//-- Mv_par03 - Data de Pgto DE ?
//-- Mv_par04 - Data de Pgto ATE ?
_cQuery += " and (ZP7_XDTPG = '"+Space(TamSX3("ZP7_XDTPG")[1])+"' or ZP7_XDTPG  BetWeen '"+Dtos(Mv_par03)+"' and '"+Dtos(Mv_par04)+"') " +_ChrBreak	
//-- Mv_par05 - Codigo da Matricula DE ?
//-- Mv_par06 - Codigo da Matricula ATE ?
_cQuery += "and ((ZP7_XMATRI BetWeen '"+Mv_par05+"' and '"+Mv_par06+"') or "+"(ZP7_XMATRI BetWeen 'r"+Mv_par05+"' and 'r"+Mv_par06+"'))"+ CRLF	
//-- Mv_par07 - Tipo(s) de Pagamento ?  
If !Empty(Alltrim(Mv_par07))
	_cQuery += " and ZP7_XTIPO in "+FormatIn(Mv_par07,";")+" "+_ChrBreak
Endif 
//-- Mv_par08 - Codigo de Evento DE ?
//-- Mv_par09 - Codigo de Evento ATE ?
_cQuery += " and ZP7_XEVENT BetWeen '"+Mv_par08+"' and '"+Mv_par09+"' " + CRLF	
//-- Mv_par10 - Codigo da Modalidade DE ?
//-- Mv_par11 - Codigo da Modalidade ATE ?
_cQuery += " and ZP7_XMODAL BetWeen '"+Mv_par10+"' and '"+Mv_par11+"' " + CRLF	
//-- Mv_par12 - Codigo da Area Tecnolog DE ?
//-- Mv_par13 - Codigo da Area Tecnolog ATE ?
_cQuery += " and ZP7_XATECN BetWeen '"+Mv_par12+"' and '"+Mv_par13+"' " + CRLF	
//-- Mv_par14 - Codigo do Curso DE ?
//-- Mv_par15 - Codigo do Curso ATE ? 
_cQuery += " and ZP7_XCURSO BetWeen '"+Mv_par14+"' and '"+Mv_par15+"' " + CRLF	
//-- Mv_par16 - Com Seguinte(s) Status ?
If !Empty(Alltrim(Mv_par16))
	_cQuery += " and ZP7_XSTATU in "+FormatIn(Mv_par16,";")+" "+ CRLF
Endif 	
//-- Mv_par17 - Do(s) Turno(s): (Matutino,Verpertino,Diurno,Noturno,Todos)  
If Mv_par17 != 5          
	_cQuery += " and ZP7_XTURNO = '"+PadR(_aTurnos[Mv_par17],TamSX3("ZP7_XTURNO")[1])+"' " + CRLF
EndIf	
//-- Mv_par18 - Sessao De ?
//-- Mv_par19 - Sessao Ate ?
_cQuery += " and ZP7_XSESSA BetWeen '"+Mv_par18+"' and '"+Mv_par19+"' " + CRLF	
//-- Ordenacao
_cQuery += " order by ZP7_XMATRI, ZP7_XSTATU, ZP7_XDESCS, ZP7_XDESMO, ZP7_XTURNO, ZP7_XSEG, ZP7_XSESSA "+_ChrBreak     
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//| Grava o Resultado da Query para consulta                         |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
MemoWrite("C:\temp\"+FunName()+"_"+ProcName()+".TXT",_cQuery)
If Select(_cAlias)>0
	(_cAlias)->(DbCloseArea())
Endif
DbUseArea(.t.,"TOPCONN",TcGenQry(,,_cQuery),_cAlias,.t.,.t.)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//| Converte os campos tipo DATA                                     |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
TCSETFIELD( _cAlias,"ZP7_XDATAG"	,"D",8,0 )
TCSETFIELD( _cAlias,"ZP7_XDTLIB"	,"D",8,0 )
TCSETFIELD( _cAlias,"ZP7_XDTPG"		,"D",8,0 )
Return .t.

/*
+--------------------------------------------------------------------------+
|{Protheus.doc}<AjustaSX1>                                                 |
|Cria as perguntas.					 	    							   |
|                                                                          |
|@Author<Antonio Dsantas>                                                  |
|@since<28/03/2014>                                                        |
|@version<Nil>                                                             |
|                                                                          |
|@parameters<Nil>                                                          |
|@return<Nil>                                                              |
+--------------------------------------------------------------------------+
|Uso        |SENAI										     			   |
+-----------+--------------------------------------------------------------+
*/
Static Function xParPerg()	
Local _aPerg := {}
//*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*
//*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*
//   //-- MAPEAMENTOS DOS CAMPOS DE SX1
//   aAdd (_aPerg,{"X1_PERGUNT","X1_PERPA","X1_PERENG","MV_CHL","C",5,0,0,"X1_GSC","X1_VALID",;
//								"X1_VAR01",("X1_DEF01" e "X1_PYNE"),"X1_DEFSPA1","X1_DEFENG1","X1_CNT01",;
//								"X1_VAR02","X1_DEF02","X1_DEFSPA2","X1_DEFENG2","X1_CNT02",;
//								"X1_VAR03","X1_DEF03","X1_DEFSPA3","X1_DEFENG3","X1_CNT03",;
//								"X1_VAR04","X1_DEF04","X1_DEFSPA4","X1_DEFENG4","X1_CNT04",;
//								"X1_VAR05","X1_DEF05","X1_DEFSPA5","X1_DEFENG5","X1_CNT05",;
//								"X1_F3","X1_GRPSXG","","X1_HELP","X1_PICTURE",{"Descricao para Tela PTG"},{"Descricao para Tela EPA"},{"Descricao para Tela USA"}})
//*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*
//*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*&*

//-- Mv_par01 =     Periodo DE?
aAdd(_aPerg, {"Periodo DE?","Periodo DE?","Periodo DE?","MV_CH1","C",8,0,0,"G","",;
					"MV_PAR01","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"ZP4REL","","","","",{"Codigo do Período a ser Pesquisado."},{"Codigo do Período a ser Pesquisado."},{"Codigo do Período a ser Pesquisado."}})

//-- Mv_par02 =     Periodo ATE?
aAdd(_aPerg, {"Periodo ATE?","Periodo ATE?","Periodo ATE?","MV_CH2","C",8,0,0,"G","",;
					"MV_PAR02","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"ZP4REL","","","","",{"Codigo do Período a ser Pesquisado."},{"Codigo do Período a ser Pesquisado."},{"Codigo do Período a ser Pesquisado."}})

//-- Mv_par03 =     Data de Pgto DE ?
aAdd(_aPerg, {"Data de Pgto DE ?","Data de Pgto DE ?","Data de Pgto DE ?","MV_CH3","D",8,0,0,"G","",;
					"MV_PAR03","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",{"Com data de pagamento a partir de"},{"Com data de pagamento a partir de"},{"Com data de pagamento a partir de"}})

//-- Mv_par04 =     Data de Pgto ATE ?
aAdd(_aPerg, {"Data de Pgto ATE ?","Data de Pgto ATE ?","Data de Pgto ATE ?","MV_CH4","D",8,0,0,"G","",;
					"MV_PAR04","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",{"Com data de pagamento Ate"},{"Com data de pagamento Ate"},{"Com data de pagamento Ate"}})

//-- Mv_par05 =     Codigo da Matricula DE ?
aAdd(_aPerg, {"Codigo da Matricula DE ?","Codigo da Matricula DE ?","Codigo da Matricula DE ?","MV_CH5","C",15,0,0,"G","",;
					"MV_PAR05","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",{"Codigo da Matricula Igual ou Maior que"},{"Codigo da Matricula Igual ou Maior que"},{"Codigo da Matricula Igual ou Maior que"}})

//-- Mv_par06 =     Codigo da Matricula ATE ?
aAdd(_aPerg, {"Codigo da Matricula ATE ?","Codigo da Matricula ATE ?","Codigo da Matricula ATE ?","MV_CH6","C",15,0,0,"G","",;
					"MV_PAR06","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",{"Codigo da Matricula Ate"},{"Codigo da Matricula Ate"},{"Codigo da Matricula Ate"}})

//-- Mv_par07 =     Tipo(s) de Pagamento ?
aAdd(_aPerg, {"Tipo(s) de Pagamento ?","Tipo(s) de Pagamento ?","Tipo(s) de Pagamento ?","MV_CH7","C",99,0,0,"G","u_MrkTipo()",;
					"MV_PAR07","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",{"'Range' dos Tipos de Pagamento, separados por (;) Ponto-e-virgula "},{"'Range' dos Tipos de Pagamento, separados por (;) Ponto-e-virgula "},{"'Range' dos Tipos de Pagamento, separados por (;) Ponto-e-virgula "}})

//-- Mv_par08 =     Codigo de Evento DE ?
aAdd(_aPerg, {"Codigo de Evento DE ?","Codigo de Evento DE ?","Codigo de Evento DE ?","MV_CH8","C",08,0,0,"G","",;
					"MV_PAR08","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",{"Codigo de Evento DE."},{"Codigo de Evento DE."},{"Codigo de Evento DE."}})

//-- Mv_par09 =     Codigo de Evento ATE ?
aAdd(_aPerg, {"Codigo de Evento ATE ?","Codigo de Evento ATE ?","Codigo de Evento ATE ?","MV_CH9","C",08,0,0,"G","",;
					"MV_PAR09","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",{"Codigo de Evento ATE."},{"Codigo de Evento ATE."},{"Codigo de Evento ATE."}})

//-- Mv_par10 =     Codigo da Modalidade DE ?
aAdd(_aPerg, {"Codigo da Modalidade DE ?","Codigo da Modalidade DE ?","Codigo da Modalidade DE ?","MV_CHa","C",02,0,0,"G","",;
					"MV_PAR10","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",{"Codigo da Modalidade DE."},{"Codigo da Modalidade DE."},{"Codigo da Modalidade DE."}})

//-- Mv_par11 =     Codigo da Modalidade ATE ?
aAdd(_aPerg, {"Codigo da Modalidade ATE ?","Codigo da Modalidade ATE ?","Codigo da Modalidade ATE ?","MV_CHb","C",02,0,0,"G","",;
					"MV_PAR11","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",{"Codigo da Modalidade ATE."},{"Codigo da Modalidade ATE."},{"Codigo da Modalidade ATE."}})

//-- Mv_par12 =     Codigo da Area Tecnolog DE ?
aAdd(_aPerg, {"Codigo da Area Tecnolog DE ?","Codigo da Area Tecnolog DE ?","Codigo da Area Tecnolog DE ?","MV_CHc","C",08,0,0,"G","",;
					"MV_PAR12","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",{"Codigo da Area Tecnolog DE."},{"Codigo da Area Tecnolog DE."},{"Codigo da Area Tecnolog DE."}})

//-- Mv_par13 =     Codigo da Area Tecnolog ATE ?
aAdd(_aPerg, {"Codigo da Area Tecnolog ATE ?","Codigo da Area Tecnolog ATE ?","Codigo da Area Tecnolog ATE ?","MV_CHd","C",08,0,0,"G","",;
					"MV_PAR13","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",{"Codigo da Area Tecnolog ATE."},{"Codigo da Area Tecnolog ATE."},{"Codigo da Area Tecnolog ATE."}})

//-- Mv_par14 =     Codigo do Curso DE ?
aAdd(_aPerg, {"Codigo do Curso DE ?","Codigo do Curso DE ?","Codigo do Curso DE ?","MV_CHe","C",10,0,0,"G","",;
					"MV_PAR14","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",{"Codigo do Curso DE."},{"Codigo do Curso DE."},{"Codigo do Curso DE."}})

//-- Mv_par15 =     Codigo do Curso ATE ?
aAdd(_aPerg, {"Codigo do Curso ATE ?","Codigo do Curso ATE ?","Codigo do Curso ATE ?","MV_CHf","C",10,0,0,"G","",;
					"MV_PAR15","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",{"Codigo do Curso ATE."},{"Codigo do Curso ATE."},{"Codigo do Curso ATE."}})

//-- Mv_par16 =     Com Seguinte(s) Status ? 
aAdd(_aPerg, {"Com Seguinte(s) Status ?","Com Seguinte(s) Status ?","Com Seguinte(s) Status ?","mv_chg","C",99,0,0,"C","u_MrkStat()",;
					"Mv_par16","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",{"Em quais Situaçoes. Em Branco considera todos."},{"Em quais Situaçoes. Em Branco considera todos."},{"Em quais Situaçoes. Em Branco considera todos."}}) 

//-- Mv_par17 =     Do(s) Turno(s): (Matutino, Verpertino, Diurno, Noturno)
aAdd(_aPerg, {"Do(s) Turno(s)","Do(s) Turno(s)","Do(s) Turno(s)","mv_chh","N",1,0,1,"C",,;//NaoVazio()
					"Mv_par17","Matutino","Matutino","Matutino","",;
					"","Verpertino","Verpertino","Verpertino","",;
					"","Diurno","Diurno","Diurno","",;
					"","Noturno","Noturno","Noturno","",;
					"","TODOS","TODOS","TODOS","",;
					"","","","","",{"Nao Imprimir Tipos ?"},{"Nao Imprimir Tipos ?"},{"Nao Imprimir Tipos ?"}}) 

//-- Mv_par18 =     Sessão De ?
aAdd(_aPerg, {"Sessão DE ?","Sessão DE ?","Sessão DE ?","MV_CHi","C",10,0,0,"G","",;
					"MV_PAR18","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"ZP6","","","","",{"Codigo Sessão Inicial."},{"Codigo Sessão Inicial."},{"Codigo Sessão Inicial."}})

//-- Mv_par19 =     Sessão Ate ?
aAdd(_aPerg, {"Sessão ATE ?","Sessão ATE ?","Sessão ATE ?","MV_CHj","C",10,0,0,"G","",;
					"MV_PAR19","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"","","","","",;
					"ZP6","","","","",{"Codigo Sessão Ate."},{"Codigo Sessão Ate."},{"Codigo Sessão Ate."}})

u_SFAJUSX1(_cPerg,_aPerg) 
Return 