#INCLUDE "PROTHEUS.ch"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ SIPCOR09     ºAutor  ³Microsiga         º Data ³ 09/05/12  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Relatório Oficial: Quadro Orçamentário por Programática    º±±
±±º          ³ Identificação CNI: RORC057/RORC058/RORC063                 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ P11 - SISTEMA INDUSTRIA                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

User Function SIPCOR09(aConfig2)
Local aArea  	:= GetArea()
Local nCont 	:= 1000
Local _nTotal   := 0
Local _aDados   := {}
Private oBrGray := TBrush():New("",CLR_GRAY)
Private oPrint
Private _nRow	:= 8000
Private nTamForm	:= 3000 //Tamanho formulario
Private lFirst		:= .f.
Private nPag		:= 0
Private cCadastro 	:= OemToAnsi("Quadro Orçamentário por Programática")
Private oFont09		:= TFont():New("Courier",9,9,.T.,.F.,5,.T.,5,.T.,.F.)
Private oFont09b	:= TFont():New("Courier",9,9,.T.,.T.,5,.T.,5,.T.,.F.)
Private oFont12b	:= TFont():New("Courier",9,12,.T.,.T.,5,.T.,5,.T.,.F.)
Private oFont12 	:= TFont():New("Courier",9,12,.T.,.F.,5,.T.,5,.T.,.F.)
// Parametros:
Private _dDataIni := aConfig2[1]
Private _dDataFim := aConfig2[2]
Private _nMoeda   := Val(aConfig2[3])
Private _cCfgCubo := aConfig2[4]
Private _cModelo  := aConfig2[5]
Private _cCubo    := Posicione("AL4",1, xFilial("AL4")+ _cCfgCubo, "AL4_CONFIG")
Private _oStruCub

// Busca estrutura do cubo selecionado para chegagem
IF Empty(_cCubo)
	Aviso("Cubo","Cubo inválido ou não informado.",{"Voltar"})
	Return()
ENDIF

// Busca estrutura do cubo selecionado para chegagem
_oStruCub := PcoStructCube( _cCubo,_cCfgCubo )

// Validação do Cubo
// DIMENSÃO ESPERADA = PROGRAMÁTICA(CV0) + QUALQUER CHAVE +TIPO DE SALDO

IF _oStruCub:ACHAVE[1] <> "CV0->CV0_CODIGO"
	Aviso("Cubo","Primeiro nível do cubo deve ser alguma ENTIDADE GERENCIAL.",{"Voltar"})
	Return()
ENDIF

IF _oStruCub:ACHAVE[Len(_oStruCub:ACHAVE)] <> "AL2->AL2_TPSALD"
	Aviso("Cubo","Último nível do cubo deve ser TIPO DE SALDO.",{"Voltar"})
	Return()
ENDIF

IF Len( _aDados := SIPCO09Cub() ) == 0
	Aviso("Aviso","Não existem dados a serem exibidos.",{"Sair"})
	Return()
ENDIF

oPrint:= TMSPrinter():New(cCadastro)

oPrint:Setup()
oPrint:SetPortrait()
oPrint:StartPage()

SIPCO09Cab(aConfig2)
lFirst := .t.

_nRow := 420
_nLin := 1

For i := 1 to Len(_aDados)
	
	If _nLin > 43
		oPrint:EndPage()
		oPrint:StartPage()
		SIPCO09Cab(aConfig2)
		_nLin := 1
		_nRow := 420
	EndIf
	
	oPrint:Say(_nRow,0070,_aDados[i][1],IIF(_aDados[i][4]=="1",oFont12b,oFont12))
	oPrint:Say(_nRow,0645,_aDados[i][2],IIF(_aDados[i][4]=="1",oFont12b,oFont12))
	oPrint:Say(_nRow,2000,Transform(_aDados[i][3],"@E 999,999,999.99"),IIF(_aDados[i][4]=="1",oFont12b,oFont12))
	
	_nTotal += IIF(_aDados[i][4]=="2",_aDados[i][3],0)
	_nRow += 60
	_nLin++
	
Next

// Box do Rodape
aCoord:={3000,060,3070,2380}
oPrint:FillRect(aCoord,oBrGray)
oPrint:Say(3007,1000,"TOTAL",oFont12b,,CLR_WHITE)

// Imprime total geral
oPrint:Say(3007,2000,Transform(_nTotal,"@E 999,999,999.99"),oFont12b,,CLR_WHITE)

oPrint:Preview()
oPrint:End()

Return()

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ SIPCO09Cab ³ Autor ³ Microsiga           ³ Data ³ Abr/2012 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Imprime cabecalho e trata quebra de pagina.                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ P11 - SISTEMA INDUSTRIA                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function SIPCO09Cab(aConfig2)
Private oFont13b	:= TFont():New("Courier",9,13,.T.,.T.,5,.T.,5,.T.,.F.)
Private oFont15b	:= TFont():New("Courier",9,16,.T.,.T.,5,.T.,5,.T.,.F.)

IF _cModelo$("1/2") // Receita/Despesa SENAI
	
	oPrint:Say(150,060,IIF(_cModelo=="1","RECEITA","DESPESA"),oFont15b)
	oPrint:Say(215,060,"ÓRGÃO: Ministério do Trabalho e Emprego",oFont13b)
	oPrint:Say(275,060,"Unidade: Sistema SENAI",oFont13b)
	
ELSE // Receita/Despesa SESI
	
	oPrint:Say(080,060,"ANEXO I",oFont15b)
	oPrint:Say(150,060,IIF(_cModelo=="3","RECEITA","DESPESA"),oFont15b)
	oPrint:Say(215,060,"ÓRGÃO: Ministério do Desenvolvimento Social e Combate à Fome",oFont13b)
	oPrint:Say(275,060,"Unidade: SESI - Serviço Social da Indústria",oFont13b)
	
ENDIF

// Moeda
oPrint:Say(290,2180,"R$ "+Alltrim(Transform(_nMoeda,"@E 9.99")),oFont13b)

// Box do Cabeçalho
aCoord:={350,060,420,2380}
oPrint:FillRect(aCoord,oBrGray)
oPrint:Say(357,0190,"Código",oFont12b,,CLR_WHITE)
oPrint:Say(357,1000,"Especificação",oFont12b,,CLR_WHITE)
oPrint:Say(357,2120,"Valor",oFont12b,,CLR_WHITE)

oPrint:Box(350,060,3000,630 )
oPrint:Box(350,630,3000,1800 )
oPrint:Box(350,1800,3000,2380 )

Return()

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³SIPCO09Cub  ºAutor  ³Microsiga         º Data ³  09/12/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Processamento do Cubo                                      º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ P11 - SISTEMA INDUSTRIA                                   º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function SIPCO09Cub()
Local _aRet     := {}
Local aFiltro   := {}
Local aCfgAux   := {}
Local aItCfgBlq := {}
Private aCfgCub := {}
Private COD_CUBO  := _cCubo

aCuboCfg := {}

aAdd(aCuboCfg, { 1  ,"Config.Cubo Serie 1",Space(LEN(AL3->AL3_CODIGO))		  ,"@!" 	 ,''  ,"AL3" ,"" ,25 ,.F. })

If ParamBox(aCuboCfg, "Configuracao de Cubos", aCfgCub,/*bOk*/,/*aButtons*/,/*lCentered*/,/*nPosx*/,/*nPosy*/, /*oDlgWizard*/, /*cArqParam*/,,.T.) //"Configuracao de Cubos"
	
	aProcessa := PcoRunCube(_cCubo, 1, "U__PCO09Sld", aCfgCub[1] , 1,.F.,.T.,aFiltro,aFiltro,.T.,aCfgAux,/*lProcessa*/,/*lVerAcesso*/,/*lForceNoSint*/,aItCfgBlq,/*aFiltCfg*/,/*cArqAKT*/,/*lLimpArqAKT*/,/*lVisao*/,.T./*lBloqueio*/)
	
	For i := 1 to Len(aProcessa)
		
		IF aProcessa[i][4] == "CV0" // Operacao
			
			_aEstrut := SIPCO09Niv(aProcessa[i][1],aProcessa[i][2][1])
			
			For y := 1 to Len(_aEstrut)
				IF (_nPos := aScan(_aRet,{|x| Alltrim(x[1]) == Alltrim(_aEstrut[y,1])})) == 0
					Aadd(_aRet,{_aEstrut[y,1],_aEstrut[y,2],_aEstrut[y,4],_aEstrut[y,3]})
				ELSE
					_aRet[_nPos,3] += _aEstrut[y,4]
				ENDIF
			Next
			
		ENDIF
		
	Next
	
ENDIF

Return(_aRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³_PCO09Sld  ºAutor  ³Microsiga          º Data ³  09/12/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Retorna saldo para o cubo                                  º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ P11 - SISTEMA INDUSTRIA                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

User Function _PCO09Sld(cConfig,cChave)
Local aRetSld := {}
Local nSldIni := 0
Local nSldFim := 0
Local _nRet   := 0

aPeriodos := PcoRetPer( _dDataIni/*dIniPer*/, _dDataFim/*dFimPer*/, "1" /*cTipoPer*/, .F. /*lAcumul*/)

For ny := 1 to Len(aPeriodos)
	
	dIni := CtoD(Subs(aPeriodos[ny],1,10))
	dFim := CtoD(Subs(aPeriodos[ny],14))
	
	aRetIni := PcoRetSld(cConfig,cChave,dIni-1)
	nCrdIni := aRetIni[1, _nMoeda]
	nDebIni := aRetIni[2, _nMoeda]
	
	aRetFim := PcoRetSld(cConfig,cChave,dFim)
	nCrdFim := aRetFim[1, _nMoeda]
	nDebFim := aRetFim[2, _nMoeda]
	
	nSldIni := nCrdIni-nDebIni
	nMovCrd := nCrdFim-nCrdIni
	nMovDeb := nDebFim-nDebIni
	nMovPer := nMovCrd-nMovDeb
	
	_nRet += nMovPer
	
Next

Return {_nRet}

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³SIPCO09Niv  ºAutor  ³Microsiga         º Data ³  26/09/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Retorna niveis sinteticos da entidade                      º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ P11 - SISTEMA INDUSTRIA                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function SIPCO09Niv(_cEntidade,_nValor)
Local _aRetNiv := {}
Local _cPlano  := StrTran(_oStruCub:ACHAVER[1],"AKD->AKD_ENT","") // Numero da Entidade
Local _lCont   := .f.

CV0->(dbSetOrder(1))
CV0->(dbSeek(XFilial("CV0")+_cPlano+_cEntidade))

// Entidade analitica
Aadd(_aRetNiv,{CV0->CV0_CODIGO,CV0->CV0_DESC,CV0->CV0_CLASSE,_nValor})

IF !Empty(CV0->CV0_ENTSUP)
	_lCont := .t.
ENDIF

While _lCont
	
	// Procura proximo nivel
	CV0->(dbSeek(XFilial("CV0")+_cPlano+CV0->CV0_ENTSUP))
	
	// Adiciona novo nivel
	Aadd(_aRetNiv,{CV0->CV0_CODIGO,CV0->CV0_DESC,CV0->CV0_CLASSE,_nValor})
	
	IF Empty(CV0->CV0_ENTSUP)
		_lCont := .f.
	ENDIF
	
	Skip()
End

Return( aSort(_aRetNiv,,,{| x,y | x[1] < y[1]}) )
