#INCLUDE 'TOTVS.CH'
#INCLUDE 'FWMVCDEF.CH'

/*/{Protheus.doc} SF0535X
@description MVC 

@author  Rafael Karczevski
@since   09/07/2019
@version 1.0   
/*/
User Function SF0535X()

	Local oBrowse
	Private oGatModel := SF535XC():New()
	Private aRotina   := MenuDef()
	Private nCount    := 0
	Public nQtdBil    := 0

	oBrowse := FWMBrowse():New()
    oBrowse:SetAlias('ZH0')
	oBrowse:SetDescription('Cupom Parque - Contratos')

	oBrowse:SetFilterDefault( "ZH0_ORIGEM = 'CGT'" )

	oBrowse:AddLegend(" ZH0_DATA >= dDataBase .and. Empty(ZH0_DATUS)", "BR_VERDE", "Valido")
	oBrowse:AddLegend(" !Empty(ZH0_DATUS)", "BR_AZUL", "Utilizado")
	oBrowse:AddLegend(" ZH0_DATA < dDataBase .and. Empty(ZH0_DATUS)", "BR_VERMELHO", "Vencido")
	
    oBrowse:Activate()
    
	FreeObj(oGatModel)

Return Nil

/*/{Protheus.doc} MenuDef   
@deion Montagem do Menu

@author  Rafael Karczevski
@since   09/07/2019
@version 1.0 
/*/
Static Function MenuDef()

	Local aRotina := {}

	aAdd( aRotina, { 'Visualizar'   , 'VIEWDEF.SF0535X', 0, 2, 0, NIL } )
    aAdd( aRotina, { 'Incluir'      , 'VIEWDEF.SF0535X', 0, 3, 0, NIL } )
    //aAdd( aRotina, { 'Alterar'      , 'VIEWDEF.SF0535X', 0, 4, 0, NIL } )
    //aAdd( aRotina, { 'Excluir'      , 'VIEWDEF.SF0535X', 0, 5, 0, NIL } )
	aAdd( aRotina, { 'Imprimir'     , 'oGatModel:Print()', 0, 4, 0, NIL } )

Return aRotina


/*/{Protheus.doc} ModelDef
@deion Monta Model de apresentação

@author  Rafael Karczevski
@since   09/07/2019
@version 1.0 
/*/
Static Function ModelDef()

	Local oModel 	  := MPFormModel():New( 'MDSF0535', {|| .T.}, {|| oGatModel:ValidModel()} )
	//Local oSTRSF0535M := FWFormStruct( 1, 'ZH0', { |X| AllTrim(X) $ 'ZH0_FILIAL,ZH0_NUMORC,ZH0_NUMNOT,ZH0_SERIE,ZH0_ITEM,ZH0_COD,ZH0_DESC,ZH0_CODBAR,ZH0_DATA,ZH0_HORA,ZH0_USADO,ZH0_DATUS,ZH0_HORUS,ZH0_VRUNIT,ZH0_CAIXA,ZH0_NUMCRT,ZH0_NUMREV' }) 
	Local oSTR0535M := FWFormStruct( 1, 'ZH0', { |X| AllTrim(X) $ 'ZH0_NUMORC,ZH0_NUMNOT,ZH0_SERIE,ZH0_DATA,ZH0_HORA,ZH0_NUMCRT,ZH0_NUMREV,ZH0_ORIGEM,ZH0_USADO' }) 
	Local oSTR0535D := FWFormStruct( 1, 'ZH0', { |X| AllTrim(X) $ 'ZH0_ITEM,ZH0_NOMCLI,ZH0_CPFCLI,ZH0_DTNASC,ZH0_COD,ZH0_DESC,ZH0_VRUNIT,ZH0_CODBAR'}) 

	//oSTRCH002PM:SetProperty('ZH0_USUARI'	, MODEL_FIELD_INIT,  { |oFields| CH002PINI('ZH0_USUARI'	,oFields) } )

	oSTR0535M:SetProperty('ZH0_NUMCRT', MODEL_FIELD_OBRIGAT, .T.)

	oSTR0535M:SetProperty('ZH0_NUMORC'	, MODEL_FIELD_WHEN,  {|oFields| .F.} )
	oSTR0535M:SetProperty('ZH0_NUMNOT'	, MODEL_FIELD_WHEN,  {|oFields| .F.} )
	oSTR0535M:SetProperty('ZH0_SERIE'	, MODEL_FIELD_WHEN,  {|oFields| .F.} )
	oSTR0535M:SetProperty('ZH0_USADO'	, MODEL_FIELD_WHEN,  {|oFields| .F.} )
	oSTR0535M:SetProperty('ZH0_USADO'	, MODEL_FIELD_INIT,  {|oFields| "2" } )
	oSTR0535M:SetProperty('ZH0_DATA'	, MODEL_FIELD_TOOLTIP, "Data Validade" )
	oSTR0535M:SetProperty('ZH0_DATA'	, MODEL_FIELD_TITULO , "Data Validade" )
	oSTR0535M:SetProperty('ZH0_DATA'	, MODEL_FIELD_INIT,  {|oFields| Date() } )
	oSTR0535M:SetProperty('ZH0_DATA'	, MODEL_FIELD_VALID, {|oFields| u_SF535XB() } )
	oSTR0535M:SetProperty('ZH0_HORA'	, MODEL_FIELD_WHEN,  {|oFields| .F.} )
	oSTR0535M:SetProperty('ZH0_HORA'	, MODEL_FIELD_INIT,  {|oFields| Time() } )
	oSTR0535M:SetProperty('ZH0_ORIGEM'	, MODEL_FIELD_WHEN,  {|oFields| .F.} )
	oSTR0535M:SetProperty('ZH0_ORIGEM'	, MODEL_FIELD_INIT,  {|oFields| "CGT" } )

	oSTR0535D:SetProperty('ZH0_CODBAR'	, MODEL_FIELD_WHEN,  {|oFields| .F.} )
	oSTR0535D:SetProperty('ZH0_ITEM'	, MODEL_FIELD_WHEN,  {|oFields| .F.} )
	oSTR0535D:SetProperty('ZH0_ITEM'	, MODEL_FIELD_INIT,  {|oFields| AllTrim(StrZero(nCount := nCount+1, 4)) })
	oSTR0535D:SetProperty('ZH0_COD'		, MODEL_FIELD_WHEN,  {|oFields| .F.} )
	oSTR0535D:SetProperty('ZH0_DESC'	, MODEL_FIELD_WHEN,  {|oFields| .F.} )
	oSTR0535D:SetProperty('ZH0_VRUNIT'	, MODEL_FIELD_WHEN,  {|oFields| .F.} )
	oSTR0535D:SetProperty('ZH0_CODBAR'	, MODEL_FIELD_INIT,  {|oFields| CreateBar() } )
	oSTR0535D:SetProperty('ZH0_NOMCLI'	, MODEL_FIELD_INIT,  {|oFields| "." } )
	//oSTR0535D:SetProperty('ZH0_DTNASC'	, MODEL_FIELD_OBRIGAT, .T. )
	oSTR0535D:SetProperty('ZH0_CPFCLI'	, MODEL_FIELD_VALID, {|oFields| oGatModel:VerifCPF() } )

	oModel:AddFields( 'ZH0MASTER', , oSTR0535M )
	oModel:AddGrid( 'ZH0DETAIL', 'ZH0MASTER', oSTR0535D )

	aCpoRelat := {  { 'ZH0_FILIAL', 'xFilial("ZH0")' },;
					{ 'ZH0_NUMCRT', 'ZH0_NUMCRT'	 },;
					{ 'ZH0_NUMREV', 'ZH0_NUMREV'	 }}

	oModel:SetRelation( 'ZH0DETAIL', aCpoRelat, ZH0->( IndexKey(3) ) )
	oModel:SetPrimaryKey( {'ZH0_FILIAL','ZH0_NUMCRT','ZH0_NUMREV'} )

	//oModel:GetModel( 'ZH0DETAIL' ):SetUniqueLine( { 'ZH0_NOMCLI' } )
	oModel:GetModel( 'ZH0DETAIL' ):SetMaxLine(9999)
     
Return oModel


/*/{Protheus.doc} ViewDef
@deion Apresentação do model e View junto

@author  Rafael Karczevski
@since   09/07/2019
@version 1.0 
/*/
Static Function ViewDef()

	Local oView
	Local oModel  := FWLoadModel( 'SF0535X' )

	//Local oSTRSF0535M := FWFormStruct( 2, 'ZH0',/* { |X|  AllTrim(X) $ 'ZH0_USUARI,ZH0_FILIAL,ZH0_FABRIC,ZH0_DIAANA,ZH0_BASE,ZH0_MARGEM,ZH0_DESTOQ,ZH0_GPPROD,ZH0_CURVA,ZH0_FORNEC,ZH0_CONDPG, ZH0_TPFRET,ZH0_FREQUE,ZH0_DIACOM,ZH0_ULTCOM,ZH0_PRXCOM'} */) 
	Local oSTR0535M := FWFormStruct( 2, 'ZH0', { |X| AllTrim(X) $ 'ZH0_NUMORC,ZH0_NUMNOT,ZH0_SERIE,ZH0_DATA,ZH0_HORA,ZH0_NUMCRT,ZH0_NUMREV,ZH0_ORIGEM,ZH0_USADO' }) 
	Local oSTR0535D := FWFormStruct( 2, 'ZH0', { |X| AllTrim(X) $ 'ZH0_ITEM,ZH0_NOMCLI,ZH0_CPFCLI,ZH0_DTNASC,ZH0_COD,ZH0_DESC,ZH0_VRUNIT,ZH0_CODBAR'}) 
	
	oView := FWFormView():New()
	oView:SetModel( oModel )

	oView:AddField( 'VZH0MASTER', oSTR0535M, 'ZH0MASTER' )
	oView:AddGrid( 'VZH0DETAIL', oSTR0535D, 'ZH0DETAIL' )

	oView:CreateHorizontalBox( 'CABECALHO', 40 )
	oView:CreateHorizontalBox( 'ITEM', 60 )

	oView:SetOwnerView( 'VZH0MASTER', 'CABECALHO' )
	oView:SetOwnerView( 'VZH0DETAIL', 'ITEM' )

	oView:EnableTitleView( 'VZH0MASTER' )
	
	oView:SetCloseOnOk( {|| .t.} )
	
Return oView

/*/{Protheus.doc} MDSF0535
@deion Ponto de Entrada Geral da Rotina
	
@author  Rafael Karczevski
@since   09/07/2019
		
@param PARAMIXB, Array, [Objeto][IdPonto][IdModel]

@return uRet,  Retorno diversos do PE

/*/
User Function MDSF0535()

	Local uRet       := .t.
	Local aAreaOld   := GetArea()
	Local oModel     := ParamIXB[1]
	Local cIdPonto   := ParamIXB[2]
	Local cIdModel   := ParamIXB[3]
	local oModelZH0  := oModel:GetModel("ZH0MASTER")
	Local oGatModel
	
	If cIdPonto == 'MODELPRE' /* Chamada antes da alteração de qualquer campo do modelo. */

	ElseIf cIdPonto == 'MODELPOS' /* Chamada na validação total do modelo. */
	
	ElseIf cIdPonto == 'FORMPRE' /* Chamada na antes da alteração de qualquer campo do formulário. */

	ElseIf cIdPonto == 'FORMPOS' /* Chamada na validação total do formulário. */

	ElseIf cIdPonto == 'FORMLINEPRE' /* Chamada na pre validação da linha do formulário. */
	
	ElseIf cIdPonto == 'FORMLINEPOS' /* Chamada na validação da linha do formulário. */
		
	ElseIf cIdPonto == 'MODELVLDACTIVE' /* Chamada na validação da ativação do Modelo. */
		
	ElseIf cIdPonto == 'MODELCOMMITTTS' /* Chamada apos a gravação total do modelo e dentro da transação. */
		nCount := 0
	ElseIf cIdPonto == 'MODELCOMMITNTTS' /* Chamada apos a gravação total do modelo e fora da transação. */
		
	ElseIf cIdPonto == 'FORMCOMMITTTSPRE' /* Chamada antes da gravação da tabela do formulário. */
		
	ElseIf cIdPonto == 'FORMCOMMITTTSPOS' /* Chamada apos a gravação da tabela do formulário. */
		
	ElseIf cIdPonto == 'MODELCANCEL' /* Cancela */
		nCount := 0
	ElseIf cIdPonto == 'BUTTONBAR' /* Usado para Criação de Botoes Estrutura: { {'Nome', 'Imagem Botap', { || bBlock } } } */
		uRet := {}
		aAdd(uRet, {'Importar CSV', '', { || ImpFunct() } } )
	EndIf
	
	RestArea(aAreaOld)
	
Return uRet

/*/{Protheus.doc} SF535XA   
@deion Usado em gatilho

@author  Rafael Karczevski
@since   09/07/2019
@version 1.0 
/*/
User Function SF535XA()

	Local oModel     := FWModelActive()
	Local oModelZH0  := oModel:GetModel("ZH0MASTER")
	Local oModeZH0D  := oModel:GetModel("ZH0DETAIL")
	Local lRet := .t.

	If INCLUI
		oGatModel:oModel := oModelZH0
		oGatModel:oModelD := oModeZH0D
		lRet := oGatModel:GerOrcam()

		oModeZH0D:LoadValue( "ZH0_CODBAR", CreateBar(.t.))

		/*For nX := 1 to (nQtdBil - 1)
			oModeZH0D:GoLine(nX)
			oModeZH0D:LoadValue( "ZH0_NOMCLI", "Cliente " + cValToChar(nX))
			oModeZH0D:AddLine()
		Next nXValidModel

		For nX := 1 to (nQtdBil)
			oModeZH0D:GoLine(nX)
			oModeZH0D:LoadValue( "ZH0_NOMCLI", "        ")
		Next nX*/
	EndIf

Return lRet

/*/{Protheus.doc} SF535XB
	@description Usado para validação de usuario do campo ZH0_DATA
	@type User Function
	@author Rafael Karczevski
	@since 05/09/2019
	@version 1.0
	@return lRet, Logico, .t. para permitir alteração.
	/*/
User Function SF535XB()

	Local lRet := .T.
	Local oModel     := FWModelActive()
	Local oModelZH0  := oModel:GetModel("ZH0MASTER")

	If oModelZH0:GetValue( "ZH0_DATA" ) < Date()
		lRet := .F.
	EndIf

Return lRet

/*/{Protheus.doc} CreateBar
	(long_description)
	@type  Static Function
	@author user
	@since date
	@version version
	@param param, param_type, param_descr
	@return return, return_type, return_description
	@example
	(examples)
	@see (links_or_references)
	/*/
Static Function CreateBar(p_lPrim)

	Local oModel     := FWModelActive()
	Local oModelZH0
	Local oModeZH0D
	Local oGatModel
	Local cRet := ""
	Default p_lPrim := .f.

	If oModel <> Nil

		oModelZH0  := oModel:GetModel("ZH0MASTER")
		oModeZH0D  := oModel:GetModel("ZH0DETAIL")
		If INCLUI
			If p_lPrim
				cRet := "3" + oModelZH0:GetValue("ZH0_NUMNOT") + AllTrim(StrZero(Val(oModeZH0D:GetValue("ZH0_ITEM")), 4)) + "2"
			Else
				cRet := "3" + oModelZH0:GetValue("ZH0_NUMNOT") + AllTrim(StrZero(Val(oModeZH0D:GetValue("ZH0_ITEM"))+1, 4)) + "2"
			EndIf
		EndIf

	EndIf

Return cRet

/*/{Protheus.doc} ImpFunct
	@description Chama a impotação
	@type Static Function
	@author Rafael karczevski
	@since 06/09/2019
	@version 1.0
	/*/
Static Function ImpFunct

	Local oModel     := FWModelActive()

	oGatMod := SF535XC():New()
	oModZH0  := oModel:GetModel("ZH0MASTER")
	oModZH0D  := oModel:GetModel("ZH0DETAIL")
	oGatMod:oModel := oModZH0
	oGatMod:oModelD := oModZH0D
	
	oGatMod:Importa()
	
	FreeObj(oGatMod)

Return
