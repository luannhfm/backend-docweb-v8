#INCLUDE "rwmake.ch"
#INCLUDE "protheus.ch"

/*/{Protheus.doc} SICOMA19
@description Funcao chamada pelo PE MT110TEL (Inclusao de campo no cabecalho de SC)
@type User Function
@author Claudinei Ferreira
@since 09/12/2011
@version 11.0
@param p_oNewDialog, object, Objeto da Dialog da Solicitação de Compras
@param p_aPosGet, array, Array contendo a posiçãi dos Gets da Solicitação de Compras
@param p_nOpcx, numeric, Opção selecionada na solicitação de compras (Inclusão, Alteração, Exclusão e Consulta)
@param p_nReg, numeric, Numero do recno do registro da solicitação de co mpras selecionada.
@history 24/01/2019, Antonio Dantas, Incluido o GET para Centro de custo uma ves que na Versao 12 o mesmo nao e apresentado por padrao.  
@history 24/01/2019, Antonio Dantas, Modifica formado e layou da apresentacao do SAY/GET na tela.
@history 24/01/2019, Antonio Dantas, Corrigida a nomeclatura das Variaveis. 
@history 04/02/2019, Alan Teles de Oliveira, Estou me baseando nos arrays com posição de linha e coluna passados nos pontos de entrada MT110GET e MT110TEL para não ter problemas com monitores de tamanhos diferentes.
@history 10/07/2019, Paulo Schwind, Retirado do cabeçalho o campo de Classificação da SC.
/*/
User Function SICOMA19(p_oNewDialog, p_aPosGet, p_nOpcx, p_nReg)

	Local aArea 		:= GetArea()
	
	Public oGetPl

	Public _cCCusto		:= IIF(p_nOpcx==3 .or. Empty(SC1->C1_CC),		Space(TamSx3("C1_CC")[1]),		SC1->C1_CC)
	Public _cItCta		:= IIF(p_nOpcx==3 .or. Empty(SC1->C1_ITEMCTA),	Space(TamSx3("C1_ITEMCTA")[1]),	SC1->C1_ITEMCTA)
	Public _cXsol		:= IIF(p_nOpcx==3 .or. Empty(SC1->C1_XSOL),		Space(TamSx3("C1_XSOL")[1]),	SC1->C1_XSOL) //Incluido por Alexandre Manini 01/02/2019
	Public _cXnumct		:= IIF(p_nOpcx==3 .or. Empty(SC1->C1_XNUMCT),	Space(TamSx3("C1_XNUMCT")[1]),	SC1->C1_XNUMCT) //Incluido por Alexandre Manini 01/02/2019
	Public _cXtiposc	:= IIF(p_nOpcx==3 .or. Empty(SC1->C1_XTIPOSC),	Space(TamSx3("C1_XTIPOSC")[1]),	SC1->C1_XTIPOSC) //Incluido por Alexandre Manini 01/02/2019
	//Public _cXclssc 	:= IIF(p_nOpcx==3 .or. Empty(SC1->C1_XCLSSC),	Space(TamSx3("C1_XCLSSC")[1]),	SC1->C1_XCLSSC) //Incluido por Cedimar Dutra Pereira 02/07/2019
	//Retirado por Paulo Schwind em  10/07/2019 
	Public _cXtpdesc	:= space(30) //Incluido por Alexandre Manini 01/02/2019
	Public _cJustCom	:= IIF(p_nOpcx==3 .or. Empty(SC1->C1_XJUSTIF),	Space(500),						SC1->C1_XJUSTIF)
	Public _cLcEnt		:= IIF(p_nOpcx==3 .or. Empty(SC1->C1_XLOCENT),	Space(500),						SC1->C1_XLOCENT)
	Public _cXclssc		:= IIF(p_nOpcx==3 .or. Empty(SC1->C1_XCLSSC),	Space(500),						SC1->C1_XCLSSC)
	Public _cXclPlc		:= IIF(p_nOpcx==3 .or. Empty(SC1->C1_XCLPLC),	Space(500),						SC1->C1_XCLPLC)

	If ValType(_aPosM110) != 'A'
		_aPosM110 := {{33}}
	EndIf

	@ _aPosM110[1, 1] + 31, p_aPosGet[1, 1] SAY Alltrim(RetTitle("C1_CC")) OF p_oNewDialog PIXEL SIZE 040,006
	@ _aPosM110[1, 1] + 30, p_aPosGet[1, 2] MSGET _cCCusto Picture PesqPict("SC1","C1_ITEMCTA") SIZE 050,006;  
							F3 CpoRetF3("C1_CC");		
							When !(p_nOpcx==2 .or. p_nOpcx==6);
							Valid NAOVAZIO() .and. CheckSX3("C1_CC",_cItCta);  
							Of p_oNewDialog PIXEL HASBUTTON

	//Campo Item contabil
	@ _aPosM110[1, 1] + 31, p_aPosGet[1, 3] SAY Alltrim(RetTitle("C1_ITEMCTA")) OF p_oNewDialog PIXEL SIZE 040,006
	@ _aPosM110[1, 1] + 30, p_aPosGet[1, 4] MSGET _cItCta Picture PesqPict("SC1","C1_ITEMCTA") SIZE 050,006;
							F3 CpoRetF3("C1_ITEMCTA");  
							When !(p_nOpcx==2 .or. p_nOpcx==6) ;
							Valid NAOVAZIO() .and. CheckSX3("C1_ITEMCTA",_cItCta) .and. Ctb105Item(); 
							Of p_oNewDialog PIXEL HASBUTTON
													
	//Alexandre Manini 01/02/2019 
	//Campo Solicitante
	/*
	@ _aPosM110[1, 1] + 31, p_aPosGet[1, 5] SAY Alltrim(RetTitle("C1_XSOL")) OF p_oNewDialog PIXEL SIZE 040,006
	@ _aPosM110[1, 1] + 30, p_aPosGet[1, 6] MSGET _cXsol  Picture PesqPict("SC1","C1_XSOL") SIZE 050,006;
							F3 CpoRetF3("C1_XSOL");  
							When !(p_nOpcx==2 .or. p_nOpcx==6) ;
							Valid NAOVAZIO() .and. CheckSX3("C1_XSOL",_cXsol); 
							Of p_oNewDialog PIXEL HASBUTTON
	*/
	//Cedimar Dutra 02/07/2019 By Cedimar Dutra Pereira
	//Campo Classificação Solicitação
    /*
	@ _aPosM110[1, 1] + 31, p_aPosGet[1, 5] SAY Alltrim(RetTitle("C1_XCLSSC")) OF p_oNewDialog PIXEL SIZE 040,006 //seCOLORS CLR_HGRAY,CLR_WHITE 
	@ _aPosM110[1, 1] + 30, p_aPosGet[1, 6] MSGET _cXclssc  Picture PesqPict("SC1","C1_XCLSSC") SIZE 050,006;
							F3 CpoRetF3("C1_XCLSSC");  
							When !(p_nOpcx==2 .or. p_nOpcx==6) ;
							Valid NAOVAZIO() .and. CheckSX3("C1_XCLSSC",_cXclssc); 
							Of p_oNewDialog PIXEL HASBUTTON
	// Retirado por Paulo Schwind 10/07/2019 
	*/
													
	//Alexandre Manini 01/02/2019 
	//Campo do Número do Contrato
	@ _aPosM110[1, 1] + 45, p_aPosGet[1, 1] SAY Alltrim(RetTitle("C1_XNUMCT")) OF p_oNewDialog PIXEL SIZE 040,006
	@ _aPosM110[1, 1] + 44, p_aPosGet[1, 2] MSGET _cXnumct  Picture PesqPict("SC1","C1_XNUMCT") SIZE 050,006;
							F3 CpoRetF3("C1_XNUMCT");  
							When !(p_nOpcx==2 .or. p_nOpcx==6) ;
							Valid CheckSX3("C1_XNUMCT",_cXnumct); 
							Of p_oNewDialog PIXEL HASBUTTON
	
	//Alexandre Manini 01/02/2019 
	//Campo do Tipo SC
	@ _aPosM110[1, 1] + 45, p_aPosGet[1, 3] SAY Alltrim(RetTitle("C1_XTIPOSC")) OF p_oNewDialog PIXEL SIZE 040,006
	@ _aPosM110[1, 1] + 44, p_aPosGet[1, 4] MSGET _cXtiposc  Picture PesqPict("SC1","C1_XTIPOSC") SIZE 050,006;
							F3 CpoRetF3("C1_XTIPOSC");  
							When !(p_nOpcx==2 .or. p_nOpcx==6) ;
							Valid NAOVAZIO() .and. CheckSX3("C1_XTIPOSC",_cXtiposc); 
							Of p_oNewDialog PIXEL HASBUTTON
	
							
	//Alexandre Manini 01/02/2019 
	//Campo do Descrição do Tipo SC
	@ _aPosM110[1, 1] + 45, p_aPosGet[1, 5] SAY Alltrim(RetTitle("C1_XTPDESC")) OF p_oNewDialog PIXEL SIZE 040,006
	@ _aPosM110[1, 1] + 44, p_aPosGet[1, 6] MSGET Posicione("COL", 1, xFilial("COL") + _cXtiposc, "COL_DESC")  Picture PesqPict("SC1","C1_XTPDESC") SIZE 050,006;
							F3 CpoRetF3("C1_XTPDESC");  
							When !(p_nOpcx==2 .or. p_nOpcx==6) ;
							Valid CheckSX3("C1_XTPDESC",_cXtpdesc); 
							Of p_oNewDialog PIXEL HASBUTTON						
    //Alexandre Manini 01/02/2019 
	//Campo Solicitante
	
	@ _aPosM110[1, 1] + 59, p_aPosGet[1, 5] SAY Alltrim(RetTitle("C1_XSOL")) OF p_oNewDialog PIXEL SIZE 040,006
	@ _aPosM110[1, 1] + 58, p_aPosGet[1, 6] MSGET _cXsol  Picture PesqPict("SC1","C1_XSOL") SIZE 050,006;
							F3 CpoRetF3("C1_XSOL");  
							When !(p_nOpcx==2 .or. p_nOpcx==6) ;
							Valid NAOVAZIO() .and. CheckSX3("C1_XSOL",_cXsol); 
							Of p_oNewDialog PIXEL HASBUTTON
	
	//Campo de Justificativa
	@ _aPosM110[1, 1] + 69, p_aPosGet[1, 1] SAY Alltrim(RetTitle("C1_XJUSTIF")) OF p_oNewDialog PIXEL SIZE 040,006
	@ _aPosM110[1, 1] + 68, p_aPosGet[1, 2] GET _cJustCom MEMO SIZE 200,20;
							When !(p_nOpcx==2 .or. p_nOpcx==6); 
							Valid !Empty(Alltrim(_cJustCom));
							PIXEL OF p_oNewDialog
							
	//Walmir Junior 10/02/2016 Campo criado para atender solicitação do CAQC, 
	//campo exclusivo para o caso de SC com produto de Ata de Registro de Preços.
	//Campo de Local Entrega
	@ _aPosM110[1, 1] + 70, p_aPosGet[1, 3] + 100 SAY Alltrim(RetTitle("C1_XLOCENT")) OF p_oNewDialog PIXEL SIZE 050,006
	@ _aPosM110[1, 1] + 69, p_aPosGet[1, 4] + 080 MSGET _cLcEnt Picture "@S200" SIZE 200,010; 
							WHEN !(p_nOpcx==2 .or. p_nOpcx==6) PIXEL OF p_oNewDialog

	//Campo de 
	@ _aPosM110[1, 1] + 95, p_aPosGet[1, 1] SAY Alltrim(RetTitle("C1_XCLSSC")) OF p_oNewDialog PIXEL SIZE 040,006
	@ _aPosM110[1, 1] + 94, p_aPosGet[1, 2] MSGET _cXclssc Picture PesqPict("SC1","C1_XCLSSC") SIZE 050,006;
							F3 CpoRetF3("C1_XCLSSC");  
							When !(p_nOpcx==2 .or. p_nOpcx==6); 
							Valid ExistCpo("ZZ9",Alltrim(_cXclssc));
							Of p_oNewDialog PIXEL HASBUTTON

	@ _aPosM110[1, 1] + 95, p_aPosGet[1, 3] SAY Alltrim(RetTitle("C1_XCLPLC")) OF p_oNewDialog PIXEL SIZE 040,006
	@ _aPosM110[1, 1] + 94, p_aPosGet[1, 4] MSGET oGetPl VAR _cXclPlc Picture PesqPict("SC1","C1_XCLPLC") SIZE 050,006;
							F3 CpoRetF3("C1_XCLPLC");  
							When !(p_nOpcx==2 .or. p_nOpcx==6); 
							Valid ValidPlc();
							Of p_oNewDialog PIXEL HASBUTTON

	RestArea(aArea)
	
Return

/*/{Protheus.doc} fTrtPlc
	@description Realiza o tratamento do conteúdo do campo placa
	@type Static Function
	@author Walmir Junior
	@since 03/10/2019
	@version 1.0
/*/
Static Function fTrtPlc(_pPlaca)
	local _cRet := _pPlaca
	
	_cRet	:= NoAcento(AnsiToOem(AllTrim(_pPlaca)))
	_cRet	:= strtran (_cRet, "º", "")
	_cRet	:= strtran (_cRet, "ª", "")
	_cRet	:= strtran (_cRet, chr (9), "") 
	_cRet	:= strtran (_cRet, "-", "")
	_cRet	:= strtran (_cRet, "~", "")
	_cRet	:= strtran (_cRet, "^", "")
	_cRet	:= strtran (_cRet, "´", "")
	_cRet	:= strtran (_cRet, "`", "")
	_cRet	:= strtran (_cRet, "[", "")
	_cRet	:= strtran (_cRet, "]", "")
	_cRet	:= strtran (_cRet, ":", "")
	_cRet	:= strtran (_cRet, ";", "")
	_cRet	:= strtran (_cRet, "/", "")
	_cRet	:= strtran (_cRet, "*", "")
	_cRet	:= strtran (_cRet, " ", "")
	
	_cRet := PAD(AllTrim(_cRet), 7)
	
return _cRet


/*/{Protheus.doc} ValidPlc
	@description Realiza a validação do preenchimento da placa
	@type Static Function
	@author Rafael Karczevski
	@since 18/09/2019
	@version 1.0
	/*/
Static Function ValidPlc()

	Local lRet := .T.
	
	If AllTrim(_cXclssc) $ AllTrim(GetNewPar("MV_XCLVEIC","001"))
		_cXclPlc := fTrtPlc(_cXclPlc)
		oGetPl:CtrlRefresh()
		If Empty(Alltrim(_cXclPlc))
			MsgAlert("Esta Classificação exige preenchimento da placa de veiculo!", "[Fonte: SICOMA19]")
			lRet := .F.
		ElseIf Len(Alltrim(_cXclPlc)) != 7
			MsgAlert("O campo placa de veiculo deve conter 7 caracteres!", "[Fonte: SICOMA19]")
			lRet := .F.
		EndIf
	Else
		If !Empty(Alltrim(_cXclPlc))
			MsgAlert("Esta Classificação não permite o preenchimento da placa de veiculo!", "[Fonte: SICOMA19]")
			lRet := .F.
		EndIf
	EndIf

Return lRet