#Include "PROTHEUS.CH"

/*/{Protheus.doc} MT100AGR
	Ponto de Entrada utilizado para gravação do histórico.
@author Paulo César P. Schwind
@since 12/04/2019
@return Nil
@type function

@history 24/06/2019, Franklin de Brito de Oliveira, Ajuste para considerar o mesmo titulo pai.
/*/
User Function MT100AGR()
	Local _aAreaSE2 := SE2->(GetArea())
	Local _aAreaSF1 := SF1->(GetArea())
   
	If (INCLUI .Or. ALTERA)
		DbSelectArea("SE2")
		SE2->(dbSetOrder(17))
		IF SE2->(dbSeek(SF1->F1_FILIAL + SF1->F1_SERIE + SF1->F1_DOC + SPACE(3) + "NF " + SF1->F1_FORNECE + SF1->F1_LOJA))
			While AllTrim( SE2->E2_FILIAL + SE2-> E2_TITPAI ) ==  AllTrim( SF1->F1_FILIAL + SF1->F1_SERIE + SF1->F1_DOC + SPACE(3) + "NF " + SF1->F1_FORNECE + SF1->F1_LOJA )
				If AllTrim(SE2->E2_TIPO) = 'INP'
					RecLock("SE2",.F.)
					SE2->E2_HIST := '20% ' + AllTrim(Posicione("SA2",1,xFilial("SA2") + SF1->F1_FORNECE + SF1->F1_LOJA,"A2_NOME")) + ' - ' + SF1->F1_DOC + ' ' +SF1->F1_SERIE
					SE2->(MsUnLock())
				EndIf
				SE2->(dbSkip())
			EndDo
		EndIf
	EndIf
	
	RestArea(_aAreaSE2)
	RestArea(_aAreaSF1)

Return Nil