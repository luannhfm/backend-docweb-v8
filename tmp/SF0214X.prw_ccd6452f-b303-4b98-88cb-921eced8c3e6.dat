#Include 'Protheus.ch'
#DEFINE NMAXPAGE	40

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³SF0214X   ºAutor  ³Walmir Junior       º Data ³11/11/2016   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Rotina utilizada em consulta especifica de produtos.      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Consulta específica                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
/*---------------------------------------------------------------------------
Modificações:
Franklin B. Oliveira                                               30/03/2017
Adicionadas as colunas Conta Contábil, Unidade de Medida e Tipo e funções
para melhoria de desempenho.
---------------------------------------------------------------------------*/
//Variável para retorno do produto selecionado na consulta específica.
Static _cProd := Space(TamSX3("B1_COD")[1])
User Function SF0214X()


Private _aProd		:= {}
Private _aDados		:= {}
Private _oBrowPd	
Private _oDlg
Private _lRet		:= .F.
Private cCampo   	:= ReadVar()

fwBrowse()

Return _lRet

User Function SF0214Z()

Return (_cProd)

Static Function fObtVlr()
	If Len(_aProd) > 0
		_cProd	:= _aProd[_oBrowPd:nAt, 4]
		_lRet	:= .T.
		_oDlg:End()
	Else
		MsgAlert("Nenhum produto selecionado!")
		_lRet	:= .F.
	EndIf
Return

/*
--------------------------------------------------------------------------------
{Protheus.doc} <fTurmaOk>
  Objetivo;
   Cria grid na MSDIALOG (fSeekSIGE).

@author<Jonas Nascimento>
@since<16/10/2015>
@version<1.00>
@receive<Nil>
@return<Nil>
@example<Nil>
@see<Nil>

	Utilizado por;
	- fSeekSIGE.

--------------------------------------------------------------------------------
*/
Static Function fwBrowse()
Local _btConfirm
Local _btCancel
Local _oGetFil
Local _cValFil	:= Space(100)
Local _oCmbFil
Local _cVlCmFl
Local _aFilt	:= { "Descrição", "Código"}
Local _oCmbTFl
Local _cVlTpFl
Local _aTpFil	:= {"Contém", "Igual a"}
Local nOcorr	:= 0
Local cCodAtu	:=  &(ReadVar())

fSelSB1( , , , @nOcorr, cCodAtu)

DEFINE MSDIALOG _oDlg TITLE 'Consulta Produtos' FROM 000, 000 TO 365, 800 COLORS 0, 16777215 PIXEL
	@ 005, 004 MsGet 		_oGetFil Var _cValFil 								SIZE 195, 012 OF _oDlg PIXEL
	@ 005, 200 MsComboBox	_oCmbTFl Var _cVlTpFl Items _aTpFil					SIZE 055, 012 OF _oDlg PIXEL
	@ 005, 260 MsComboBox	_oCmbFil Var _cVlCmFl Items _aFilt					SIZE 055, 012 OF _oDlg PIXEL
	@ 005, 320 BUTTON		_btConfirm	PROMPT "Pesquisar" ;	
	ACTION(FWMsgRun(, {|| fSelSB1(SubStr(_cVlTpFl, 1,1), SubStr(_cVlCmFl, 1,1), Alltrim(_cValFil), @nOcorr) , Iif( (nOcorr >= NMAXPAGE), CfgPagLbx(@_oBrowPd, @_aProd, @nOcorr), .F. )},;
			"Efetuando Consulta...", "Aguarde..."));
			SIZE 037, 012 OF _oDlg PIXEL
	
	/* Cria a GRID */				   //C. Contábil|UM|Tipo|Codigo|Produto|														
	@ 020, 004 LISTBOX _oBrowPd Fields HEADER "C. Contábil", "UM", "Tipo", "Codigo", "Produto" SIZE 390, 142 OF _oDlg PIXEL //ColSizes 25,  100						
	_oBrowPd:SetArray(_aProd)
	_oBrowPd:bLine := {|| {	_aProd[_oBrowPd:nAt,1],;
							_aProd[_oBrowPd:nAt,2],;
							_aProd[_oBrowPd:nAt,3],;
							_aProd[_oBrowPd:nAt,4],;
							_aProd[_oBrowPd:nAt,5]}}
							
	@ 168, 005 BUTTON _btConfirm	PROMPT "OK"		 	ACTION(fObtVlr())	SIZE 037, 012 OF _oDlg PIXEL
	@ 168, 050 BUTTON _btCancel		PROMPT "Cancelar"	ACTION(_oDlg:End())	SIZE 037, 012 OF _oDlg PIXEL
ACTIVATE MSDIALOG _oDlg CENTER ON INIT Iif( (nOcorr >= NMAXPAGE), CfgPagLbx(@_oBrowPd, @_aProd, @nOcorr), .F. ) 
	
Return

Static Function fSelSB1(_cTpCon, _cTpFl, _cFil, nOcorr, cCodAtu)

Local _cQuery	:= ""
Local cChave	:= ""

Private _cAlias := GetNextAlias()

_cQuery	:= " Select B1_COD, B1_DESC, B1_CONTA, B1_UM, B1_TIPO, R_E_C_N_O_ as RECNUM  From "+RetSqlName("SB1")
_cQuery	+= " Where D_E_L_E_T_ = ' ' AND"
//_cQuery	+= " B1_XPRDVEN <> 'S' AND"

If	"ADZ_PRODUT" $ cCampo
	_cQuery	+= "      B1_XPRDVEN <> 'N' "
	_cQuery	+= " AND ( B1_XPRDFIL = '" + SUBSTR(cFilAnt,0,2) + "' OR B1_XPRDFIL = '06' ) "
Else
	_cQuery	+= " B1_XPRDVEN <> 'S' "
EndIf

_cQuery	+= " AND B1_MSBLQL <> '1'"

If !Empty(_cFil)
	If _cTpFl == "C"
		_cQuery	+= Iif ( _cTpCon == "I", " AND B1_COD = '"+_cFil+"'", " AND B1_COD Like '%"+_cFil+"%'")
		_cQuery	+= " Order By  B1_COD "
	Else
		_cQuery	+= Iif ( _cTpCon == "I",  " AND B1_DESC Like '"+_cFil+"%'"," AND B1_DESC Like '%"+_cFil+"%'")
		_cQuery	+= " Order By  B1_DESC "
	EndIf
Else
 	If .Not. Empty(cCodAtu)
 		_cQuery	+= "AND B1_COD >= '" + cCodAtu + "'"
 	EndIf
	_cQuery	+= " Order By  B1_COD "
EndIf

If Select("TMPSB1")>0
	TMPSB1->(DbCloseArea())
Endif  

//-- ********************************************************************
//--    Retorna {TMPSB1} Tabela temporaria com o resultado da Query 
//-- ********************************************************************
dbUseArea(.T., "TOPCONN", TCGENQRY(,,_cQuery),"TMPSB1", .T., .T.)
TcSetField( "TMPSB1", "RECNUM", "N", 10, 0 ) // Utilizar tcsetfield para o campo RECNO
dbSelectArea("TMPSB1")
TMPSB1->(dbGoTop())

/* Limpa array para preencher com novo resultado. */
aSize(_aProd,0)
nOcorr := 0

/* Popula Array da GRID */
While !TMPSB1->(EOF()) .And. (nOcorr <= NMAXPAGE)
	AADD(_aProd, {TMPSB1->B1_CONTA,; 
				  TMPSB1->B1_UM,; 
				  TMPSB1->B1_TIPO,;
				  TMPSB1->B1_COD,;
				  TMPSB1->B1_DESC,;
				  TMPSB1->RECNUM})
	nOcorr++
	TMPSB1->(dbSkip())
EndDo

Return

/*/{Protheus.doc} CfgPagLbx
	Função para adicionar páginas em pesquisas com mais de 40 resultados
	
@author Franklin Oliveira
@since 30/03/2017
@param _oBrowPd, Object, Objeto da lista de dados
@param _aProd, Array, Array com os dados
@param nOcorr, numeric, numero de ocorrências
@type function
/*/
Static Function CfgPagLbx(_oBrowPd, _aProd, nOcorr)

_oBrowPd:bSkip := {|NSKIP, NOLD, nMax| nMax:=EVAL( _oBrowPd:BLOGICLEN ), NOLD := _oBrowPd:NAT, _oBrowPd:NAT += NSKIP,;
 					_oBrowPd:NAT := MIN( MAX( _oBrowPd:NAT, 1 ), nMax ),;
 					If(_oBrowPd:nAt == nMax, fNextPag(@_oBrowPd, @_aProd, @nOcorr), .F.),;
 					_oBrowPd:NAT - NOLD}

Return

/*/{Protheus.doc} fNextPag
	Função para adicionar registros em pesquisas com mais de 40 resultados
	
@author Franklin Oliveira
@since 30/03/2017
@param _oBrowPd, Object, Objeto da lista de dados
@param _aProd, Array, Array com os dados
@param nOcorr, numeric, numero de ocorrências
@type function
/*/
Static Function fNextPag(_oBrowPd, _aProd, nOcorr)
	
	TMPSB1->(dbGoTo(_aProd[_oBrowPd:nAt, 6]))
	
	/* Popula Array da GRID */
	While !TMPSB1->(EOF()) .And. (nOcorr <= (nOcorr + NMAXPAGE))
		AADD(_aProd, {TMPSB1->B1_CONTA,; 
				  TMPSB1->B1_UM,; 
				  TMPSB1->B1_TIPO,;
				  TMPSB1->B1_COD,;
				  TMPSB1->B1_DESC,;
				  TMPSB1->RECNUM})
		nOcorr++
		TMPSB1->(dbSkip())
EndDo
	
Return (Nil)