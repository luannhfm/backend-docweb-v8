#INCLUDE "PROTHEUS.CH"
#INCLUDE "RWMAKE.CH"
#INCLUDE "TBICONN.CH"


/*/{Protheus.doc} DEVUPD0
@description Tela para seleção da empresa

@author  Fernando Alencar
@version P11 e P10
@since   13/11/2011

@obs Incluido uso da DbGrid por Helitom Silva - 28/08/2014

/*/
User Function DEVUPD0()

	Local aData 	:= {}
	Local aSelData 	:= {}

	aData 	 	:= _LoadData()
	aSelData 	:= _SelectData(aData)

Return aSelData


/*/{Protheus.doc} _LoadData
@description Carrega a lista de empresas do ambiente

@author  Fernando Alencar
@version P11 e P10
@since   15/09/2011

@return  empresas selecionadas

/*/
Static Function _LoadData()

	Local aData := {}
	Local cAux  := ''
	
	dbSelectArea( 'SM0' )
	dbGoTop()

	While !SM0->( EOF() )
	
		//If cAux <> SM0->M0_CODIGO
		
			aAdd( aData, { Recno(), SM0->M0_CODIGO, SM0->M0_CODFIL, SM0->M0_FILIAL } )
			
			cAux := SM0->M0_CODIGO
			
		//EndIf
					
		SM0->( dbSkip() )
		
	End

Return aData


/*/{Protheus.doc} SelectTools
@description Exibe tela onde o usuário poderá selecionar quais empresas

@author  Fernando Alencar
@version P11 e P10
@since   15/09/2011

@return  empresas selecionadas

/*/
Static Function _SelectData(aData)

	Local lInverte 	:= .t.
	Local cMarca 	:= "OK"
	Local oMainWnd
	Local nOpc 		:= 0

	BLACK_COLOR 	:= 0
	BLUE_COLOR 		:= 8388608
	WHITE_COLOR 	:= 16777215
	GRAY_COLOR 		:= 8421504

	SetPrvt("oMainWnd","oFont","oPanel","oBrwTrb","oBtnExec")
	
	oMainWnd 	:= MSDialog():New(0,0,400,500, "DevTools",,,,,,,,,.T.)

	oFont		:= TFont():New('Arial',,60, .t.)
	oPanel		:= tPanel():New(0,0,"DICIONÁRIO",oMainWnd,oFont,.F.,,WHITE_COLOR,GRAY_COLOR,255,22,,)

	oBrwTrb		:= DbGrid():Create(30,5,175,245,GD_UPDATE,,,,,,,,,, oMainWnd, _Header(aData), {}, 1, 1)
	oBrwTrb:oBrowse:lHScroll  	:= .F.
	oBrwTrb:oBrowse:nScrollType := 1
	
	oBtnExec := TButton():New(180,185,'&Executar', oMainWnd, {|| aData := _GetData(), nOpc := 1, oMainWnd:End() },60,15,,,,.T.)

	oMainWnd:Activate(,,,.T.,,, {|| _aCols(aData)})

	If !(nOpc = 1)
		aData := {}
	EndIf

Return aData


/*/{Protheus.doc} Header

@author  Fernando Alencar
@since   13/11/2011
@return  Array

/*/
Static Function _Header(aData)

	Local aHeader := {}

	aAdd(aHeader, {"codigo",	"CODIGO", 	"@!",   Len(aData[1][2]),	0,"" ,,"C" ,,,,,,"V",,,.F.})
	aAdd(aHeader, {"Filial",	"FILIAL", 	"@!",	Len(aData[1][3]),	0,"" ,,"C" ,,,,,,"V",,,.F.})
	aAdd(aHeader, {"Nome"  ,	"NOME"  , 	"@!",	Len(aData[1][4]),	0,"" ,,"C" ,,,,,,"V",,,.F.})
		
Return aHeader


/*/{Protheus.doc} _CriaTrab

@author  Fernando Alencar
@since   13/11/2011
@return  Alias

/*/
Static Function _aCols(aData)
	
	Local nX := 0
	
	oBrwTrb:Limpar()
	
	For nX := 1 To Len(aData)

		oBrwTrb:AddLinha()
		
		oBrwTrb:SetColuna('CODIGO', aData[nX][2])
		oBrwTrb:SetColuna('FILIAL', aData[nX][3])
		oBrwTrb:SetColuna('NOME'  , aData[nX][4])
		
	Next
	
	oBrwTrb:PosLinha(1)
	oBrwTrb:Refresh()
	
Return


/*/{Protheus.doc} _GetData
@description Retorna as linhas selecionadas em array multidimencional

@author  Fernando Alencar
@since   13/11/2011

@return  Alias

/*/
Static Function _GetData()

	Local aData := {}
	Local nX	:= 0
	
	For nX := 1 to oBrwTrb:GetQtdLinha()
		
		oBrwTrb:PosLinha(nX)
		
		If oBrwTrb:Marcado() 
			
			aadd(aData, { oBrwTrb:GetColuna('CODIGO') ,;
				          oBrwTrb:GetColuna('FILIAL') ,;
				          oBrwTrb:GetColuna('NOME')   })
				          
		EndIf

	Next

Return aData


/*/{Protheus.doc} MyOpenSM0Ex
@description Funcao de processamento abertura do SM0 modo exclusivo

@author  Microsiga
@since   14/06/2010

@obs Uso MyOpenSM - Gerado por EXPORDIC / Upd. V.3.10 EFS 

@return  lOpen, Logico, Se abriu em modo exclusivo .T.

/*/
Static Function MyOpenSM0Ex()

	Local lOpen := .F.
	Local nLoop := 0

	For nLoop := 1 To 20
		dbUseArea( .T., , 'SIGAMAT.EMP', 'SM0', .F., .F. )

		If !Empty( Select( 'SM0' ) )
			lOpen := .T.
			dbSetIndex( 'SIGAMAT.IND' )
			Exit
		EndIf
		Sleep( 500 )
	Next nLoop

	If !lOpen
		ApMsgStop( 'Não foi possível a abertura da tabela ' + ;
				   'de empresas de forma exclusiva.', 'ATENÇÃO' )
	EndIf

Return lOpen