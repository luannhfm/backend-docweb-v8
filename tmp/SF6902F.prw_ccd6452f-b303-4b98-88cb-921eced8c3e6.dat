#Include 'Protheus.ch'
#Include 'Parmtype.ch'

/*/{Protheus.doc} SF6902F
	Função para validar se as datas de inicio e fim da execução estão preenchidos
	ou se estão com valores permitidos.
	Valido apenas para contrato de vendas. 

@author Franklin de Brito de Oliveira
@since 16/11/2015
@version 1.00
@return lRet, Lógico, se .T. Continua inclusão, se .F. não confirma a inclusão

/*/
User Function SF6902F()

Local lRet := .T.

	If INCLUI .Or. ALTERA
		//Data de inicio da execução em branco
		If Empty(M->CN9_XDTINI) 
			Aviso(FunName()+"/"+ProcName(), "É necessário preencher a data de inicio da execução", {"OK"}, 3)
			lRet := .F.
		
		//Data de fim da execução em branco
		ElseIf Empty(M->CN9_XDTFIM)
			Aviso(FunName()+"/"+ProcName(), "É necessário preencher a data de fim da execução", {"OK"}, 3)
			lRet := .F.
		
		//Data de inicio da execução Menor do que inicio da vigência
		ElseIf M->CN9_XDTINI < M->CN9_DTINIC 
			Aviso(FunName()+"/"+ProcName(), "A data de incio da execução não pode ser anterior ao inicio da vigência do contrato", {"OK"}, 3)
			lRet := .F.
		
		//Data de inicio da execução maior do que fim da vigência
		ElseIf M->CN9_XDTINI > M->CN9_DTFIM 
			Aviso(FunName()+"/"+ProcName(), "A data de incio da execução não pode ser posterior ao fim da vigência do contrato", {"OK"}, 3)
			lRet := .F.
		
		//Data de término da execução menor do que inicio da vigência
		ElseIf M->CN9_XDTFIM < M->CN9_DTINIC
			Aviso(FunName()+"/"+ProcName(), "A data de término da execução não pode ser anterior ao inicio da vigência do contrato", {"OK"}, 3)
			lRet := .F.
		
		//Data de término da execução maior do que fim da vigência
		ElseIf M->CN9_XDTFIM > M->CN9_DTFIM 
			Aviso(FunName()+"/"+ProcName(), "A data de término da execução não pode ser posterior ao fim da vigência do contrato", {"OK"}, 3)
			lRet := .F.
		End
	EndIf

Return lRet