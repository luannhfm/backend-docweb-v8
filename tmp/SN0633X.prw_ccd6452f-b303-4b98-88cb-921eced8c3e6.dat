#Include 	"Protheus.ch"
#Include	"Rwmake.ch"
#Include	"Totvs.ch"
#Include	"TopConn.CH"

/*
+--------------------------------------------------------------------------+
|{Protheus.doc}<SN0633X>                                                   |
|303 - Relatorio de Pagamentos - Sessao de Pagamentos                	   |
|                                                                          |
|@Author<Diogo C. Barros>                                                  |
|@since<28/03/2014>                                                        |
+--------------------------------------------------------------------------+
|@version<Nil>                                                             | 
+--------------------------------------------------------------------------+
| Revisao em: 11/07/2014                              por: Antonio Dantas  |
| Ajuste para atender solicitacao Guilherme via e-mailem 10/07/2014 14:30  |
| Implementado Filtros por SESSAO                                          |
+--------------------------------------------------------------------------+
|@parameters<Nil>                                                          |
|@return<Nil>                                                              |
+--------------------------------------------------------------------------+
|Uso        |SENAI										     			   |
+-----------+--------------------------------------------------------------+
*/
User Function SN0633X()
Local oReport
Private _cPerg 		:= "SN0633X" 		//Nome da pergunta cadastrado no SX1
Private _cAlias01 	:= GetNextAlias() 	// A variavel recebe um nome de uma tabela temporária sequencial
Private _aSM0 		:= {}
_aSM0 := GETAREA()
AjustaSX1(_cPerg)	//Valida as perguntas, atualizando sempre conforme a do código
If Pergunte(_cPerg,.T.) = .F.
	Return()
EndIf
oReport := ReportDef()
oReport:PrintDialog()
RESTAREA(_aSM0)		//Restaura a area salva
Return


/*
+--------------------------------------------------------------------------+
|{Protheus.doc}<ReportDef>                                                 |
|Relatorio de Pagamentos - sessao de Pagamentos.    							   |
|                                                                          |
|@Author<Diogo C. Barros>                                                  |
|@since<28/03/2014>                                                        |
|@version<Nil>                                                             |
|                                                                          |
|@parameters<Nil>                                                          |
|@return<Nil>                                                              |
+--------------------------------------------------------------------------+
|Uso        |SENAI										     			   |
+-----------+--------------------------------------------------------------+
*/
Static Function ReportDef()
Local oReport
Local cReport	:= "SN0633X"
Local cTitulo	:= "303 - Relatorio de Pagamentos - Sessao de Pagamentos"
Local cDesc		:= cTitulo
//Verifica se existe a pergunta (.F. -> não chama a tela de parametros)
Pergunte(_cPerg,.F.)
oReport := TReport():New(cReport,cTitulo ,_cPerg,{|oReport| ReportPrint(oReport)},cDesc)
oSec01:=TRSection():New(oReport,"")
//New(oParent,cName,cAlias,cTitle,cPicture,nSize,lPixel,bBlock,cAlign,lLineBreak,cHeaderAlign,lCellBreak,nColSpace,lAutoSize,nClrBack,nClrFore,lBold)
TRCell():New(oSec01,"Codigo"		,,"Cod.Sessao"		,,12)
TRCell():New(oSec01,"Ano"			,,"Ano"				,, 05)
TRCell():New(oSec01,"Mes"			,,"Mes"				,, 15)  	
TRCell():New(oSec01,"Periodo"		,,"Periodo"			,, 20)
TRCell():New(oSec01,"DtAbert"  		,,"Data Abertura"	,, 15)
TRCell():New(oSec01,"DtLibera"		,,"Data Liberação"	,, 15)
TRCell():New(oSec01,"DtRemes"		,,"Data Remessa"	,, 15)
TRCell():New(oSec01,"Quantidade"	,,"Qtd Matric"		,, 20)
TRCell():New(oSec01,"ValTotal"		,,"Vlr.Total"		,, 20)
TRCell():New(oSec01,"Situacao"		,,"Situacao"		,, 30)
//New(oCell,cName,cFunction,oBreak,cTitle,cPicture,uFormula,lEndSection,lEndReport,lEndPage,oParent,bCondition,lDisable,bCanPrint)
TRFunction():New(oSec01:Cell("Codigo")		,nil,"COUNT",,,"@E 999999"				,,.T.,.F.,.F.,oSec01)
TRFunction():New(oSec01:Cell("Quantidade")	,nil,"SUM"	,,,"@E 999,999,999,999"		,,.T.,.F.,.F.,oSec01)
TRFunction():New(oSec01:Cell("ValTotal")	,nil,"SUM"	,,,"@E 999,999,999,999.99"	,,.T.,.F.,.F.,oSec01)
Return(oReport)

/*
+--------------------------------------------------------------------------+
|{Protheus.doc}<ReportPrint>                                               |
|Relatorio de Pagamentos - sessao de Pagamentos. 						   |
|                                                                          |
|@Author<Diogo C. Barros>                                                  |
|@since<28/03/2014>                                                        |
|@version<Nil>                                                             |
|                                                                          |
|@parameters<Nil>                                                          |
|@return<Nil>                                                              |
+--------------------------------------------------------------------------+
|Uso        |SENAI										     			   |
+-----------+--------------------------------------------------------------+
*/
Static Function ReportPrint(oReport)
Local _oSec01 		:= oReport:Section(1)
Local _nQReg		:= 0
Pergunte(_cPerg,.F.)
//_oFaturas:Cell("Emissao" 	):SetBlock({|| Substr(fQrySE1->E1_EMISSAO,7,2) + "/" + Substr(fQrySE1->E1_EMISSAO,5,2) + "/" + Substr(fQrySE1->E1_EMISSAO,1,4) })
_oSec01:Cell("Codigo"	):SetBlock({|| (_cAlias01)->SESSAO })
_oSec01:Cell("Ano"		):SetBlock({|| (_cAlias01)->ANO })
_oSec01:Cell("Mes"		):SetBlock({|| MesExtenso( (_cAlias01)->MES ) })
_oSec01:Cell("Periodo"	):SetBlock({|| (_cAlias01)->PERIODO })
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//| Antonio Dantas                                          12/06/2014  |
//| Ajustes para adequação do Relastorio ao formato de dados (DATA).    |
//| Apresenta as Data no formado: DD/MM/AAAA.                           |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
&&-- _oSec01:Cell("DtAbert"	):SetBlock({|| Substr((_cAlias01)->DATA_ABERTURA,7,2) + "/" + Substr((_cAlias01)->DATA_ABERTURA,5,2) + "/" + Substr((_cAlias01)->DATA_ABERTURA,1,4) })
&&-- _oSec01:Cell("DtFecha"	):SetBlock({|| Substr((_cAlias01)->DATA_FECHAMENTO,7,2) + "/" + Substr((_cAlias01)->DATA_FECHAMENTO,5,2) + "/" + Substr((_cAlias01)->DATA_FECHAMENTO,1,4) })
&&-- _oSec01:Cell("DtLibera"	):SetBlock({|| Substr((_cAlias01)->DATA_LIBERACAO,7,2) + "/" + Substr((_cAlias01)->DATA_LIBERACAO,5,2) + "/" + Substr((_cAlias01)->DATA_LIBERACAO,1,4) })
_oSec01:Cell("DtAbert"	):SetBlock({|| (_cAlias01)->DATA_ABERTURA 		})
_oSec01:Cell("DtLibera"	):SetBlock({|| (_cAlias01)->DATA_LIBERACAO 	})  
_oSec01:Cell("DtRemes"	):SetBlock({|| (_cAlias01)->DATA_REMES 		})  
&&--
_oSec01:Cell("Quantidade"):SetBlock({|| Transform( (_cAlias01)->QUANTIDADE,"@E 999,999,999" ) })
_oSec01:Cell("Quantidade"):SetHeaderAlign("RIGHT")
_oSec01:Cell("Quantidade"):SetAlign("RIGHT")
&&--
_oSec01:Cell("ValTotal"	):SetBlock({|| Transform( (_cAlias01)->VALOR_TOTAL,"@E 999,999,999.99" ) })
_oSec01:Cell("ValTotal"	):SetHeaderAlign("RIGHT")
_oSec01:Cell("ValTotal"	):SetAlign("RIGHT")
&&--
_oSec01:Cell("Situacao"	):SetBlock({|| fPegSitu((_cAlias01)->SITUACAO) })
MsAguarde({|| CursorWait(), fQryPAG(), CursorArrow()},"Selecionando Registros","Buscando Informações dos Pagamentos...")
MsAguarde({|| CursorWait(), (_cAlias01)->(DbEval({|| _nQReg++})), CursorArrow() }, "Registros", "Verificando a Quantidade de Registros...")
oReport:SetMeter(_nQReg)
(_cAlias01)->(dbGoTop())
_oSec01:Init()
While (_cAlias01)->(!EOF())
	// Sai caso seja pressionado botao cancelar
	If oReport:Cancel()
		Exit
	EndIf
	_oSec01:PrintLine()
	(_cAlias01)->(dbSkip())
	oReport:IncMeter() // Incrementa a regua de progressão
EndDo
_oSec01:Finish()
(_cAlias01)->(dbCloseArea())
Return(Nil)


/*
+--------------------------------------------------------------------------+
|{Protheus.doc}<fQryPAG>                                                   |
|Monta query que busca as matriculas. 	    							   |
|                                                                          |
|@Author<Diogo C. Barros>                                                  |
|@since<28/03/2014>                                                        |
|@version<Nil>                                                             |
|                                                                          |
|@parameters<Nil>                                                          |
|@return<Nil>                                                              |
+--------------------------------------------------------------------------+
|Uso        |SENAI										     			   |
+-----------+--------------------------------------------------------------+
*/
Static Function fQryPAG()
Local _cQry 	:= ""
Local _cMvPar09 := "("
Pergunte(_cPerg,.F.)
_cMvPar09	+= Alltrim(MV_PAR09) + ")"
_cQry += " SELECT ZP7_FILIAL FILIAL, ZP7_XSESSA SESSAO, ZP7_XSEG, Substring(ZP7_XDATAG,5,2) MES , Substring(ZP7_XDATAG,1,4) ANO,ZP7_XPERIO PERIODO, " + CRLF
_cQry += " ZP6_XDATA DATA_ABERTURA, ZP6_XDTFIM DATA_FECHAMENTO, ZP7_XDTLIB DATA_LIBERACAO, " + CRLF
_cQry += " ZPA_XDATA DATA_REMES, COUNT(*) as QUANTIDADE, SUM(ZP7_XVALOR) VALOR_TOTAL, ZP7_XSTATU SITUACAO" + CRLF
_cQry += " FROM " + RetSqlName("ZP7") + " ZP7 " + CRLF
_cQry += " LEFT JOIN " + RetSqlName("ZP6") + " ZP6 ON ZP6.D_E_L_E_T_ <> '*' and ZP6_FILIAL = ZP7_FILIAL and ZP6_XCOD = ZP7_XSESSA " + CRLF
_cQry += " and (ZP6.ZP6_XDATA = '"+Space(TAMSX3("ZP6_XDATA")[1])+"' or ZP6.ZP6_XDATA BETWEEN '"+Dtos(MV_PAR03)+"' and '"+Dtos(MV_PAR04)+"')" + CRLF
_cQry += " LEFT JOIN " + RetSqlName("ZPA") + " ZPA ON ZPA.D_E_L_E_T_ = ' ' and ZPA_FILIAL = ZP7_FILIAL and ZPA.ZPA_XCONVE = ZP7.ZP7_XCONVE and " + CRLF
_cQry += " ZPA.ZPA_XCOD = ZP7.ZP7_XREMES and ZPA.ZPA_XTIPO = 'PG' " + CRLF
_cQry += " and (ZPA.ZPA_XDATA = '"+Space(TAMSX3("ZPA_XDATA")[1])+"' or ZPA_XDATA BETWEEN '"+Dtos(MV_PAR05)+"' and '"+Dtos(MV_PAR06)+"')" + CRLF
_cQry += " WHERE ZP7.D_E_L_E_T_ <> '*' " + CRLF
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//| Antonio Dantas                                          12/06/2014  |
//| Ajustes para adequação do Relastorio ao formato de dados (DATA).    |
//| Apresenta as Data no formado: DD/MM/AAAA.                           |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
_cQry += " and ZP7_FILIAL = '"+cFilAnt+"' " + CRLF
_cQry += " and ZP7_XPERIO BETWEEN '"+MV_PAR01+"' and '"+MV_PAR02+"' " + CRLF
_cQry += " and (ZP7_XDTLIB = '"+Space(TAMSX3("ZP7_XDTLIB")[1])+"' or ZP7_XDTLIB BETWEEN '"+Dtos(MV_PAR07)+"' and '"+Dtos(MV_PAR08)+"') " + CRLF
If !Empty(Alltrim(MV_PAR09))
	_cQry += " and ZP7_XSTATU IN "+FormatIn(MV_PAR09,";")+" " + CRLF
EndIf
&&--   
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//| Antonio Dantas                                            11/07/2014 |
//| Ajuste para atender solicitacao de Guilherme via e-mailem 10/07/2014 |
//| 14:30. Implementado Filtros por SESSAO e por RANGE de STATUS         |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
_cQry += " and ZP7_XSESSA BETWEEN '"+MV_PAR10+"' and '"+MV_PAR11+"' "+ CRLF
&&--   
_cQry += "GROUP BY ZP7_FILIAL, ZP7_XPERIO, ZP7_XSEG ,ZP7_XSESSA, ZP7_XDATAG, ZP6_XDATA, ZP6_XDTFIM, ZP7_XDTLIB, ZPA_XDATA ,ZP7_XSTATU " 	+ CRLF    
_cQry += "Order by ZP7_FILIAL, ZP7_XPERIO, ZP7_XSEG ,ZP7_XSESSA, ZP7_XDATAG, ZP6_XDATA, ZP6_XDTFIM, ZP7_XDTLIB, ZPA_XDATA ,ZP7_XSTATU " 	+ CRLF    
MemoWrite("C:\temp\"+FunName()+"_"+ProcName()+".TXT",_cQry)
If Select(_cAlias01) > 0
	DBSelectArea(_cAlias01)
	(_cAlias01)->(DbCloseArea())
EndIf
dbUseArea(.T.,"TOPCONN",TcGenQry(,,ChangeQuery(_cQry)),_cAlias01,.T.,.F.)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//| Antonio Dantas                                          12/06/2014  |
//| Ajustes para adequação do Relastorio ao formato de dados (DATA).    |
//| Apresenta as Data no formado: DD/MM/AAAA.                           |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
TCSETFIELD(_cAlias01,"ZP7_XPERIO"		,"D",8,0)
TCSETFIELD(_cAlias01,"ZP6_XDATA" 		,"D",8,0)
TCSETFIELD(_cAlias01,"ZP7_XDTLIB"		,"D",8,0)  
//-- 
TCSETFIELD(_cAlias01,"DATA_ABERTURA"	,"D",8,0)
TCSETFIELD(_cAlias01,"DATA_FECHAMENTO"	,"D",8,0)
TCSETFIELD(_cAlias01,"DATA_LIBERACAO"	,"D",8,0)   
TCSETFIELD(_cAlias01,"DATA_REMES"		,"D",8,0)   
Return()

/*
+--------------------------------------------------------------------------+
|{Protheus.doc}<AjustaSX1>                                                 |
|Cria as perguntas.					 	    							   |
|                                                                          |
|@Author<Diogo C. Barros>                                                  |
|@since<28/03/2014>                                                        |
|@version<Nil>                                                             |
|                                                                          |
|@parameters<Nil>                                                          |
|@return<Nil>                                                              |
+--------------------------------------------------------------------------+
|Uso        |SENAI										     			   |
+-----------+--------------------------------------------------------------+
*/
Static Function AjustaSX1(_cPerg)
Local _aHelp01 := {"Informe o Periodo de/ate"				}
Local _aHelp02 := {"Informe a Data de Abertura de/ate"		}
Local _aHelp03 := {"Informe a Data de Fechamento de/ate"	}
Local _aHelp04 := {"Informe a Data de LIBERACAO de/ate"		}
Local _aHelp05 := {"Informa as Situacoes p/ Filtro."	}
Local _aHelp06 := {"Informe o Cod. da Sessão de/ate"}
AADD(_aHelp05, " Exemplo com uma Situacao : ")
AADD(_aHelp05, " 'A' ")
AADD(_aHelp05, " Exemplo com tres Situacoes : ")
AADD(_aHelp05, " 'A','B','C' ")
AADD(_aHelp05, " Para contemplar todas as Situacoes")
AADD(_aHelp05, " deixe o campo em branco")
DbSelectArea("SX1")
DbSetOrder(1)
//--------------------------------------------------------------------
//PutSX1 - cGrupo, cOrdem, _cPergunt, cPerSpa, cPerEng, cVar, cTipo, nTamanho, nDecimal, nPresel, cGSC, cValid, cF3, cGrpSxg, cPyme, cVar01, cDef01, cDefSpa1, cDefEng1, cCnt01, cDef02, cDefSpa2, cDefEng2, cDef03, cDefSpa3, cDefEng3, cDef04, cDefSpa4, cDefEng4, cDef05, cDefSpa5, cDefEng5, aHelpPor, aHelpEng, aHelpSpa, cHelp
u_SFPUTSX1(_cPerg, "01", "Periodo de		?","","","mv_ch1","C",08,00,00,"G","" ,"ZP4REL","","","mv_par01","","","","","","","","","","","","","","","","",_aHelp01,"","","")
u_SFPUTSX1(_cPerg, "02", "Periodo Ate		?","","","mv_ch2","C",08,00,00,"G","" ,"ZP4REL","","","mv_par02","","","","","","","","","","","","","","","","",_aHelp01,"","","")
u_SFPUTSX1(_cPerg, "03", "Dt Abertura de	?","","","mv_ch3","D",08,00,00,"G","" ,""   	,"","","mv_par03","","","","","","","","","","","","","","","","",_aHelp02,"","","")
u_SFPUTSX1(_cPerg, "04", "Dt Abertura Ate	?","","","mv_ch4","D",08,00,00,"G","" ,""   	,"","","mv_par04","","","","","","","","","","","","","","","","",_aHelp02,"","","")
u_SFPUTSX1(_cPerg, "05", "Dt Remessa de		?","","","mv_ch5","D",08,00,00,"G","" ,""   	,"","","mv_par05","","","","","","","","","","","","","","","","",_aHelp03,"","","")
u_SFPUTSX1(_cPerg, "06", "Dt Remessa Ate	?","","","mv_ch6","D",08,00,00,"G","" ,""   	,"","","mv_par06","","","","","","","","","","","","","","","","",_aHelp03,"","","")
u_SFPUTSX1(_cPerg, "07", "Dt Liberação de	?","","","mv_ch7","D",08,00,00,"G","" ,""   	,"","","mv_par07","","","","","","","","","","","","","","","","",_aHelp04,"","","")
u_SFPUTSX1(_cPerg, "08", "Dt Liberação Ate	?","","","mv_ch8","D",08,00,00,"G","" ,""   	,"","","mv_par08","","","","","","","","","","","","","","","","",_aHelp04,"","","")
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//| Antonio Dantas                                            11/07/2014 |
//| Ajuste para atender solicitacao de Guilherme via e-mailem 10/07/2014 |
//| 14:30. Implementado Filtros por SESSAO.                              |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
&&--u_SFPUTSX1(_cPerg, "09", "Situacao			?","","","mv_ch9","C",50,00,00,"G","" ,""	 ,"","","mv_par09","","","","","","","","","","","","","","","","",_aHelp05,"","","")  
u_SFPUTSX1(_cPerg, "09", "Com Seguinte(s) Status ?"	,"","","mv_ch9","C",99,00,00,"G","u_MrkStat()"		,""	 	,"","","mv_par9","","","","","","","","","","","","","","","","",_aHelp05,"","","")
u_SFPUTSX1(_cPerg, "10", "Sessão de		?"			,"","","mv_cha","C",10,00,00,"G","" 		   		,"ZP6"	,"","","mv_par10","","","","","","","","","","","","","","","","",_aHelp06,"","","")
u_SFPUTSX1(_cPerg, "11", "Sessão Ate	?"			,"","","mv_chb","C",10,00,00,"G","" 				,"ZP6"	,"","","mv_par11","","","","","","","","","","","","","","","","",_aHelp06,"","","")
Return(Nil)

/*
+--------------------------------------------------------------------------+
|{Protheus.doc}<fPegSitu>                                                 |
|Cria as perguntas.					 	    							   |
|                                                                          |
|@Author<Diogo C. Barros>                                                  |
|@since<28/03/2014>                                                        |
|@version<Nil>                                                             |
|                                                                          |
|@parameters<_cCodSit>                                                     |
|_cCodSit = Codigo da Situacao (ZP7_XSTATU)                                |
|@return<_cDesc>                                                           |
|_cDesc = Descricao do codigo                                              |
+--------------------------------------------------------------------------+
|Uso        |SENAI										     			   |
+-----------+--------------------------------------------------------------+
*/
Static Function fPegSitu(_cCodSit)
Local _cDescricao 	:= ""
Local _nP			:= 0
Local _cRangeST 	:= "BTCLxEORPND"
Local _aStatus 		:={	"Bloqueado p/Remessa"	,;
						"Transf. p/Prox Sessao"	,;
						"Corrigido"				,;
						"Liberado p/Remessa"	,;
						"Não Liberado p/Remessa",;
						"Remessa Gerada"		,;
						"Ordem de Pgto Agendada",;
						"Rejeitado"				,;
						"Pago"					,;
						"Não Pago"				,;
						"Devolvido"				 }
_nP			:= At(Alltrim(_cCodSit),_cRangeST)  
_cDescricao	:= If(_nP>0,Alltrim(_cCodSit)+"-"+_aStatus[_nP],"DESCONHECIDO")
Return _cDescricao
