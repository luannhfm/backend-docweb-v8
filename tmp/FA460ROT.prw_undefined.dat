#Include 'Protheus.ch'

/*/{Protheus.doc} FA460ROT
@description O ponto de entrada FA460ROT pode ser utilizado para incluir mais opções na variável aRotina (parâmetro utilizado pelo ponto de entrada) para que o usuário possa alterar. O retorno da variável deve ser um array com todas as opções que o usuário pode utilizar.
@obs Utilizado para adicionar rotinas de renegociação e de baixa de títulos com cartão de credito ou debito.
@type User Function
@author Alan Teles de Oliveira
@since 04/12/2018
@version 12.1.17
@param ParamIXB, Vetor, array com aRotina
@return aRet, array, aRotina com rotinas adicionais.
@see http://tdn.totvs.com/pages/releaseview.action?pageId=6071233
/*/
User Function FA460ROT()

    Local nX    := 0
    Local aRet  := {}
    Local aRen  := {}
    
    aAdd(aRen, {'Incluir',          'u_SFINA20()',  0, 4})
    aAdd(aRen, {'Termo de Divida',  'u_DTFINR03()', 0, 4})
    
    For nX := 1 to Len(ParamIXB)

        If ParamIXB[nX][2] == 'FA460Visul'
            aAdd(aRet, {'Inc. Renegociação', 'u_SFINA20()',  0, 4})                    
        EndIf

        aAdd(aRet, ParamIXB[nX])

    Next

    aAdd(aRet, {'Renegociação', aRen, 0, 6}) // Renegociação Titulos
    aAdd(aRet, {'Baixa com Cartão', 'u_SFFN462A()', 0, 9})

Return aRet
