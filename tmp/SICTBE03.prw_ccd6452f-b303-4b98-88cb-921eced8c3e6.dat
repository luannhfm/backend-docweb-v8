#Include "Protheus.Ch"
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ SICTBE03 ³ Autor ³ CADUBITSKI            ³ Data ³ Ago/2012 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ ExecBlock utilizado no LP 500/010.                         ³±±
±±³          ³ Retornar o valor que compete a filial corrente ao mutuo.   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
User Function SICTBE03()

Local aArea		:= GetArea()
Local nRet		:= 0 //Retorna o valor de mutuo referente a filial corrente
Local cAli 		:= GetNextAlias()        
Local cAli2		:= GetNextAlias()        
Local cFilSE1 	:= SE1->E1_FILIAL 
Local cMutuo	:= SE1->E1_XMUTUO
Local nVlrSE2	:= 0 //Valor do titulo principal que originou o mutuo
Local nRatMut	:= 0 //Valor total do mutuo rateado entre as empresas

If Alltrim(SE1->E1_PARCELA) == '1'

	If Empty(cMutuo)
		Return(nRet)
	EndIf

	//Select para localizar o titulo principal do mutuo
	BeginSQL Alias cAli
		SELECT 
			E2_FILIAL,
			(E2_VALOR+E2_VRETPIS+E2_VRETCOF+E2_VRETCSL+E2_VRETIRF+E2_VRETINS+E2_ISS) AS VLRTOT
	
		FROM %table:SE2% SE2
	
		WHERE E2_FILIAL = %Exp:cFilSE1%
			AND SE2.%NotDel%                           
			AND E2_XMUTUO = %Exp:cMutuo%
	EndSQL
	
	//MemoWrite("\SYSTEM\SICTBE01.SQL",GetLastQuery()[2])
	
	DbSelectArea((cAli))
	If Select((cAli)) > 0
		If !Empty((cAli)->VLRTOT)
			nVlrSE2 := (cAli)->VLRTOT
		EndIf
	EndIf
	DbCloseArea((cAli))
	
	//Select para identificar o somatorio de mutuo distribuido
	BeginSQL Alias cAli2
		SELECT 
			SUM(ZX_VALOR) AS VALOR
	
		FROM %table:SZX% SZX
	
		WHERE ZX_FILIAL = %xFilial:SZX%
			AND SZX.%NotDel%                           
			AND ZX_RATEIO = %Exp:cMutuo%
	EndSQL
	
	DbSelectArea((cAli2))
	If Select((cAli2)) > 0
		If !Empty((cAli2)->VALOR)
			nRatMut := (cAli2)->VALOR
		EndIf
	EndIf
	DbCloseArea((cAli2))
	
	nRet := nVlrSE2 - nRatMut
	
EndIf

RestArea(aArea)
Return(nRet)