#include "totvs.ch"
#include "restful.ch"

//-------------------------------------------------------------------
/*/{Protheus.doc} Lancamentos Contabeis
Declaração do GET de Lancamentos Contábeis
@author Walmir Junior
@since 18/09/2020
@version 1.0
/*/
//-------------------------------------------------------------------
WSRESTFUL lancontabil DESCRIPTION 'endpoint Lancamentos Contabeis - VENUS ' FORMAT "application/json,text/html"
    WSDATA Page              AS INTEGER OPTIONAL
    WSDATA aQueryString      AS ARRAY    OPTIONAL
    
 	WSMETHOD GET LancContabil;
	    DESCRIPTION "Retorna uma lista de Lancamentos Contabeis";
	    WSSYNTAX "/api/lancontabil" ;
        PATH "/api/lancontabil" ;
	    PRODUCES APPLICATION_JSON
 	
END WSRESTFUL

//-------------------------------------------------------------------
/*/{Protheus.doc} GET LancContabil
Método GET com id LancContabil
@author Walmir Junior
@since 18/09/2020
@version 1.0
/*/
//-------------------------------------------------------------------
WSMETHOD GET LancContabil QUERYPARAM Page WSREST lancontabil
Return getLancContabil(self)

//-------------------------------------------------------------------
/*/{Protheus.doc} GET getPrdList
Função para tratamento da requisicao GET
@author Walmir Junior
@since 18/09/2020
@version 1.0
/*/
//-------------------------------------------------------------------
Static Function getLancContabil( oWS )
   Local lRet  as logical
   Local oLanc as object
   DEFAULT oWS:Page      := 1  
   lRet        := .T.
   
   oLanc := LanContabilAdapter():new( 'GET' )
   oLanc:setPage(oWS:Page)
   oLanc:setPageSize(10)
   oLanc:setUrlFilter(oWS:aQueryString)
   oLanc:GetListLanCtbil()
   //Se tudo ocorreu bem, retorna os dados via Json
   If oLanc:lOk
       oWS:SetResponse(oLanc:getJSONResponse())
   Else
   //Ou retorna o erro encontrado durante o processamento
       SetRestFault(oLanc:GetCode(),oLanc:GetMessage())
       lRet := .F.
   EndIf
   //faz a desalocação de objetos e arrays utilizados
   oLanc:DeActivate()
   oLanc := nil
   
Return lRet
