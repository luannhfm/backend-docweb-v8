/*/
===============================================================================
Autor.............: Peder Munksgaard (Do.it Sistemas)
-------------------------------------------------------------------------------
Data..............: 12/11/2014
-------------------------------------------------------------------------------
Descrição.........: Rotina de acerto da tabela SX3 à fim de modificar os campos
                    X3_TITULO contendo o nome 'CR' ou 'UO' por 'I Contabil' e 
                    'Centro Custo' respectivamente.
                    
                    Solicitado pela auditoria interna da FIEMT com a finalidade
                    de padronizar as nomenclaturas utilizadas por todos os 
                    setores.
-------------------------------------------------------------------------------
Alteração.........: (dd/mm/aaaa) - Motivo
-------------------------------------------------------------------------------
Partida...........: Fórmulas, Lactos Padronizados, Menu de usuário
-------------------------------------------------------------------------------
Função............: u_DTUPD01
===============================================================================
/*/

#Include 'Protheus.ch'

User Function DTUPD01()
   
   SetPrvt('_cCritica','_cFile','_aArea')
   
   _aArea := SX3->(GetArea())
   
   _cFile := "C:\Temp\X3_UO_CR.csv"
   
   FwMakeDir("C:\Temp",.F.)   
         
   Processa({||_fAceSX3()} , 'Acertando nome de campos CR e UO...' )
   
   Msginfo(" O processo de acerto dos nomes de CR e UO foram concluídos" + CRLF + ;
           " Foi gerado o arquivo de log: " + _cFile                     + CRLF + ;
           " Favor enviar o arquivo acima para o e-mail abaixo:        " + CRLF + ;
           CRLF + ;         
           " peder.munksgaard@doitsistemas.com.br ")
           
   RestArea(_aArea)
   
Return NiL

Static Function _fAceSX3()

   Local _nRegs  := 0
   
   Local _cRecno := '0'
   
   _cCritica := "Campo;Nome Anterior;Novo Nome;Recno" + CRLF
      
   Begin Transaction   
   
      dbSelectArea("SX3")
      _nRegs := SX3->(LastRec())
   
      ProcRegua(_nRegs)
      
      SX3->(dbGotop())
      
      While SX3->(!Eof())
         
         Incproc()
         
         _cRecno := Alltrim(Str(SX3->(Recno())))
         
         If SX3->X3_GRPSXG == '004' .And. Rtrim(SX3->X3_TITULO) == 'UO'
                                     
            _cCritica += Alltrim(SX3->X3_CAMPO) + ';' + Alltrim(SX3->X3_TITULO) + ';Centro Custo;' + _cRecno + CRLF                
            Reclock("SX3",.F.)
            Replace X3_TITULO With 'Centro Custo'
            Replace X3_TITSPA With 'Centro Costo'
            Replace X3_TITENG With 'Cost Center'                        
            SX3->(Msunlock())            
                                                
         Elseif SX3->X3_GRPSXG == '005' .And. Rtrim(SX3->X3_TITULO) == 'CR'
          
            _cCritica += Alltrim(SX3->X3_CAMPO) + ';' + Alltrim(SX3->X3_TITULO) + ';I Contabil;' + _cRecno + CRLF                  
            Reclock("SX3",.F.)
            Replace X3_TITULO With 'I Contabil'
            Replace X3_TITSPA With 'Item Cuenta'
            Replace X3_TITENG With 'Acct. Item'                        
            SX3->(Msunlock())
                                                              
         Endif
         
         SX3->(dbSkip())
         
      End
      
   End Transaction
   
   Memowrite(_cFile, _cCritica)
      
Return NiL