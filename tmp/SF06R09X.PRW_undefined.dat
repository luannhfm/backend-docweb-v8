/*/{Protheus.doc} SF06R09X
Relatorio de Registros de SPC e Envio a Terceirizada.

@author 	Luiz Junior - J2A
@since 	29/11/2016
@version 	1.0
/*/

User Function SF06R09X

	Local   oReport
	Private cPerg     := "SF06R09X"
	
	CriaSX1(cPerg)
	If !Pergunte(cPerg,.T.)
		Return(Nil)
	EndIf

	oReport  := ReportDef()
	oReport:PrintDialog()	
		
Return

/*/{Protheus.doc} ReportDef
(long_description)
@author j2a.luizjunior
@since 29/11/2016
@version 1.0
@example
(examples)
@see (links_or_references)
/*/

Static Function ReportDef(cPerg)
	
	Local oReport
	Local cIDCelula	:= ""	
	Local cNomeRel	:= "SF06R09X"
	
	Local cTitRel	:= "Relatorio de registros de SPC e envio a Terceirizada"
	Local cDescRel	:= "Relatorio de registros de SPC e envio a Terceirizada"

	oReport := TReport():New( cNomeRel, cTitRel, cPerg, { |oReport| ReportPrint(oReport) }, cDescRel )
	
	oReport:SetLandScape(.T.)
	//oReport:SetPortrait() 
	oReport:SetTotalInLine(.F.) 
	oReport:Page()
	
	//-> Titulos
	oTitulo := TRSection():New(oReport, "Titulos" ,{"SE1"})
	
	oCod    := TRCell():New(oTitulo,"A1_COD"      ,"SA1")
	oCod:SetSize(30)
	
	oNome   := TRCell():New(oTitulo,"A1_NOME"     ,"SA1")
	oNome:SetSize(140)		
	
	oCGC    := TRCell():New(oTitulo,"A1_CGC"      ,"SA1")
	oCGC:SetSize(65)
	
	oEmiss  := TRCell():New(oTitulo,"E1_EMISSAO"  ,"SE1")
	oEmiss:SetSize(35)	
	
	oVenc   := TRCell():New(oTitulo,"E1_VENCTO"   ,"SE1")
	oVenc:SetSize(35)
	
	oNum    := TRCell():New(oTitulo,"E1_NUM"      ,"SE1")
	oNum:SetSize(45)	
	
	oPref   := TRCell():New(oTitulo,"E1_PREFIXO"  ,"SE1")
	oPref:SetSize(30)
	
	oParc   := TRCell():New(oTitulo,"E1_PARCELA"  ,"SE1")
	oParc:SetSize(30)
	
	oValor  := TRCell():New(oTitulo,"E1_VALOR"    ,"SE1")
	oValor:SetSize(30)
	
	oNumBor := TRCell():New(oTitulo,"E1_NUMBOR"   ,"SE1")
	
	oSit    := TRCell():New(oTitulo,"E1_SITUACA"  ,"SE1")
	oSit:SetSize(30)		
	
	//oXSPC   := TRCell():New(oTitulo,"E1_XSPC"     ,"SE1")
	//oXSPC:SetSize(30)	
	
	oDTSpc  := TRCell():New(oTitulo,"E1_XSPCDT"   ,"SE1")
	oXMoExc := TRCell():New(oTitulo,"E1_XMOEXC"   ,"SE1")
	oXESPC  := TRCell():New(oTitulo,"E1_XEXCSPC"  ,"SE1")
	//oXDR    := TRCell():New(oTitulo,"E1_XDR"      ,"SE1")
	oXDTTer := TRCell():New(oTitulo,"E1_XDTTER"   ,"SE1")	

Return(oReport)

/*/{Protheus.doc} ReportPrint
(long_description)
@author j2a.luizjunior
@since 29/11/2016
@version 1.0
@example
(examples)
@see (links_or_references)
/*/

Static Function ReportPrint(oReport)

	Local cAlias     := GetNextAlias()		 
	Local cSql       := ""
	Local oSec1      := oReport:Section(1)	
		
	
	//-> De Cliente/De Loja 
	If !Empty(MV_PAR01) .And. !Empty(MV_PAR02) 
		cSql += " AND E1_CLIENTE >= '"  + MV_PAR01 + "' "
		cSql += " AND E1_LOJA    >= '"  + MV_PAR02 + "' "	
	EndIf	
	
	//-> Ate Cliente/Ate Loja
	If !Empty(MV_PAR03) .And. !Empty(MV_PAR04) 
		cSql += " AND E1_CLIENTE <= '"  + MV_PAR03 + "' "
		cSql += " AND E1_LOJA    <= '"  + MV_PAR04 + "' "	
	EndIf	
	
	//-> De Prefixo/Ate Prefixo
	If !Empty(MV_PAR05) .And. !Empty(MV_PAR06) 
		cSql += " AND E1_PREFIXO BETWEEN '"  + MV_PAR05 + "' AND '" + MV_PAR06 + "'"	
	EndIf
	
	//-> De Titulo/Ate Titulo
	If !Empty(MV_PAR07) .And. !Empty(MV_PAR08) 
		cSql += " AND E1_NUM BETWEEN '"  + MV_PAR07 + "' AND '" + MV_PAR08 + "'"	
	EndIf
	
	//-> De Natureza/Ate Natureza
	If !Empty(MV_PAR09) .And. !Empty(MV_PAR10) 
		cSql += " AND E1_NATUREZ BETWEEN '"  + MV_PAR09 + "' AND '" + MV_PAR10 + "'"	
	EndIf	

	//-> De Emissao/Ate Emissao
	If !Empty(MV_PAR11) .And. !Empty(MV_PAR12) 
		cSql += " AND E1_EMISSAO BETWEEN '"  + DToS(MV_PAR11) + "' AND '" + DToS(MV_PAR12) + "'"	
	EndIf
	
	//-> De Vencimento/Ate Vencimento
	If !Empty(MV_PAR13) .And. !Empty(MV_PAR14) 
		cSql += " AND E1_VENCTO BETWEEN '"  + DToS(MV_PAR13) + "' AND '" + DToS(MV_PAR14) + "'"	
	EndIf
	
	If MV_PAR15 = 2
	
		//-> De Bordero/Ate Bordero
		If !Empty(MV_PAR16) .And. !Empty(MV_PAR17) 
			cSql += " AND E1_NUMBOR BETWEEN '"  + MV_PAR16 + "' AND '" + MV_PAR17 + "'"	
		EndIf			
	
		//-> Situação
		If !Empty(MV_PAR18)  
			cSql += " AND E1_SITUACA = '"  + MV_PAR18 + "' "	
		EndIf
		
	Else
	
		//-> De Bordero/Ate Bordero
		If !Empty(MV_PAR16) .And. !Empty(MV_PAR17) 
			cSql += " AND E1_XNUMBOR BETWEEN '"  + MV_PAR16 + "' AND '" + MV_PAR17 + "'"	
		EndIf			
	
		//-> Situação
		If !Empty(MV_PAR18)  
			cSql += " AND E1_XSITUAC = '"  + MV_PAR18 + "' "	
		EndIf
	
	EndIf
	
	//-> De SPC/Ate SPC
	If !Empty(MV_PAR19) .And. !Empty(MV_PAR20) 
		cSql += " AND E1_XSPCDT BETWEEN '"  + DToS(MV_PAR19) + "' AND '" + DToS(MV_PAR20) + "'"	
	EndIf
	
	//-> De Terceirizada/Ate Terceirizada  
	If !Empty(MV_PAR21) .And. !Empty(MV_PAR22) 
		cSql += " AND E1_XDTTER BETWEEN '"  + DToS(MV_PAR21) + "' AND '" + DToS(MV_PAR22) + "'"	
	EndIf			
	
	cSql := "%"+cSql+"%"
	
	BeginSql Alias cAlias
		
		SELECT A1_COD,
			   A1_NOME,
			   A1_CGC,		       
		 	   E1_NUM,
		       E1_VENCTO,
		       E1_EMISSAO,
		       E1_PREFIXO,
		       E1_PARCELA,
		       E1_VALOR,
		       E1_NUMBOR,
		       E1_SITUACA,
		       E1_XSPC,
		       E1_XSPCDT,
		       E1_XMOEXC,
		       E1_XEXCSPC,
		       E1_XDR,
		       E1_XDTTER		       
		FROM   %Table:SE1% E1
		INNER  JOIN %Table:SA1% A1
		ON     A1.A1_COD    = E1.E1_CLIENTE
		AND    A1.A1_LOJA   = E1.E1_LOJA		
		AND    E1.E1_FILIAL = %xFilial:SE1%
		AND    E1.%NOTDEL%		
		AND    A1.%NOTDEL%
		%Exp:cSql%
		ORDER BY E1_FILIAL,E1_CLIENTE,E1_LOJA 
	
	EndSql

	While !(cAlias)->(Eof())
	
		oSec1:Init()
		
		oSec1:Cell("A1_COD"):SetValue(AllTrim((cAlias)->A1_COD))
		oSec1:Cell("A1_NOME"):SetValue(AllTrim((cAlias)->A1_NOME))
		oSec1:Cell("A1_CGC"):SetValue(AllTrim((cAlias)->A1_CGC))						
		oSec1:Cell("E1_NUM"):SetValue(AllTrim((cAlias)->E1_NUM))
		oSec1:Cell("E1_VENCTO"):SetValue(DToC(SToD((cAlias)->E1_VENCTO)))
		oSec1:Cell("E1_EMISSAO"):SetValue(DToC(SToD((cAlias)->E1_EMISSAO)))
		oSec1:Cell("E1_PREFIXO"):SetValue((cAlias)->E1_PREFIXO)		
		oSec1:Cell("E1_PARCELA"):SetValue((cAlias)->E1_PARCELA)							
		oSec1:Cell("E1_VALOR"):SetValue((cAlias)->E1_VALOR)		
		oSec1:Cell("E1_NUMBOR"):SetValue((cAlias)->E1_NUMBOR)		
		oSec1:Cell("E1_SITUACA"):SetValue((cAlias)->E1_SITUACA)		
		//oSec1:Cell("E1_XSPC"):SetValue((cAlias)->E1_XSPC)
		oSec1:Cell("E1_XSPCDT"):SetValue(DToC(SToD((cAlias)->E1_XSPCDT)))
		oSec1:Cell("E1_XMOEXC"):SetValue((cAlias)->E1_XMOEXC)
		oSec1:Cell("E1_XEXCSPC"):SetValue(DToC(SToD((cAlias)->E1_XEXCSPC)))
		//oSec1:Cell("E1_XDR"):SetValue((cAlias)->E1_XDR)
		oSec1:Cell("E1_XDTTER"):SetValue(DToC(SToD((cAlias)->E1_XDTTER)))
		
		oSec1:Printline()
		
		If oReport:Cancel()
			Return(Nil)
		EndIf
		
		(cAlias)->(DbSkip())
	EndDo
	
	(cAlias)->(DbCloseArea())
	
	oSec1:Finish()

Return

/*/{Protheus.doc} CriaSX1
(long_description)
@author j2a.luizjunior
@since 29/11/2016
@version 1.0
@example
(examples)
@see (links_or_references)
/*/

Static Function CriaSX1

	Local aHelp := {}
	
	//-> Texto do help em português,inglês, espanhol
	AAdd(aHelp, {{"De Cliente"                     }  ,{""},{""}})//01
	AAdd(aHelp, {{"De Loja"                        }  ,{""},{""}})//02	
	AAdd(aHelp, {{"Ate Cliente"                    }  ,{""},{""}})//03
	AAdd(aHelp, {{"Ate Loja"                       }  ,{""},{""}})//04
	AAdd(aHelp, {{"De Prefixo"                     }  ,{""},{""}})//05
	AAdd(aHelp, {{"Ate Prefixo"                    }  ,{""},{""}})//06
	AAdd(aHelp, {{"De Titulo"                      }  ,{""},{""}})//07
	AAdd(aHelp, {{"Ate Titulo"                     }  ,{""},{""}})//08
	AAdd(aHelp, {{"De Natureza"                    }  ,{""},{""}})//09
	AAdd(aHelp, {{"Ate Natureza"                   }  ,{""},{""}})//10
	AAdd(aHelp, {{"De Emissao"                     }  ,{""},{""}})//11
	AAdd(aHelp, {{"Ate Emissao"                    }  ,{""},{""}})//12
	AAdd(aHelp, {{"De Vencimento"                  }  ,{""},{""}})//13
	AAdd(aHelp, {{"Ate Vencimento"                 }  ,{""},{""}})//14
	AAdd(aHelp, {{"Considera somente Terceirizada" }  ,{""},{""}})//15
	AAdd(aHelp, {{"De Bordero"                     }  ,{""},{""}})//16
	AAdd(aHelp, {{"Ate Bordero"                    }  ,{""},{""}})//17
	AAdd(aHelp, {{"Situação"                       }  ,{""},{""}})//18
	AAdd(aHelp, {{"De SPC"                         }  ,{""},{""}})//19
	AAdd(aHelp, {{"Ate SPC"                        }  ,{""},{""}})//20
	AAdd(aHelp, {{"De Terceirizada"                }  ,{""},{""}})//21
	AAdd(aHelp, {{"Ate Terceirizada"               }  ,{""},{""}})//22	
	
	u_SFPUTSX1(cPerg,"01","De Cliente"       ,"","","mv_ch1"  ,"C",09,00,00,"G","","SA1" ,"","","mv_par01",""   ,"","","",""   ,"","","","","","","","","","","" ,aHelp[1,1]  ,aHelp[1,2]  ,aHelp[1,3]  ,"")
	u_SFPUTSX1(cPerg,"02","De Loja"          ,"","","mv_ch2"  ,"C",04,00,00,"G","",""    ,"","","mv_par02",""   ,"","","",""   ,"","","","","","","","","","","" ,aHelp[2,1]  ,aHelp[2,2]  ,aHelp[2,3]  ,"")
	u_SFPUTSX1(cPerg,"03","Ate Cliente"      ,"","","mv_ch3"  ,"C",09,00,00,"G","","SA1" ,"","","mv_par03",""   ,"","","",""   ,"","","","","","","","","","","" ,aHelp[3,1]  ,aHelp[3,2]  ,aHelp[3,3]  ,"")
	u_SFPUTSX1(cPerg,"04","Ate Loja"         ,"","","mv_ch4"  ,"C",04,00,00,"G","",""    ,"","","mv_par04",""   ,"","","",""   ,"","","","","","","","","","","" ,aHelp[4,1]  ,aHelp[4,2]  ,aHelp[4,3]  ,"")
	u_SFPUTSX1(cPerg,"05","De Prefixo"       ,"","","mv_ch5"  ,"C",03,00,00,"G","",""    ,"","","mv_par05",""   ,"","","",""   ,"","","","","","","","","","","" ,aHelp[5,1]  ,aHelp[5,2]  ,aHelp[5,3]  ,"")
	u_SFPUTSX1(cPerg,"06","Ate Prefixo"      ,"","","mv_ch6"  ,"C",03,00,00,"G","",""    ,"","","mv_par06",""   ,"","","",""   ,"","","","","","","","","","","" ,aHelp[6,1]  ,aHelp[6,2]  ,aHelp[6,3]  ,"")
	u_SFPUTSX1(cPerg,"07","De Titulo"        ,"","","mv_ch7"  ,"C",09,00,00,"G","",""    ,"","","mv_par07",""   ,"","","",""   ,"","","","","","","","","","","" ,aHelp[7,1]  ,aHelp[7,2]  ,aHelp[7,3]  ,"")
	u_SFPUTSX1(cPerg,"08","Ate Titulo"       ,"","","mv_ch8"  ,"C",09,00,00,"G","",""    ,"","","mv_par08",""   ,"","","",""   ,"","","","","","","","","","","" ,aHelp[8,1]  ,aHelp[8,2]  ,aHelp[8,3]  ,"")
	u_SFPUTSX1(cPerg,"09","De Natureza"      ,"","","mv_ch9"  ,"C",10,00,00,"G","","SED" ,"","","mv_par09",""   ,"","","",""   ,"","","","","","","","","","","" ,aHelp[9,1]  ,aHelp[9,2]  ,aHelp[9,3]  ,"")
	u_SFPUTSX1(cPerg,"10","Ate Natureza"     ,"","","mv_ch10" ,"C",10,00,00,"G","","SED" ,"","","mv_par10",""   ,"","","",""   ,"","","","","","","","","","","" ,aHelp[10,1] ,aHelp[10,2] ,aHelp[10,3] ,"")
	u_SFPUTSX1(cPerg,"11","De Emissao"       ,"","","mv_ch11" ,"D",08,00,00,"G","",""    ,"","","mv_par11",""   ,"","","",""   ,"","","","","","","","","","","" ,aHelp[11,1] ,aHelp[11,2] ,aHelp[11,3] ,"")
	u_SFPUTSX1(cPerg,"12","Ate Emissao"      ,"","","mv_ch12" ,"D",08,00,00,"G","",""    ,"","","mv_par12",""   ,"","","",""   ,"","","","","","","","","","","" ,aHelp[12,1] ,aHelp[12,2] ,aHelp[12,3] ,"")
	u_SFPUTSX1(cPerg,"13","De Vencimento"    ,"","","mv_ch13" ,"D",08,00,00,"G","",""    ,"","","mv_par13",""   ,"","","",""   ,"","","","","","","","","","","" ,aHelp[13,1] ,aHelp[13,2] ,aHelp[13,3] ,"")
	u_SFPUTSX1(cPerg,"14","Ate Vencimento"   ,"","","mv_ch14" ,"D",08,00,00,"G","",""    ,"","","mv_par14",""   ,"","","",""   ,"","","","","","","","","","","" ,aHelp[14,1] ,aHelp[14,2] ,aHelp[14,3] ,"")
	u_SFPUTSX1(cPerg,"15","Somente Terc.?"   ,"","","mv_ch15" ,"C",01,00,00,"C","",""    ,"","","mv_par15","Sim","","","","Nao","","","","","","","","","","","" ,aHelp[15,1] ,aHelp[15,2] ,aHelp[15,3] ,"")
	u_SFPUTSX1(cPerg,"16","De Bordero"       ,"","","mv_ch16" ,"C",06,00,00,"G","",""    ,"","","mv_par16",""   ,"","","",""   ,"","","","","","","","","","","" ,aHelp[16,1] ,aHelp[16,2] ,aHelp[16,3] ,"")
	u_SFPUTSX1(cPerg,"17","Ate Bordero"      ,"","","mv_ch17" ,"C",06,00,00,"G","",""    ,"","","mv_par17",""   ,"","","",""   ,"","","","","","","","","","","" ,aHelp[17,1] ,aHelp[17,2] ,aHelp[17,3] ,"")
	u_SFPUTSX1(cPerg,"18","Situação"         ,"","","mv_ch18" ,"C",01,00,00,"G","","07"  ,"","","mv_par18",""   ,"","","",""   ,"","","","","","","","","","","" ,aHelp[18,1] ,aHelp[18,2] ,aHelp[18,3] ,"")
	u_SFPUTSX1(cPerg,"19","De SPC"           ,"","","mv_ch19" ,"D",08,00,00,"G","",""    ,"","","mv_par19",""   ,"","","",""   ,"","","","","","","","","","","" ,aHelp[19,1] ,aHelp[19,2] ,aHelp[19,3] ,"")
	u_SFPUTSX1(cPerg,"20","Ate SPC"          ,"","","mv_ch20" ,"D",08,00,00,"G","",""    ,"","","mv_par20",""   ,"","","",""   ,"","","","","","","","","","","" ,aHelp[20,1] ,aHelp[20,2] ,aHelp[20,3] ,"")
	u_SFPUTSX1(cPerg,"21","De Terceirizada"  ,"","","mv_ch21" ,"D",08,00,00,"G","",""    ,"","","mv_par21",""   ,"","","",""   ,"","","","","","","","","","","" ,aHelp[21,1] ,aHelp[21,2] ,aHelp[21,3] ,"")
	u_SFPUTSX1(cPerg,"22","Ate Terceirizada" ,"","","mv_ch22" ,"D",08,00,00,"G","",""    ,"","","mv_par22",""   ,"","","",""   ,"","","","","","","","","","","" ,aHelp[22,1] ,aHelp[22,2] ,aHelp[22,3] ,"")

Return