#INCLUDE "PROTHEUS.CH"
#INCLUDE "TOPCONN.CH"
#INCLUDE "RWMAKE.CH"
#INCLUDE "FONT.CH"
#INCLUDE "COLORS.CH"
#INCLUDE "PARMTYPE.CH"
#INCLUDE "FWMBROWSE.CH"
#INCLUDE "FWMVCDEF.CH"
#INCLUDE "FWCOMMAND.CH"
/*
+--------------------------------------------------------------------------+
|{Protheus.doc}<SN062LX>                                                   |
| CANCELA A REMESSA DE SOLICITAÇÃO DE LIBEAÇÃO DE CARTÕES PRÉ PAGO.        |
|                                                                          |
|@Author<Antonio Dantas>                                                   |
|@since<10/12/2014>                                                        |
|@version<Nil>                                                             |
|                                                                          |
|@parameters:                                                              |
|@receive                                                                  |
|<>                                                                        |
|@return                                                                   |
|<Nil>                                                                     |
+-----------+--------------------------------------------------------------+
|Uso        |FIEMT - Federacao das Industrias no Estado de Mato Grosso     |
+-----------+--------------------------------------------------------------+
*/
User Function SN062LX()
Local _cPeriodo		:= ZP6->ZP6_XPERIO
Local _cCalend		:= ZP6->ZP6_XSEG
Local _cSessao		:= ZP6->ZP6_XCOD
Local _cFilterD 	:= ""
//--
Local _oBrowse 		:= FWMBrowse():New()
Private aRotina 	:= MenuDef() 
_cFilterD := "ZPA_FILIAL == '"+FwxFilial("ZPA")+"' .and. ZPA_XPERID == '"+_cPeriodo+"' .and. ZPA_XSEG == '"+_cCalend+"'"
_cFilterD += " .and. ZPA->ZPA_XSESSA == '"+_cSessao+"'.and. ZPA->ZPA_XTIPO == 'RL' .and. ZPA->ZPA_XSTATU == 'R'" 
//--
DbSelectArea("ZPA")                                                                                  	
DbSetOrder(1)
ZPA->(DbGoTop())
_oBrowse:SetAlias("ZPA")
_oBrowse:SetDescription("Controle de Remessas de Liberação")
_oBrowse:SetFilterDefault(_cFilterD)  
_oBrowse:Activate()
Return Nil

/*
+--------------------------------------------------------------------------+
|{Protheus.doc}<MenuDef>                                                   |
|Responsavel pela criacao dos botoes, alterar, incluir, excluir e demais.  |
|                                                                          |
|@Author<Antonio Dantas>                                                  |
|@since<10/12/2014>                                                        |
|@version<Nil>                                                             |
|                                                                          |
|@parameters<Nil>                                                          |
|@return<_oModel>                                                          |
+--------------------------------------------------------------------------+
|Uso        |SENAI    													   |
+-----------+--------------------------------------------------------------+
*/
Static Function MenuDef()
Local _aRotina 	:= {}
ADD OPTION _aRotina TITLE "Pesquisar"	ACTION "PESQBRW"         	OPERATION 1 ACCESS 0
ADD OPTION _aRotina TITLE "Visualizar" 	ACTION "VIEWDEF.SN062LX" 	OPERATION 2 ACCESS 0
ADD OPTION _aRotina TITLE "Cancelar"   	ACTION "u_fCancRem()" 		OPERATION 5 ACCESS 0
Return _aRotina

/*
+--------------------------------------------------------------------------+
|{Protheus.doc}<ModelDef>                                                  |
|Contem  a  construcao e a definicao do Model, lembrando que o Modelo de   |
|dados (Model) contem as regras de negocio.							   	   |
|                                                                          |
|@Author<Antonio Dantas>                                                   |
|@since<10/12/2014>                                                        |
|@version<Nil>                                                             |
|                                                                          |
|@parameters<Nil>                                                          |
|@return<_oModel>                                                          |
+--------------------------------------------------------------------------+
|Uso        |SENAI    													   |
+-----------+--------------------------------------------------------------+
*/
Static Function ModelDef()
Local _oModel	:= Nil 
Local _oStZPA	:= FWFormStruct(1,"ZPA")
//-- Objeto de modelo de dados
_oModel := MPFormModel():New("SN062LXX")
//-- A  estrutura  do  modelo  de  dados  (Model) 
_oModel:AddFields("ZPAMASTER",/*cOwner*/,_oStZPA)
//-- Chave Primaria
_oModel:SetPrimaryKey({"ZPA_FILIAL","ZPA_XCOD"})
Return _oModel


/*
+--------------------------------------------------------------------------+
|{Protheus.doc}<ViewDef>                                                   |
|Contem a construcao e a definicao da View, ou seja, contrucao da interface|
|                                                                          |
|@Author<Antonio Dantas>                                                   |
|@since<10/12/2014>                                                        |
|@version<Nil>                                                             |
|                                                                          |
|@parameters<Nil>                                                          |
|@return<_oModel>                                                          |
+--------------------------------------------------------------------------+
|Uso        |SENAI										     			   |
+-----------+--------------------------------------------------------------+
*/
Static Function ViewDef()
Local _oModel 	:= FWLoadModel("SN062LX")
Local _oStZPAV	:= FWFormStruct(2,"ZPA") 
Local _oView   	:= FWFormView():New()
//-- Defino modelo da view
_oView:SetModel(_oModel)
//-- Mesma regra do modeldef
_oView:AddField("VW_ZPA",_oStZPAV,"ZPAMASTER")
//-- Crio a tela
_oView:CreateHorizontalBox("REMESSAS",100)
//-- Defino owner da tela
_oView:SetOwnerView("VW_ZPA","REMESSAS")
Return _oView  

/*
+--------------------------------------------------------------------------+
|{Protheus.doc}<fCancRem>                                                   |
|Função de Preparação a Chamada a função de Cancelameno da Remessa         |
|                                                                          |
|@Author<Antonio Dantas>                                                   |
|@since<10/12/2014>                                                        |
|@version<Nil>                                                             |
|                                                                          |
|@parameters<Nil>                                                          |
|@return<_oModel>                                                          |
+--------------------------------------------------------------------------+
|Uso        |SENAI										     			   |
+-----------+--------------------------------------------------------------+
*/
User Function fCancRem() 
FWMsgRun(, {|| u_fExcRem() }, "Cancela Remessa", "Aprocessando Cartões..." )
Return   


/*
+--------------------------------------------------------------------------+
|{Protheus.doc}<fExcRem>                                                   |
| Função efetiva o CANCELAMENTO da Remessa de Solicitação de LIberação de  |
| Cartões Pré Pago                                                         |
|                                                                          |
|@Author<Antonio Dantas>                                                   |
|@since<10/12/2014>                                                        |
|@version<Nil>                                                             |
|                                                                          |
|@parameters<Nil>                                                          |
|@return<_oModel>                                                          |
+--------------------------------------------------------------------------+
|Uso        |SENAI										     			   |
+-----------+--------------------------------------------------------------+
*/
User Function fExcRem()
Local _cPeriodo		:= ZP6->ZP6_XPERIO
Local _cCalend		:= ZP6->ZP6_XSEG
Local _cSessao		:= ZP6->ZP6_XCOD
Local _cCodRemes	:= ZPA->ZPA_XCOD
Local _cConvenio 	:= ZPA->ZPA_XCONVE   
Local _lTudoOK		:= .f.
//-- 
dbSelectArea("ZP7")
ZP7->(dbSetOrder(5))		//-- Convenio+Periodo+Seg Calendar+Sessao+Matricula
ZP7->(dbSeek(FwxFilial("ZP7")+_cConvenio+_cPeriodo+_cCalend+_cSessao))
//-- 
Do While ZP7->(!Eof()) .and. ZP7->ZP7_FILIAL == FwxFilial("ZP7") .and. ZP7->ZP7_XCONVE == _cConvenio .and.;
         ZP7->ZP7_XPERIO == _cPeriodo .and. ZP7->ZP7_XSEG == _cCalend .and. ZP7->ZP7_XSESSA == _cSessao
	//-- **********************************************************************
	//--     Localiza O CARTAO e Posiciona o Cadastro de Beneficiarios 
	//-- **********************************************************************
	dbSelectArea("ZP3")
	ZP3->(dbSetOrder(6)) 		//-- Numero de Referencia do CARTAO 
	If (ZP3->(dbSeek(ZP7->ZP7_XNRREF)))   
		If ZP3->ZP3_XIDREM == _cCodRemes
	 		ZP3->(RecLock("ZP3",.f.))
			Replace ZP3->ZP3_XIDREM With ""						//-- ID Remessa
			Replace ZP3->ZP3_XDTREM With Ctod("  /  /  ")      	//-- Dat da GERAÇÃO da remessa 
			Replace ZP3->ZP3_XSEQUE With ""						//-- Seq Remessa  
			Replace ZP3->ZP3_XSTATU With "P"					//-- Remessa de solicitacao de PREPARADA
	 		ZP3->(MsUnLock())
			ZP3->(dbCommit())
			_lTudoOK := .t.
		Endif 	
	Endif 
	//-- Proxima Matricula 
	ZP7->(dbSkip())
Enddo	
If _lTudoOK
	ZPA->(RecLock("ZPA",.f.))
	Replace ZPA->ZPA_XOCORR  	With "Remessa EXCLUIDA pelo Operador ["+UPPER(cUserName)+"], em ["+dtoc(dDataBase)+"-"+Time()+"]."
	Replace ZPA->ZPA_XTIPO  	With "XX"
	Replace ZPA->ZPA_XSTATU 	With "C"
	ZPA->(MsUnLock())
	ZPA->(DbCommit())
Endif
Return 

