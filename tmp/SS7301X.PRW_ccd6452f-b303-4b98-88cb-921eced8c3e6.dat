#INCLUDE "PROTHEUS.CH"
#INCLUDE "TOPCONN.CH"
#INCLUDE "RWMAKE.CH"
#INCLUDE "FONT.CH"
#INCLUDE "COLORS.CH"
#INCLUDE "PARMTYPE.CH"
#INCLUDE "FWMBROWSE.CH"
#INCLUDE "FWMVCDEF.CH"
#INCLUDE "FWCOMMAND.CH"
/*
--------------------------------------------------------------------------------
{Protheus.doc} <SS7301X>
 Modelo MVC para Cadastro de Medas de Visitas, envolve as tabelas:
 Pai	: ZCB - Metas de Visitas
 Filha	: ZCC - Item das Metas de Visitas 	  

@author<Antonio Dantas>
@since<09/02/2015>
@version<1.00>
@receive<Nil>
@return<Nil>
@example<Nil>
@see<Nil>
--------------------------------------------------------------------------------
*/
User Function SS7301X()
Local _cAlias  	:= "ZCB"
Local _cTitle  	:= "Metas de Visitas"
Local _oBrowse 	:= FWMBrowse():New()
Private aRotina	:= MenuDef()
_oBrowse:SetAlias(_cAlias)
_oBrowse:SetDescription(_cTitle)
//-- Define a Legenda para modelo
_oBrowse:AddLegend("ZCB_ATIVA == 'S' "	,"GREEN" ,"Ativa" 		)
_oBrowse:AddLegend("ZCB_ATIVA == 'N' "	,"RED"	,"Desativada" 	)
_oBrowse:Activate()
Return NIL

/*
--------------------------------------------------------------------------------
{Protheus.doc} <MenuDef>
 Definicao do MenuDef

@author<Antonio Dantas>
@since<09/02/2015>
@version <1.00>
@return< _aRotina (a) - Vetor com as opcoes de Menu >
@example<Nil>
@see<Nil>
--------------------------------------------------------------------------------
*/
Static Function MenuDef()
Local _aRotina 		:= {}
ADD OPTION _aRotina TITLE "Pesquisar"	ACTION "PESQBRW"			OPERATION 1 ACCESS 0
ADD OPTION _aRotina TITLE "Visualizar"	ACTION "VIEWDEF.SS7301X"	OPERATION 2 ACCESS 0
ADD OPTION _aRotina TITLE "Incluir"	ACTION "VIEWDEF.SS7301X"	OPERATION 3 ACCESS 0
ADD OPTION _aRotina TITLE "Alterar"	ACTION "VIEWDEF.SS7301X"	OPERATION 4 ACCESS 0
ADD OPTION _aRotina TITLE "Excluir"	ACTION "VIEWDEF.SS7301X"	OPERATION 5 ACCESS 0
ADD OPTION _aRotina TITLE "Imprimir"	ACTION "VIEWDEF.SS7301X"	OPERATION 8 ACCESS 0  
ADD OPTION _aRotina TITLE "Copiar"		ACTION "VIEWDEF.SS7301X"	OPERATION 9 ACCESS 0
Return _aRotina

/*
--------------------------------------------------------------------------------
{Protheus.doc} <ModelDef>
 Definicao do ModelDef

@author<Antonio Dantas>
@since<09/02/2015>
@version <1.00>
@return< _oModel (o) - Objeto com a Definicao do Modelo >
@example<Nil>
@see<Nil>
--------------------------------------------------------------------------------
*/
Static Function ModelDef()
Local _oStruZCB := FWFormStruct(1,"ZCB")
Local _oStruZCC := FWFormStruct(1,"ZCC")
Local _oModel   := Nil
_oModel		:= MPFormModel():New("SS7301XP",/*PreValid*/,/*Valid*/,/*Grava*/ )
_oModel:AddFields("ZCBMASTER"	, /*cOwner*/	,_oStruZCB)
_oModel:AddGrid("ZCCDETAIL"		, "ZCBMASTER"	,_oStruZCC,{|_oMdlGrd, _nLin, _cAction, _cField| fVldLine(_oMdlGrd, _nLin, _cAction, _cField )} )
_oModel:SetPrimaryKey({"ZCB_FILIAL","ZCB_CODIGO"})
_oModel:SetRelation("ZCCDETAIL", {{"ZCC_FILIAL", "FwxFilial('ZCC')"},{"ZCC_META", "ZCB_CODIGO"}},ZCC->(IndexKey(1)))
_oModel:SetDescription("Item das Metas de Visita")
_oModel:GetModel("ZCBMASTER"):SetDescription("Metas")
_oModel:GetModel("ZCCDETAIL"):SetDescription("Item")
Return _oModel


/*
--------------------------------------------------------------------------------
{Protheus.doc} <ViewDef>
 Definicao do ViewDef

@author<Antonio Dantas>
@since<09/02/2015>
@version <1.00>
@return< _oView (o) - Objeto com a Definicao do Visao do Modelo  >
@example<Nil>
@see<Nil>
--------------------------------------------------------------------------------
*/
Static Function ViewDef()
Local _oStruZCB := FWFormStruct(2,"ZCB",/*{|_cFild| xDefFilds(_cFild)}*/)
Local _oStruZCC := FWFormStruct(2,"ZCC",/*{|_cFild| xDefFilds(_cFild)}*/)
Local _oModel   := FWLoadModel("SS7301X")
Local _oView    := FWFormView():New()
//+---------------------------------------------------------------------+
//|  Monta Box Horizontal - Superior                                    |
//+---------------------------------------------------------------------+
_oView:SetModel(_oModel)
_oView:CreateHorizontalBox("SUPERIOR",50)
//-- 1.1) ZCB - Metas de Visita 
_oView:CreateVerticalBox("ZCB_MET",100,"SUPERIOR")
_oView:AddField("VIEW_ZCB",_oStruZCB,"ZCBMASTER")
_oView:EnableTitleView("VIEW_ZCB","Metas")
_oView:SetOwnerView("VIEW_ZCB","ZCB_MET")
//+---------------------------------------------------------------------+
//|  Monta Box Horizontal - Inferior                                    |
//+---------------------------------------------------------------------+
_oView:CreateHorizontalBox("INFERIOR",50)
//-- 1.2) ZCC - Item da Meta de Visita
_oView:CreateVerticalBox("ZCC_ITEM",100,"INFERIOR")
_oView:AddGrid("VIEW_ZCC",_oStruZCC,"ZCCDETAIL", /*bLinePre*/,/*bLinePost*/, /*bPreVal*/,/*bPosVal*/, /*BLoad*/) 
_oView:EnableTitleView("VIEW_ZCC","Item da Meta")
_oView:SetOwnerView("VIEW_ZCC","ZCC_ITEM")
//-- Removes os campos da Grid
_oStruZCC:RemoveField("ZCC_FILIAL")
_oStruZCC:RemoveField("ZCC_META")
Return _oView


/*
--------------------------------------------------------------------------------
{Protheus.doc} <fVldLine>
 Valida o Codigo do Vendedor com relação a duplicidade de ocorrencias de Metas
 nos itens da Meta

@author<Antonio Dantas>
@since<09/02/2015>
@version <1.00>
@receive
<   _oMdlGrd (o) - oModelGrid
       _nLin (n) - Numero da Linha do Grid "Apos Carregado o Model"
    _cAction (c) - Define a Acao na GRID: 
                   CANSETVALUE - Pre-Validação Antes de Imputar o valor
                      SETVALUE - Validação apos o ENTER; Informou o valor 
                        DELETE - Apagou a Linha
                      UNDELETE - Recuperou uma linha deletada
     _cField (c) - Nome do campo afetado.
>
@return
<   _lReturn (l) - (.t.) Pode Executar a Acao
                   (.f.) Nao Pode executar a Acao 
>
@example<Nil>
@see<Nil>
--------------------------------------------------------------------------------
*/
Static Function fVldLine(_oMdlGrd, _nLin, _cAction, _cField)
Local _lReturn	:= .T.       
Local _oModel		:= FWModelActive()      
Local _nOper		:= _oModel:GetOperation()   		//-- Define a Acao no FORMULARIO
Local _oMdlItem	:= _oModel:GetModel("ZCCDETAIL")	//-- Item da Meta    
Local _aHearItem	:= _oMdlItem:aHeader
Local _aColsItem	:= _oMdlItem:aCols  
Local _N			:= _oMdlItem:NLine					//-- Posicao do Ponteiro na Sessao 
Local _nPVed		:= aScan(_aHearItem,{|X| UPPER(AllTrim(X[2]))=="ZCC_VENDED"})
Local _nPStat		:= aScan(_aHearItem,{|X| UPPER(AllTrim(X[2]))=="ZCC_ATIVA"})
Local _cStatus	:= _aColsItem[_N,_nPStat]  
//-- Se estiver 3=Incluindo ou 4=Alterando 
If _nOper == 3 .or. _nOper == 4 
	Do Case
		Case Upper(_cAction) == "CANSETVALUE"	//-- Pre-Validação Antes de Imputar o valor no campo
			//Nil 
		Case Upper(_cAction) == "SETVALUE"		//-- Validação apos o ENTER; Informou o valor no campo
			//+--------------------------------------------------------------------+
			//| Tive que incluir aqui a verificação para campo "ZCC_NOME", uma vez |
			//| que quanto o campo "ZCC_VENDED" e preenchido GATILHA o campo       |
			//| "ZCC_NOME" o gatilho modifica o FOCO, antes da saida do campo.     |
			//+--------------------------------------------------------------------+
			If _cField == "ZCC_VENDED" .or. _cField == "ZCC_NOME"
				_lReturn := fVldVend(M->ZCC_VENDED,_N,_aColsItem,_nPVed) 
			Endif 
		Case Upper(_cAction) == "DELETE"		//-- Apagou a Linha
			//Nil 
		Case Upper(_cAction) == "UNDELETE"		//-- Recuperou uma linha apagada
			//Nil 
	EndCase
Endif
Return _lReturn

/*
--------------------------------------------------------------------------------
{Protheus.doc} <fVldVend>
 Valida o Codigo do Vendedor que esta sendo incluido.
 Regra: Se o vendedor já estiver sido incluido nas Metas não permite uma nova 
        inclusao, mesmo que a meta existente esteja DESATIVADA ou INATIVA.
	
@author<Antonio Dantas>
@since<09/02/2015>
@version <1.00>
@receive
<    _cCodVend (c) - Codigo do Vendedor que esta sendo validado
       _NLinha (n) - Posição (LINHA) em que se encontra o registro que esta 
                     sendo validado
    _aColsItem (a) - Array contendo as informações do [aCols] da grid em edição
        _nPVed (n) - Posição do campo "Codigo do Vendedor" na Grid/Array
>
@return
<   _lReturn (l) - (.t.) Se o codigo estiver OK
                   (.f.) Se existir algum empedimento para inclusão do
                         codigo do vendedor 
>
@example<Nil>
@see<Nil>
--------------------------------------------------------------------------------
*/
Static Function fVldVend(_cCodVend,_NLinha,_aColsItem,_nPVed) 
Local _lReturn 	:= .t.
Local _nTAMaCol := Len(_aColsItem[_NLinha])
_nCtaA := 0
For _nCtaA := 1 to Len(_aColsItem)
	If _nCtaA != _NLinha
		If _aColsItem[_nCtaA,_nPVed] == _cCodVend
			_lReturn := .f.
			If _aColsItem[_nCtaA,_nTAMaCol] 
				Help(,,"HELP",,"Confirme primeiro a exclusão da META lançada para este vendedor!",1,0)    
			Else
				Help(,,"HELP",,"Já existe META lançada para este Vendedor. Verifique!",1,0)    
			Endif
		Endif 
	Endif  
Next _nCtaA
Return _lReturn