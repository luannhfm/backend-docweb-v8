#Include "Protheus.ch"

/*/{Protheus.doc} SF06R07X
Relatorio de Fatura de Cartao de Credito.

@author 	Luiz Junior - J2A
@since 	13/10/2016
@version 	1.0
/*/

User Function SF06R07X

	Local   oReport
	Private cPerg   := ""	
	
	If Funname(0) == "SF06R07X"
		cPerg := "SF06R07X"
		CriaSX1(cPerg)
		If !Pergunte(cPerg,.T.)
			Return(Nil)
		Else
			If !ChkDados(MV_PAR01)
				Help(" ",1,"RECNO")
				Return
			EndIf	
		EndIf	
	EndIf

	oReport  := ReportDef()
	oReport:PrintDialog()

Return

/*/{Protheus.doc} ReportDef
(long_description)
@author j2a.luizjunior
@since 13/10/2016
@version 1.0
@example
(examples)
@see (links_or_references)
/*/

Static Function ReportDef(cPerg)
	
	Local oReport
	Local cIDCelula	:= ""	
	Local cNomeRel	:= "SF06R07X"
	Local cTitRel	:= "Relatorio de Fatura de Cartão"
	Local cDescRel	:= "Relatorio de Fatura de Cartão"
	
	oReport := TReport():New( cNomeRel, cTitRel, cPerg, { |oReport| ReportPrint(oReport) }, cDescRel )
	oReport:SetPortrait() 
	oReport:SetTotalInLine(.F.) 
	oReport:Page()
	
	//-> Fatura
	oFatura := TRSection():New(oReport, "Fatura", {"SE2"}) 
	TRCell():New(oFatura,"E2_FILIAL"    ,"SE2")
	TRCell():New(oFatura,"E2_FORNECE"   ,"SE2")
	TRCell():New(oFatura,"E2_LOJA"      ,"SE2")
	TRCell():New(oFatura,"E2_PREFIXO"   ,"SE2")
	TRCell():New(oFatura,"E2_NUM"       ,"SE2")
	TRCell():New(oFatura,"E2_PARCELA"   ,"SE2")
	TRCell():New(oFatura,"E2_TIPO"      ,"SE2")
	TRCell():New(oFatura,"E2_EMISSAO"   ,"SE2")
	TRCell():New(oFatura,"E2_VENCREA"   ,"SE2")
	TRCell():New(oFatura,"E2_VALOR"     ,"SE2")
	
	//-> Titulos
	oTitulo := TRSection():New(oReport, "Titulo", {"SE2"})
	TRCell():New(oTitulo,"E2_FILIAL"    ,"SE2")
	TRCell():New(oTitulo,"E2_FORNECE"   ,"SE2")
	TRCell():New(oTitulo,"E2_LOJA"      ,"SE2")
	TRCell():New(oTitulo,"E2_PREFIXO"   ,"SE2")
	TRCell():New(oTitulo,"E2_NUM"       ,"SE2")
	TRCell():New(oTitulo,"E2_PARCELA"   ,"SE2")
	TRCell():New(oTitulo,"E2_TIPO"      ,"SE2")
	TRCell():New(oTitulo,"E2_EMISSAO"   ,"SE2")
	TRCell():New(oTitulo,"E2_VENCREA"   ,"SE2")
	TRCell():New(oTitulo,"E2_VALOR"     ,"SE2")
		
	TRFunction():New(oFatura:Cell("E2_NUM")  ,"T_VAL" ,"COUNT",,"Fatura",,,.T.,.F.)
	TRFunction():New(oFatura:Cell("E2_VALOR"),"T_VAL" ,"SUM"  ,,"Valor Fatura",,,.T.,.F.)
	
	TRFunction():New(oTitulo:Cell("E2_NUM")  ,"T_VAL" ,"COUNT",,"Titulos",,,.T.,.F.)
	TRFunction():New(oTitulo:Cell("E2_VALOR"),"T_VAL" ,"SUM"  ,,"Valor Total",,,.T.,.F.)
	
Return(oReport)

/*/{Protheus.doc} ReportPrint
(long_description)
@author j2a.luizjunior
@since 13/10/2016
@version 1.0
@example
(examples)
@see (links_or_references)
/*/

Static Function ReportPrint(oReport)

	Local cAlias      := GetNextAlias()
	Local cAlFat      := GetNextAlias()  
	Local cSQL        := ""	 
	Local oSec1       := oReport:Section(1)
	Local oSec2       := oReport:Section(2)
	Local cE2_FATURA  := ""	
	Local cE2_FATPREF := ""//SE2->E2_PREFIXO

	oSec1:Init()

	If !Empty(cPerg) 
		
		cE2_FATURA  := MV_PAR01
		cE2_FATPREF := MV_PAR02

		DbSelectArea("SE2")
		DbSetOrder(1)
		If DbSeek(xFilial("SE2") + MV_PAR02 + MV_PAR01)

			oSec1:Cell("E2_FILIAL"):SetValue(SE2->E2_FILIAL)
			oSec1:Cell("E2_FORNECE"):SetValue(SE2->E2_FORNECE)
			oSec1:Cell("E2_LOJA"):SetValue(SE2->E2_LOJA)	
			oSec1:Cell("E2_PREFIXO"):SetValue(SE2->E2_PREFIXO)
			oSec1:Cell("E2_NUM"):SetValue(SE2->E2_NUM)	
			oSec1:Cell("E2_PARCELA"):SetValue(SE2->E2_PARCELA)
			oSec1:Cell("E2_TIPO"):SetValue(SE2->E2_TIPO)
			oSec1:Cell("E2_EMISSAO"):SetValue(DToC(SE2->E2_EMISSAO))
			oSec1:Cell("E2_VENCREA"):SetValue(DToC(SE2->E2_VENCREA))			
			oSec1:Cell("E2_VALOR"):SetValue(SE2->E2_VALOR)
		
		EndIf		
			
	Else
	
		cE2_FATURA  := SE2->E2_NUM 
		cE2_FATPREF := SE2->E2_PREFIXO

		oSec1:Cell("E2_FILIAL"):SetValue(SE2->E2_FILIAL)
		oSec1:Cell("E2_FORNECE"):SetValue(SE2->E2_FORNECE)
		oSec1:Cell("E2_LOJA"):SetValue(SE2->E2_LOJA)	
		oSec1:Cell("E2_PREFIXO"):SetValue(SE2->E2_PREFIXO)
		oSec1:Cell("E2_NUM"):SetValue(SE2->E2_NUM)	
		oSec1:Cell("E2_PARCELA"):SetValue(SE2->E2_PARCELA)
		oSec1:Cell("E2_TIPO"):SetValue(SE2->E2_TIPO)
		oSec1:Cell("E2_EMISSAO"):SetValue(DToC(SE2->E2_EMISSAO))
		oSec1:Cell("E2_VENCREA"):SetValue(DToC(SE2->E2_VENCREA))			
		oSec1:Cell("E2_VALOR"):SetValue(SE2->E2_VALOR)
		
	EndIf
	
	oSec1:Printline()
		
	BeginSql Alias cAlias
		
		SELECT E2_FILIAL,
			   E2_PREFIXO,
			   E2_NUM,
			   E2_PARCELA,
			   E2_TIPO,
			   E2_EMISSAO,
			   E2_VENCREA,
			   E2_VALOR,	 
			   E2_FORNECE,
			   E2_LOJA
		FROM   %Table:SE2%
		WHERE  E2_FATURA  = %Exp:cE2_FATURA%	   
		AND    E2_FATPREF = %Exp:cE2_FATPREF%
		AND    E2_FILIAL  = %xFilial:SE2%
		AND    %NotDel%
		ORDER BY E2_NUM,E2_PARCELA
		
	EndSql

	While !(cAlias)->(Eof())
		
		//-> Titulos
		oSec2:Init()
		oSec2:Cell("E2_FILIAL"):SetValue((cAlias)->E2_FILIAL)
		oSec2:Cell("E2_FORNECE"):SetValue((cAlias)->E2_FORNECE)
		oSec2:Cell("E2_LOJA"):SetValue((cAlias)->E2_LOJA)	
		oSec2:Cell("E2_PREFIXO"):SetValue((cAlias)->E2_PREFIXO)
		oSec2:Cell("E2_NUM"):SetValue((cAlias)->E2_NUM)	
		oSec2:Cell("E2_PARCELA"):SetValue((cAlias)->E2_PARCELA)
		oSec2:Cell("E2_TIPO"):SetValue((cAlias)->E2_TIPO)
		oSec2:Cell("E2_EMISSAO"):SetValue(DToC(SToD((cAlias)->E2_EMISSAO)))
		oSec2:Cell("E2_VENCREA"):SetValue(DToC(SToD((cAlias)->E2_VENCREA)))			
		oSec2:Cell("E2_VALOR"):SetValue((cAlias)->E2_VALOR)
			
		oSec2:Printline()
		
		If oReport:Cancel()
			Return(Nil)
		EndIf
		
		(cAlias)->(DbSkip())
	EndDo

	(cAlias)->(DbCloseArea())

	oSec1:Finish()
	oSec2:Finish()

Return

/*/{Protheus.doc} ChkDados
(long_description)
@author j2a.luizjunior
@since 11/11/2016
@version 1.0
@example
(examples)
@see (links_or_references)
/*/

Static Function ChkDados(pFat)

	Local cFatura  := pFat
	Local cAlias   := GetNextAlias()
	Local lRet     := .T.
	Local cSql     := ""
	
	If Select(cAlias) > 0
		(cAlias)->(DbCloseArea())
	EndIf
	
	If !Empty(cBordero)
		cSql += " AND E2_FATURA   = '" + cFatura + "' "
	Else		 
		lRet := .F.
		Return lRet	
	EndIf
	
	cSql := "%" + cSql + "%"
	
	BeginSql Alias cAlias
	
		SELECT E2_NUM
		FROM   %TABLE:SE2%
		WHERE  E2_FILIAL = %xFilial:SE2%
		AND    %NOTDEL% 		
		%Exp:cSql%
		ORDER BY E2_NUM 
		
	EndSql
	
	If Empty((cAlias)->E2_NUM)
		lRet := .F.
	EndIf

Return lRet

/*/{Protheus.doc} CriaSx1
(long_description)
@author j2a.luizjunior
@since 13/10/2016
@version 1.0
@example
(examples)
@see (links_or_references)
/*/

Static Function CriaSx1

	Local aHelp := {}
	
	//-> Texto do help em português,inglês, espanhol
	AAdd(aHelp, {{"Nº Fatura"},{""},{""}})
	AAdd(aHelp, {{"Prefixo"},{""},{""}})
	
	u_SFPUTSX1(cPerg,"01","Nº Fatura","","","mv_ch1","C",09,00,00,"G","","","","","mv_par01","","","","","","","","","","","","","","","","",aHelp[1,1] ,aHelp[1,2] ,aHelp[1,3] ,"")
	u_SFPUTSX1(cPerg,"02","Prefixo"  ,"","","mv_ch2","C",03,00,00,"G","","","","","mv_par02","","","","","","","","","","","","","","","","",aHelp[2,1] ,aHelp[2,2] ,aHelp[2,3] ,"")

Return