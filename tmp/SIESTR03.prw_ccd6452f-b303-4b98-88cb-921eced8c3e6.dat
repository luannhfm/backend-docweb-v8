#Include "Protheus.ch"
#Include "TopConn.ch"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³SIESTR03  ºAutor  ³Felipe Alves        º Data ³  17/01/13   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Relatorio de Transferencia                                 º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³P11 - SISTEMA INDUSTRIA                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

User Function SIESTR03(cTransf, nTipo)
Local aArea           := GetArea()
Local lRet            := .T.
Local cQuery          := ""
Local cTab            := ""
Local cData           := ""
Local cHora           := ""
Local cFilOri         := ""
Local cFilDes         := ""
Local cNF             := ""
Local aRegs           := {}
Local nLinha          := 0
Local cPag            := 1
Local lBrush          := .F.
Local nI              := 0
Local oBrush          := TBrush():New( , CLR_HGRAY)
Local aPergs          := {}
Local aRet            := {}

Private oPrint
Private oFont1        := TFont():New("Arial Narrow", , 18, , .T., , , , , .F.)
Private oFont2        := TFont():New("Arial Narrow", , 10, , .T., , , , .T., .F.)
Private oFont3        := TFont():New("Arial Narrow", , 14, , .T., , , , , .F.)
Private oFont4        := TFont():New("Arial Narrow", , 12, , .F., , , , , .F.)

cQuery              := "SELECT" + CRLF
cQuery              += "T0.ZAF_FILORI," + CRLF
If (nTipo == 1)
	cQuery            += "T0.ZAF_DATA," + CRLF
	cQuery            += "T0.ZAF_HORA," + CRLF
	cQuery            += "T0.ZAF_FILDES," + CRLF
	cQuery            += "T0.ZAF_NFTRAN," + CRLF
Endif
cQuery              += "T0.ZAF_PRODUT," + CRLF
cQuery              += "T0.ZAF_DESCRI," + CRLF
cQuery              += "T1.B1_UM,"
cQuery              += "SUM(T0.ZAF_QTDE) [ZAF_QTDE]," + CRLF
cQuery              += "T0.ZAF_LOCAL" + CRLF
cQuery              += "FROM " + RetSQLName("ZAF") + " T0" + CRLF
cQuery              += "INNER JOIN " + RetSQLName("SB1") + " T1 ON (T1.B1_COD = T0.ZAF_PRODUT)" + CRLF
cQuery              += "WHERE T0.ZAF_FILIAL = '" + xFilial("ZAF") + "'" + CRLF
If (nTipo == 1)
	cQuery              += "AND T0.ZAF_TRANSF = '" + cTransf + "'" + CRLF
Elseif (nTipo == 2)
	aAdd(aPergs, {1, "Transferência De"  , cTransf           , ""          , "", ""     , "", 80, .F.})
	aAdd(aPergs, {1, "Transferência Até" , Replicate("Z", 20), ""          , "", ""     , "", 80, .F.})
	aAdd(aPergs, {1, "Data De"           , '01/01/2001'      , "99/99/9999", "", ""      , "", 40, .F.})
	aAdd(aPergs, {1, "Data Até"          , '31/12/2020'      , "99/99/9999", "", ""      , "", 40, .F.})
	aAdd(aPergs, {1, "Filial Destino De" , Space(12)         , ""          , "", "SM0FIL", "", 80, .F.})
	aAdd(aPergs, {1, "Filial Destino Até", Replicate("Z", 12), ""          , "", "SM0FIL", "", 80, .F.})
	
	If (ParamBox(aPergs, "Parâmetros", @aRet, , , , , , , , .T., .T.))
		cQuery            += "AND T0.ZAF_TRANSF BETWEEN '" + aRet[1] + "' AND '" + aRet[2] + "'" + CRLF
		cQuery            += "AND T0.ZAF_DATA BETWEEN '" + DToS(CToD(aRet[3])) + "' AND '" + DToS(CToD(aRet[4])) + "'" + CRLF
		cQuery            += "AND T0.ZAF_FILDES BETWEEN '" + aRet[5] + "' AND '" + aRet[6] + "'" + CRLF
	Endif
Endif
cQuery              += "AND T0.D_E_L_E_T_ = ''" + CRLF
cQuery              += "AND T1.B1_FILIAL = '" + xFilial("SB1") + "'" + CRLF
cQuery              += "AND T1.D_E_L_E_T_ = ''" + CRLF
cQuery              += "GROUP BY "
cQuery              += "T0.ZAF_FILORI," + CRLF
If (nTipo == 1)
	cQuery            += "T0.ZAF_DATA," + CRLF
	cQuery            += "T0.ZAF_HORA," + CRLF
	cQuery            += "T0.ZAF_FILDES," + CRLF
	cQuery            += "T0.ZAF_NFTRAN," + CRLF
Endif
cQuery              += "T0.ZAF_PRODUT," + CRLF
cQuery              += "T0.ZAF_DESCRI," + CRLF
cQuery              += "T1.B1_UM," + CRLF
cQuery              += "T0.ZAF_LOCAL" + CRLF

cQuery                := ChangeQuery(cQuery)

cTab                  := GetNextAlias()

TcQUERY cQuery NEW ALIAS ((cTab))

DbSelectArea((cTab))
(cTab)->(DbGoTop())

While ((cTab)->(!Eof()))
	cData               := Iif(nTipo == 1, DToC(SToD((cTab)->(ZAF_DATA))), "DIVERSOS")
	cHora               := Iif(nTipo == 1, (cTab)->(ZAF_HORA), "DIVERSOS")
	cFilOri             := (cTab)->(ZAF_FILORI)
	cFilDes             := Iif(nTipo == 1, (cTab)->(ZAF_FILDES), "DIVERSOS")
	cNF                 := Iif(nTipo == 1, (cTab)->(ZAF_NFTRAN), "DIVERSOS")
	
	aAdd(aRegs, {(cTab)->(ZAF_PRODUT), (cTab)->(ZAF_DESCRI), Transform((cTab)->(ZAF_QTDE), "@E 999,999,999.99"), (cTab)->(ZAF_LOCAL), (cTab)->(B1_UM)})
	
	(cTab)->(DbSkip())
Enddo

(cTab)->(DbCloseArea())

If (Len(aRegs) > 0)
	oPrint              := TmsPrinter():New("Romaneio de Entrega - Transferência entre Empresas e Filiais (Separação de Estoque)")
	oPrint:SetLandScape()
	oPrint:SetPaperSize(DMPAPER_A4)
	If !(oPrint:Setup())
		lRet              := .F.
	Endif
	
	If (lRet)
		oPrint:StartPage()
		
		nLinha            := 320
		CabRel(Iif(nTipo == 1, cTransf, "DIVERSOS"), cData, cHora, cFilOri, cFilDes, cNF)
		CabReg()
		
		For nI := 1 To Len(aRegs)
			If nLinha >= 2400
				nLinha        := 320
				oPrint:EndPage()
				oPrint:SetLandScape()
				oPrint:StartPage()
				CabRel(cTransf, cData, cHora, cFilOri, cFilDes, cNF)
				CabReg()
			EndIf
			
			If (lBrush)
				oPrint:FillRect({nLinha, 0022, nLinha+55, 3480}, oBrush)
				lBrush        := .F.
			Else
				lBrush        := .T.
			Endif
			
			oPrint:Say(nLinha, 0050, aRegs[nI, 1], oFont4)
			oPrint:Say(nLinha, 0550, aRegs[nI, 2], oFont4)
			oPrint:Say(nLinha, 2150, aRegs[nI, 5], oFont4)
			oPrint:Say(nLinha, 2285, aRegs[nI, 3], oFont4)
			oPrint:Say(nLinha, 2785, aRegs[nI, 4], oFont4)
			
			nLinha          += 60
		Next nI
		
		oPrint:EndPage()
		oPrint:Preview()
		Ms_Flush()
		oPrint:End()
	Endif
Else
	Aviso("Impressão", "Nenhum registro encontrado.", {"OK"}, 3)
Endif

RestArea(aArea)
Return(lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³SIESTR03   ºAutor  ³Felipe Alves        º Data ³  17/01/13   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³                                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function CabRel(cTransf, cData, cHora, cFilOri, cFilDes, cNF)
Local lRet  := .T.
Local cLogo := GetSrvProfString("Startpath", "") + "firjan.bmp"

oPrint:SayBitmap(0040, 0030, cLogo, 0600, 0180)

oPrint:Box(0030, 0010, 0240, 3485)
oPrint:Say(0040, 0800, "ROMANEIO DE ENTREGA - TRANSFERÊNCIA ENTRE EMPRESAS E FILIAIS (SEPARAÇÃO DE ESTOQUE)", oFont1)

oPrint:Say(0110, 0800, "TRANSFERÊNCIA", oFont2)
oPrint:Say(0110, 1070, " : " + cTransf, oFont2)

oPrint:Say(0150, 0800, "FILIAL ORIGEM", oFont2)
oPrint:Say(0150, 1070, " : " + RetDFil(cFilOri), oFont2)

oPrint:Say(0190, 0800, "NF/ SÉRIE", oFont2)
oPrint:Say(0190, 1070, " : " + cNF, oFont2)

oPrint:Say(0110, 1820, "DATA", oFont2)
oPrint:Say(0110, 2090, " : " + cData, oFont2)

oPrint:Say(0150, 1820, "FILIAL DESTINO", oFont2)
oPrint:Say(0150, 2090, " : " + RetDFil(cFilDes), oFont2)

oPrint:Say(0110, 2840, "HORA", oFont2)
oPrint:Say(0110, 3110, " : " + cHora, oFont2)
Return(lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³SIESTR03   ºAutor  ³Felipe Alves        º Data ³  17/01/13   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³                                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function CabReg()
Local lRet := .T.

oPrint:Box(0250, 0010, 2460, 3485)
oPrint:Box(0310, 0010, 0310, 3485)
oPrint:Say(0255, 0050, "PRODUTO"   , oFont3)
oPrint:Say(0255, 0550, "DESCRIÇÃO" , oFont3)
oPrint:Say(0255, 2150, "U.M."      , oFont3)
oPrint:Say(0255, 2285, "QTDE. SEP.", oFont3)
oPrint:Say(0255, 2785, "LOCAL"     , oFont3)
oPrint:Say(0255, 3285, "OK"        , oFont3)
Return(lRet)

Static Function RetDFil(cCodFil)
Local aArea := GetArea()
Local aAreaSM0 := GetArea("SM0")
Local cDescri := ""

DbSelectArea("SM0")
SM0->(DbGoTop())

While (SM0->(!Eof()))
	If (AllTrim(SM0->(M0_CODFIL)) == AllTrim(cCodFil))
		cDescri := AllTrim(SM0->(M0_CODFIL)) + " - " + SM0->(M0_NOME)
	Endif
	
	SM0->(DbSkip())
Enddo

RestArea(aArea)
RestArea(aAreaSM0)
Return(cDescri)
