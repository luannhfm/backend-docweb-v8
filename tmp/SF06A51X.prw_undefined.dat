#INCLUDE "rwmake.ch"
#INCLUDE "protheus.ch"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³SF06A51X  ºAutor  ³Leonardo P de Castroº Data ³  15/11/09   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ IMPRIME O CONTRATO DE NEGOCIACAO E AS PARCELAS             º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ P-10 MEGA MODAS                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
User Function SF06A51X(_aContrato,_cOrc)

Local _nX			:= 0
Local nL				:= 0
Local nC				:= 0
Local nI				:= 0
Local nCL			:= 0
Local cPorta		:="LPT1"
Local CR				:= Chr(13)+Chr(10)

Local nLin			:= 0
Local nLinQ			:= 0
Local nLinY			:= 0
Local nSal			:= 0
Local nIni			:= 0
Local nTam			:= 0
Local _aLinha     :={}
Local cContrato	:= ''

Private _nLarg 	:= 74
Private _nAlt		:= 145
Private _nColI		:= 15
Private _nColF		:= _nLarg+_nColI

Default	_aContrato := {}

_cTexto := "Pelo presente Termo de Confissao de Divida e na melhor forma de direito, confesso devedor(a) "
_cTexto += "de " + AllTrim(SM0->M0_NOMECOM) + ", no valor de R$ " + Transform(_aContrato[3],"@E 99,999.99") + " "
_cTexto += "atualizado ate a presente data, sem a incidencia de encargos ou juros pre-fixados, reputado liquido "
_cTexto += "certo e exigivel, servindo o presente como titulo de credito executivo para todos os fins e efeitos legais."
_cTexto += "Comprometo-me a pagar o valor acima mencionado nas condicoes abaixo estabelecidas, ficando certo que a "
_cTexto += "falta ou atraso no pagamento de qualquer parcela implicara no cancelamento do acordo e vencimento antecipado das " 
_cTexto += "demais e, neste caso, responderei a procedimento judicial cabivel, em especial ao disposto nos arts. 585 a 587 do CPC"

_aLinha := JustTexto(_cTexto,47)

/*
1 - Codigo Cliente + Loja do Cliente
2 - DtCompra
3 - VlrCompr
4 - VlrFinanciado
5 - Entrada
6 - VlrParcelado
7 - NumContrato
8 - Array Parcelas - sendo
	1 - Prefixo
	2 - Num Titulo
	3 - Parcela
	4 - Tipo Titulo
	5 - Valor Titulo
	6 - Vencimento
*/

If Len(_aContrato) = 0		//	<<<< DADOS PARA TESTTE
	aAdd( _aContrato, '00000201' )
	aAdd( _aContrato, StoD('20080818') )
	aAdd( _aContrato, 2000 )
	aAdd( _aContrato, 2000 )
	aAdd( _aContrato, 0    )
	aAdd( _aContrato, 2000 )
	aAdd( _aContrato, '123123123' )
	aAdd( _aContrato, {{'CUP','000001','01','NF ',201.01,StoD('20080820')}} )
EndIF

MSCBPRINTER("ELTRON",cPorta,,,.F.)
MSCBLOADGRF("MEGAMODA.PCX")			//MSCBLOADGRF('AVENIDA.PCX')
MSCBLOADGRF("TESOURA.PCX")			//MSCBLOADGRF('AVENIDA.PCX')


MSCBBEGIN(1,6)

MSCBWRITE('Q40,0'+CR)

//Picote Inicial para Identificar o tamanho dos documentos
nLin := 1
MSCBGRAFIC(_nColI+1,nLin,"TESOURA")
nLin += 1
For nJ := _nColI To (_nLarg+_nColI) Step Round((_nLarg+_nColI)/30,0)
	MSCBLineH(nJ,nLin,nJ+Round((_nLarg+_nColI)/60,0),2)
Next nJ

nLin += 2

MSCBGRAFIC(_nColI+2,nLin,"MEGAMODA")
MSCBSAY(_nColI+20,nLin,'Loja: '+cFilAnt+'-'+AllTrim(SM0->M0_NOME),"N","2","2,3",,,,,.T.)

nLin += 08
MSCBSAY(_nColI+10,nLin,"TERMO DE CONFISSAO DE DIVIDA","N","4","1,1",,,,,.T.)

nLin 	+= 4
nLinQ	:= nLin-1
nIni	:= 0
nTam  := 47
nSal  := 5

For nX:=1 to len(_aLinha)         
      nLin += nSal
		MSCBSAY(_nColI+2,nLin,_aLinha[nX],"N","2","2",,,,,.T.)
Next nX

nLin += 7
MSCBSAY(_nColI+10,nLin,"TITULOS NEGOCIADOS","N","4","1,1",,,,,.T.)
nLin += 5
MSCBSAY(_nColI+2,nLin,"Titulo        Vencto.         Valor  V.Corrigido","N","1","2",,,,,.T.)
nLin += 4
DbSelectArea("ZAM")
ZAM->(DbSetOrder(1))

	If ZAM->(DbSeek(xFilial("ZAM") + _cOrc))
 		While ZAM->(!Eof()) .And. ZAM->ZAM_CODNEG = _cOrc
 		
 			If ZAM->ZAM_OPER = "G"
 				ZAM->(DbSkip())
 				Loop
 			EndIF   
 			
			MSCBSAY(_nColI+2,nLin,ZAM->ZAM_NUM+"/"+ZAM->ZAM_PARCEL+"  "+DtoS(ZAM->ZAM_VENCTO)+"    "+Transform(ZAM->ZAM_VALOR,"@E 99,999.99")+"    "+Transform(ZAM->ZAM_VALCOR,"@E 99,999.99"),"N","1","2",,,,,.T.)
			nLin += 5							
			ZAM->(DbSkip())
		EndDo
	EndIF

nLin += 7
MSCBSAY(_nColI+10,nLin,"FORMA DE PAGAMENTO","N","4","1,1",,,,,.T.)
nLin += 5
MSCBSAY(_nColI+2,nLin,"Parcela      Vencto.         Valor","N","1","2",,,,,.T.)
nLin += 4
DbSelectArea("ZAM")
ZAM->(DbSetOrder(1))

	If ZAM->(DbSeek(xFilial("ZAM") + _cOrc))
 		While ZAM->(!Eof()) .And. ZAM->ZAM_CODNEG = _cOrc
 		
 			If ZAM->ZAM_OPER = "B"
 				ZAM->(DbSkip())
 				Loop
 			EndIF   
 			
			MSCBSAY(_nColI+2,nLin,ZAM->ZAM_NUM+"/"+ZAM->ZAM_PARCEL+" "+DtoS(ZAM->ZAM_VENCTO)+"    "+Transform(ZAM->ZAM_VALOR,"@E 99,999.99"),"N","1","2",,,,,.T.)
			nLin += 5				
			ZAM->(DbSkip())
		EndDo
	EndIF
	
nLin += 7
MSCBSAY(_nColI+2,nLin,"ASSINATURA:   " + Replicate('-',nTam-14),"N","2","2",,,,,.T.)
nLin += 5
MSCBSAY(_nColI+2,nLin,"                 NOME: "+Posicione('SA1',1,xFilial('SA1')+_aContrato[1],'A1_NOME'),"N","1","2",,,,,.T.)
nLin += 5
MSCBSAY(_nColI+2,nLin,"                 CPF: "+Posicione('SA1',1,xFilial('SA1')+_aContrato[1],'A1_CGC'),"N","1","2",,,,,.T.)

nLin += 5
MSCBSAY(_nColI+2,nLin,"TESTEMUNHAS","N","2","1,2",,,,,.T.)
nLin += 10
MSCBSAY(_nColI+2,nLin,"1-"+Replicate('-',nTam-2),"N","2","1,2",,,,,.T.)
nLin += 10
MSCBSAY(_nColI+2,nLin,"2-"+Replicate('-',nTam-2),"N","2","1,2",,,,,.T.)

nLin += 6

nLin += nSal
nLin += 1

nL := 1
nC	:= 0

nLinY := nLin  //Para a Barra divisora das parcelas

For nI := 1 To Len(_aContrato[8])
	
	If nC >= 2
		nLin += nSal
		nC := 0
	EndIf
	
	If nL > 8
		Exit
	EndIf
	
	nC++
	nCL := _nColI+2 + ((nC-1) * (Round(_nLarg/2,0)))
	
	MSCBSAY(nCL,nLin,_aContrato[8][nI][3],"N","2","2",,,,,.T.)
	MSCBSAY(nCL+03,nLin,DtoC(_aContrato[8][nI][6]),"N","2","2",,,,,.T.)
	MSCBSAY(nCL+19,nLin,Transform(_aContrato[8][nI][5],'@E 999,999.99'),"N","2","2",,,,,.T.)
	
Next nI

MSCBLineV((Round(_nLarg/2,0)+_nColI),nLinY,nLin+2,4)
//MSCBBOX(_nColI,nLinQ,_nColI+_nLarg,_nAlt)

MSCBEND()

PrintParcs(_aContrato)

MSCBCLOSEPRINTER()

Return( Nil )




/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³PRINTPARCSºAutor  ³JULIO STORINO       º Data ³  08/20/08   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³IMPRIME AS PARCELAS DO CARNE DE PAGAMENTO                   º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ P-10 MEGAMODAS                                                º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function PrintParcs(_aTitulos)

Local	nLin			:= 0
Local nI				:= 0
Local nJ				:= 0
Local nK				:= 0
Local nLinQ			:= 0
Local nSal			:= 0
Local lPage			:= .F.

/* 1 - Codigo Cliente + Loja do Cliente
2 - DtCompra
3 - VlrCompr
4 - VlrFinanciado
5 - Entrada
6 - VlrParcelado
7 - NumContrato
8 - Array Parcelas - sendo
   1 - Prefixo
   2 - Num Titulo
   3 - Parcela
   4 - Tipo Titulo
   5 - Valor Titulo
   6 - Vencto */

//Imprime as parcelas
For nI := 1 To (Len(_aTitulos[8])*2)

	If !(Mod(nI,2) = 0)
		MSCBBEGIN(1,6)
		lPage := .F.
		nK++
		nLin := 1
		MSCBGRAFIC(_nColI+1,nLin,"TESOURA")
		nLin += 1
		For nJ := _nColI To (_nLarg+_nColI) Step Round((_nLarg+_nColI)/30,0)
			MSCBLineH(nJ,nLin,nJ+Round((_nLarg+_nColI)/60,0),2)
		Next nJ
	EndIf
	
	nLin += 3
	nSal := 2
	
	MSCBGRAFIC(_nColI+2,nLin,"MEGAMODA")

	nLin += 3
	MSCBSAY(_nColI+28,nLin,'Loja: '+cFilAnt+'-'+AllTrim(SM0->M0_NOME),"N","1","2,3",,,,,.T.)
	
	nLin += 5
	
	MSCBLineH(_nColI,nLin,_nLarg+_nColI,2)
	nLin += 1
	MSCBSAY(_nColI+1,nLin,"NOME DO CLIENTE","N","1","1,1",,,,,.T.)
	nLin += nSal + 1
	MSCBSAY(_nColI+3,nLin,Posicione('SA1',1,xFilial('SA1')+_aTitulos[1],'A1_NOME'),"N","4","1,1",,,,,.T.)

	nLin += nSal + 2
	nLinQ := nLin
	MSCBLineH(_nColI,nLin,_nLarg+_nColI,2)	

	nLin += 1
	MSCBSAY(_nColI+1,nLin,"COD CLIENTE","N","1","1,1",,,,,.T.)	
	MSCBSAY(_nColI+(Round(_nLarg/3,0))+1,nLin,"NEGOCIACAO","N","1","1,1",,,,,.T.)	 
	MSCBSAY(_nColI+(Round(_nLarg/3,0)*2)+1,nLin,"DATA DA NEGOCIACAO","N","1","1,1",,,,,.T.)	 
	nLin += nSal + 1
	MSCBSAY(_nColI+3,nLin,_aTitulos[1],"N","4","1,1",,,,,.T.) 
	MSCBSAY(_nColI+(Round(_nLarg/3,0))+3,nLin,_aTitulos[7],"N","4","1,1",,,,,.T.)
	MSCBSAY(_nColI+(Round(_nLarg/3,0)*2)+3,nLin,DtoC(_aTitulos[2]),"N","4","1,1",,,,,.T.)

	nLin += nSal + 2
	MSCBLineH(_nColI,nLin,_nLarg+_nColI,2)	

	nLin += 1
	MSCBSAY(_nColI+1,nLin,"PARCELA","N","1","1,1",,,,,.T.)	
	MSCBSAY(_nColI+(Round(_nLarg/3,0))+1,nLin,"VENCIMENTO","N","1","1,1",,,,,.T.)	 
	MSCBSAY(_nColI+(Round(_nLarg/3,0)*2)+1,nLin,"VALOR DA PARCELA","N","1","1,1",,,,,.T.)	 
	nLin += nSal + 1
	MSCBSAY(_nColI+9,nLin,_aTitulos[8][nK][3],"N","4","1,1",.T.,,,,.T.) 
	MSCBSAY(_nColI+(Round(_nLarg/3,0))+5,nLin,DtoC(_aTitulos[8][nK][6]),"N","4","1,1",.T.,,,,.T.)
	MSCBSAY(_nColI+(Round(_nLarg/3,0)*2)+3,nLin,Transform(_aTitulos[8][nK][5],'@E 999,999.99'),"N","4","1,1",,,,,.T.)

	nLin += nSal + 2
	MSCBLineH(_nColI,nLin,_nLarg+_nColI,2)	
	
	nLin += 1
	MSCBSAY(_nColI+1,nLin,"EMISSAO DO CARNE","N","1","1,1",,,,,.T.)	
	MSCBSAY(_nColI+(Round(_nLarg/3,0))+1,nLin,"ACRESCIMOS","N","1","1,1",,,,,.T.)	 
	MSCBSAY(_nColI+(Round(_nLarg/3,0)*2)+1,nLin,"VALOR TOTAL","N","1","1,1",,,,,.T.)	 
	nLin += nSal + 1
	MSCBSAY(_nColI+5,nLin,DtoC(dDataBase),"N","4","1,1",,,,,.T.) 
	
	nLin += nSal + 2
	MSCBLineH(_nColI,nLin,_nLarg+_nColI,3)	
	
	MSCBLineV(_nColI+(Round(_nLarg/3,0)),nLinQ,nLin,3)
	MSCBLineV(_nColI+(Round(_nLarg/3,0)*2),nLinQ,nLin,3)

	nLin += nSal
	MSCBSAY(_nColI+3,nLin,"EVITE PAGAR JUROS. QUITE SUA PRESTACAO EM DIA","N","2","1,1",,,,,.T.)	

	nLin += nSal + 2
	MSCBLineH(_nColI,nLin,_nLarg+_nColI,3)	

	nLin += 2
   
	If (Mod(nI,2) = 0)
		MSCBSAYBAR(_nColI+12,nLin,_aTitulos[8][nK][1]+_aTitulos[8][nK][2]+_aTitulos[8][nK][3],'N','MB07',09,.T.,.T.,,,3,2) //Barras

		nLin += 13
		MSCBLineH(_nColI,nLin,_nLarg+_nColI,3)	
	
		nLin += 07
	Else
		nLin += 1
		MSCBSAY(_nColI+1,nLin,"AUTENTICACAO","N","1","1,1",,,,,.T.)	
	
		nLin += 18
	EndIF
	
	If !(Mod(nI,2) = 0)
		For nJ := _nColI To (_nLarg+_nColI) Step Round((_nLarg+_nColI)/20,0)
	
			MSCBLineH(nJ,nLin,nJ+Round((_nLarg+_nColI)/40,0),3)
		
		Next nJ
	EndIf		

	If Mod(nI,2) = 0
		nLin := 0
		MSCBEND()
		lPage := .T.
	EndIF

Next nI

If !lPage
	MSCBEND()
EndIf

Return( Nil )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³JustTexto ºAutor  ³Leonardo P de Castroº Data ³  15/11/09   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Rotina para ajustar textos em colunas predefinidas.        º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function JustTexto(cTextJust,nTamCol)

Local cTextEdit := cTextJust
Local aTextResult := {}

nTotLinhas := MlCount(cTextEdit,nTamCol)

For t := 1 To nTotLinhas

	aAdd(aTextResult, MemoLine(cTextEdit,nTamCol,t))
	nCodAsc := 170
	aTextResult[t] := AllTrim(aTextResult[t])
	aTextResult[t] := StrTran(aTextResult[t], " ", Chr(nCodAsc))
	
	While .T.
		If Len(AllTrim(aTextResult[t])) == nTamCol .Or. t == nTotLinhas
			For r := 170 To nCodAsc
				aTextResult[t] := StrTran(aTextResult[t], Chr(r), " ")
			Next r
			Exit
		EndIf
		
		nPosSpaco := At(Chr(nCodAsc),aTextResult[t])
		
		If nPosSpaco > 0
			aTextResult[t] := Subs(aTextResult[t],1,nPosSpaco-1) + Space(2)+;
			Subs(aTextResult[t],nPosSpaco+1,(nTamCol-(nPosSpaco-1)))
			aTextResult[t] := AllTrim(aTextResult[t])
			aTextResult[t] := StrTran(aTextResult[t],Chr(nCodAsc),Chr(nCodAsc+1))
		Else
			nPosSpaco := At(Space(2),aTextResult[t])
			aTextResult[t] := Subs(aTextResult[t],1,nPosSpaco-1) + Space(3)+;
			Subs(aTextResult[t],nPosSpaco+2,(nTamCol-(nPosSpaco-2)))
		EndIF
		
		nCodAsc+=1
	EndDo
Next

Return(aTextResult)