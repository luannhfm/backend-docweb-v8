#Include 'Protheus.ch'
#Include 'Parmtype.ch'

/*/{Protheus.doc} MT120OK
	MT120OK - Validações Específicas de Usuário
	LOCALIZAÇÃO : Function A120TudOk() responsável pela validação de todos 
	os itens da GetDados do Pedido de Compras / Autorização de Entrega.

	EM QUE PONTO : O ponto se encontra no final da função e é disparado 
	após a confirmação dos itens da getdados e antes do rodapé da dialog
	do PC, deve ser utilizado para validações especificas do usuario onde 
	será controlada pelo retorno do ponto de entrada caso for .F. o processo 
	será interrompido e se .T. será validado.
	
	Este P.E. será utilizado para validar se o fornecedor utilizado no pedido 
	de compras é do tipo F - Pessoa Física e se o mesmo possui numero NIT.
	OBS: Só valerá para inclusão manual. Validará também se o campo de justi-
	ficativa 'C7_OBS' está preenchido.

@author franklin B. Oliveira
@since 02/02/2016
@version 1.00

@see http://tdn.totvs.com/pages/releaseview.action?pageId=6085483

/*/
User Function MT120OK()

Local aArea		:= GetArea()
Local lRet 		:= .T.
Local nPosObs   := aScan(aHeader,{|x| AllTrim(x[2]) == 'C7_OBS'})
Local nX		:= 0	
	
	dbSelectArea("SA2")
	dbSetOrder(1)
	
	//A Validação só ocorrerá para inclusão manual
	If !l120Auto
		//Validação do fornecedor
		If dbSeek(xFilial("SA2") + cA120Forn + cA120Loj)
			If SA2->A2_TIPO <> "F"
				Help("ESPECIFICO", 1, "HELP", "FORNECEDOR INVÁLIDO","Só é possível inserir Pedido de Compra para Fornecedor PF - Pessoa Física", 1, 0)
				lRet := .F.
			ElseIf Empty(SA2->A2_NIT)
				Help("ESPECIFICO", 1, "HELP", "FORNECEDOR INVÁLIDO","Cadastro de Fornecedor sem o Numero NIT", 1, 0)
				lRet := .F.
			ElseIf SA2->A2_XATIVO != '1'
				Help("ESPECIFICO", 1, "HELP", "FORNECEDOR INVÁLIDO","Cadastro de Fornecedor incompleto, ";
											+ "entrar em contato com assessoria tributária para liberar o cadastro.", 1, 0)
				lRet := .F.
			EndIf
		EndIf
		
		//Validação da Justificativa
		If lRet
			For nX := 1 To Len( aCols )
				If Vazio(aCols[nX][nPosObs])
					Help("ESPECIFICO", 1, "HELP", "EM BRANCO","Falta justificar o serviço no campo observação do Item [" + StrZero(nX, 3) + "]", 1, 0)
					lRet := .F.
					Exit
				EndIf
			Next nX
		EndIf
	EndIf
	
	RestArea(aArea)
	
Return lRet