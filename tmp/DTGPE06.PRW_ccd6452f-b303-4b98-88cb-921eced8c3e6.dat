#include "PROTHEUS.CH"

//-- Diretivas indicando as colunas para Matriz de Verbas
#define MN_QTDCOL	 12

#define MN_MES      1
#define MN_ANO      2
#define MN_MAT      3
#define MN_NOME     4
#define MN_CC       5
#define MN_DESCCC   6
#define MN_ITEM     7
#define MN_DESCITE  8
#define MN_HRMES    9
#define MN_HORAS   10
#define MN_PERCEN  11
#define MN_RECNO   12
                                                   	
/*/f/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
<Descricao> : Exclusão de Estrura de Rateio
<Autor> : Fábrica DOIT SP
<Data> : 16/05/2014
<Parametros> : Nil
<Retorno> : Nil
<Processo> : FIEMT – Folha de Pagamento
<Tipo> : 
<Obs> : 
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
*/

User Function DTGPE06()

//-- Dialog
Local oDlg
Local oPanel
Local nItem,nLine,nPos

//-- Controle de dimensoes de objetos
Local aObjects	    := {}
Local aInfo		    := {}
Local aPosObj	    := {}  
Local aSize		    := {}
Local lOk		    := .F.
Local nL			:= 0
Private aItens     := {}
Private aBotoes    := {}

Private oCodFunc   := Nil
Private oNomeFunc  := Nil
Private oHorasMes  := Nil
Private oMesBase   := Nil
Private oAnoBase   := Nil

Private oListItens := Nil
Private aTitItens  := {}

Private aMesBase   := {}
Private aAnoBase   := {}                                     
Private cMesBase   := ""                                     
Private cPeriodo	:= GETMV("MV_FOLMES")
Private cAnoBase   := substr(cPeriodo,1,4) //StrZero( Year( dDataBase ), 4 )

//---------------------------------
//
//--------------------------------------------------

Aadd( aTitItens, "Mês"             )
Aadd( aTitItens, "Ano"             )
Aadd( aTitItens, "Matrícula"       )
Aadd( aTitItens, "Nome"            )
Aadd( aTitItens, "Centro de Custo" )
Aadd( aTitItens, "Descrição"       )
Aadd( aTitItens, "Item Contábil"   )
Aadd( aTitItens, "Descrição"       )
Aadd( aTitItens, "Horas Mês"       )
Aadd( aTitItens, "Horas Alocado"   )
Aadd( aTitItens, "Percentual (%)"  )

//---------------------------------
//
//--------------------------------------------------

For nL := 2000 To Year( dDataBase )

   Aadd( aAnoBase, Str( nL, 4 ) )

Next

Aadd( aMesBase, "Janeiro"   )
Aadd( aMesBase, "Fevereiro" )
Aadd( aMesBase, "Março"     )
Aadd( aMesBase, "Abril"     )
Aadd( aMesBase, "Maio"      )
Aadd( aMesBase, "Junho"     )
Aadd( aMesBase, "Julho"     )
Aadd( aMesBase, "Agosto"    )
Aadd( aMesBase, "Setembro"  )
Aadd( aMesBase, "Outubro"   )
Aadd( aMesBase, "Novembro"  )
Aadd( aMesBase, "Dezembro"  )

cMesBase := aMesBase[ VAL(SUBSTR(cPERIODO,5,2))] //aMesBase[ Month( dDataBase ) ]

//-- Dimensoes padroes
aSize   := MsAdvSize()
AAdd( aObjects, { 100, 020, .T., .F., .T. } )
AAdd( aObjects, { 100, 100, .T., .T. } )
AAdd( aObjects, { 100, 020, .F., .F. } )
aInfo   := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 5, 5 }
aPosObj := MsObjSize( aInfo, aObjects,.T.)

MsgRun( "Aguarde, selecionando informações...",, {|| GPE06Qry(.T.)} )

DEFINE MSDIALOG oDlg TITLE "Exclusão de Estrutura de Rateio" FROM aSize[7],00 TO aSize[6],aSize[5] PIXEL
		
	@ 005,005 Say "Mês:"                                                 Of oDlg Pixel
	@ 013,005 ComboBox oMesBase Var cMesBase  Items aMesBase Size 100,36 Of oDlg Pixel

	@ 005,115 Say "Ano:"                                                 Of oDlg Pixel
	@ 013,115 ComboBox oAnoBase Var cAnoBase  Items aAnoBase Size 070,36 Of oDlg Pixel

	@ 013,195 Button "Filtrar"                               Size 040,11 Of oDlg Pixel Action ( GPE06Qry() )

     //------------------ Documentos

	aPosObj[2,1] += 5

	oFolder := TFolder():NEW( aPosObj[2,1],004, { "Centro de Custo / Item Contábil" },, oDlg, 1,,, .t.,, aPosObj[2,4],aPosObj[3,3] )

	@ 005,005 LISTBOX oListItens Fields HEADER;
		  aTitItens[01],;
		  aTitItens[02],;
		  aTitItens[03],;
		  aTitItens[04],;
		  aTitItens[05],;
		  aTitItens[06],;
		  aTitItens[07],;
		  aTitItens[08],;
		  aTitItens[09],;
		  aTitItens[10],;
		  aTitItens[11] SIZE (aPosObj[2,4]-10), (aPosObj[2,3]-aPosObj[2,1]) OF oFolder:aDialogs[1] PIXEL 
			
	oListItens:SetArray( aItens )
	oListItens:bLine   := { || {            aItens[ oListItens:nAT,MN_MES    ]               ,;
									                aItens[ oListItens:nAT,MN_ANO    ]               ,;
									                aItens[ oListItens:nAT,MN_MAT    ]               ,;
									                aItens[ oListItens:nAT,MN_NOME   ]               ,;
									                aItens[ oListItens:nAT,MN_CC     ]               ,;
									                aItens[ oListItens:nAT,MN_DESCCC ]               ,;
									                aItens[ oListItens:nAT,MN_ITEM   ]               ,;
									                aItens[ oListItens:nAT,MN_DESCITE]               ,;
									     Transform( aItens[ oListItens:nAT,MN_HRMES  ], "@E 999"   ) ,;
									     Transform( aItens[ oListItens:nAT,MN_HORAS  ], "@E 999"   ) ,;
									     Transform( aItens[ oListItens:nAT,MN_PERCEN ], "@E 999.99") }}

ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar( oDlg, {||( Iif( GPE06Val(), oDlg:End(), .T. ) )}, {|| oDlg:End() },, aBotoes )

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ GPE06QRY        ³ Marcelo Coutinho    º Data ³  16/05/14    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                             º±±
±±º          ³                                                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ DTGPE04                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function GPE06QRY(lInicio)
Local cAliasQry  := GetNextAlias()
Local aAreaAtu   := GetArea()
Local cQuery     := ''
Local nDelItens  := Len( aItens )

Local nSizeCTT   := 4
Local nSizeCTD   := 4
Local nI			:= 0
Default lInicio  := .F.
//--------------------------------------
//
//-----------------------------------------------------------

For nI := 1 To nDelItens

    aDel( aItens, nI )
Next

aSize( aItens, 0 )

//--------------------------------------
//
//-----------------------------------------------------------

cQuery :=        "Select Distinct ZD6.ZD6_MESANO, ZD6.ZD6_MAT, SRA.RA_NOME, ZD6.ZD6_CC, CTT.CTT_DESC01, ZD6.ZD6_ITEM, CTD.CTD_DESC01, ZD6.ZD6_HRMES, ZD6.ZD6_HRTRAB, ZD6.ZD6_PERCEN, ZD6.R_E_C_N_O_ ZD6_RECNO"

cQuery += CRLF + "  From " + RetSqlName("ZD6") + " ZD6 "

cQuery += CRLF + "  Left Join " + RetSqlName("CTT") + " CTT On SubStr( CTT.CTT_FILIAL, 1, " + Str( nSizeCTT, 2 ) + " ) = SubStr( ZD6.ZD6_FILIMP, 1, " + Str( nSizeCTT, 2 ) + ") And CTT.CTT_CUSTO = ZD6.ZD6_CC   And CTT.D_E_L_E_T_ = ' '"
cQuery += CRLF + "  Left Join " + RetSqlName("CTD") + " CTD On SubStr( CTD.CTD_FILIAL, 1, " + Str( nSizeCTD, 2 ) + " ) = SubStr( ZD6.ZD6_FILIMP, 1, " + Str( nSizeCTD, 2 ) + ") And CTD.CTD_ITEM  = ZD6.ZD6_ITEM And CTD.D_E_L_E_T_ = ' '"
cQuery += CRLF + "  Left Join " + RetSqlName("SRA") + " SRA On SRA.RA_MAT     = ZD6.ZD6_MAT  And SRA.D_E_L_E_T_ = ' '"

cQuery += CRLF + " Where ZD6.D_E_L_E_T_ = ' '"
//cQuery += CRLF + "   And ZD6.ZD6_MESANO = '" + cAnoBase + StrZero( Iif( lInicio, Month( dDataBase ), oMesBase:nAt ), 2 ) + "'"
cQuery += CRLF + "   And ZD6.ZD6_MESANO = '" + cPeriodo + "'"

cQuery += CRLF + " Order By ZD6.ZD6_MESANO, ZD6.ZD6_MAT, ZD6.ZD6_CC, ZD6.ZD6_ITEM "

cQuery := ChangeQuery( cQuery )

dbUseArea( .T., "TOPCONN", TCGenQry(,,cQuery), cAliasQry, .T., .T. )     
TCSetField(cAliasQry,"ZD6_HRMES" ,"N",TamSX3("ZD6_HRMES" )[1])
TCSetField(cAliasQry,"ZD6_HRTRAB","N",TamSX3("ZD6_HRTRAB")[1])
TCSetField(cAliasQry,"ZD6_PERCEN","N",TamSX3("ZD6_PERCEN")[1],TamSX3("ZD6_PERCEN")[2])

//--------------------------------------
//
//-----------------------------------------------------------

While (cAliasQry)->(!Eof())

	Aadd( aItens, Array( MN_QTDCOL ) )

	nPosItens := Len( aItens )

	aItens[nPosItens][MN_MES    ] := SubStr( (cAliasQry)->( ZD6_MESANO ), 5, 2 )
	aItens[nPosItens][MN_ANO    ] := SubStr( (cAliasQry)->( ZD6_MESANO ), 1, 4 )
	aItens[nPosItens][MN_MAT    ] :=         (cAliasQry)->( ZD6_MAT    )
	aItens[nPosItens][MN_NOME   ] :=         (cAliasQry)->( RA_NOME    )
	aItens[nPosItens][MN_CC     ] :=         (cAliasQry)->( ZD6_CC     )
	aItens[nPosItens][MN_DESCCC ] :=         (cAliasQry)->( CTT_DESC01 )
	aItens[nPosItens][MN_ITEM   ] :=         (cAliasQry)->( ZD6_ITEM   )
	aItens[nPosItens][MN_DESCITE] :=         (cAliasQry)->( CTD_DESC01 )
	aItens[nPosItens][MN_HRMES  ] :=         (cAliasQry)->( ZD6_HRMES  )
	aItens[nPosItens][MN_HORAS  ] :=         (cAliasQry)->( ZD6_HRTRAB )
	aItens[nPosItens][MN_PERCEN ] :=         (cAliasQry)->( ZD6_PERCEN )
	aItens[nPosItens][MN_RECNO  ] :=         (cAliasQry)->( ZD6_RECNO  )

	(cAliasQry)->(dbSkip())
End

If Len( aItens ) == 0

   Aadd( aItens, Array( MN_QTDCOL ) )

	aItens[Len( aItens )][MN_MES    ] := StrZero( Month( dDataBase ), 2 )
	aItens[Len( aItens )][MN_ANO    ] := StrZero(  YEar( dDataBase ), 4 )
	aItens[Len( aItens )][MN_MAT    ] := ""
	aItens[Len( aItens )][MN_NOME   ] := ""
	aItens[Len( aItens )][MN_CC     ] := ""
	aItens[Len( aItens )][MN_DESCCC ] := ""
	aItens[Len( aItens )][MN_ITEM   ] := ""
	aItens[Len( aItens )][MN_DESCITE] := ""
	aItens[Len( aItens )][MN_HRMES  ] := 0
	aItens[Len( aItens )][MN_HORAS  ] := 0
	aItens[Len( aItens )][MN_PERCEN ] := 0.00
	aItens[Len( aItens )][MN_RECNO  ] := 0
EndIf

(cAliasQry)->(dbCloseArea())

RestArea(aAreaAtu)

Return NIL

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ GPE06Val    ºAutor  ³ Marcelo Coutinho   º Data ³  16/05/14 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Valida Exclusão                                             º±±
±±º          ³                                                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ DTGPE06                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function GPE06Val()

Local nValida 	:= GETMV("MV_BLQRAT") //Dia de corte de lanãmento de rateio
Local nDiaCorte	:= Day(DATE())
Local lRet := .F.


If nDiaCorte > nValida
   MsgAlert("Não é mais permitido esta exclusão. Data limite para manutenção é o dia "+Str(nValida))

Else

	If MsgYesNo( "ATENÇÃO"+CRLF+CRLF+"Ao executar a esclusão da Estrutura de Rateio, será excluída a estrutura deste período para TODOS os funcionários, e também serão excluídos os rateios realizados com a estrura que irá ser apagada, sendo necessário novo cálculo de rateio."+CRLF+"Se forem poucas as alterações, faça através da funcionalidade Manutenção de Rateio."+CRLF+CRLF +"Confirma exclusão dos registros?" )

		MsgRun( "Aguarde, excluindo as informações...",, {|| GPE06Exc()} )
		lRet := .T.
	EndIf
EndIf
     
Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ GPE06Exc    ºAutor  ³ Marcelo Coutinho   º Data ³  16/05/14 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Exclusão de Registros                                       º±±
±±º          ³                                                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ DTGPE06                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function GPE06Exc()

Local nX := 0

dbSelectArea( "ZD7" )
dbSetOrder( 2 )

For nX := 1 To Len( aItens )

	ZD6->( dbGoTo( aItens[ nX ][ MN_RECNO ] ) )

	RecLock("ZD6", .F.)		

  	ZD6->( DbDelete() )
	ZD6->( MsUnLock() )

Next

If ZD7->( dbSeek( cAnoBase + StrZero( oMesBase:nAt, 2 ) ) )

	While ZD7->ZD7_MESANO == ( cAnoBase + StrZero( oMesBase:nAt, 2 ) ) .And. ZD7->( !Eof() )

		RecLock("ZD7", .F.)		

  		ZD7->( DbDelete() )
		ZD7->( MsUnLock() )

		ZD7->( dbSkip() )

	End

EndIf

MsgInfo( "Registros excluídos com sucesso!" )

Return .T.

/*/f/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
<Descricao> : Rotina para controle de versao
<Data> : 24/05/2014
<Parametros> : Nenhum
<Retorno> : cRet
<Processo> : Controle de versao
<Tipo> (Menu,Trigger,Validacao,Ponto de Entrada,Genericas,Especificas ) : V
<Autor> : Doit Sistemas
<Obs> :
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
*/

User Function DTGPE06V()

Local cRet := ""
cRet := "20140523"
 
Return cRet