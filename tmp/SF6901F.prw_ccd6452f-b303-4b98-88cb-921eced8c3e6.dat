#Include 'protheus.ch'
#Include 'parmtype.ch'

/*/{Protheus.doc} SF6901F
	Função para validar os campos CN9_XDTINI e CN9_XDTFIM
	passando o campo a ser validado por parâmetro
	
@author Franklin B. Oliveira
@since 23/10/2015
@version 1.00
@param pCampo, Nome do campo a ser validado (CN9_XDTINI ou CN9_XDTFIM)

@return lRet, Se .T. data válida, se .F. data inválida
/*/
User Function SF6901F(pCampo)

Local cCampo 	:= pCampo
Local dIniCtr	:= M->CN9_DTINIC
Local dFimCtr	:= M->CN9_DTFIM
Local lRet		:= .T.

	If Upper(cCampo) == "CN9_XDTINI"
		//Data de inicio da execução Menor do que inicio da vigência
		If M->CN9_XDTINI < dIniCtr
			Help("ESPECIFICO", 1, "HELP", "INICIO EXECUÇÃO INVÁLIDA","A data de inicio da execução não pode ser menor que a data do inicio da vigência do contrato", 1, 0)
			lRet := .F.
			
		//Data de inicio da execução maior do que fim da vigência
		ElseIf  M->CN9_XDTINI > dFimCtr
			Help("ESPECIFICO", 1, "HELP", "INICIO EXECUÇÃO INVÁLIDA","A data de inicio da execução não pode ser maior que a data do término da vigência do contrato", 1, 0)
			lRet := .F.		
		EndIf
	ElseIf Upper(cCampo) == "CN9_XDTFIM"
		//Data de término da execução menor do que inicio da vigência
		If M->CN9_XDTFIM < dIniCtr
			Help("ESPECIFICO", 1, "HELP", "TÉRMINO EXECUÇÃO INVÁLIDA","A data de término da execução não pode ser menor que a data do inicio da vigência do contrato", 1, 0)
			lRet := .F.
		 
		 //Data de término da execução maior do que fim da vigência
		 ElseIf M->CN9_XDTFIM > dFimCtr
		 	Help("ESPECIFICO", 1, "HELP", "TÉRMINO EXECUÇÃO INVÁLIDA","A data de término da execução não pode ser maior que a data do término da vigência do contrato", 1, 0)
			lRet := .F.
		EndIf
	EndIf

Return lRet