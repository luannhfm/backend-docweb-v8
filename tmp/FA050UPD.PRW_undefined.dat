#Include 'Protheus.ch'
                      
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³FA050UPD  ºAutor  ³Microsiga           º Data ³  19/03/14   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ PE para validacao exclusao de titulo                       º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
Criado   - Edmar Tinti - 25/03/14        
Alterado - Auremar Duarte - 03/04/2014 ( Colocar somente a Elaine Zanella como habilitada para apagar títulos )
Alterado - Auremar Duarte - 14/11/2014 ( Incluir parâmetro para usuários habilitados a realizar exclusão )
Alterado - Jose Leite - CSI - 22/09/2016 ( Parametro exclusivo para o Contas a Pagar. )
Alterado - Caio lima  - j2a - 23/08/2017 retirada a validação para titulos tipo PA
*/          
User Function FA050UPD()
	Local _aArea		:= GetArea()
	Local _lRet 		:= .T.
	Local _cUsrExc 	:= GetNewPar("MV_XHEXCAP","000000;000044;000080")
	Local _cMsg := ""
	
	Pergunte("FIN050",.F.)
	
	RestArea(_aArea)
	
	If Type("lF050Auto") == "L" .And. .Not. lF050Auto .And. IsInCallStack("FA050DELET")
		If .Not. __cUserId $ _cUsrExc
			If SE2->E2_TIPO == "PA " .AND. SE2->E2_NUMBOR <> Padr("", TamSX3("E2_NUMBOR")[1])
				_lRet := .F.
				_cMsg := "Titulos PA somente podem ser excluidos antes da geração do bordero para o mesmo."
			ElseIf SE2->E2_TIPO == "PA " .AND. SE2->E2_NUMBOR == Padr("", TamSX3("E2_NUMBOR")[1])
				_lRet := .T.
			Else
				_lRet := .F.
				_cMsg := "Operação exlusão de titulos não está permitida para este usuario!"+ CRLF +;
					"O mesmo poderá ser baixado com o motivo de baixa CANCELADO!"
			EndIf
		EndIf	
	EndIf
	
	If !_lRet
		ApMsgStop(_cMsg,"FA050UPD")
	EndIf
	
	RestArea(_aArea)
Return(_lRet)
