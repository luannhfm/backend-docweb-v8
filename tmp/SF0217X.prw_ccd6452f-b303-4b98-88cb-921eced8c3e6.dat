#INCLUDE 'RWMAKE.CH'
#INCLUDE "TOTVS.CH"
#INCLUDE 'PROTHEUS.CH'
#INCLUDE 'TOPCONN.CH'
#INCLUDE "TBICONN.CH"
#INCLUDE "TBICODE.CH"
#INCLUDE "REPORT.CH"

/* 
-------------------------------------------------------------------------------
	{Protheus.doc}<SF0217X>
	 
	
	@Parametros<Nil> 
	@author<Walmir Junior> 
	@since<18/11/2016> 
	@version
		< 
		--------------------------------------------------------------------
		 Walmir Junior 
		--------------------------------------------------------------------
		>
	@receive<Nil>
	@return<Nil>
	@example<Nil> 
	@see<Nil> 
-------------------------------------------------------------------------------
*/

User Function SF0217X()

Local _cPerg 		:= "SF0217X"
Local _cTitulo		:= "Relatório Pré Nota OFM" 		// Titulo
Private _oReport	:= Nil 
Private _oSection1	:= Nil 
Private _aForn		:= {}
Private _cAlias		:= GetNextAlias()

CriaSX1(_cPerg)
If !Pergunte(_cPerg,.T.)
	Return(Nil)
EndIf

//--
_oReport := TReport():New("SF0217X",_cTitulo,_cPerg,{|_oReport| PrintReport(_oReport)} )

_oReport:SetLandscape()	// Orientado como retrato.
_oReport:SetEdit(.T.)
_oReport:SetTotalInLine(.F.)

//--Sessão Pré Nota
_oSection1 := TRSection():New(_oReport,"Pré Nota OFM", {_cAlias})
//_oSection1:lHeaderSection := .F.
_oSection1:nLinesBefore := 0
_oSection1:SetTotalInLine(.T.)
_oSection1:SetReadOnly()
//_oSection1:lHeaderVisible := .F.

TRCell():New(_oSection1,"D1_FILIAL"	, _cALias ,"FILIAL" 		, "@!"	,20)
TRCell():New(_oSection1,"D1_EMISSAO", _cALias ,"EMISSAO"		,		,20)
TRCell():New(_oSection1,"D1_FORNECE", _cALias ,"COD FORN"		,		,20)
TRCell():New(_oSection1,"FORNECE"	, _cALias ,"FORNECEDOR"		,		,50)//FORNECE
TRCell():New(_oSection1,"D1_PEDIDO" , _cALias ,"NR PEDIDO"		,		,20)
TRCell():New(_oSection1,"D1_DOC"  	, _cALias ,"DOCUMENTO"		,		,20)
TRCell():New(_oSection1,"D1_SERIE"  , _cALias ,"SERIE"			,		,20)
TRCell():New(_oSection1,"D1_XOFM"  	, _cALias ,"ORDEM FORNECIMENTO"		,		,30	)
TRCell():New(_oSection1,"D1_TOTAL"  , _cALias ,"TOTAL"			, "@E 9,999,999.99"	,20)

_oSection1:AutoSize()
_oReport:PrintDialog()

Return Nil

/*
+----------+-----------+-------+-------------------+------+-------------+
|Programa  |PRINTREPORT|Autor  |Walmir Junior      | Data |18/11/2016   |
+----------+-----------+-------+-------------------+------+-------------+
|Descrição | Implementa a Rotina  PrintReport                           |
|          |                                                            |
+----------+------------------------------------------------------------+
|Uso       | AP                                                         |
+----------+------------------------------------------------------------+
*/
Static Function PrintReport(_oReport)

MsgRun( "Selecionando registros...", "Aguarde" ,  {|| GeraQuery() } )

_oReport:Section(1):Print()

Return

//ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
// funcao que realiza o sql do relatorio
//ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
Static Function GeraQuery()
Local _cSql := ""

_cSql += " SELECT "			+CRLF
_cSql += "   D1_FILIAL,"	+CRLF
_cSql += "   D1_EMISSAO,"	+CRLF
_cSql += "   D1_FORNECE,"	+CRLF
_cSql += "   A2_NOME AS FORNECE,"	+CRLF
_cSql += "   D1_PEDIDO,"	+CRLF
_cSql += "   D1_DOC,"		+CRLF
_cSql += "   D1_SERIE,"		+CRLF
_cSql += "   D1_XOFM,"		+CRLF
_cSql += "   D1_TOTAL"		+CRLF
_cSql += " FROM "			+CRLF
_cSql += " 	"+RetSqlName("SD1")+" SD1 INNER JOIN "	+CRLF
_cSql += " 	"+RetSqlName("SA2")+" SA2 ON SA2.A2_FILIAL = '"+xFilial("SA2")+"' AND SA2.A2_COD = SD1.D1_FORNECE AND SA2.A2_LOJA = SD1.D1_LOJA "	+CRLF
_cSql += " WHERE "						+CRLF

If !Empty(mv_par01) .And. !Empty(mv_par02)
	_cSql += " 	D1_FILIAL Between '"+mv_par01+"' AND '"+mv_par02+"'  AND"	+CRLF
EndIf 
If !Empty(mv_par03) .And. !Empty(mv_par04)
	_cSql += " 	D1_FORNECE	>=  '"+mv_par03+"' AND"
	_cSql += " 	D1_LOJA		>= 	'"+mv_par04+"' AND"	+CRLF	
EndIf
If !Empty(mv_par05) .And. !Empty(mv_par06) 
	_cSql += " D1_FORNECE <= '" + mv_par05 + "' AND"
	_cSql += " D1_LOJA    <= '" + mv_par06 + "' AND"	
EndIf 
If !Empty(mv_par07) .And. !Empty(mv_par08)
	_cSql += " 	D1_EMISSAO Between '"+ DToS(mv_par07)+"' AND '"+ DToS(mv_par08)+"'  AND"	+CRLF
EndIf 
If !Empty(mv_par09) .And. !Empty(mv_par10)
	_cSql += " 	D1_XOFM Between '"+mv_par09+"' AND '"+mv_par10+"'  AND"	+CRLF
EndIf 

_cSql += " 	SD1.D_E_L_E_T_	= ' ' AND"	+CRLF
_cSql += " 	SA2.D_E_L_E_T_	= ' '"	+CRLF

MemoWrite("C:\Temp\SF0217X.txt" , _cSql) // escreve em arquivo de texto e se nao encontrar cria o arquivo

If Select(_cALias) > 0
	(_cALias)->(dbClosearea())
Endif

DbUseArea(.T., "TOPCONN", TCGenQry(,,ChangeQuery(_cSql)), _cALias, .F., .F.)

Return


/*
+----------+----------+-------+--------------------+------+-------------+
|Programa  |CRIASX1   |Autor  |Walmir Junior       | Data |18/11/2016   |
+----------+----------+-------+--------------------+------+-------------+
|Descrição | Cria o Grupo de perguntas para relatorio                   |
|          |                                                            |
+----------+------------------------------------------------------------+
|Uso       | AP                                                         |
+----------+------------------------------------------------------------+
*/
Static Function CriaSX1(_cPerg)
Local aHelp := {}
// Texto do help em portugues , ingles, espanhol
AAdd(aHelp, {{"Informe a Filial Inicial"			}, {""}, {""}})
AAdd(aHelp, {{"Informe a Filial Final"				}, {""}, {""}})
AAdd(aHelp, {{"Informe o Fornecedor Inicial"		}, {""}, {""}})
AAdd(aHelp, {{"Informe a Loja do Fornecedor Inicial"}, {""}, {""}})
AAdd(aHelp, {{"Informe o Fornecedor Final"			}, {""}, {""}})
AAdd(aHelp, {{"Informe o Loja do Fornecedor Final"	}, {""}, {""}})
AAdd(aHelp, {{"Informe a Data de Emissão Inicial"	}, {""}, {""}})
AAdd(aHelp, {{"Informe a Data de Emissão Final"		}, {""}, {""}})
AAdd(aHelp, {{"Informe a Ordem de Fornecimento Inicial"	}, {""}, {""}})
AAdd(aHelp, {{"Informe a Ordem de Fornecimento Final"	}, {""}, {""}})

u_SFPUTSX1(_cPerg,"01","De Filial?"		,"","","mv_ch1"	,"C",TamSX3("D1_FILIAL")[1]	,00,00,"G","","SM0"	,"","","mv_par01","","","",""	,"","","","","","","","","","","","",aHelp[01,1] ,aHelp[01,2] ,aHelp[01,3] ,"")
u_SFPUTSX1(_cPerg,"02","Até Filial?"	,"","","mv_ch2" ,"C",TamSX3("D1_FILIAL")[1]	,00,00,"G","","SM0" ,"","","mv_Par02","","","",""	,"","","","","","","","","","","","",aHelp[02,1] ,aHelp[02,2] ,aHelp[02,3] ,"")
u_SFPUTSX1(_cPerg,"03","De Fornecedor?"	,"","","mv_ch3" ,"C",TamSX3("D1_FORNECE")[1],00,00,"G","","SA2A","","","mv_Par03","","","",""	,"","","","","","","","","","","","",aHelp[03,1] ,aHelp[03,2] ,aHelp[03,3] ,"")
u_SFPUTSX1(_cPerg,"04","Da Loja?"       ,"","","mv_ch4" ,"C",TamSX3("D1_LOJA")[1]	,00,00,"G","",""    ,"","","mv_par04","","","",""	,"","","","","","","","","","","","",aHelp[04,1] ,aHelp[04,2] ,aHelp[04,3] ,"")
u_SFPUTSX1(_cPerg,"05","Até Fornecedor?","","","mv_ch5" ,"C",TamSX3("D1_FORNECE")[1],00,00,"G","","SA2A","","","mv_Par05","","","",""	,"","","","","","","","","","","","",aHelp[05,1] ,aHelp[05,2] ,aHelp[05,3] ,"")
u_SFPUTSX1(_cPerg,"06","Da Loja?"       ,"","","mv_ch6" ,"C",TamSX3("D1_LOJA")[1]	,00,00,"G","",""    ,"","","mv_par06","","","",""	,"","","","","","","","","","","","",aHelp[06,1] ,aHelp[06,2] ,aHelp[06,3] ,"")
u_SFPUTSX1(_cPerg,"07","De Emissao?"	,"","","mv_ch7" ,"D",TamSX3("D1_EMISSAO")[1],00,00,"G","",""   	,"","","mv_Par07","","","",""	,"","","","","","","","","","","","",aHelp[07,1] ,aHelp[07,2] ,aHelp[07,3] ,"")
u_SFPUTSX1(_cPerg,"08","Até Emissao?"	,"","","mv_ch8" ,"D",TamSX3("D1_EMISSAO")[1],00,00,"G","",""   	,"","","mv_Par08","","","",""	,"","","","","","","","","","","","",aHelp[08,1] ,aHelp[08,2] ,aHelp[08,3] ,"")
u_SFPUTSX1(_cPerg,"09","De OFM?"		,"","","mv_ch9" ,"C",TamSX3("D1_XOFM")[1]	,00,00,"G","",""   	,"","","mv_Par09","","","",""	,"","","","","","","","","","","","",aHelp[09,1] ,aHelp[09,2] ,aHelp[09,3] ,"")
u_SFPUTSX1(_cPerg,"10","Até OFM?"		,"","","mv_chA" ,"C",TamSX3("D1_XOFM")[1]	,00,00,"G","",""   	,"","","mv_Par10","","","",""	,"","","","","","","","","","","","",aHelp[10,1] ,aHelp[10,2] ,aHelp[10,3] ,"")

Return