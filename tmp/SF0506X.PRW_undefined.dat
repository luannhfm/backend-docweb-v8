#INCLUDE "PROTHEUS.CH" 
#INCLUDE "RWMAKE.CH"
#INCLUDE "FONT.CH"
#INCLUDE "COLORS.CH"

/*/{Protheus.doc} SF0505X
(long_description)
@author j2a.luizjunior
@since 10/07/2017
@version 1.0
@example
(examples)
@see (links_or_references)
/*/

User Function SF0506X

	Local cEstat := U_SFGN001A(ProcName(0), "SF0506X")
	Local   aCoors            := FWGetDialogSize(oMainWnd)
	Local   oTFont            := TFont():New('Courier new',,16,.T.)
	Local   oTPAN             := TFont():New('Courier new',,30,.T.) 
	Local   cRotina           := "Portaria"
 	Local   oWindow           := Nil
 	//-------------------------------------------------------------------------------------------------------------------------------
	Local   oPortaria         := SF0507X():New()
 	Local   lCondicao         := .F. 
	Local   oFontN            := TFont():New( "Arial",,-11,,.T. )
	Local   oFont             := TFont():New( "Arial",,-11,,.F. )
	Private aBotao            := {}
 	Private cTimer            := ""
 	Private cData             := ""
 	Private oGrp5   ,oGrp6   ,oTitulo,oGet1  ,oCarteira ,oGet2    ,oVencto  ,oGet3  ,cVenCup,cCor     ,oGrp9     ,cDepCar,oValid  
 	Private oDepen  ,oGet4   ,oDepCar,oGet5  ,oGrp7     ,oCor     ,oGet6    ,oPan3  ,cDesc  ,cNome    ,cVencto   ,oGet8    
 	Private oPan2   ,oGrp2   ,oGrp3  ,oBmp   ,oBmp1     ,cBarras  ,oTAcess  ,oGBarra,cNumero,cCarteira,cDepen    ,cPath  
 	Private oProduto,cProduto,oLote  ,cLote  ,oValidade ,cValidade,oValor   ,cValor ,cBarCup,oDescri  ,cCodProd  ,oCart  
 	Private cEmpresa,cDescEmp,cDCart ,cDepTit,cCategoria,cCBarras ,cContrato,cRevisa,cDescri,oBarCar  ,cMatricula,cValid
	//-------------------------------------------------------------------------------------------------------------------------------  
	aAdd(aBotao,{"Atualiza Estoque",{|| MRun()  },"At Estoque"})
	cBarras                   := Space(20)
	oWindow                   := MsDialog():New(aCoors[1], aCoors[2], 617, 804, cRotina,,, .F.,,,,,, .T.,,, .T.)
	oWindow:bInit             := {||EnchoiceBar(oWindow,{|| oWindow:End()},{|| oWindow:End()},.F.,aBotao)}
	oFWLayer := FWLayer():New() 
	oFWLayer:Init(oWindow)
	oFWLayer:AddLine("Lin01", 20, .F.)                      
	oFWLayer:addCollumn("Col01", 100, .T., "Lin01")
	oFWLayer:addWindow("Col01", "Win01", "Informe o codigo de barras para validar o acesso",100,.T.,.F.,,"Lin01")
	oPnl1                     := oFWLayer:GetColPanel("Col01","Lin01")
	oPan1                     := TPanel():New(020,005,"",oPnl1,oTFont,.T.,,CLR_YELLOW,CLR_WHITE,395,36)
	
	oGrpTST                   := TGroup():New(000,000,35,395,""  ,oPan1,CLR_BLACK,CLR_WHITE,.T.,.F.  )
	
	oGrp                      := TGroup():New(000,003,31,190," Portaria "  ,oPan1,CLR_BLACK,CLR_WHITE,.T.,.F.  )
	oGrp0                     := TGroup():New(000,200,31,392," Cuiabá, MT ",oPan1,CLR_BLACK,CLR_WHITE,.T.,.F.  )
	oBarras                   := TSay():New(007,015,{|| "Informe o Codigo de Barras:"               },oPan1,,,.F.,.F.,.F.,.T.,CLR_BLUE,CLR_WHITE,120,008)
	oGBarra                   := TGet():New(016,015,{|u| If(PCount() > 0 , cBarras  := u , cBarras )},oPan1,60,008,'',{|| oPortaria:CHKCODBAR(cBarras)},CLR_BLACK,CLR_WHITE,,,,.T.,"",,,.F.,.F.,,.F.,.F.,"","",,)	
	oGBarra:SetFocus()
	oData                     := TSay():New(007,210,{|| "Data:"                                     },oPan1,,,.F.,.F.,.F.,.T.,CLR_BLUE,CLR_WHITE,120,008)
	oGet                      := TGet():New(016,210,{|u| If(PCount() > 0 , cData   := u , cData    )},oPan1,60,008,'',,CLR_BLACK,CLR_WHITE,,,,.T.,"",,,.F.,.F.,,.F.,.F.,"","",,)	
	oGet:lReadOnly            := .T.
	oHora                     := TSay():New(007,310,{|| "Hora:"                                     },oPan1,,,.F.,.F.,.F.,.T.,CLR_BLUE,CLR_WHITE,120,008)
	oGet0                     := TGet():New(016,310,{|u| If(PCount() > 0 , cTimer   := u , cTimer  )},oPan1,60,008,'',,CLR_BLACK,CLR_WHITE,,,,.T.,"",,,.F.,.F.,,.F.,.F.,"","",,)	
	oGet0:lReadOnly           := .T.
	//-------------------------------------------------------------------------------------------------------------------------------
	//-> ACESSO ---------------------------------------------------------------------------------------------------------------------	
	oGrp3                     := TGroup():New(060,003,170,403,"",oWindow,CLR_BLACK,CLR_WHITE,.T.,.F.  )
	oGrp3:lVisibleControl     := .F.
	oBmp                      := TBITMAP():CREATE(oWindow)
	oBmp:cName                := "oBmp"
	oBmp:cCaption             := "oBmp:"
	oBmp:nLeft                := 010
	oBmp:nTop                 := 127
	oBmp:nWidth               := 900
	oBmp:nHeight              := 206
	oBmp:lShowHint            := .F.
	oBmp:lReadOnly            := .F.	
	oBmp:lStretch             := .F.
	oBmp:lAutoSize            := .F.
	oBmp:lVisibleControl      := .F.
	//-------------------------------------------------------------------------------------------------------------------------------
	//-> FOTO X CARTEIRA------------------------------------------------------------------------------------------------------------- 
	oPan2                     := TPanel():New(180,006,"",oWindow,oTFont,.T.,,CLR_YELLOW,CLR_WHITE,120,75)
	oGrp10                    := TGroup():New(179.5,005,255.8,127,"",oWindow,CLR_BLACK,CLR_WHITE,.T.,.F.  )
	oGrp10:lVisibleControl    := .F.	
	oCart                     := TBITMAP():CREATE(oPan2)
	oCart:cName               := "oCart"
	oCart:cCaption            := "oCart:"
	oCart:nLeft               := 000
	oCart:nTop                := 000
	oCart:nWidth              := 240
	oCart:nHeight             := 150
	oCart:lShowHint           := .F.
	oCart:lReadOnly           := .F.
	oCart:lVisibleControl     := .F.
	oCart:lStretch            := .F.
	oCart:lAutoSize           := .F.	
	If cFilAnt == "02MT0012"
		oCart:cBmpFile        := "\PARQUE\Cart_parque.bmp"
	Else
		oCart:cBmpFile        := "\PARQUE\img\Carteirinha_cac_01.bmp"
	EndIf		
	oBmp2                     := TBITMAP():CREATE(oPan2)
	oBmp2:cName               := "oBmp"
	oBmp2:cCaption            := "oBmp:"
	oBmp2:lShowHint           := .F.
	oBmp2:lReadOnly           := .F.
	oBmp2:lVisibleControl     := .F.
	oBmp2:lStretch            := .F.
	oBmp2:lAutoSize           := .F.
	oCategoria                := TSay():New(005,065,{|u| If(PCount() > 0 , cCategoria := u , cCategoria  )},oPan2,,oFontN,.F.,.F.,.F.,.T.,CLR_BLACK,CLR_WHITE,120,008)
	If cFilAnt == "02MT0008"
		oBmp2:nLeft           := 160
		oBmp2:nTop            := 047
		oBmp2:nWidth          := 59
		oBmp2:nHeight         := 78
		oDescCart             := TSay():New(012,001,{|u| If(PCount() > 0 , cDCart     := u , cDCart      )},oPan2,,oFont ,.F.,.F.,.F.,.T.,CLR_BLACK,CLR_WHITE,120,008)
		oDepTit               := TSay():New(017,001,{|u| If(PCount() > 0 , cDepTit    := u , cDepTit     )},oPan2,,oFontN,.F.,.F.,.F.,.T.,CLR_BLACK,CLR_WHITE,120,008)
		oEmpresa              := TSay():New(024,001,{|u| If(PCount() > 0 , cEmpresa   := u , cEmpresa    )},oPan2,,oFont ,.F.,.F.,.F.,.T.,CLR_BLACK,CLR_WHITE,120,008)
		oDescEmp              := TSay():New(029,001,{|u| If(PCount() > 0 , cDescEmp   := u , cDescEmp    )},oPan2,,oFontN,.F.,.F.,.F.,.T.,CLR_BLACK,CLR_WHITE,120,008)
		oValid                := TSay():New(037,001,{|u| If(PCount() > 0 , cValid     := u  , cValid     )},oPan2,,oFontN,.F.,.F.,.F.,.T.,CLR_BLACK,CLR_WHITE,120,008)
		oBarCar               := TSay():New(045,001,{|u| If(PCount() > 0 , cCBarras   := u , cCBarras    )},oPan2,,oFontN,.F.,.F.,.F.,.T.,CLR_BLACK,CLR_WHITE,120,008)
	Else
		oBmp2:nLeft           := 170
		oBmp2:nTop            := 050
		oBmp2:nWidth          := 59
		oBmp2:nHeight         := 78
		oDescCart             := TSay():New(017,001,{|u| If(PCount() > 0 , cDCart     := u , cDCart      )},oPan2,,oFont ,.F.,.F.,.F.,.T.,CLR_BLACK,CLR_WHITE,120,008)
		oDepTit               := TSay():New(023,001,{|u| If(PCount() > 0 , cDepTit    := u , cDepTit     )},oPan2,,oFontN,.F.,.F.,.F.,.T.,CLR_BLACK,CLR_WHITE,120,008)
		oEmpresa              := TSay():New(029,001,{|u| If(PCount() > 0 , cEmpresa   := u , cEmpresa    )},oPan2,,oFont ,.F.,.F.,.F.,.T.,CLR_BLACK,CLR_WHITE,120,008)
		oDescEmp              := TSay():New(035,001,{|u| If(PCount() > 0 , cDescEmp   := u , cDescEmp    )},oPan2,,oFontN,.F.,.F.,.F.,.T.,CLR_BLACK,CLR_WHITE,120,008)
		oValid                := TSay():New(042,001,{|u| If(PCount() > 0 , cValid     := u  , cValid     )},oPan2,,oFontN,.F.,.F.,.F.,.T.,CLR_BLACK,CLR_WHITE,120,008)
		oBarCar               := TSay():New(050,001,{|u| If(PCount() > 0 , cCBarras   := u , cCBarras    )},oPan2,,oFontN,.F.,.F.,.F.,.T.,CLR_BLACK,CLR_WHITE,120,008)
	EndIf
	//-------------------------------------------------------------------------------------------------------------------------------
	//-> TITULAR --------------------------------------------------------------------------------------------------------------------
	oGrp8                     := TGroup():New(177,130,210,403," Titular ",oWindow,CLR_BLACK,CLR_WHITE,.T.,.F.  )
	oGrp8:lVisibleControl     := .F.
	oTitulo                   := TSay():New(185,137,{|| "Nome:"                                          },oWindow,,,.F.,.F.,.F.,.T.,CLR_BLUE,CLR_WHITE,120,008)
	oTitulo:lVisibleControl   := .F.
	oGet4                     := TGet():New(194,137,{|u| If(PCount() > 0 , cNome     := u , cNome       )},oWindow,120,008,'',,CLR_BLACK,CLR_WHITE,,,,.T.,"",,,.F.,.F.,,.F.,.F.,"","",,)	
	oGet4:lVisibleControl     := .F.
	oGet4:lReadOnly           := .T.
	oCarteira                 := TSay():New(185,265,{|| "N. Carteira:"                                   },oWindow,,,.F.,.F.,.F.,.T.,CLR_BLUE,CLR_WHITE,120,008)
	oCarteira:lVisibleControl := .F.
	oGet5                     := TGet():New(194,265,{|u| If(PCount() > 0 , cCarteira := u , cCarteira   )},oWindow,60,008,'',,CLR_BLACK,CLR_WHITE,,,,.T.,"",,,.F.,.F.,,.F.,.F.,"","",,)	
	oGet5:lVisibleControl     := .F.
	oGet5:lReadOnly           := .T.
	oVencto                   := TSay():New(185,334,{|| "Vencimento:"                                    },oWindow,,,.F.,.F.,.F.,.T.,CLR_BLUE,CLR_WHITE,120,008)
	oVencto:lVisibleControl   := .F.
	oGet7                     := TGet():New(194,334,{|u| If(PCount() > 0 , cVencto   := u , cVencto     )},oWindow,60,008,'',,CLR_BLACK,CLR_WHITE,,,,.T.,"",,,.F.,.F.,,.F.,.F.,"","",,)	
	oGet7:lVisibleControl     := .F.
	oGet7:lReadOnly           := .F.
	//-------------------------------------------------------------------------------------------------------------------------------
	//-> DEPENDENTE -----------------------------------------------------------------------------------------------------------------
	oGrp9                     := TGroup():New(217,130,252,403," Dependentes ",oWindow,CLR_BLACK,CLR_WHITE,.T.,.F.  )
	oGrp9:lVisibleControl     := .F.
	oDepen                    := TSay():New(225,137,{|| "Nome:"                                          },oWindow,,,.F.,.F.,.F.,.T.,CLR_BLUE,CLR_WHITE,120,008)
	oDepen:lVisibleControl    := .F.
	oGet8                     := TGet():New(234,137,{|u| If(PCount() > 0 , cDepen   := u , cDepen       )},oWindow,120,008,'',,CLR_BLACK,CLR_WHITE,,,,.T.,"",,,.F.,.F.,,.F.,.F.,"","",,)	
	oGet8:lVisibleControl     := .F.
	oGet8:lReadOnly           := .T.
	oDepCar                   := TSay():New(225,265,{|| "N. Carteira:"                                   },oWindow,,,.F.,.F.,.F.,.T.,CLR_BLUE,CLR_WHITE,120,008)
	oDepCar:lVisibleControl   := .F.
	oGet9                     := TGet():New(234,265,{|u| If(PCount() > 0 , cDepCar  := u , cDepCar      )},oWindow,120,008,'',,CLR_BLACK,CLR_WHITE,,,,.T.,"",,,.F.,.F.,,.F.,.F.,"","",,)	
	oGet9:lVisibleControl     := .F.
	oGet9:lReadOnly           := .T.
	//-------------------------------------------------------------------------------------------------------------------------------
	//-> PULSEIRAS ------------------------------------------------------------------------------------------------------------------
	If cFilAnt != "02MT0008"  
		oGrp7                 := TGroup():New(259,130,294,403," Pulseiras ",oWindow,CLR_BLACK,CLR_WHITE,.T.,.F.  )
		oGrp7:lVisibleControl := .F.
		oCor                  := TSay():New(267,137,{|| "Cor:"                                    },oWindow,,,.F.,.F.,.F.,.T.,CLR_BLUE,CLR_WHITE,120,008)
		oCor:lVisibleControl  := .F.
		oCor:lReadOnly        := .T.
		oGet6                 := TGet():New(276,137,{|u| If(PCount() > 0 , cCor   := u , cCor     )},oWindow,80,008,'',,CLR_BLACK,CLR_WHITE,,,,.T.,"",,,.F.,.F.,,.F.,.F.,"","",,)	
		oGet6:lVisibleControl := .F.
		oPan3                 := TPanel():New(268,220,"",oWindow,oTFont,.T.,,CLR_YELLOW,CLR_BLUE,160,020)
		oPan3:lVisibleControl := .F.
	EndIf
	//-------------------------------------------------------------------------------------------------------------------------------
	//-> INGRESSO -------------------------------------------------------------------------------------------------------------------
	oGrp6                     := TGroup():New(177,130,210,403," Ingresso ",oWindow,CLR_BLACK,CLR_WHITE,.T.,.F.  )
	oGrp6:lVisibleControl     := .F.
	oCupom                    := TSay():New(185,137,{|| "Descrição:"                                         },oWindow,,,.F.,.F.,.F.,.T.,CLR_BLUE,CLR_WHITE,120,008)
	oCupom:lVisibleControl    := .F.
	oGet1                     := TGet():New(194,137,{|u| If(PCount() > 0 , cDesc    := u , cDesc            )},oWindow,120,008,'',,CLR_BLACK,CLR_WHITE,,,,.T.,"",,,.F.,.F.,,.F.,.F.,"","",,)	
	oGet1:lVisibleControl     := .F.
	oGet1:lReadOnly           := .T.
	oNumero                   := TSay():New(185,265,{|| "Numero:"                                            },oWindow,,,.F.,.F.,.F.,.T.,CLR_BLUE,CLR_WHITE,120,008)
	oNumero:lVisibleControl   := .F.
	oGet2                     := TGet():New(194,265,{|u| If(PCount() > 0 , cCodProd := u , cCodProd         )},oWindow,60,008,'',,CLR_BLACK,CLR_WHITE,,,,.T.,"",,,.F.,.F.,,.F.,.F.,"","",,)	
	oGet2:lVisibleControl     := .F.
	oGet2:lReadOnly           := .T.
	oVencto                   := TSay():New(185,334,{|| "Vencimento:"                                        },oWindow,,,.F.,.F.,.F.,.T.,CLR_BLUE,CLR_WHITE,120,008)
	oVencto:lVisibleControl   := .F.
	oGet3                     := TGet():New(194,334,{|u| If(PCount() > 0 , cVenCup  := u , cVenCup          )},oWindow,60,008,'',,CLR_BLACK,CLR_WHITE,,,,.T.,"",,,.F.,.F.,,.F.,.F.,"","",,)	
	oGet3:lVisibleControl     := .F.
	oGet3:lReadOnly           := .T.
	oCVE                      := TBITMAP():CREATE(oPan2)
	oCVE:cName                := "oCVE"
	oCVE:cCaption             := "oCVE:"
	oCVE:nLeft                := 000
	oCVE:nTop                 := 000
	oCVE:nWidth               := 240
	oCVE:nHeight              := 150
	oCVE:lShowHint            := .F.
	oCVE:lReadOnly            := .F.
	oCVE:lVisibleControl      := .F.
	oCVE:lStretch             := .F.
	oCVE:lAutoSize            := .F.
	oCVE:lVisibleControl      := .F.
	oProduto                  := TSay():New(018,001,{|u| If(PCount() > 0 , cProduto   := u , cProduto  ) },oPan2,,oFontN,.F.,.F.,.F.,.T.,CLR_BLACK,CLR_WHITE,120,008)
	oLote                     := TSay():New(024,001,{|u| If(PCount() > 0 , cLote      := u , cLote     ) },oPan2,,oFont ,.F.,.F.,.F.,.T.,CLR_BLACK,CLR_WHITE,120,008)
	oValidade                 := TSay():New(031,001,{|u| If(PCount() > 0 , cValidade  := u , cValidade ) },oPan2,,oFont,.F.,.F.,.F.,.T.,CLR_BLACK,CLR_WHITE,120,008)
	oValor                    := TSay():New(037,001,{|u| If(PCount() > 0 , cValor     := u , cValor    ) },oPan2,,oFont ,.F.,.F.,.F.,.T.,CLR_BLACK,CLR_WHITE,120,008)
	oDescri                   := TSay():New(048,001,{|u| If(PCount() > 0 , cDescri    := u , cDescri   ) },oPan2,,oFontN,.F.,.F.,.F.,.T.,CLR_BLACK,CLR_WHITE,120,008)
	//-------------------------------------------------------------------------------------------------------------------------------
	oTimer                    := TTimer():New(1,     {|| fTimer()   },oWindow)	
	oTAcess                   := TTimer():New(10000, {|| fCloseObj()},oWindow)
	oWindow:lEscClose         := .T. 
	oWindow:lCentered         := .T.
	oTimer:Activate()
	oWindow:Activate()	
Return

/*/{Protheus.doc} fTimer
(long_description)
@author j2a.luizjunior
@since 11/07/2017
@version 1.0
@example
(examples)
@see (links_or_references)
/*/

Static Function fTimer
	
	Local cEstat := U_SFGN001A(ProcName(0), "SF0506X")

 	cTimer     := Time()
 	cData      := DToc(Date())	
	
	oGet0:Refresh()
	oGet:Refresh()

Return

/*/{Protheus.doc} fCloseObj
(long_description)
@author j2a.luizjunior
@since 12/07/2017
@version 1.0
@example
(examples)
@see (links_or_references)
/*/

Static function fCloseObj()

	Local cEstat := U_SFGN001A(ProcName(0), "SF0506X")

	//-> Foto -------------------------------
	oGrp10:lVisibleControl      := .F.
	oCart:lVisibleControl       := .F.	
	oBmp2:lVisibleControl       := .F.
	oEmpresa:lVisibleControl    := .F. 
	oValid:lVisibleControl      := .F.
	oDescEmp:lVisibleControl    := .F. 
	oBarras:lVisibleControl     := .F. 
	oDescCart:lVisibleControl   := .F. 
	oDepTit:lVisibleControl     := .F.
	oCategoria:lVisibleControl  := .F.
	oBarCar:lVisibleControl     := .F.
	cCategoria                  := ""
	cDepTit                     := ""
	cDCart	                    := ""
	cEmpresa                    := ""
	cDescEmp                    := ""
	cCBarras                    := ""
	cValid                      := ""
	//------------------------------------------
	//-> Titular -------------------------------
	oGrp8:lVisibleControl       := .F.
	oTitulo:lVisibleControl     := .F.
	oGet4:lVisibleControl       := .F.
	oCarteira:lVisibleControl   := .F.
	oGet5:lVisibleControl       := .F.
	oVencto:lVisibleControl     := .F.
	oGet7:lVisibleControl       := .F.	
	//------------------------------------------
	//-> Dependente-----------------------------
	cDepCar                     := ""
	cDepen                      := ""
	oGrp9:lVisibleControl       := .F.
	oDepen:lVisibleControl      := .F.
	oGet8:lVisibleControl       := .F.
	oDepCar:lVisibleControl     := .F.
	oGet9:lVisibleControl       := .F.
	//------------------------------------------
	//-> Cupom ---------------------------------
	cDesc                       := ""
	cNumero                     := ""
	cVenCup	                    := ""
	oGrp3:lVisibleControl       := .F.
	oBmp:lVisibleControl        := .F.
	oGrp6:lVisibleControl       := .F.
	oCupom:lVisibleControl      := .F.
	oGet1:lVisibleControl       := .F.
	oNumero:lVisibleControl     := .F.
	oGet2:lVisibleControl       := .F.
	oVencto:lVisibleControl     := .F.
	oGet3:lVisibleControl       := .F.
	oCVE:lVisibleControl        := .F.
	oDescri:lVisibleControl     := .F.
	oValor:lVisibleControl      := .F.
	cValidade                   := ""  
	cProduto                    := ""
	cLote                       := ""
	cBarCup                     := ""
	cValor                      := ""
	oProduto:lVisibleControl    := .F.
	oLote:lVisibleControl       := .F.     
	oValidade:lVisibleControl   := .F. 
	//-----------------------------------------	
	//-> Pulseira -----------------------------
	If cFilant != "02MT0008"    
		oGrp7:lVisibleControl   := .F.
		oCor:lVisibleControl    := .F.
		oGet6:lVisibleControl   := .F.
		oPan3:lVisibleControl   := .F.
	EndIf
	//-----------------------------------------	
	oGBarra:SetFocus()
	oTAcess:Deactivate()			
Return

/*/{Protheus.doc} MRun
(long_description)
@author j2a.luizjunior
@since 09/08/2017
@version 1.0
@example
(examples)
@see (links_or_references)
/*/

Static Function MRun 

	Local cEstat := U_SFGN001A(ProcName(0), "SF0506X")

	FWMsgRun(,{||AtEstoque()},"Atualizando Estoque...", "Aguarde")

Return

/*/{Protheus.doc} AtEstoque
(long_description)
@author j2a.luizjunior
@since 09/08/2017
@version 1.0
@example
(examples)
@see (links_or_references)
/*/

Static Function AtEstoque 

	Local cEstat := U_SFGN001A(ProcName(0), "SF0506X")
	Local   cAlZH4 := GetNextAlias()
	Local   cTPAux := ""
	Local   cTipo  := ""
	Local   cCod   := ""
	Local   cItem  := ""   
	Local   nQProd := 0
	Local   aZH4   := {}
	
	BeginSql Alias cAlZH4
	
		SELECT ZH4_FILIAL,
		       ZH4_TIPO,
		       ZH4_COD,
		       ZH4_ITEM
		FROM   %Table:ZH4% 
		WHERE ZH4_TIPO   != '7' 
		AND   ZH4_ACESSO  = '1'
		AND   ZH4_ATUSD3 != '1'
		AND   %NotDel%
		ORDER BY ZH4_TIPO,ZH4_ITEM;
	
	EndSql
	
	While !(cAlZH4)->(Eof()) 
		
		If cTipo != (cAlZH4)->ZH4_TIPO 
			
			If nQProd > 0
			
				lSucess  := GRVSD3(cTipo,nQProd,cCod,cItem,aZH4)
				 
				If !lSucess
					Aviso("Aviso", "Não foi possivel atualizar estoque, verifique!", {"Ok"}, 1)
					Return
				Else
					ATUZH4(aZH4)	
				EndIf
							
			EndIf
			
			cCod   := (cAlZH4)->ZH4_COD
			cItem  := (cAlZH4)->ZH4_ITEM  
			cTPAux += (cAlZH4)->ZH4_TIPO + "u"
			cTipo  := (cAlZH4)->ZH4_TIPO
			nQProd := 0
			aZH4   := {}
			
		EndIf
		
		aAdd(aZH4,{(cAlZH4)->ZH4_COD,(cAlZH4)->ZH4_ITEM,(cAlZH4)->ZH4_TIPO,"1"})
		nQProd++
		
		(cAlZH4)->(DbSkip())
		
		If (cAlZH4)->(Eof()) .And. nQProd > 0 
		
			lSucess  := GRVSD3(cTipo,nQProd,cCod,cItem,aZH4)
			 
			If !lSucess
				Aviso("Aviso", "Não foi possivel atualizar estoque, verifique!", {"Ok"}, 1)
				Return
			Else
				ATUZH4(aZH4)	
			EndIf
		
		EndIf
		
	EndDo
	
	Aviso("Aviso", "Atualização realizada com sucesso!", {"Ok"}, 1)
	
Return

/*/{Protheus.doc} GRVSD3
(long_description)
@author j2a.luizjunior
@since 09/08/2017
@version 1.0
@example
(examples)
@see (links_or_references)
/*/

Static Function GRVSD3(pTipo,pQuant,pCod,pItem)

	Local cEstat := U_SFGN001A(ProcName(0), "SF0506X")
	Local   cTipo       := pTipo
	Local   cTM         := GetNewPar("MV_XTMSD3",'599')
	Local   cProduto    := ""
	Local   cUM         := ""
	Local   cArmazem    := GetNewPar("MV_XLOCAL",'01' )
	Local   nQuant      := pQuant
	Local   cCod        := pCod
	Local   cItem       := pItem
	Local   aMata240    := Array(0)
	Local   nX          := 0
	Local   lRet        := .F.
	Private lMsErroAuto := .F.
	
	cDocSD3 := PEGADOCSD3("1","")
	
	Do Case			
		Case cTipo $ "1|2" // TITULAR/DEPENDENTE
			cProduto := GetNewPar("MV_XPRCOR","000000000041402")
			cUM      := Posicione("SB1",1,xFilial("SB1") + cProduto     , "B1_UM"      )
		Case cTipo == "3"  // CUPOM
			cProd    := Posicione("ZH0",2,xFilial("ZH0") + cCod         , "ZH0_COD"    )
			cProduto := Posicione("ZH3",1,xFilial("ZH3") + cProd        , "ZH3_PRDEST" )
			cUM      := Posicione("SB1",1,xFilial("SB1") + cProduto     , "B1_UM"      )			
		Case cTipo == "4"  // LOCAÇÃO
			cProd    := Posicione("ZH5",1,xFilial("ZH5") + cCod         , "ZH5_PROD"   )
			cProduto := Posicione("ZH3",1,xFilial("ZH3") + cProd        , "ZH3_PRDEST" )
			cUM      := Posicione("SB1",1,xFilial("SB1") + cProduto     , "B1_UM"      )			
		Case cTipo == "5"  // CONVITE
			cProd    := Posicione("ZH6",1,xFilial("ZH6") + cCod         , "ZH6_PROD"   )
			cProduto := Posicione("ZH3",1,xFilial("ZH3") + cProd        , "ZH3_PRDEST" )
			cUM      := Posicione("SB1",1,xFilial("SB1") + cProduto     , "B1_UM"      )			
	EndCase	
	
	nX++
	aAdd(aMata240, {})
	aAdd(aMata240[nX], {'D3_TM'     , cTM                  , Nil})
	aAdd(aMata240[nX], {'D3_COD'    , cProduto             , Nil})
	aAdd(aMata240[nX], {'D3_UM'     , cUM                  , Nil})
	aAdd(aMata240[nX], {'D3_QUANT'  , nQuant               , Nil})
	aAdd(aMata240[nX], {'D3_LOCAL'  , cArmazem             , Nil})
	aAdd(aMata240[nX], {'D3_DOC'    , cDocSD3              , Nil})
	aAdd(aMata240[nX], {'D3_EMISSAO', dDataBase            , Nil})
	aAdd(aMata240[nX], {'D3_USUARIO', Substr(cUsuario,7,15), Nil})

	For nX := 1 to Len(aMata240)
		MSExecAuto({|x,y| mata240(x,y)},aMata240[nX],3)
		If lMsErroAuto
			DisarmTransaction()
			Mostraerro()
			Return(.f.)
		Else	
			lRet        := .T.	
			PEGADOCSD3("2",cDocSD3)
		EndIf
	Next nX
		
Return lRet

/*/{Protheus.doc} PEGADOCSD3
(long_description)
@author j2a.luizjunior
@since 09/08/2017
@version 1.0
@example
(examples)
@see (links_or_references)
/*/

Static Function PEGADOCSD3(pTipo,pNum)

	Local cEstat := U_SFGN001A(ProcName(0), "SF0506X")
	Local cRet := ""
	Local cTipo := pTipo
	Local cNum  := pNum
	
	If cTipo == "1"		
		cRet       := GetMv("MV_XDOCD3",,.T.)
		cRet       := Soma1(cRet,9)
	ElseIf GetMv("MV_XDOCD3") < cNum			
		If RecLock("SX6",.F.)
			Replace X6_CONTEUD With cNum 
			Replace X6_CONTSPA With cNum
			Replace X6_CONTENG With cNum
			SX6->(MsUnLock())
		EndIf	
	EndIf

Return cRet

/*/{Protheus.doc} ATUZH4
(long_description)
@author j2a.luizjunior
@since 09/08/2017
@version 1.0
@example
(examples)
@see (links_or_references)
/*/

Static Function ATUZH4(pZH4)

	Local cEstat := U_SFGN001A(ProcName(0), "SF0506X")
	Local aZH4  := pZH4 
	
	For nF := 1 To Len(aZH4)
	
		DbSelectarea("ZH4")
		DbSetOrder(1)
		If DbSeek(xFilial("ZH4") + aZH4[nF][1] + aZH4[nF][2] + aZH4[nF][3] + aZH4[nF][4])
		
			While !ZH4->(Eof()) .And. xFilial("ZH4") + aZH4[nF][1] + aZH4[nF][2] + aZH4[nF][3] + aZH4[nF][4] == ZH4->ZH4_FILIAL + ZH4->ZH4_COD + ZH4->ZH4_ITEM + ZH4->ZH4_TIPO + ZH4->ZH4_ACESSO
				
				If RecLock("ZH4",.F.)
					
					ZH4->ZH4_ATUSD3 := "1"
					
					ZH4->(MsUnlock())
					
				EndIf
				
				ZH4->(DbSkip())
			EndDo
		EndIf
		
	Next nF	
	
Return