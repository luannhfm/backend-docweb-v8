#Include 'Protheus.ch'

/*/{Protheus.doc} M030EXC
Ponto de Entrada é chamado antes de executar a rotina de exclusão dos clientes. 
Pode ser utilizado para continuar ou abandonar a rotina de exclusão.

-----------------------------------------------------------------------
   Antonio Dantas                                            26/02/2015 
   1) Criacao neste mesmo fonte, logo abaixo, a funcao: [fMarkSA1()]
      que preenche o Campo [A1_XOPER - Dta Exclusao - DN] com a data
      de exclusao no formato AAAAMMAA, para ser utilizado na INTEGRACAO 
      CRM x DN.        
   2) Incluida a Chamada a funcao [fMarkSA1()]
-----------------------------------------------------------------------

@author 	Jose Leite de Barros Neto
@since 	27/06/2014
@version 	1.0
@return  Nil, Nulo

/*/

User Function M030EXC()

	Local _aArea 	:= GetArea()
	Local _cCodCli 	:= SA1->(A1_COD)
	Local _cPessoa	:= SA1->(A1_PESSOA)
	Local _cClvl	:= ""
	
	If _cPessoa == "F"
		_cClvl := "CF"+_cCodCli
	Else
		_cClvl := "CJ"+_cCodCli 
	EndIf
		
	//Deleta Classe Valor
	fDelCTH(_cClvl) 
	
	//+------------------------------------------------------------------+
	//| Antonio Dantas                                        26/11/2015 |
	//| Preenche o Campo [A1_XOPER - Dta Exclusao - DN] com a data de    |
	//| exclusao no formato AAAAMMAA, para ser utilizado na INTEGRACAO   |
	//| CRM x DN.                                                        |        
	//+------------------------------------------------------------------+
	fMarkSA1()
	
	RestArea(_aArea)
	
Return( Nil )

/** {Protheus.doc} fDelCTH
Funcao para Deletar a Classe Valor do cliente

@param: 	<Nil>
@author: 	Jose Leite de Barros Neto
@since: 	27/06/2014
@Uso: 		SFIEMT
*/
Static Function fDelCTH(pClvl)
	
	//Deletar a Classe Valor para cliente
	DbSelectArea("CTH")
	DbSetOrder(1) //CTH_FILIAL+CTH_CLVL
	If DbSeek(xFilial("CTH")+pClvl)
		If RecLock("CTH",.F.)
			dbDelete()
			MsUnlock()
		Endif
	EndIf
	
Return( Nil )

/*
--------------------------------------------------------------------------------
{Protheus.doc} <fMarkSA1>
   #CONTROLE_DN
   Preenche o Campo [A1_XOPER - OPER DN] com a data de exclusao no formato 
   AAAAMMAA:hh:mm, para ser utilizado na INTEGRACAO CRM x DN.        

@author<Antonio Dantas>
@since<26/11/2015>
@version<1.00>
@receive<Nil>
@return<Nil>
@example<Nil>
@see<Nil>
--------------------------------------------------------------------------------
*/
Static Function fMarkSA1()
If SA1->(RecLock("SA1",.f.))
	Replace SA1->A1_XOPER With Substr(SA1->A1_XOPER,1,30)+Dtos(dDataBase)+':'+Substr(Time(),1,5)
	SA1->(MsUnlock())
	SA1->(dbCommit())
Endif
Return Nil