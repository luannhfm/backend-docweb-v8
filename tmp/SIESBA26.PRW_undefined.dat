#INCLUDE "PROTHEUS.CH"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³SIESBA26   ºAutor  ³Microsiga          º Data ³  28/02/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Receptor de EAI - Renegociacao (FINA280 Fatura Receber)    º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MP 11.5 - Sistema Industria                                º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

User Function SIESBA26()
Local _cError    := ""
Local _cWarning  := ""
Local _cDelimit  := "_"
Local aStru      := {}
Local _cXML      := ""
Local _cData     := Dtos(Date())
Local _cTime     := Time()
Local _cDateTime := Transform(_cData,"@R 9999-99-99")+"T"+_cTime+"Z"
Local _cFile     := "FINA280"+"3"+_cData+StrTran(_cTime,":","")+".XML"

// Montagem das tags do XML
_cXML += '<TOTVSIntegrator>'
_cXML += '<GlobalProduct>TOTVS|ESB</GlobalProduct>'
_cXML += '<GlobalFunctionCode>EAI</GlobalFunctionCode>'
_cXML += '<GlobalDocumentFunctionCode>FINA280</GlobalDocumentFunctionCode>'
_cXML += '<GlobalDocumentFunctionDescription>Renegociacao</GlobalDocumentFunctionDescription>'
_cXML += '<DocVersion>1.0</DocVersion>'
_cXML += '<DocDateTime>'+_cDateTime+'</DocDateTime>'
_cXML += '<DocIdentifier></DocIdentifier>'
_cXML += '<DocCompany>'+cEmpAnt+'</DocCompany>'
_cXML += '<DocBranch>'+cFilAnt+'</DocBranch>'
_cXML += '<DocName></DocName>'
_cXML += '<DocFederalID></DocFederalID>'
_cXML += '<DocType>2</DocType>'
_cXML += '<Message>'
_cXML += '<Layouts>'
_cXML += '<Identifier>FINA280</Identifier>'
_cXML += '<Version>1.0</Version>'
_cXML += '<FunctionCode></FunctionCode>'
_cXML += '<Content>'
_cXML += '<FINA280 Operation="3" version="1.01">'
_cXML += '<SE1MASTER modeltype="FIELDS">'
_cXML += '<E1_FILIAL order="1">'
_cXML += '<value>'+cFilAnt+'</value>'
_cXML += '</E1_FILIAL>'
_cXML += '<CFATURA order="2">'
_cXML += '<value>'+CFATURA+'</value>'
_cXML += '</CFATURA>'
_cXML += '<items>'
For x := 1 to Len(aMark)
	SE1->(dbGoTo(aMark[x]))
	_cXML += '<item id="'+Alltrim(Str(x))+'" deleted="0">'
	_cXML += '<E1_PREFIXO>'+SE1->E1_PREFIXO+'</E1_PREFIXO>'
	_cXML += '<E1_NUM>'+SE1->E1_NUM+'</E1_NUM>'
	_cXML += '<E1_PARCELA>'+SE1->E1_PARCELA+'</E1_PARCELA>'
	_cXML += '<E1_TIPO>'+SE1->E1_TIPO+'</E1_TIPO>'
	_cXML += '<E1_CLIENTE>'+SE1->E1_CLIENTE+'</E1_CLIENTE>'
	_cXML += '<E1_LOJA>'+SE1->E1_LOJA+'</E1_LOJA>'
	_cXML += '</item>'
Next
_cXML += '</items>'
_cXML += '<SE1DETAIL modeltype="GRID">'
_cXML += '<items>'
For y := 1 to Len(aCols)
	_cXML += '<item id="'+Alltrim(Str(y))+'" deleted="0">'
	_cXML += '<E1_PREFIXO>'+GDFieldGet("E1_PREFIXO",y)+'</E1_PREFIXO>'
	_cXML += '<E1_NUM>'+GDFieldGet("E1_NUM",y)+'</E1_NUM>'
	_cXML += '<E1_PARCELA>'+GDFieldGet("E1_PARCELA",y)+'</E1_PARCELA>'
	_cXML += '<E1_VENCTO>'+Dtoc(GDFieldGet("E1_DTFATUR",y))+'</E1_VENCTO>'
	_cXML += '<E1_VALOR>'+Alltrim(Transform(GDFieldGet("E1_VLCRUZ",y),"@E 999,999,999.99"))+'</E1_VALOR>'
	_cXML += '</item>'
Next
_cXML += '</items>'
_cXML += '</SE1DETAIL>'
_cXML += '</SE1MASTER>'
_cXML += '</FINA280>'
_cXML += '</Content>'
_cXML += '</Layouts>'
_cXML += '</Message>'
_cXML += '</TOTVSIntegrator>'

// Transforma a string XML em Objeto
oXML := XMLParser( _cXML, _cDelimit, @_cError, @_cWarning )

//Verifica se a estrutura foi criada
IF !(Empty(_cError) .and. Empty(_cWarning))
	Return()
ENDIF

// Geração do arquivo
U_SIXMLDATA(oXML,_cFile)

Return()
